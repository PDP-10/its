	ABS
	ORG	17700
/
/ ASSEMBLE WITH IA AND GET LISTING FILE.  RUN LISTING FILE THROUGH
/  BINTRN TO STRIP OFF OCTAL (WITH EDIT REMOVE GLITCHES AND INSERT
/  LABEL BINLDR).  INSERT SOME ZEROS BEFORE 1ST WORD TO KLUDGE 4K
/  1ST CHAR GARBLE (BINLDR 0; 001032 ... ).  SEGMENT OLD IA:
/  >S BINLDR,ENDLDR    THEN    >T DK IAA SRC,IAB,IAC _ DK IA (Y)
/  THEN  >T DK IA SRC _ DK IAA,O,IAC (W)   TO PUT TOGETHER WITH NEW
/  LOADER.   FINALLY PATCH INTO SYSTEM:  >READR 17600 IA
/
LOAD	RCF		/IOT MUST BE FIRST TO UNLOCK ROM
COUNT	JMS	.	/-WC
CHECK	LAC	COUNT	/CHECKSUM
	SAM	LCHECK
	JMP	HEAD
	LWC	17777-DLIST+1	/IF 8K (LV), START LINE
	DLA
	DON
HEAD	CAL
	DAC	CHECK
	JMS	WORD	/LOAD OR TRANSFER ADDRESS
	DAC	LOAD
	ASP
	JMP	DONE
	JMS	WORD	/WORD COUNT (COMP)
	DAC	COUNT
	JMS	WORD	/CHECKSUM
/
NEXT	JMS	WORD	/DATA
	DAC*	LOAD
	LAC	LOAD
	SAR	3
	AND	MASK
	IOR	SETY
	DAC	DLIST
	LAC*	LOAD
	ISZ	LOAD
	ISZ	COUNT
	JMP	NEXT	/LOOP TO END OF BLOCK
	ADD	CHECK
	ASZ
	JMP	.		/CHECKSUM ERROR
	JMP	HEAD		/NEXT BLOCK
/
DONE	DOF
	IAC
	ASN
	HLT
	JMS	WORD	/LAST WORD IS GARBAGE
	JMP*	LOAD
/
WORD	ZRO
	ADD	CHECK
	DAC	CHECK	/ACCUMULATE CHECKSUM
	CAL
	RSF
	JMP	.-1
	RRC		/READ HIGH ORDER HALF
	RAL	3
	RAL	3
	RAL	2
	RSF
	JMP	.-1
	RRC		/READ LOW ORDER HALF
	JMP*	WORD
/
SETY	DLYA	0
MASK	OCT	1777
/
DLIST	DLYA	0
	DLXA	0
	DSTS	1
	046000
	021777
	000000
	DJMP	DLIST
LCHECK	ZRO	CHECK
/
LL=.-LOAD;LL=76-LL
	.IFNEG	LL
	.ERROR	YOU LOSE, LOADER CAN BE AT MOST 76 WRDS (ROM LIMIT)
	.ENDC
	.IFPNZ	LL
	.REPT	LL
	0
	.ENDC
/
/
/
/DEL REST
	END

