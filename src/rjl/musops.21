	IFE TBOX,[
;OLD LOGO MUSIC SYSTEM
;AS OPPOSED TO THE SYSTEM OF WINOGRAD AND BAMBERGER
	IFN OMFLAG,[


PLAY:	SKIPG NVOICE
	ERROR NUMVOI
	PUSHJ P,NUMVAL
	JUMPL E,[ERROR]
	MOVEM E,UNITS
	MOVE E,NVOICE
	MOVEM E,MUSTEM
PLAY1:	PUSHJ P,GNE
	 ERROR ERMSSG
	SOS CPP
	PUSHJ P,EVAL
	PUSHJ P,NUMVAL
	MOVN D,MUSTEM
	MOVEM E,MUSTEM+5(D)
	AOJGE D,PLAY2
	MOVNM D,MUSTEM
	JRST PLAY1
PLAY2:	MOVEI A,40
	MOVN B,NVOICE
PLAY3:	SKIPG C,MUSTEM+5(B)
	ERROR ARGWNG
	CAILE C,64.
	ERROR ARGWNG
	ADDM A,MUSTEM+5(B)
	AOJL B,PLAY3
	PUSHJ P,MUSON
PLAY4:	MOVE A,UNITS
PLAY5:	SOJL A,COMEX
	MOVN B,NVOICE
	TLNE F,BREAKF
	ERROR BREAK
	.IOT TYOC,MUSTEM+5(B)
	AOJL B,.-1
	JRST PLAY5

MUSON:	SKIPLE SILENT
	POPJ P,
	.IOT TYOC,MUSONC
	MOVEM A,SILENT
	POPJ P,

VOICES:	PUSHJ P,NUMVAL
	JUMPL E,[ERROR]
	CAILE E,4
	ERROR
	MOVEM E,NVOICE
	ADD E,VOICEC
	PUSHJ P,MUSON
	.IOT TYOC,E
	JRST COMEX

OLDMUS:	MOVEI A,143
	MOVEM A,VOICEC
	SETOM MUSONC
	SETOM TTYONC
	JRST COMEX

NEWMUS:	MOVEI A,137
	MOVEM A,VOICEC
	MOVEI A,146
	MOVEM A,MUSONC
	MOVEI A,145
	MOVEM A,TTYONC
	JRST COMEX
]

;MORE LOGO MUSIC SYSTEM
	IFE OMFLAG,[

;INITIALIZE STORAGE SPACE FOR THE MUSIC BUFFER
MBINIT:
MBINI1:	HRRZI C,@WTOP
	ADDI C,MBLGTH+2	;FIRST AND LAST WORD AREN'T USED
	MOVE G,LCORE
	LSH G,12
	SUBI G,1
	CAMGE C,G
	JRST MBINI2
	TLZ F,GCF
	EXPAND WS
	JRST MBINI1
MBINI2:	MOVEI A,MBLGTH+2
	HRRZ B,WTOP
	ADDI B,1
	HRLI B,010700+W
	MOVEM A,@B
	ADDM A,WTOP
	AOS WTOP
	MOVEM B,MBUFP	;MBUFP HOLDS THE BASE OF THE AREA
	JRST COMEX


VOICES:	PUSHJ P,NUMVAL	;SET THE NUMBER OF VOICES
	CAILE E,
	CAILE E,4
	ERROR MBVOC
	MOVEM E,NVOICE
	JRST COMEX

OLDMUS:	MOVEI A,143
	MOVEM A,VOICEC
	SETOM MUSONC
	SETOM TTYONC
	JRST COMEX

NEWMUS:	MOVEI A,143
	MOVEM A,VOICEC
	MOVEI A,141
	MOVEM A,MUSONC
	MOVEI A,142
	MOVEM A,TTYONC
	MOVEI A,143
	MOVEM A,BOTHOC
	JRST COMEX


MBCNT:	MOVE B,MBPOS	;OUTPUT THE CURRENT BUFFER COUNT
	ROT B,6
	ANDI B,77	;GET THE BYTE POSITION
	IDIVI B,6	;CHARACTERS ARE 6 BITS LONG
	HRRZ A,MBPOS
	IMULI A,6	;6 BYTES TO THE WORD
	SUB A, B	;THIS WORKS SINCE BUFFER BEGINS AT WORD #1, NOT 0
	JRST NUMRET


;MOVE AHEAD ARG NUMBER OF PLACES IN MUSIC BUFFER

MBNXT:	PUSH	P,[COMEX]	;SO THAT THE RETURN CAN BE VIA A POPJ
	PUSHJ P,NUMVAL	;MOVE BUFFER TO NEXT POSITION
	JUMPL E,[ERROR MBFOR]
MBNXT0:	MOVE	B,E	;(VOICE ENTERS HERE)
MBNXT1:	MOVE A,E
	IDIVI A,6	;CALCULATE # OF WORDS MBPOS IS BEING MOVED
	ADDM A,MBPOS	;MBPOS IS BYTE POINTER TO PLACE IN MUSIC BUFFER
	AOJA B, MBNXT3	;B IS REMAINDER FROM DIVIDE ABOVE

MBNXTX:	MOVE	B,N	;NUMBER OF VOICES (SING ENTERS HERE)
MBNXT2:	JUMPE B,MBPUT9	;ARG OF 0 HAS NO EFFECT
	IBP MBPOS
MBNXT3:	HRRZ A,MBPOS
	CAILE A, MBLGTH	;IS THE BUFFER FULL?
	ERROR MBFULL
	SOJA B,MBNXT2


;PUT A NOTE INTO THE MUSIC BUFFER

MBPUT:	PUSH	P,[COMEX]	;SO RETURN CAN BE VIA POPJ
	PUSHJ P,NUMVAL
MBPUTX:	CAIL E,	;(SING ENTERS HERE THE FIRST TIME)
	CAILE E,63.	;ALLOWABLE RANGE FOR NOTES
	ERROR MBNOTE
MBPUTY:	MOVEI A,@MBUFP	;(SING ENTERS HERE AFTER FIRST TIME)
	ADD A,MBPOS
	DPB E,A
	SKIPN MBMAX
	AOS MBMAX	;USED AS FLAG TO INDICATE THERE IS SOMETHING IN THE BUFFER
MBPUT9:	POPJ	P,


;OUTPUT THE BUFFER TO THE MUSIC BOX

MBOUT:	PUSH P,[COMEX]
MBOUTX:	MOVE C,BOTHOC	;OUTPUT THE BUFFER TO THE MUSIC BOX
	PUSHJ P,TYOII	;TURN BOTH OFF
	MOVE C,VOICEC
	ADDI C,4
	PUSHJ P,TYOII	;SET NUMBER OF VOICES.
	MOVEI D,5		;IT TAKES 5
MBOUT1:	MOVEI C,40		;SILENT NOTES
	PUSHJ P,TYOII		;TO SHUT UP
	SOJG D,MBOUT1		;ALL THE VOICES.
	MOVE C,VOICEC
	ADD C,NVOICE
	PUSHJ P,TYOII
	MOVEI C,40
	PUSHJ P,TYOII
	MOVE C,MUSONC
	PUSHJ P,TYOII
	HRRZ D,MBPOS
	CAMLE D,MBMAX
	MOVEM D,MBMAX	;IF ERROR OCCURED, MAKE MAX TAKE ACCOUNT OF CURRENT POSITION
	MOVEI	D,@MBUFP
	HRLZI B,604	;BYTE POINTER INDEXED ON D TO BE INCREMENTED
MBOUT2:	TLZE	F,BREAKF	;HAS ^G BEEN TYPED?
	ERROR BREAK
	ILDB C,B
	ADDI C,40
	PUSHJ P,TYOII
	HRRZ A,B
	CAMG A,MBMAX
	JRST MBOUT2
	MOVN A,NVOICE
	MOVE C,TTYONC
	JRST TYOII	;TURN TELETYPE BACK ON

MBCLR:	PUSH P,[COMEX]
MBCLRX:	SETZM MBMAX	;CLEAR BUFFER
	SETZM MBPOS
	MOVEI A,MBLGTH	;LENGTH OF MUSIC BUFFER, IN WORDS
	MOVEI D,@MBUFP
	ADD A,D	;NOW A CONTAINS ADDRESS OF END OF BUFFER
MBCLR2:	ADDI	D,1
	SETZM	@D
	CAILE	A,@D
	JRST MBCLR2
	JRST MBSTRX

MBSTRT:	PUSH P,[COMEX]
MBSTRX:	HRRZ A,MBPOS	;START BACK AT BEGINNING OF BUFFER FOR NEW VOICE
	CAILE A,MBLGTH
	MOVEI A,MBLGTH
	CAMLE A,MBMAX
	MOVEM A,MBMAX	;CHECK FOR NEW MAXIMUM
	HRLZI A,600
	IBP A
	MOVEM A,MBPOS	;SET UP BYTE POINTER
	POPJ P,


;SING A SINGLE NOTE AND DURATION

	PUSHJ	P,REVPR	;ENTERS HERE FOR MULTIPLE ARGUMENTS
SING:	SKIPN	MBUFP	;CHECK TO SEE THAT STARTMUSIC HAS BEEN RUN
	ERROR MBNOTI
	PUSHJ	P,NUMVAL	;GET THE DURATION IN E
	JUMPG	E,SING1	;DURATION IS > 0
	JUMPE	E,SING10	;IF 0, DO NOTHING
	ERROR MBDUR	;NEGATIVE DURATIONS AREN'T ALLOWED
SING1:	MOVE	M,E	;SAVE DURATION IN M
	PUSHJ	P,NUMVAL	;GET PITCH IN E
	ADDI	E,25.	;I.E. 0 CORRESPONDS TO 25 (C)
	MOVE	N,NVOICE	;NUMBER OF VOICES
	PUSHJ	P,MBPUTX	;PUT E IN THE MUSIC BUFFER
	PUSHJ	P,MBNXTX	;INCREMENT POSITION IN BUFFER BY N BYTES
	SOJLE	M,SING9	;DEC DURATION.  DONE IF 0 NOW.
	SOJLE	M,SING3	;DEC DURATION.  IF 0, THEN DUR WAS 2.
	CAIG	E,2	;IS THE NOTE BOOM OR GRITCH (OR REST)
	MOVEI	E,0	;IF IT IS, ONLY PLAY ONE PER NOTE
SING2:	PUSHJ	P,MBPUTY	;PUT THE NOTE AGAIN
	PUSHJ	P,MBNXTX	;INCREMENT POSITION IN BUFFER
	SOJG	M,SING2	;LOOP BACK AND PUT THE NOTE AGAIN
SING3:	MOVEI	E,0	;TO GENERATE SILENCE
	PUSHJ	P,MBPUTY	;INSERT SILENCE AFTER THE NOTE
	PUSHJ	P,MBNXTX	;INCREMENT POSITION IN BUFFER

SING9:	JRST	COMEX	;RETURN

;DURATION WAS 0.  POP  ONE ARG FROM STACK. THEN RETURN
SING10:	SOJA	S,COMEX
]
]