	;VECTOR OPERATIONS.
VECTRP:	PUSHJ	P,VEKTRP
	JRST	ISTRUE
	JRST	ISFALSE


VEKTRP:	MOVE	A,(S)
	TLNN	A,SENTF
	JRST	NOTVEK
	PUSH	S,A
	PUSHJ	P,COUNT
	POP	S,A
	HRLI	A,010700+W
	ILDB	C,A
	CAIE	C,"2
	JRST	NOTVEK
	ILDB	C,A
	JUMPN	C,NOTVEK
	PUSH	S,(S)
	PUSHJ	P,FIRST
	PUSHJ	P,VEKNUM
	JRST	NOTVEK
	PUSH	S,(S)
	PUSHJ	P,LAST
	PUSHJ	P,VEKNUM
	JRST	NOTVEK
	AOS	(P)
NOTVEK:	POP	S,A
	POPJ	P,


VEKNUM:	POP	S,A
	TLNE	A,SENTF+EMPTYF
NOVNUM:	POPJ	P,
	HRLI	A,010700+W
	TRZ	F,TF
	ILDB	C,A
	JUMPE	C,NOVNUM
	SETZ	D,
	CAIE	C,"+
	CAIN	C,"-
VKNM.1:	ILDB	C,A
	JUMPE	C,VKNM.2
	CAIL	C,"0
	CAILE	C,"9
	JRST	.+2
	JSP	D,VKNM.1
	CAIE	C,".
	JRST	NOVNUM
	TRON	F,TF
	JRST	VKNM.1
	JRST	NOVNUM
VKNM.2:	JUMPE	D,NOVNUM
	AOS	(P)
	POPJ	P,


VSUB:	MOVE	A,(S)
	PUSH	S,A
	PUSHJ	P,VEKTRP
	ERROR	NOTVEC
	MOVE	A,(S)
	PUSH	S,A
	PUSHJ	P,FIRST
	PUSHJ	P,VNEGAT
	MOVE	A,(S)
	EXCH	A,-1(S)
	MOVEM	A,(S)
	PUSHJ	P,LAST
	PUSHJ	P,CONS
	JRST	VADD


VNEGAT:	PUSHJ	P,NEWSTR
	POP	S,A
	HRLI	A,010700+W
	HRRZI	D,"-
	ILDB	C,A
	CAIN	C,"+
	JRST	VNEG.1
	CAIE	C,"-
	JRST	VNEG.4
	HRRZI	D,"+
VNEG.1:	IDPB	D,B
VNEG.2:	ILDB	C,A
	JUMPE	C,ENDSTR
VNEG.3:	IDPB	C,B
	JRST	VNEG.2
VNEG.4:	IDPB	D,B
	JRST	VNEG.3


VECTOR:	PUSH	S,(S)
	PUSH	S,-2(S)
	PUSHJ	P,VEKNUM
	ERROR	.
	PUSHJ	P,VEKNUM
	ERROR	.
	JRST	CONS


VADD:	MOVE	A,-1(S)
	PUSH	S,A
	PUSHJ	P,VEKTRP
	ERROR	NOTVEC
	MOVE	A,(S)
	PUSH	S,A
	PUSHJ	P,VEKTRP
	ERROR	NOTVEC
	MOVE	A,(S)
	EXCH	A,-1(S)
	MOVEM	A,(S)
	PUSH	S,A
	PUSHJ	P,FIRST
	MOVE	A,(S)
	EXCH	A,-1(S)
	MOVEM	A,(S)
	PUSHJ	P,LAST
	MOVE	A,-1(S)
	EXCH	A,-2(S)
	EXCH	A,(S)
	MOVEM	A,-1(S)
	PUSH	S,(S)
	PUSHJ	P,FIRST
	MOVE	A,(S)
	EXCH	A,-1(S)
	MOVEM	A,(S)
	PUSHJ	P,LAST
	PUSHJ	P,VNCNVF
	PUSH	P,A
	PUSHJ	P,VNCNVF
	PUSH	P,A
	PUSHJ	P,VNCNVF
	PUSH	P,A
	PUSHJ	P,VNCNVF
	PUSH	P,A
	MOVE	A,-3(P)
	PUSHJ	P,SIND		;SIN(A2)
	FMPR	A,-2(P)
	PUSH	P,A
	MOVE	A,-2(P)
	PUSHJ	P,SIND
	FMPR	A,-1(P)
;FALLS THROUGH

;FALLS IN
	FADRB	A,(P)		;VERTICAL COMPONENT
	MOVMS	A
	FSBR	A,[0.00001]
	JUMPG	A,.+2
	SETZM	(P)
	MOVE	A,-4(P)
	PUSHJ	P,COSD
	FMPR	A,-3(P)
	PUSH	P,A
	MOVE	A,-3(P)
	PUSHJ	P,COSD
	FMPR	A,-2(P)
	FADRB	A,(P)		;HORIZONTAL COMPONENT
	MOVMS	A
	FSBR	A,[0.00001]
	JUMPG	A,VADD.1
	SETZM	(P)
	SETZ	A,
	SKIPN	-1(P)
	JRST	VADD.2
	MOVE	A,[90.0]
	SKIPG	-1(P)
	MOVNS	A
	JRST	VADD.2
VADD.1:	MOVE	A,-1(P)
	FDVR	A,(P)
	PUSHJ	P,ATAN
	MOVMS	A
	MOVE	B,(P)
	XOR	B,-1(P)
	TLNE	B,400000
	MOVNS	A
	SKIPG	(P)
	FADR	A,[180.0]
	JUMPGE	A,VADD.2
	FADR	A,[360.0]
VADD.2:	PUSH	P,A
	MOVE	A,-1(P)
	FMPRM	A,-1(P)
	MOVE	A,-2(P)
	FMPR	A,-2(P)
	FADR	A,-1(P)		;R3 ** 2
	PUSHJ	P,SQRT
	POP	P,B
	SUB P,[6,,6]
	PUSH	P,B
	PUSHJ	P,VNMRET
	POP	P,A
	PUSHJ	P,VNMRET
	JRST	CONS


ATAN:	PUSH	P,B
ATAN1:	MOVM	B,A
	CAMG	B,[145000,,0]
	JRST	AT5
	PUSH	P,D
	HLLO	D,A
	CAML	B,[233000,,0]
	JRST	AT4
	PUSH	P,C
	MOVSI	C,201400
	CAMG	B,C
	TRZA	D,-1
	FDVM	C,B
	TLC	D,(D)
	PUSH	P,B
	FMP	B,B
	MOVE	C,[1.448631538]
	FAD	C,B
	MOVE	A,[-0.2647686202]
	FDVM	A,C
	FAD	C,B
	FAD	C,[3.316335425]
	MOVE	A,[-7.106760045]
	FDVM	A,C
	FAD	C,B
	FAD	C,[6.762139240]
	MOVE	A,[3.709256262]
	FDV	A,C
	FAD	A,[0.1746554388]
	FMP	A,(P)
	TRNE	D,-1
	FSB	A,[1.570796]
	POP	P,C
	POP	P,C
	JRST	.+2
AT4:	MOVE	A,[1.570796]
	SKIPGE	D
	MOVNS	A
	POP	P,D
AT5:	FDVR	A,[0.017453292]
	POP	P,B
	POPJ	P,



VNCNVF:	MOVE	D,[1.0]
	MOVE	B,[0.0]
	POP	S,A
	TLNE	A,EMPTYF
	JRST	VCVF.5
	HRLI	A,010700+W
	ILDB	C,A
	CAIN	C,"+
	JRST	VCVF.2
	CAIE	C,"-
	JRST	VCVF.1
	MOVE	D,[-1.0]
	JRST	VCVF.2
VCVF.1:	JUMPE	C,VCVF.5
	CAIN	C,".
	JRST	VCVF.3
	FMPR	B,[10.0]
	FADR	B,FPDIGT-"0(C)
VCVF.2:	ILDB	C,A
	JRST	VCVF.1
VCVF.3:	MOVE	E,[10.0]
VCVF.4:	ILDB	C,A
	JUMPE	C,VCVF.5
	MOVE	C,FPDIGT-"0(C)
	FDVR	C,E
	FADR	B,C
	FMPR	E,[10.0]
	JRST	VCVF.4
VCVF.5:	FMPR	B,D
	MOVE	A,B
	POPJ	P,


VNMRET:	TRZ	F,TF
	TLNN	A,400000
	JRST	VNRT.1
	TRO	F,TF
	MOVE	B,[0.0]
	FSBRB	B,A
VNRT.1:	PUSH	P,A
	PUSHJ	P,NEWSTR
	HRRZI	C,"-
	TRZE	F,TF
	IDPB	C,B
	SETZ	G,
	CAMGE	A,[1.0]
	JRST	VNRT.7
	PUSHJ	P,VFIX
	JUMPN	A,.+2
	ERROR	.
	MOVE	D,A
	TLC	A,232000
	FADR	A,A
	EXCH	A,(P)
	FSBR	A,(P)
	EXCH	A,(P)
	MOVE	A,[10000000000.]
	PUSH	P,B
VNRT.2:	IDIV	D,A
	JUMPN	D,VNRT.3
	IDIVI	A,10.
	EXCH	D,E
	JUMPG	A,VNRT.2
	ERROR	.
VNRT.3:	POP	P,B
	TRO	F,TF
	HRRZI	C,"0(D)
	IDPB	C,B
	HRRZI	G,1(G)
	MOVE	D,E
	CAIG	A,1
	JRST	VNRT.4
	PUSH	P,B
	IDIVI	A,10.
	IDIV	D,A
	JRST	VNRT.3
VNRT.4:	CAIL	G,8.
	JRST	VNRT.6
	HRRZI	C,".
	IDPB	C,B
VNRT.5:	MOVE	A,[10.0]
	FMPRB	A,(P)
	PUSHJ	P,VFIX
	HRRZI	C,"0(A)
	TLC	A,232000
	FADR	A,A
	EXCH	A,(P)
	FSBR	A,(P)
	EXCH	A,(P)
	IDPB	C,B
	CAIE	C,"0
	TROA	F,TF
	TRNE	F,TF
	HRRZI	G,1(G)
	MOVE	A,(P)
	CAIG	G,8.
	JUMPG	A,VNRT.5
VNRT.6:	POP	P,A
	JRST	ENDSTR
VNRT.7:	HRRZI	C,"0
	IDPB	C,B
	CAME	A,[0.0]
	JRST	VNRT.4
	JRST	VNRT.6


FPDIGT:	0.0
	1.0
	2.0
	3.0
	4.0
	5.0
	6.0
	7.0
	8.0
	9.0


VFIX:	JUMPE C,CPOPJ
	PUSH	P,B
	MULI	A,400
	TSC	A,A
	ASH	B,-243(A)
	MOVE	A,B
	SKIPG	(P)
	AOS	A
	POP	P,B
	POPJ	P,

SCALRM:	MOVE	A,-1(S)
	PUSH	S,A
	PUSHJ	P,VEKNUM
	ERROR	BDSCLR
	MOVE	A,(S)
	PUSH	S,A
	PUSH	S,A
	PUSHJ	P,VEKTRP
	ERROR	NOTVEC
	PUSHJ	P,FIRST
	MOVE	A,(S)
	EXCH	A,-1(S)
	MOVEM	A,(S)
	MOVE	A,-2(S)
	PUSH	S,A
	PUSHJ	P,VNCNVF
	PUSH	P,A
	MOVE	A,-1(S)
	PUSH	S,A
	PUSHJ	P,VNCNVF
	FMPR	A,(P)
	PUSHJ	P,VNMRET
	POP	S,A
	POP	S,B
	MOVEM	A,-1(S)
	MOVEM	B,(S)
	POP	P,A
	PUSHJ	P,LAST
	JRST	CONS


DOTPRD:	PUSH	S,-1(S)
	PUSH	S,-1(S)
	PUSHJ	P,VEKTRP
	ERROR	NOTVEC
	PUSHJ	P,VEKTRP
	ERROR	NOTVEC
	MOVE	A,-1(S)
	EXCH	A,(S)
	PUSH	S,A
	PUSH	S,A
	PUSHJ	P,FIRST
	PUSHJ	P,VNCNVF
	PUSH	P,A
	PUSHJ	P,LAST
	PUSHJ	P,VNCNVF
	PUSH	P,A
	PUSHJ	P,FIRST
	PUSHJ	P,VNCNVF
	FMPRM	A,-1(P)
	PUSHJ	P,LAST
	PUSHJ	P,VNCNVF
	FSBR	A,(P)
	PUSHJ	P,COSD
	FMPR	A,-1(P)
	POP	P,B
	POP	P,B
	JRST	VNMRET
STEPNT:	PUSH	S,-1(S)
	PUSHJ	P,POINTP
	ERROR	BADPNT
	PUSH	S,-1(S)
	PUSH	S,-2(S)
	PUSHJ	P,FIRST
	MOVE	A,(S)
	EXCH	A,-1(S)
	MOVEM	A,(S)
	PUSHJ	P,LAST
	PUSHJ	P,VNCNVF
	PUSH	P,A		;Y0
	PUSHJ	P,VNCNVF
	PUSH	P,A		;X0
	MOVE	A,(S)
	MOVEM	A,-1(S)
	PUSHJ	P,VEKTRP
	ERROR	NOTVEC
	PUSH	S,(S)
	PUSHJ	P,LAST
	PUSHJ	P,VNCNVF
	PUSH	P,A		;ANGLE
	PUSHJ	P,FIRST
	PUSHJ	P,VNCNVF
	PUSH	P,A		;RADIUS
	MOVE	A,-1(P)
	PUSHJ	P,COSD
	FMPR	A,(P)
	FADRM	A,-2(P)		;NEW X
	MOVE	A,-1(P)
	PUSHJ	P,SIND
	FMPR	A,(P)
	FADRM	A,-3(P)		;NEW Y
	POP	P,B
	POP	P,B
	POP	P,A
	PUSHJ	P,VNMRET
	POP	P,A
	PUSHJ	P,VNMRET
	JRST	CONS



POINTP:	MOVE	A,(S)
	TLNN	A,SENTF
	JRST	NOTPNT
	PUSH	S,A
	PUSHJ	P,COUNT
	POP	S,A
	HRLI	A,010700+W
	ILDB	C,A
	CAIE	C,"2
	JRST	NOTPNT
	ILDB	C,A
	JUMPN	C,NOTPNT
	PUSH	S,(S)
	PUSHJ	P,FIRST
	PUSHJ	P,VEKNUM
	JRST	NOTPNT
	PUSH	S,(S)
	PUSHJ	P,LAST
	PUSHJ	P,VEKNUM
	JRST	NOTPNT
	AOS	(P)
NOTPNT:	POP	S,A
	POPJ	P,


GENVEC:	PUSH	S,-1(S)
	PUSH	S,-1(S)
	PUSHJ	P,POINTP
	ERROR	BADPNT
	PUSHJ	P,POINTP
	ERROR	BADPNT
	PUSH	S,(S)
	PUSHJ	P,FIRST
	PUSHJ	P,VNCNVF
	PUSH	P,A
	PUSHJ	P,LAST
	PUSHJ	P,VNCNVF
	PUSH	P,A		;Y2
	PUSH	S,(S)
	PUSHJ	P,FIRST
	PUSHJ	P,VNCNVF
	FSBRM	A,-1(P)		;X1-X2
	PUSHJ	P,LAST
	PUSHJ	P,VNCNVF
	FSBRB	A,(P)		;Y1-Y2
	FMPR	A,(P)		;DY**2
	MOVE	B,-1(P)
	FMPR	B,-1(P)
	FADR	A,B
	PUSHJ	P,SQRT
	PUSHJ	P,VNMRET
	POP	P,A
	POP	P,B
	SKIPN	B
	MOVE	A,[100000.0]
	SKIPE	B
	FDVR	A,B
	PUSHJ	P,ATAN
	PUSHJ	P,VNMRET
	JRST	CONS
IFN NOVA,[
SLIDE:	MOVE A,NEWX
	PUSHJ	P,VNMRET
	MOVE	A,NEWY
	PUSHJ	P,VNMRET
	PUSHJ	P,CONS
	MOVE	A,(S)
	EXCH	A,-1(S)
	MOVEM	A,(S)
	PUSHJ	P,STEPNT
	PUSH	S,(S)
	PUSHJ	P,FIRST
	MOVE	A,(S)
	EXCH	A,-1(S)
	MOVEM	A,(S)
	PUSHJ	P,LAST
	POP	S,A
	PUSHJ	P,VNMFIX
	EXCH	A,(S)
	PUSHJ	P,VNMFIX
	PUSH	S,A
	JRST	SETXY
]


VNMFIX:	HRLI	A,010700+W
	PUSHJ	P,NEWSTR
VNFX.1:	ILDB	C,A
	JUMPE	C,VNFX.2
	CAIN	C,".
	JRST	VNFX.2
	IDPB	C,B
	JRST	VNFX.1
VNFX.2:	PUSHJ	P,ENDSTR
	POP	S,A
	POPJ	P,
]		;END OF LFLAG=1 CONDITIONAL
