TITLE BUBBLING ASSQ

;;; BSSQ IS LIKE ASSQ, BUT IF IT FINDS A PAIR IT BUBBLES IT
;;; TOWARD THE FRONT OF THE A-LIST BY DOING TWO RPLACA'S.

.FASL
.INSRT SYS:.FASL DEFS

.ENTRY BSSQ SUBR 0003		;2 ARGS
BSSQ:	MOVS C,(B)		;WORKS FOR SECOND ARG = NIL!
	HLRZ T,(C)
	CAIN T,(A)
	 JRST BSSQ7
BSSQ0:	HLRZ C,C
	JUMPE C,BSSQ7
	MOVS AR1,(C)
	HLRZ T,(AR1)
	CAIN T,(A)
	 JRST BSSQ2
	HLRZ AR1,AR1
	JUMPE AR1,BSSQ8
	MOVS B,(AR1)
	HLRZ T,(B)
	CAIN T,(A)
	 JRST BSSQ4
	HLRZ B,B
	JUMPE B,BSSQ9
	MOVS C,(B)
	HLRZ T,(C)
	CAIE T,(A)
	 JRST BSSQ0
	HLRZ T,(AR1)
	HRLM C,(AR1)
	HRLM T,(B)
BSSQ7:	MOVEI A,(C)
	POPJ P,

BSSQ2:	HLRZ T,(B)
	HRLM AR1,(B)
	HRLM T,(C)
BSSQ8:	MOVEI A,(AR1)
	POPJ P,

BSSQ4:	HLRZ T,(C)
	HRLM B,(C)
	HRLM T,(AR1)
BSSQ9:	MOVEI A,(B)
	POPJ P,

FASEND
