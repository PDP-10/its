TITLE FLIGHT SIM.

A=1
B=2
C=3
D=4
X=5
Y=6
I=7
J=10
P=11

LC%ES==12
LP%IF==13
ZP%MD==14
Z%IN==12

U=15
V=16
W=17

IMD==6
ZMD==0
PMD==1
VMD==4
VCMD==5
CMD==3
IMX=574
OMX==570
IORSET=633550
DISCH==5
SDISCH==4
TTCH==6
CLCH==2
SISET==100
PDS==100
DDT=34000
DSIS==3000
MXMEM==12100+CLCH

FOOF==1


; UUO DEFINEINGS

PRNT=001000,,0
SAC=002000,,0
UAC=003000,,0
WATE=004000,,0	;UNUSED UUO
VAAX=005000,,0	;UNUSED UUO
MACDMP=37400
R==V
S==J

Z%SC==U
Z%ST==W

IMD==6
ZMD==0
PMD==1
VMD==4
CMD==3

SIXTH==252
PDS==300
MX==1
TEN==2
IMX==574
STDS==2440
MVDS==3000

DEFINE 4PUT L
IRPS Q,,A B C D,R,,L
	MOVEI 0,R
	JSP Q,(Q)
TERMIN
TERMIN
.OPEN=.OPEN
.CLOSE=.CLOSE
.DSTART=.DSTART
.DSTOP=.DSTOP
.DCLOCE=.DCLOSE
.IOT=.IOT
.SLEEP=.SLEEP
.SUSET=.SUSET
.DISMIS=.DISMIS
.CORE=.CORE

	LOC 41
	JSR UUOH

	LOC 40+2*TTCH
	JSR TTYBK

	LOC 40+2*CLCH
	JSR CLOCK

	LOC 40+2*DISCH
	BLKO DIS,BLKOP
	JRST 4,.

	LOC 40+2*SDISCH
	JSR DISIN
	JRST 4,.
LOC 100


GO:	CONO IORSET+CLCH
	CONO 460,0
	CONO TTY,TTCH
	MOVE A,[JSR UUOH]
	MOVEM A,41
	MOVE P,[-PDS,,PDL-1]
	PUSHJ P,DSTOP

	SETZM REMX
	MOVE A,[50.0]
	MOVEM A,REMY
	MOVE A,NODEG
	PUSHJ P,ANGXY
	MOVEM A,SINA
	MOVEM B,SINB
	MOVE A,[360.0]
	FDVR A,NODEG
	PUSHJ P,FIX
	MOVEM A,NOLINS

	CONO 420,40
	SETZM COUNT'
	SETOM TTYOFF'
	SETOM ONCE'
	SETOM ONCE1'
	SETZM TRACE'
	MOVEI A,SISET-1
	MOVEM A,INPNTR
	MOVEM A,OTPNTR
	CONO PI,12276

	MOVEI A,XTAB
	MOVEM A,MACDMP+0
	MOVEI A,YTAB
	MOVEM A,MACDMP+1
	MOVEI A,ZTAB
	MOVEM A,MACDMP+2
	MOVEI A,XTAB1
	MOVEM A,MACDMP+3
	MOVEI A,YTAB1
	MOVEM A,MACDMP+4
	MOVEI A,ZTAB1
	MOVEM A,MACDMP+5
IDL:	JRST TEST1

TIME:	0

TEST1:	MOVE P,[-PDS,,PDL-1]	; GO FLY
	PUSHJ P,DSTOP
	MOVSI A,-6
	SETZM ROL(A)
	AOBJN A,.-1
	MOVE A,[1000.0]
	MOVEM A,XE

	MOVEI A,3777
	MOVEM A,EVLI
	PUSHJ P,SETUPF
	MOVE A,TIME
	PUSHJ P,FLT
	MOVEM A,LTIME
RELIN1:	PUSHJ P,SWBP
	PUSHJ P,FLY
	PUSHJ P,MVINS
	PUSHJ P,MAIRP

	PUSHJ P,DCRFT
	JRST RELIN1

TEST:	MOVE P,[-PDS,,PDL-1]	; RANDOME POSISHIONING
	PUSHJ P,DSTOP
	SETZM DZON
	MOVSI A,-6
	SETZM ROL(A)
	AOBJN A,.-1
	MOVSI A,(1000.0)
	MOVEM A,XE

RELIN:	PUSH P,U
	PUSH P,V
	PUSH P,W
	PUSH P,I
	PUSH P,J

	PUSHJ P,SWBP
	DATAI 420,B

	SETZM A
	TLNN B,100000
	MOVN A,TIPA
	TLNN B,40000
	MOVE A,TIPA
	FADRM A,PCH

	MOVE A,[360.0]
	SKIPGE PCH
	FADRM A,PCH
	MOVE A,[-360.0]
	CAMLE A,[360.0]
	FADRM A,PCH

	SETZM A
	TLNN B,400000
	MOVE A,TIPA
	TLNN B,200000
	MOVN A,TIPA
	FADRM A,YAW

	MOVE A,[360.0]
	SKIPGE YAW
	FADRM A,YAW
	MOVE A,[-360.0]
	CAMLE A,[360.0]
	FADRM A,YAW

	TLNN B,20000
	SETOM RMONE
	TLNN B,20000
	JRST CONT
	SKIPE RMONE
	AOS A,GOING
	SETZM RMONE
	MOVE A,GOING
	CAIL A,4
	SETZB A,GOING
	SKIPN GOING
	JRST CONT
	CAIN A,2
	JRST CONT

	CAIN A,1
	MOVE B,[MOVE C,VEMC(B)]
	CAIN A,3
	MOVE B,[MOVN C,VEMC(B)]
	MOVEM B,PMI

	MOVSI B,-3
REMV:	MOVE A,MXTB
	SUBI A,3777
	PUSHJ P,FLT
	FMPR A,[0.005]
PMI:	JRST 4,.
	FMPR C,A
	FADRM C,XE(B)
	AOBJN B,REMV

CONT:	POP P,J
	POP P,I
	POP P,W
	POP P,V
	POP P,U

	PUSHJ P,DCRFT
	JRST RELIN

DCRFT:	PUSHJ P,TRNS
ITS:	TRN GETITS
	MOVEI A,WORLD
	PUSHJ P,GETPE
IFN FOOF,[	SETOM DOBJ
		MOVEI A,LFOO
		TRN P,GETPE
		SKIPL DOBJ
		SETZM FOOP]

	MOVEI A,RUNW
	PUSHJ P,GETPE
	MOVEI A,HEL
	TRN P,GETPE
DSENT:	MOVEI A,3000	;STOP DISPLAY
	IDPB A,BP
	PUSHJ P,DSTART
	POPJ P,


TIPA:	1.0
RMONE:	0
GOING:	0
FOOP:	77777

GETITS:	MOVEI A,WORLD1
	PUSHJ P,GETPE
	JRST DSENT
IFN FOOF,[LFOO:	AOS I,FOOP
	CAIG I,10
	POPJ P,
	SETZM DOBJ
	MOVE I,[200-BARIND,,200]
	JRST ILOOP]

WORLD1:	MOVSI I,-8
	JRST ILOOP
WORLD:	MOVSI I,-36
	JRST ILOOP
PYR:	MOVSI I,-8
ILOOP:	MOVE U,XTAB(I)
	MOVE V,YTAB(I)
	MOVE W,ZTAB(I)
	JSP A,(A)
ILOOP1:	MOVE U,XTAB1(I)
	MOVE V,YTAB1(I)
	MOVE W,ZTAB1(I)
	JSP A,(A)
	AOBJN I,ILOOP
	POPJ P,

GFROB:	MOVSI U,@GFSC
GFSC:	MOVSI W,236000
	MOVE V,W
	XOR V,U
	TLC V,236000
GFLP:	JSP A,(A)
	MOVSI V,40
	ADDB V,W
	XOR V,U
	TLC V,236000
	JSP A,(A)
	CAMGE W,[236740,,]
	JRST GFLP
	ADD U,[40,,]
	TLNE U,740
	JRST GFSC
	POPJ P,

RUNW:	MOVSI I,(MOVN V,(I))
	HLLM I,ILOOP+1
	HLLM I,ILOOP1+1
	MOVE I,[-14,,9]
	PUSHJ P,ILOOP
	MOVSI I,(MOVN U,(I))
	HLLM I,ILOOP
	HLLM I,ILOOP1
	MOVE I,[-10,,8+5]
	PUSHJ P,ILOOP
	MOVSI I,(MOVE V,(I))
	HLLM I,ILOOP+1
	HLLM I,ILOOP1+1
	MOVE I,[-10,,8+5]
	PUSHJ P,ILOOP
	MOVE I,[-1,,8]
	PUSHJ P,ILOOP
	MOVSI I,(MOVE U,(I))
	HLLM I,ILOOP
	HLLM I,ILOOP1
	POPJ P,

HEL:	SETZM W
	MOVE I,NOLINS
HELBK:	SETZB U,V
	JSP A,(A)
	MOVE U,REMX
	MOVE V,REMY
	JSP A,(A)
	FMPR U,SINB
	FMPR V,SINA
	FADR U,V
	MOVEM U,SAVX
	MOVE U,REMX
	MOVE V,REMY
	FMPR V,SINB
	FMPR U,SINA
	FSBR V,U
	MOVE U,SAVX
	MOVEM U,REMX
	MOVEM V,REMY
	FADR W,AOSZ
	SOJG I,HELBK
	POPJ P,

NODEG:	13.5
AOSZ:	13.0

REMX:	0
REMY:	0
SINA:	0
SINB:	0
NOLINS:	0


TRNS:	MOVEM A,INDX'
	MOVSI C,-3	;GET SIN ROL PCH YAW
RESIN:	MOVE A,ROL(C)
	PUSHJ P,SIND
	MOVEM A,SINR(C)
	AOBJN C,RESIN

	MOVSI C,-3	;GET COS ROL PCH YAW
RECOS:	MOVE A,ROL(C)
	PUSHJ P,COSD
	MOVEM A,COSR(C)
	AOBJN C,RECOS

	MOVE A,COSY	;GET VECTOR EMC
	FMPR A,COSP
	MOVEM A,VEMC	;X COMPONENT
	MOVE A,SINY
	FMPR A,COSP
	MOVEM A,VEMC+1	;Y COMPONENT
	MOVE A,SINP
	MOVEM A,VEMC+2	;Z COMPONENT

	SETOM FTRN'	;GET VMC AND VMPC
	SETZM C
GETPRM:	MOVE A,SINY
	FMPR A,COSR
	MOVE B,COSY
	FMPR B,SINP
	FMPR B,SINR
	FSBR A,B
	MOVEM A,VMC(C)	; X COMP

	MOVN A,COSY
	FMPR A,COSR
	MOVE B,SINY
	FMPR B,SINP
	FMPR B,SINR
	FSBR A,B
	MOVEM A,VMC+1(C)	; Y COMP

	MOVE A,COSP
	FMPR A,SINR
	MOVEM A,VMC+2(C)	; Z COMP
	AOSE FTRN
	POPJ P,
	EXCH A,SINR
	EXCH A,COSR
	EXCH A,SINR
	MOVNS COSR
	MOVEI C,3
	JRST GETPRM


GETPE:	JSP A,(A)
	SETZM FPFLG'
	PUSHJ P,GETXYT
	MOVEM X,X1
	MOVEM Y,Y1
	JSP A,(A)
	PUSHJ P,GETXYT

	SKIPN B,FPFLG
	JRST WING
	CAIN B,2
	JRST GETPE
	SKIPGE D
	EXCH X,X1
	SKIPGE D
	EXCH Y,Y1
	FSBRM X,X1	; X1 Y1 IS BAD PNT
	FSBRM Y,Y1
	EXCH X,X1
	EXCH Y,Y1
	FSC X,33
	FSC Y,33
	EXCH X,X1
	EXCH Y,Y1
	FADRM X,X1
	FADRM Y,Y1

WING:	MOVEM A,SAVA1
	PUSHJ P,DVEC
	SKIPE DLIN
	SETOM DOBJ
	MOVE A,SAVA1
	JRST GETPE

SAVA1:	0
DOBJ:	0

GETXYT:	MOVE B,U
	FSBR B,XE
	MOVEM B,VPE
	FMPR B,VEMC
	MOVE C,V
	FSBR C,YE
	MOVEM C,VPE+1
	FMPR C,VEMC+1
	FADR B,C
	MOVE C,W
	FSBR C,ZE
	MOVEM C,VPE+2
	FMPR C,VEMC+2
	FADR B,C
	MOVSI D,(-1.0)
	JFCL 10,.+1
	FDVR D,B	; D CONTANES T
	JFCL 10,MUNGD
FMUNG:	SKIPGE D
	AOS FPFLG

	MOVE B,VPE
	FMPR B,VMC
	MOVE C,VPE+1
	FMPR C,VMC+1
	FADR B,C
	MOVE C,VPE+2
	FMPR C,VMC+2
	FADR B,C
	MOVE X,D
	FMPR X,B
	FDVR X,SCALE	; X CONTANES 340 X

	MOVE B,VPE
	FMPR B,VMPC
	MOVE C,VPE+1
	FMPR C,VMPC+1
	FADR B,C
	MOVE C,VPE+2
	FMPR C,VMPC+2
	FADR B,C
	MOVE Y,D
	FMPR Y,B
	FDVR Y,SCALE	; Y CONTANES 340 Y
	FADR X,[511.0]
	FADR Y,[511.0]
	POPJ P,

MUNGD:	ASH D,-12
	TLC D,300400
	JRST FMUNG
X1:	0
Y1:	0

ROL:	0
PCH:	0
YAW:	0

XE:	0
YE:	0
ZE:	0

SINR:	0
SINP:	0
SINY:	0

COSR:	0
COSP:	0
COSY:	0
VEMC:	BLOCK 3
VMC:	BLOCK 3	;VMC MUST FOLOW VMPC
VMPC:	BLOCK 3
VPE:	BLOCK 3
SCALE:	0.002
DVEC:	SETOM DLIN
	SETZM ONEON'
	JUMPL X,SECT
	CAMLE X,[1023.0]
	JRST SECT
	JUMPL Y,SECT
	CAMG Y,[1023.0]
	SETOM ONEON
SECT:	SKIPGE X1
	JRST NOTTWO
	MOVE A,[1023.0]
	CAMG A,X1
	JRST NOTTWO
	SKIPGE Y1
	JRST NOTTWO
	CAMG A,Y1
	JRST NOTTWO
	SKIPL ONEON
	JRST SECON
TWOON:	PUSHJ P,XYFIX
	EXCH X,X1
	EXCH Y,Y1
	PUSHJ P,XYFIX

NEWDV:	TRN TWOON1

	MOVEI ZP%MD,PMD
	MOVE Z%IN,UVIN
	PUSHJ P,ZRM
	MOVEI ZP%MD,VMD
	MOVEI LP%IF,0
	PUSHJ P,PNT
	MOVE A,X
	MOVE B,Y
REVEC:	MOVN X,A
	MOVN Y,B
	ADD X,X1
	ADD Y,Y1
	MOVM C,X
	MOVM D,Y
	CAILE C,177
	JRST TLONG
	CAILE D,177
	JRST TLONG
	MOVEI LC%ES,1
	MOVEI LP%IF,1
	JRST LIN

TLONG:	SETZM DOEX'
	CAMLE C,D
	SETOM DOEX
	SKIPE DOEX
	EXCH C,D
	IMULI C,376
	IDIV C,D
	AOS C
	ASH C,-1
	MOVEI D,177
	SKIPE DOEX
	EXCH C,D
	SKIPGE X
	MOVNS C
	SKIPGE Y
	MOVNS D
	MOVE X,C
	MOVE Y,D
	MOVEI LC%ES,0
	MOVEI LP%IF,1
	PUSHJ P,LIN
	ADD A,X
	ADD B,Y
	JRST REVEC

SECON:	MOVE C,X1
	MOVE D,Y1
	FSBR C,X
	FSBR D,Y
	MOVM ZP%MD,C
	MOVM B,D
	SUB ZP%MD,B
	SETZM INVRT
	MOVE A,X1
	MOVE B,Y1
	JUMPL ZP%MD,SECON1
	EXCH A,B
	EXCH C,D
	SETOM INVRT
SECON1:	MOVEM A,XSAV
	MOVEM B,YSAV
	JRST SQZVEC

NOTTWO:	SKIPL ONEON
	JRST NUNON
	MOVE C,X
	MOVE D,Y
	FSBR C,X1
	FSBR D,Y1
	MOVM A,C
	MOVM B,D
	SETZM INVRT
	CAMG B,A
	SETOM INVRT
	CAMG B,A
	EXCH X,Y
	CAMG B,A
	EXCH D,C
	MOVEM X,XSAV
	MOVEM Y,YSAV
	JRST SQZVEC
NUNON:	MOVE A,X
	MOVE B,Y
	FSBR A,X1
	FSBR B,Y1
	MOVMS A
	MOVMS B
	SETZM INVRT'
	CAMG B,A
	SETOM INVRT
	SKIPE INVRT
	EXCH X,Y
	SKIPN INVRT
	JRST .+4
	EXCH A,X1
	EXCH A,Y1
	EXCH A,X1

	MOVEI A,0
	PUSHJ P,GETX
	SKIPL B
	JRST FLGMIN
	MOVE A,[1023.0]
	PUSHJ P,GETX
	JUMPL B,DVLOS

FLGMAX:	MOVE C,A
	MOVE D,[1023.0]
	MOVEM C,XSAV'
	MOVEM D,YSAV'
	CAMG Y,Y1
	FSBR C,X
	CAMG Y,Y1
	FSBR D,Y
	CAMLE Y,Y1
	FSBR C,X1
	CAMLE Y,Y1
	FSBR D,Y1
	JRST SPOUT

FLGMIN:	MOVE C,A
	SETZM D
	MOVEM C,XSAV
	MOVEM D,YSAV
	CAML Y,Y1
	FSBR C,X
	CAML Y,Y1
	FSBR D,Y
	CAMGE Y,Y1
	FSBR C,X1
	CAMGE Y,Y1
	FSBR D,Y1
SPOUT:	SKIPN INVRT
	JRST SQZVEC
	EXCH X,Y
	EXCH A,X1
	EXCH A,Y1
	EXCH A,X1

SQZVEC:	MOVM A,D
	FDVR A,[-126.99]
	FDVR C,A
	FDVR D,A

	SKIPE INVRT
	EXCH C,D
	MOVE X,XSAV
	MOVE Y,YSAV
	SKIPE INVRT
	EXCH X,Y
	PUSHJ P,XYFIX
	MOVEI ZP%MD,PMD
	MOVE Z%IN,UVIN
	PUSHJ P,ZRM
	MOVEI ZP%MD,VCMD
	MOVEI LP%IF,0
	PUSHJ P,PNT
	MOVE X,C
	MOVE Y,D
	PUSHJ P,XYFIX
	MOVEI LC%ES,1
	MOVEI LP%IF,1
	JRST LIN

GETX:	MOVE B,Y1
	FSBR A,Y
	FSBR B,Y
	FDVR A,B
	SKIPGE A
	JRST SPLOS
	CAML A,[1.0]
	JRST SPLOS
	MOVE B,[1.0]
	FSBR B,A
	FMPR B,X
	FMPR A,X1
	FADR A,B
	SKIPGE A
	JRST SPLOS
	CAMLE A,[1023.0]
	JRST SPLOS
	SETZM B
	POPJ P,

SPLOS:	SETOM B
	POPJ P,

DVLOS:	SETZM DLIN
	POPJ P,

DLIN:	0
UVIN:	7

TWOON1:	MOVEI A,020150	;GO IN TO POINT MODE SCALE 2 INT 7
	IDPB A,BP
	SUB X,X1
	SUB Y,Y1
	ASH X,-1
	ASH Y,-1
	ADDM X,X1
	ADDM Y,Y1
	MOVMS X
	MOVMS Y
	MOVEI A,200000
	DPB X,[000700,,A]
	DPB Y,[100700,,A]
	MOVE X,X1
	MOVE Y,Y1
	TRO X,102000
	TRO Y,220000
	IDPB Y,BP
	IDPB X,BP
	IDPB A,BP
	TRC A,500200
	IDPB A,BP
	POPJ P,


MAIRP:	MOVE A,ALRTIP
	MOVEM A,ROL

	MOVE A,CMPS
	PUSHJ P,FLT
	FMPR A,[0.0879]
	MOVEM A,YAW

	MOVE A,EVLI
	PUSHJ P,FLT
	FMPR A,[0.0219]
	FSBR A,[45.0]
	MOVEM A,PCH

	MOVE A,WAIS
	PUSHJ P,FLT
	MOVE B,A
	MOVE A,YAW
	PUSHJ P,COSD
	FMPR A,B
	FMPR A,BUGMV
	FADRM A,XE
	MOVE A,YAW

	PUSHJ P,SIND
	FMPR A,B
	FMPR A,BUGMV
	FADRM A,YE

	MOVE A,ALTUD
	PUSHJ P,FLT
	FMPR A,[0.244]
	MOVEM A,ZE

	POPJ P,

BUGMV:	-0.003
DSTART:	PUSH P,A
	PUSH P,B
	MOVE B,WCHBP
	MOVEI A,DSL1-1
	TRNE B,1
	MOVEI A,DSL2-1
	MOVEM A,DSLP'
	SETZM DSTOPD
	CAME A,WUTDIS
	TRN .-1
	POP P,B
	POP P,A
CPOPJ:	POPJ P,

DSTOP:	SETOM DSTOPD'
	CONO DIS,100
	POPJ P,

DISIN:	0
	CONSO DIS,7
	JRST RELOD
	CONSO DIS,400	;2400 PIT PEN DEBUG FETCH
	JRST APLG

	SKIPE DZON
	JRST RELOD

	AOSG TICK
	JRST RELOD
	CONO DIS,0
	JRST 12,@DISIN

APLG:	CONO DIS,15200+SDISCH_3+DISCH
	JRST 12,@DISIN

RELOD:	MOVEM A,DSAVA

	SKIPL DZON
	JRST NORMD
	SETCMM ZOST
	MOVEI A,DSL0-1
	SKIPE ZOST

NORMD:	MOVE A,DSLP
	MOVEM A,BLKOP'
	MOVEM A,WUTDIS'
	CONO DIS,100+SDISCH_3+DISCH
	MOVE A,DSAVA
	JRST 12,@DISIN

SWBP:	PUSH P,A
	PUSH P,X
	PUSH P,Y
	AOS A,WCHBP'
	MOVE X,[442200,,DSL1]
	MOVE Y,[442200,,DSL2]
	MOVEM X,BP'
	TRNE A,1
	MOVEM Y,BP
	POP P,Y
	POP P,X
	POP P,A
	POPJ P,


TICK:	0
ZOST:	0
DSAVA:	0

OVER:	PUSH P,A	; OVER SKIPS WHEN DISL IS FULL
	PUSH P,B
	MOVE A,WCHBP
	MOVEI B,DSL1
	TRNE A,1
	MOVEI B,DSL2
	HRRZ A,BP
	SUB A,B
	CAIL A,MVDS-20
	AOS -2(P)
	POP P,B
	POP P,A
	POPJ P,
;FLOATING POINT SINE AND COSINE. 
;ANS IN A,B; COSQ GIVES COS OF LAST SIN ARG, -SIN OF LAST COS
SIND:	FMPR A,[.01745329251994]	;PI/180
	JRST SIN

COSD:	FMPR A,[.01745329251994]
COS:	FADR A,[1.5707963267]	;PI/2
SIN:	CAMG A,[164475536723]	;.00015148419(SAVES A CONSTANT. COULD USE .000211431983)
	CAMGE A,[-.0001]	;SMALLNESS OF ABS X PERMITS SIN X _ X
	JRST .+2
	POPJ P,	;AND THREATENS UNDERFLOW
	PUSH P,B
	FDVR A,[1.5707963267]	;PI/2
	MOVEM A,SINTM'
	MULI A,400
	TSC A,A	;CAML A,...SETZB B,SINTM WOULD PREVENT HUGE ANSWER FOR ARG > 2**27
	ASH B,-243(A)
	DPB B,[10100,,SINOB]
	MOVNS A,B
	ANDCMI A,1
	TLC A,232000
	FAD A,A
	FADRB A,SINTM
SINQ:	TRNN B,2
SINR1:	SKIPA B,SINTM
	MOVNS B,SINTM
	FMPR B,[164475536723]	;.00015148419
	FMP A,A
	FADR A,[574377353254]	;-4.0084431
	FMPR B,A
	FADR A,[574142162777]	;-6.4652405
	FMPR B,A
	FADR A,[202444705313]	;2.2881683
	FMPR A,A
	FADR A,[210547761702]	;179.98621
	FMPRB A,B
	POP P,B
	POPJ P,

COSQ:	MOVSI A,(1.0)
	SKIPL SINTM
	MOVNS A
	FADRB A,SINTM
SINOB:	SETCMI B,.
	JRST SINQ


;FLOATING POINT SQUARE ROOT FUNCTION

SQRT:	MOVEM B,BSAV1
	MOVMS A
	JUMPLE A,SQ2
	ASHC A,-33
	SUBI A,201
	ROT A,-1
	HRRM A,SQ1
	LSH A,-43
	ASH B,-10
	FSC B,177(A)
	MOVEM B,ST
	FMP B,S1(A)
	FAD B,S2(A)
	MOVE A,ST
	FDV A,B
	FAD B,A
	FSC B,-1
	MOVE A,ST
	FDV A,B
	FADR A,B
SQ1:	FSC A,0
SQ2:	MOVE B,BSAV1
	POPJ P,

S1:	0.8125
	0.578125
S2:	0.302734
	0.421875

ST: 0
BSAV1:	0

;FLOATING POINT ARCTANGENT, VERSION 1

ATAN:	MOVEM B,BSAV
	MOVM B,A
	CAMG B,A1
	JRST ATANX
	MOVEM A,C2
	CAML B,A2
	JRST AT4
	MOVEM C,C1
	MOVSI C,201400
	SETZM I1
	CAMG B,C
	AOSA I1
	FDVM C, B
	MOVEM B,C3
	FMP B, B
	MOVE C,KB3
	FAD C, B
	MOVE A,KA3
	FDVM A,C
	FAD C, B
	FAD C,KB2
	MOVE A,KA2
	FDVM A,C
	FAD C,B
	FAD C,KB1
	MOVE A,KA1
	FDV A,C
	FADR A,KB0
	FMPR A,C3
	MOVE B,A3
	SKIPN I1
	FSBRM B,A
	SKIPA C,C1
AT4:	MOVE A,A3
	SKIPGE C2
	MOVNS A
ATANX:	MOVE B,BSAV
	POPJ P,

A1:	145000000000
A2:	233000000000
A3:	201622077325
KB0:	0.1746554388
KB1:	6.762139240
KB2:	3.316335425
KB3:	1.448631538
KA1:	3.709256262
KA2:	-7.106760045
KA3:	-0.2647686202



C1:	0
C2:	0
C3:	0
I1:	0
BSAV:	0


TTYBK:	0
	PUSH P,A
	PUSH P,B

	CONSZ TTY,10
	JRST TYOINT
	CONSZ TTY,40
	JRST TYIINT
	JRST 4,0

TYIINT:	DATAI TTY,A
	ANDI A,177
TBACK:	MOVE B,COUNT
	CAIL B,SISET
	JRST .+2
	PUSHJ P,TYO
	MOVEM A,TYI
	XCT TTAB(A)
	JRST OUT1

TYOINT:	SKIPLE COUNT
	JRST .+4
	CONO TTY,200+TTCH
	SETOM TTYOFF'
	JRST OUT1
	MOVE A,OTPNTR
	DATAO TTY,TTYTAB(A)
	SOS COUNT
	SOSL OTPNTR
	JRST OUT1
	MOVEI A,SISET-1
	MOVEM A,OTPNTR

OUT1:	POP P,B
	POP P,A
	JRST 12,TTYBK@



TYO:	PUSH P,B
	MOVE B,COUNT
	CAIL B,SISET
	JRST .-2
	MOVE B,INPNTR
	MOVEM A,TTYTAB(B)
	AOS COUNT
	SOSL INPNTR
	JRST OUT
	MOVEI B,SISET-1
	MOVEM B,INPNTR

OUT:	AOSN TTYOFF
	CONO TTY,10+TTCH
	POP P,B
	POPJ P,


TTYW:	SKIPL TTYOFF
	JRST .-1
	POPJ P,

TYI:	0


CLOCK:	0
	PUSH P,A
	PUSH P,B
	PUSH P,C
	CONSZ 200000
	JRST PIDLOV
AOST:	CONSO 1000
	JRST SOMLOS
	AOS TIME
	SKIPE TRACE
	AOS TIME1'
	CONO 001000+CLCH

	MOVE A,[-3,,150]
XPT1:	DATAO IMX,A
	CONO IMX,0
	CONSO IMX,10
	JRST .-1
	DATAI IMX,B
	ASH B,1
	TLC B,232000
	FAD B,B
	MOVEM B,MXTB3-150(A)
	AOBJN A,XPT1

	MOVEI A,171
XPT2:	DATAO IMX,A
	CONO IMX,0
	CONSO IMX,10
	JRST .-1
	DATAI IMX,A
	ASH A,1
	PUSHJ P,FLT
	MOVEM A,MXTB3+3
	MOVE A,[-8,,70]
XINPUT:	DATAO IMX,A
	CONO IMX,0
	CONSO IMX,10
	JRST .-1
	DATAI IMX,B
	ASH B,1
	MOVEM B,MXTB-70(A)
	AOBJN A,XINPUT

TMOUT:	SKIPN TRACE
	JRST TMOUT1
	JFCL
	SETZM NOCAL'
	MOVSI B,-20
RECNT:	MOVE A,PTAB(B)
	AND A,[777000,,0]
	CAME A,[TRN 0]
	AOS NOCAL
	AOBJN B,RECNT
	MOVE B,NOCAL
	EXCH B,LNOCAL'
	SKIPG NOCAL
	PUSHJ P,PT1
	CAMN B,NOCAL
	JRST SAMEN
	SETOM ONCE
	SETOM ONCE1

SAMEN:	SKIPL ONCE
	JRST NORM
	MOVEI B,1770
	IDIV B,MULBG
	MOVN B,B
	HRLZ B,B
	PUSHJ P,PROUT
	AOBJN B,.-1
	JRST TMOUT1

NORM:	PUSHJ P,PROUT


TMOUT1:	SKIPGE DSTOPD
	JRST RELOUT

	SETOM TICK
	CONSO DIS,7
	CONO PI,4000+200_-<SDISCH>

RELOUT:	POP P,C
	POP P,B
	POP P,A
	JRST 12,@CLOCK

PROUT:	PUSH P,B
	MOVSI C,-20
REPR:	XCT PTAB(C)
	MOVE B,PTAB(C)
	AND B,[777000,,0]
	CAME B,[TRN 0]
	PUSHJ P,PT1
	AOBJN C,REPR
	POP P,B
	POPJ P,


PIDLOV:	CONO 400000
	PRNT [ASCII /PDL OVER FLOW
/]
	JRST 10,.+1
	PUSHJ P,TTYW
	JRST DDT

SOMLOS:	CONI A
	CONO 0
	PRNT [ASCII /NON X MEM OR SOME LOSS CONI A
/]
	JRST 10,.+1
	PUSHJ P,TTYW
	JRST DDT


PT3:	PUSHJ P,.+1
PT2:	PUSHJ P,.+1
PT1:	PUSH P,X
	PUSH P,Y
	SAC
	AOSN ONCE
	SETZM NOPTSS
	SKIPN NOPTSS
	PUSHJ P,SETUP
	MOVE X,TIME1
	IMUL X,MULBG
	ANDI X,1777
	MOVE Y,A
	ASH Y,-4
	ADDI Y,1000
	MOVEI ZP%MD,PMD
	MOVEI LP%IF,1
	PUSHJ P,PNT
	AOS X,NOPTSS'
NPBUG:	MOVEI Y,1770
	IDIV Y,MULBG
	IMUL Y,NOCAL
	CAML X,Y
	PUSHJ P,ZEROL
	UAC
	POP P,Y
	POP P,X
	POPJ P,

SETUP:	SKIPE NOPTSS
	POPJ P,
	SKIPN ONCE
	PUSHJ P,DSTOP
	SETZM WCHBP
	PUSHJ P,SWBP
	MOVEI ZP%MD,PMD
	MOVEI Z%IN,3
	PUSHJ P,ZRM
	POPJ P,

ZEROL:	SETZM NOPTSS
	MOVEI ZP%MD,ZMD
	MOVEI LP%IF,0
	PUSHJ P,PNT
	MOVEI X,3000
	IDPB X,BP
	AOSE ONCE1
	POPJ P,
	MOVEI X,DSL2-1
	MOVEM X,DSLP
	SETZM DSTOPD
	POPJ P,
	POPJ P,

MULBG:	3

IMXBK1:	0
	CONSO IMX,10
	JRST .-1
;	MOVEM A,IMXPTR
	MOVEI A,100
	SOJG A,.
	SETZM A
	LDB A,[301400,,MXTB1+0]
	MOVEM A,MXTB+0
	LDB A,[141400,,MXTB1+0]
	MOVEM A,MXTB+1
	LDB A,[001400,,MXTB1+0]
	MOVEM A,MXTB+2
	LDB A,[301400,,MXTB1+1]
	MOVEM A,MXTB+3
	LDB A,[141400,MXTB1+1]
	MOVEM A,MXTB+4
	LDB A,[001400,,MXTB1+1]
	MOVEM A,MXTB+5
	LDB A,[301400,,MXTB1+2]
	MOVEM A,MXTB+6
	LDB A,[141400,,MXTB1+2]
	MOVEM A,MXTB+7
	MOVE A,[-8,,MXTB1-1]
;	EXCH A,IMXPTR
;	CONO IMX,37760+IMXCH
	JRST 12,@IMXBK


IMXBK:	0
;	BLKI IMX,IMXPTR
	JRST REIMX
	JRST 12,@IMXBK
REIMX:	MOVEM A,ISAVA
	MOVE A,[-8,,MXTB-1]
;	MOVEM A,IMXPTR
	CONO IMX,12040
	MOVE A,ISAVA
	JRST 12,@IMXBK
DCBK:	0
;	MOVEM A,MXTBP
	AOS A,WDC'
	TRNE A,1
	JRST DCBK1
	MOVE A,[-8,,MXTB-1]
;	EXCH A,MXTBP
	CONO 764,21140
;	CONO DC,4160+DCCH
	JRST 12,@DCBK

DCBK1:	JRST 12,@DCBK
MXTB1:	BLOCK 8
ISAVA:	0

UUOH:	0
	PUSH P,A
	PUSH P,40
	PUSH P,UUOH
	LDB A,[330500,,40]
	XCT UUOTAB(A)
	POP P,UUOH
	POP P,40
	POP P,A
	JRST @UUOH


GETNO:	MOVE A,NO
	SETZM NO
	POPJ P,

;UUO RUTEENS


PRINT:	MOVSI A,440700
	HRR A,40
	MOVEM A,ULDBP'
PBACK:	MOVE A,COUNT
	CAIL A,SISET
	POPJ P,
	ILDB A,ULDBP
	CAIN A,"
	POPJ P,
	PUSHJ P,TYO
	JRST PBACK

SACR:	EXCH 12,SAV11'
	EXCH 13,SAV12'
	EXCH 14,SAV13'
	EXCH 15,SAV14'
	EXCH 16,SAV15'
	EXCH 17,SAV16'
	POPJ P,

UACR:	EXCH 12,SAV13'
	EXCH 13,SAV12'
	EXCH 14,SAV13'
	EXCH 15,SAV14'
	EXCH 16,SAV15'
	EXCH 17,SAV16'
	POPJ P,


WATER:	POPJ P,	;UNUSED UUO
VARUT:	POPJ P,	;UNUSED UUO

NVWAT:	HRRZ A,40
	CAML A,NVBUG
	JRST NVWAT
	POPJ P,

NVBUG:	30

;TTY RUTEENS

NUMBER:	MOVE B,NO'
	LSH B,3
	ADDI B,-60(A)
	MOVEM B,NO'
	POPJ P,

IDL1:	MOVE P,[-PDS,,PDL-1]
	JRST 10,IDL

FLSHIO:	CONO DIS,100
	CONO IMX,0
	DATAO 20,[-1]
	POPJ P,

GODDT:	POP P,B
	POP P,A
	CONO PI,400
	JRST DDT

PR:	CONO TTY,200+TTCH
	SETOM TTYOFF
	CONO PI,200
	JRST 12,@TTYBK
RODE==50000.0
VCSIS==400

XTAB:	1000.0	; PYRIMID (8 LINES)
	1064.0
	RODE
	1000.0
	1000.0
	1064.0
	1064.0
	1000.0

	100.0	; RUN WAY (13 LINES)
	100.0
	100.0
	0
	0
;	0
	50.0
	37.5
	62.5
	25.0
	50.0
	75.0
	100.0
	100.0

	100.0	; GLIDE SLOPE (1 LINES>

	-1000000.0	;HORIZON (4 LINES)
	+1000000.0
	+1000000.0
	-1000000.0

	500000.0	;MOUNTINS
	250000.0
	500000.0
	250000.0
	BLOCK VCSIS

YTAB:	00.0	; PIRMID
	-RODE
	64.0
	RODE
	00.0
	00.0
	64.0
	64.0

	-1700.0	; RUNWAY
	-1700.0
	-1200.0
	-50.0
	-450.0
;	-850.0
	-800.0
	-925.0
	-925.0
	-1050.0
	-1050.0
	-1050.0
	-1700.0
	-1500.0

	0	; GLIDE SLOPE

	-1000000.0	; HORIZON
	-1000000.0
	+1000000.0
	+1000000.0

	1000000.0	;MOUNTINS
	1000000.0
	-1000000.0
	-1000000.0

	BLOCK VCSIS

ZTAB:	REPEAT 10,0	; PIRMID
	REPEAT 15,0	; RUN WAY
	-200.0		; GLIDE SLOPE
	REPEAT 4,0	;HORIZON
	REPEAT 4,0	;MOUNTAINS

	BLOCK VCSIS

XTAB1:	RODE	; PIRMID
	1064.0
	1000.0
	1000.0
	1032.0
	1032.0
	1032.0
	1032.0

	100.0	;RUN WAY
	-100.0
	-100.0
	0
	0
;	0
	50.0
	37.5
	62.5
	25.0
	50.0
	75.0
	0
	0

	-100.0	; GLIDE SLOPE

	+1000000.0	; HORIZON
	+1000000.0
	-1000000.0
	-1000000.0

	375000.0	;MOUNTINS
	375000.0
	375000.0
	375000.0

	BLOCK VCSIS

YTAB1:	00.0	; PIRIMID
	RODE
	64.0
	-RODE
	32.0
	32.0
	32.0
	32.0

	1700.0	; RUN WAY
	-1700.0
	-1200.0
	-350.0
	-750.0
;	-1150.0
	-900.0
	-1025.0
	-1025.0
	-1150.0
	-1150.0
	-1150.0
	-1500.0
	-1300.0

	0	; GLIDE SLOPE

	-1000000.0	; HORIZON
	+1000000.0
	+1000000.0
	-1000000.0

	1000000.0	;MOUNTINS
	1000000.0
	-1000000.0
	-1000000.0

	BLOCK VCSIS

ZTAB1:	00.0	; PIRIMED
	00.0
	00.0
	00.0
	45.0
	45.0
	45.0
	45.0


	REPEAT 15,0	; RUN WAY

	-200.0		; GLIDE SLOPE

	REPEAT 4,0	; HORIZON

	50000.0	;MOUNTINS
	50000.0
	30000.0
	30000.0

	BLOCK VCSIS

IFN FOOF,[
BARIND==200

ZZ==.
	DEFINE LINE1 X0,Y0,Z0,X1,Y1,Z1
	LOC XTAB+BARIND
	X0
	LOC YTAB+BARIND
	Y0
	LOC ZTAB+BARIND
	Z0
	LOC XTAB1+BARIND
	X1
	LOC YTAB1+BARIND
	Y1
	LOC ZTAB1+BARIND
	Z1
BARIND==BARIND+1
	TERMIN

	DEFINE HBAR LN,LD,RD,XO,YO,XT,YT
	LINE1 \<XO>,\<YO>,0.0,\<XO>,\<.OP FADR YO <.OP FMPR YT LN>>,0.0
	LINE1 \<XO>,\<.OP FADR YO <.OP FMPR YT LN>>,0.0,\<.OP FSBR XO <.OP FMPR 5.0 XT>>,\<.OP FADR YO <.OP FMPR YT <.OP FADR LN RD>>>,7.0
	LINE1 \<.OP FSBR XO <.OP FMPR 5.0 XT>>,\<.OP FADR YO <.OP FMPR YT <.OP FADR LN RD>>>,7.0,\<.OP FSBR XO <.OP FMPR 10.0 XT>>,\<.OP FADR YO <.OP FMPR LN YT>>,0.0
	LINE1 \<.OP FSBR XO <.OP FMPR 10.0 XT>>,\<.OP FADR YO <.OP FMPR LN YT>>,0.0,\<XO>,\<.OP FADR YO <.OP FMPR YT LN>>,0.0
	LINE1 \<.OP FSBR XO <.OP FMPR 5.0 XT>>,\<.OP FADR YO <.OP FMPR YT <.OP FADR LN RD>>>,7.0,\<.OP FSBR XO <.OP FMPR 5.0 XT>>,\<.OP FADR YO <.OP FMPR YT LD>>,7.0
	LINE1 \<.OP FSBR XO <.OP FMPR 5.0 XT>>,\<.OP FADR YO <.OP FMPR YT LD>>,7.0,\<XO>,\<YO>,0.0
	LINE1 \<XO>,\<YO>,0.0,\<.OP FSBR XO <.OP FMPR 10.0 XT>>,\<YO>,0.0
	LINE1 \<.OP FSBR XO <.OP FMPR 10.0 XT>>,\<YO>,0.0,\<.OP FSBR XO <.OP FMPR 5.0 XT>>,\<.OP FADR YO <.OP FMPR LD YT>>,7.0
	LINE1 \<.OP FSBR XO <.OP FMPR 10.0 XT>>,\<YO>,0.0,\<.OP FSBR XO <.OP FMPR 10.0 XT>>,\<.OP FADR YO <.OP FMPR LN YT>>,0.0
	TERMIN

	DEFINE VBAR LN,LD,RD,XO,YO,XT,YT
	LINE1 \<XO>,\<YO>,0.0,\<.OP FADR XO <.OP FMPR XT LN>>,\<YO>,0.0
	LINE1 \<.OP FADR XO <.OP FMPR XT LN>>,\<YO>,0.0,\<.OP FADR XO <.OP FMPR XT <.OP FADR LN RD>>>,\<.OP FSBR YO <.OP FMPR 5.0 YT>>,7.0
	LINE1 \<.OP FADR XO <.OP FMPR XT <.OP FADR LN RD>>>,\<.OP FSBR YO <.OP FMPR 5.0 YT>>,7.0,\<.OP FADR XO <.OP FMPR LN XT>>,\<.OP FSBR YO <.OP FMPR 10.0 YT>>,0.0
	LINE1 \<.OP FADR XO <.OP FMPR LN XT>>,\<.OP FSBR YO <.OP FMPR 10.0 YT>>,0.0,\<.OP FADR XO <.OP FMPR XT LN>>,\<YO>,0.0
	LINE1 \<.OP FADR XO <.OP FMPR XT <.OP FADR LN RD>>>,\<.OP FSBR YO <.OP FMPR 5.0 YT>>,7.0,\<.OP FADR XO <.OP FMPR XT LD>>,\<.OP FSBR YO <.OP FMPR 5.0 YT>>,7.0
	LINE1 \<.OP FADR XO <.OP FMPR XT LD>>,\<.OP FSBR YO <.OP FMPR 5.0 YT>>,7.0,\<XO>,\<YO>,0.0
	LINE1 \<XO>,\<YO>,0.0,\<XO>,\<.OP FSBR YO <.OP FMPR 10.0 YT>>,0.0
	LINE1 \<XO>,\<.OP FSBR YO <.OP FMPR 10.0 YT>>,0.0,\<.OP FADR XO <.OP FMPR LD XT>>,\<.OP FSBR YO <.OP FMPR 5.0 YT>>,7.0
	LINE1 \<XO>,\<.OP FSBR YO <.OP FMPR 10.0 YT>>,0.0,\<.OP FADR XO <.OP FMPR LN XT>>,\<.OP FSBR YO <.OP FMPR 10.0 YT>>,0.0
	TERMIN

	VBAR 100.0,5.0,-5.0,0.0,0.0,1.0,-1.0
	HBAR 60.0,-5.0,-5.0,100.0,10.0,1.0,1.0
	HBAR 40.0,-5.0,-5.0,55.0,10.0,1.0,1.0

	VBAR 80.0,-5.0,5.0,10.0,100.0,1.0,-1.0
	HBAR 70.0,5.0,-5.0,100.0,100.0,1.0,1.0
	VBAR 80.0,-5.0,5.0,90.0,170.0,-1.0,1.0
	HBAR 70.0,5.0,-5.0,0.0,170.0,-1.0,-1.0

	VBAR 80.0,-5.0,5.0,10.0,200.0,1.0,-1.0
	HBAR 70.0,5.0,-5.0,100.0,200.0,1.0,1.0
	VBAR 80.0,-5.0,5.0,90.0,270.0,-1.0,1.0
	HBAR 70.0,5.0,-5.0,0.0,270.0,-1.0,-1.0

LOC ZZ
]
TTYTAB:	BLOCK SISET
INPNTR:	SISET-1
OTPNTR:	SISET-1
SENSD:	-1
PAT:	BLOCK 100
UUOTAB:	REPEAT 40,JRST 4,1
TTAB:	REPEAT 200,JFCL
	MXTB2+5

XTAB2:	BLOCK 20
YTAB2:	BLOCK 20
XTAB3:	BLOCK 20
YTAB3:	BLOCK 20


PTAB:	REPEAT 20,TRN
	JRST 4,.

ZZ==.

	LOC PTAB
	REPEAT 4,MOVE A,MXTB+.RPCNT
LOC ZZ
MXTB3:	BLOCK 5
MXTB:	BLOCK 12
MXTB2:	BLOCK 8

MXTB2P:	MXTB2+3
	MXTB2+7
	MXTB2+6
	MXTB2+0
	MXTB2+1
	MXTB2+2

MXTB2Q:	MXTB2+4

; UUOS

ZZ==.


	LOC UUOTAB+PRNT_<-33>
	PUSHJ P,PRINT

	LOC UUOTAB+SAC_<-33>
	PUSHJ P,SACR

	LOC UUOTAB+UAC_<-33>
	PUSHJ P,UACR

	LOC UUOTAB+WATE_<-33>
	PUSHJ P,WATER	;UNUSED UUO

	LOC UUOTAB+VAAX_<-33>
	PUSHJ P,VARUT	;UNUSED UUO

	LOC ZZ


;  TTY COMANDS

ZZ==.


	LOC TTAB+"C	;SENS OF DISP
	SETCMM DZON

	LOC TTAB+"D	;GO TO DDT
	JRST GODDT

	LOC TTAB+"F	; GO FLY
	JRST 10,TEST1

	LOC TTAB+"I	;JRST TO GO
	JRST 10,GO

	LOC TTAB+11	;^I JRST IDL
	PUSHJ P,IDL1

	LOC TTAB+"L	;FLUSH ALL I/O
	PUSHJ P,FLSHIO

	LOC TTAB+"P
	JRST 10,CPOTS	; CALABRATE POTS

	LOC TTAB+"T
	JRST 10,TEST


	LOC TTAB+"X	;TRACE SCOPE
	SETCMM TRACE


	LOC TTAB+"Z	;SETZM NUMBER BUFF
	PUSHJ P,GETNO

	LOC TTAB+"0	;INPUT NUMBERS INTO BUFF
	REPEAT 10,PUSHJ P,NUMBER

	LOC ZZ



;		JRST 4   ERRORS


;TTY SPAZZ JRST 4,0

;UUOH SPAZZ JRST 4,1

SETUPF:	SETOM FIRST'
	SETOM FIRST1'
	SETZM BSTOL'
	SETZM ALRTIP'
	SETZM CMPS'
	MOVEI A,7
	MOVEM A,CHIN1'
	SETOM A
	PUSHJ P,DRINS
	POPJ P,

FLY1:	MOVE A,MXTB3+3
	FSBR A,MINP+3
	FMPR A,DIFP+3
	FSBR A,TOPS
	MOVN A,A
	PUSHJ P,FIX
	MOVEM A,THRI

	MOVE A,MXTB3+1
	FSBR A,MINP+1
	FMPR A,DIFP+1
	FSBR A,TOPS
	MOVN A,A
	PUSHJ P,FIX
	MOVEM A,EVLI1
	PUSHJ P,ISSTL

	MOVE A,MXTB3+2
	FSBR A,MINP+2
	FMPR A,DIFP+2
	JFCL
	JFCL
	PUSHJ P,FIX
	MOVEM A,ALRI

	MOVE A,MXTB3
	FSBR A,MINP
	FMPR A,DIFP
	JFCL
	JFCL
	PUSHJ P,FIX
	MOVEM A,RUDI1

	POPJ P,

CPOTS:	MOVE P,[-PDS,,PDL-1]
	PUSHJ P,RSTAB
	MOVSI B,-4
REPT:	MOVE A,MXTB3(B)
	CAMGE A,MINP(B)
	MOVEM A,MINP(B)
	CAMLE A,MAXP(B)
	MOVEM A,MAXP(B)
	MOVE A,MAXP(B)
	FSBR A,MINP(B)
	MOVE C,TOPS
	FDVR C,A
	MOVEM C,DIFP(B)
	AOBJN B,REPT
	JRST REPT-1

MAXP:	3438.0
	4072.0
	1316.0
	3676.0

MINP:	2322.0
	196.0
	676.0
	2722.0

DIFP:	3.6693548
	1.0565015
	6.3984375
	4.2924528
î
RSTAB:	MOVSI A,-4
	MOVE B,TOPS
	SETZM MAXP(A)
	MOVEM B,MINP(A)
	AOBJN A,.-2
	POPJ P,

EVLI1:	3777
RUDI1:	0
TOPS:	4095.0
STL1:	2000
STL2:	777
LTFOO:	0

ISSTL:	MOVEI A,3777
	MOVE B,WAIS
	CAMG B,STL1
	MOVEM A,EVLI1
	MOVE B,EVLI
	CAMGE B,STL2
	MOVEM A,EVLI1
	MOVE A,TIME
	ADDI A,60.
	MOVEM A,LTFOO
	POPJ P,
FLY:	TRN FLY1
	MOVE A,MXTB
	MOVEM A,THRI'
	MOVEI A,3777
	MOVEM A,RUDI1
	MOVNI B,100
	MOVN C,B
	DATAI 420,A
	TLNN A,100000
	ADDM B,EVLI1
	TLNN A,40000
	ADDM C,EVLI1
	MOVEI B,3777
	TLNN A,400000
	MOVEI B,3777+2000
	TLNN A,200000
	MOVEI B,3777-2000
	MOVEM B,ALRI'
	SKIPGE EVLI1
	SETZM EVLI1
	MOVEI A,7777
	CAMGE A,EVLI1
	MOVEM A,EVLI1
	POPJ P,


INIMX:	PUSH P,B
	HLRZ B,A
	HRRZ A,A
	DATAO IMX,A
	CONO IMX,0
	CONSO IMX,10
	JRST .-1
	DATAI IMX,A
	ASH A,(B)
	POP P,B
	POPJ P,

THRICH:	0,,15
RUDICH:	2,,150
EVLICH:	3,,151
ALRICH:	2,,152
EVLI:	0
RUDI:	0

INIT:	MOVE A,RUDICH
	PUSHJ P,INIMX
	MOVEI B,3777
	SUB B,A
	MOVEM B,RUDIBG'

	MOVE A,EVLICH
	PUSHJ P,INIMX
	MOVEI B,3777
	SUB B,A
	MOVEM B,EVLIBG'

	MOVE A,ALRICH
	PUSHJ P,INIMX
	MOVEI B,3777
	SUB B,A
	MOVEM B,ALRIBG'

	POPJ P,

DRINS:	PUSH P,A
	PUSH P,X
	PUSH P,Y
	PUSH P,R
	MOVE A,[442200,,DSL0]
	MOVEM A,BP'
	PUSHJ P,DTIC
	PUSHJ P,DCOM
	TRN P,DADF
	PUSHJ P,DAIS
	PUSHJ P,DART
	PUSHJ P,DALT
	PUSHJ P,DRPM
	PUSHJ P,DTBK
	PUSHJ P,DRAT
;	PUSHJ P,DIPS

	MOVEI A,3000
	IDPB A,BP
	SETOM DZON'

	POP P,R
	POP P,Y
	POP P,X
	POP P,A
	POPJ P,


MVINS:	MOVE A,TIME
	PUSHJ P,FLT
	EXCH A,LTIME
	FSBR A,LTIME
	FMPR A,[-0.1]
	MOVEM A,ETIME

	MOVE A,MXTB
	MOVEI B,0
	CAIL A,3777
	SETOM B
	SKIPE AUTO
	PUSHJ P,AINS
	MOVEM B,DZON
	HRRZ B,BOTOM
	SETZM GRND
	CAMN B,ALTUD
	SETOM GRND
	PUSHJ P,MRPM
	PUSHJ P,MART
	PUSHJ P,MAIS
	PUSHJ P,MRAT
	PUSHJ P,MALT
	PUSHJ P,MTBK
	PUSHJ P,MCOM
	PUSHJ P,MTIC
;	PUSHJ P,MIPS
	POPJ P,


ETIME:	0
LTIME:	0
GRND:	0
AUTO:	-1
DHITE:	3000

AINS:	MOVE A,ALTUD
	CAMG A,DHITE
	SETZB B
	CAML A,DHITE
	SETOM B
	POPJ P,

DGLS:	MOVEI A,SIXTH*1
	MOVEM A,XOFS
	MOVEI A,SIXTH*5
	MOVEM A,YOFS
	MOVE I,DIN
	MOVEI S,1
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE R,[10.0]
	PUSHJ P,RING
	MOVEI A,071423
	PUSHJ P,DNAM
	POPJ P,

DIN:	4

DTIC:	MOVEI A,SIXTH*1
	MOVEM A,XOFS
	MOVEI A,SIXTH*5
	MOVEM A,YOFS
	MOVE I,DIN
	MOVEI S,1
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE R,[0.1]
	PUSHJ P,RING
	MOVE R,[0.2]
	PUSHJ P,RING
	MOVE R,[0.3]
	PUSHJ P,RING
	MOVE R,[10.0]
	PUSHJ P,RING
	MOVEI A,241103
	PUSHJ P,DNAM
	MOVEI A,TCLC
	MOVEI B,60.
	MOVEI C,4
	MOVE X,[0.0]
	MOVE Y,[360.0]
	SETOM 360DG'
	PUSHJ P,LINS
	POPJ P,

DCOM:	MOVEI A,SIXTH*3
	MOVEM A,XOFS
	MOVEI A,SIXTH*5
	MOVEM A,YOFS
	MOVE I,DIN
	MOVEI S,1
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE R,[0.1]
	PUSHJ P,RING
	MOVE R,[0.2]
	PUSHJ P,RING
	MOVE R,[0.3]
	PUSHJ P,RING
	MOVE R,[10.0]
	PUSHJ P,RING
	MOVEI A,040722
	PUSHJ P,DNAM
DCOM1:	MOVEI A,TCOM
	MOVEI B,36.
	MOVEI C,2
	MOVE X,[0.0]
	MOVE Y,[360.0]
	SETOM 360DG'
	PUSHJ P,LINS
	POPJ P,

DADF:	MOVEI A,SIXTH*5
	MOVEM A,XOFS
	MOVEI A,SIXTH*5
	MOVEM A,YOFS
	MOVE I,DIN
	MOVEI S,1
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE R,[10.0]
	PUSHJ P,RING
	MOVEI A,010406
	PUSHJ P,DNAM
	JRST DCOM1

DNAM:	PUSH P,X
	PUSH P,Y
	MOVE X,BP
	MOVEM X,SBP1'
	MOVE X,[-1.0]
	MOVE Y,[-3.0]
	PUSHJ P,POS
	MOVEI ZP%MD,CMD
	PUSHJ P,ZRMF
	MOVEI LC%ES,1
	PUSHJ P,CHR
	POP P,Y
	POP P,X
	POPJ P,
DAIS:	MOVEI A,SIXTH*1
	MOVEM A,XOFS
	MOVEI A,SIXTH*3
	MOVEM A,YOFS
	MOVE I,DIN
	MOVEI S,1
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE R,[0.1]
	PUSHJ P,RING
	MOVE R,[0.2]
	PUSHJ P,RING
	MOVE R,[0.3]
	PUSHJ P,RING
	MOVE R,[10.0]
	PUSHJ P,RING
	MOVEI A,TAIS
	MOVEI B,21.
	MOVEI C,1
	MOVE X,[0.0]
	MOVE Y,[320.0]
	SETZM 360DG
	PUSHJ P,LINS
	MOVEI A,110123
	PUSHJ P,DNAM
	MOVE A,[0.0]
	MOVE B,[2.0]
	MOVE X,[4.5]
	MOVE Y,[1.5]
	PUSHJ P,BOX
	MOVE X,[-1.5]
	MOVE Y,[2.0]
	PUSHJ P,POS
	MOVEI ZP%MD,CMD
	MOVEI Z%IN,3
	MOVEI Z%SC,1
	MOVEI Z%ST,0
	MOVE A,BP
	MOVEM A,SBPC'
	PUSHJ P,ZRMF
	MOVEI A,232401
	IDPB A,BP
	MOVEI A,141437
	IDPB A,BP
	POPJ P,

DART:	MOVEI A,SIXTH*3
	MOVEM A,XOFS
	MOVEI A,SIXTH*3
	MOVEM A,YOFS
	MOVE I,DIN
	MOVEI S,1
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE R,[10.0]
	PUSHJ P,RING
	MOVE R,[7.5]
	PUSHJ P,RING
	MOVE A,[0.0]
	MOVE B,[0.0]
	MOVE X,[10.0]
	MOVE Y,[0.5]
	PUSHJ P,BOX
	POPJ P,

DALT:	MOVEI A,SIXTH*5
	MOVEM A,XOFS
	MOVEI A,SIXTH*3
	MOVEM A,YOFS
	MOVE I,DIN
	MOVEI S,1
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE R,[0.1]
	PUSHJ P,RING
	MOVE R,[0.2]
	PUSHJ P,RING
	MOVE R,[0.3]
	PUSHJ P,RING
	MOVE R,[10.0]
	PUSHJ P,RING
	MOVEI A,TALT
	MOVEI B,50.
	MOVEI C,4.
	MOVE X,[0.0]
	MOVE Y,[360.0]
	SETOM 360DG
	PUSHJ P,LINS
	MOVEI A,011424
	PUSHJ P,DNAM
	POPJ P,

DRPM:	MOVEI A,SIXTH*1
	MOVEM A,XOFS
	MOVEI A,SIXTH*1
	MOVEM A,YOFS
	MOVE I,DIN
	MOVEI S,1
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE R,[0.1]
	PUSHJ P,RING
	MOVE R,[0.2]
	PUSHJ P,RING
	MOVE R,[0.3]
	PUSHJ P,RING
	MOVE R,[10.0]
	PUSHJ P,RING
	MOVEI A,TRPM
	MOVEI B,36.
	MOVEI C,4
	MOVE X,[200.0]
	MOVE Y,[520.0]
	SETZM 360DG
	PUSHJ P,LINS
	MOVEI A,222015
	PUSHJ P,DNAM
	POPJ P,

DTBK:	MOVEI A,SIXTH*3
	MOVEM A,XOFS
	MOVEI A,SIXTH*1
	MOVEM A,YOFS
	MOVE I,DIN
	MOVEI S,1
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE R,[0.1]
	PUSHJ P,RING
	MOVE R,[0.2]
	PUSHJ P,RING
	MOVE R,[0.3]
	PUSHJ P,RING
	MOVE R,[10.0]
	PUSHJ P,RING
	MOVE X,[2.0]
	MOVE Y,[2.0]
	MOVE A,[0.0]
	MOVE B,[8.0]
	PUSHJ P,BOX
	MOVE A,[6.0]
	MOVE B,[5.0]
	PUSHJ P,BOX
	MOVE A,[-6.0]
	MOVE B,[5.0]
	PUSHJ P,BOX
	MOVEI I,4
	MOVE X,[10.0]
	MOVE Y,[2.0]
	MOVE A,[0.0]
	MOVE B,[-5.0]
	PUSHJ P,BOX
	MOVE X,[2.0]
	PUSHJ P,BOX
	MOVE X,[-6.0]
	MOVE Y,[5.0]
	PUSHJ P,POS
	MOVEI ZP%MD,CMD
	PUSHJ P,ZRMF
	MOVEI A,143737
	IDPB A,BP
	MOVE X,[6.0]
	MOVE Y,[5.0]
	PUSHJ P,POS
	MOVEI ZP%MD,CMD
	PUSHJ P,ZRMF
	MOVEI A,223730
	IDPB A,BP
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE R,[1.0]
	MOVEI I,7
	MOVEI A,-120
	ADDM A,YOFS
	PUSHJ P,RING
	MOVE A,SBPP
	MOVEM A,SBP'
	LDB A,SBP
	MOVEM A,SBALL'
	POPJ P,

DRAT:	MOVEI A,SIXTH*5
	MOVEM A,XOFS
	MOVEI A,SIXTH*1
	MOVEM A,YOFS
	MOVE I,DIN
	MOVEI S,1
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE R,[0.1]
	PUSHJ P,RING
	MOVE R,[0.2]
	PUSHJ P,RING
	MOVE R,[0.3]
	PUSHJ P,RING
	MOVE R,[10.0]
	PUSHJ P,RING
	MOVEI A,TRAT
	MOVEI B,12.
	MOVEI C,0
	MOVE X,[0.0]
	MOVE Y,[360.0]
	SETOM 360DG
	PUSHJ P,LINS
	MOVEI A,031415
	PUSHJ P,DNAM
	POPJ P,

DIPS:	PUSH P,A
	PUSH P,X
	PUSH P,Y
	PUSH P,GAN
	MOVE X,[1.0]
	MOVEM X,GAN
;	HLRZ X,XYTEN
;	HRRZ Y,XYTEN
	MOVEI S,2
	MOVEI I,7
	ADDI Y,11
	MOVEM X,XOFS
	MOVEM Y,YOFS
	MOVEI A,355240
	PUSHJ P,DNAM
	MOVE A,SBP1
	MOVEM A,SIPS'
	POP P,GAN
	POP P,Y
	POP P,X
	POP P,A
	POPJ P,

MTIC:	MOVEI A,SIXTH*1
	MOVEM A,XOFS
	MOVEI A,SIXTH*5
	MOVEM A,YOFS
	MOVE I,NIN
	MOVEI S,0

	MOVE R,[15.0]
	MOVE A,TIME
	PUSHJ P,FLT
	FDVR A,[10.0]
	PUSHJ P,TNED
	FDVR A,[60.0]
	MOVE R,[14.0]
	PUSHJ P,TNED
	FDVR A,[12.0]
	MOVE R,[11.0]
	JRST TNED

TNED:	SKIPN DZON
	POPJ P,
	PUSH P,A
	PUSHJ P,ANGXY
	SETZM X
	MOVE Y,R
	FMPR Y,GAN
	PUSHJ P,ROT1
	PUSHJ P,LINE
	POP P,A
	POPJ P,
	
MCOM:	MOVEI A,SIXTH*3
	MOVEM A,XOFS
	MOVEI A,SIXTH*5
	MOVEM A,YOFS
	MOVE R,LNTHN
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE I,NIN
	MOVEI S,0
	MOVE A,CMPS
	CAILE A,7777
	SUBI A,7777
	SKIPGE A
	ADDI A,7777
	MOVEM A,CMPS
	MOVE A,COMPI
	ASH A,-7
	PUSHJ P,FLT
	FMPR A,ETIME
	PUSHJ P,FIX
	ADDB A,CMPS
	SKIPN DZON
	POPJ P,
	PUSHJ P,NEDL
	PUSHJ P,LINE
	POPJ P,

NIN:	5

MADF:	POPJ P,

MAIS:	MOVEI A,SIXTH*1
	MOVEM A,XOFS
	MOVEI A,SIXTH*3
	MOVEM A,YOFS
	SKIPE GRND
	MOVE A,THRI
	SKIPL GRND
	MOVE A,EVLI
	SKIPL GRND
	ADDI A,440
	SUB A,WAIS
	PUSHJ P,FLT
	MOVE B,[0.015]
	SKIPE GRND
	PUSHJ P,ROLING
	FMPR A,B
	FMPR A,ETIME
	PUSHJ P,FIX
	ADDB A,WAIS
	MOVE R,LNTHN
	MOVE X,[0.0]
	MOVE Y,[340.0]
	MOVE I,NIN
	MOVEI S,0
	SKIPN DZON
	POPJ P,
	PUSHJ P,NEDL
	PUSHJ P,LINE
	POPJ P,

ROLING:	MOVE B,[0.004]
	SKIPGE A
	MOVE B,[0.01]
	POPJ P,

MART:	MOVEI A,SIXTH*3
	MOVEM A,XOFS
	MOVEI A,SIXTH*3
	MOVEM A,YOFS
	MOVE I,NIN
	MOVEI S,0
	MOVE A,EVLI1
	SUB A,EVLI
	ASH A,-4
	PUSHJ P,FLT
	FMPR A,ETIME
	PUSHJ P,FIX
	ADDB A,EVLI
	PUSHJ P,FLT
	FDVR A,[4095.0]
	FMPR A,[180.0]
	MOVEM A,SAVAF'
	MOVE X,[0.0]
	MOVE Y,[-15.0]
	FMPR X,GAN
	FMPR Y,GAN
	SKIPN DZON
	JRST .+3
	PUSHJ P,ANGXY
	PUSHJ P,ROT1
	MOVEM X,X1F'
	MOVEM Y,Y1F'
	MOVN A,SAVAF
	MOVE X,[0.0]
	MOVE Y,[-15.0]
	FMPR X,GAN
	FMPR Y,GAN
	PUSHJ P,ANGXY
	PUSHJ P,ROT1
	MOVE A,ALRI
	SUBI A,3777
	PUSHJ P,FLT
	FMPR A,[0.001]
	MOVN A,A

MDED:	MOVEM A,MABA'
	MOVMS A
	CAMG A,DEDZ
	SETZB A,MABA
	HRRZ A,BOTOM
	CAME A,ALTUD
	JRST NOBT
	MOVE A,BTBUG
	SKIPL ALRTIP
	MOVN A,BTBUG
	MOVEM A,MABA
	MOVM A,ALRTIP
	CAMLE A,BTBUG
	JRST NOBT
	SETZB A,ALRTIP
	SETZM MABA
NOBT:	MOVE A,MABA

	FMPR A,ETIME
	FADRB A,ALRTIP

	MOVEM A,MABA
	MOVMS A
	CAMG A,TLIM
	JRST NOLIM
	MOVE A,TLIM
	SKIPGE MABA
	MOVN A,TLIM
	MOVEM A,MABA
	MOVEM A,ALRTIP

NOLIM:	MOVE A,MABA
	PUSHJ P,ANGXY
	PUSHJ P,ROT1
	MOVEM X,X2'
	MOVEM Y,Y2'
	MOVE X,X1F
	MOVE Y,Y1F
	SKIPN DZON
	POPJ P,
	PUSHJ P,ROT1
	PUSHJ P,POSLIN
	POPJ P,

TLIM:	70.0
DEDZ:	0.2
BTBUG:	2.0

MALT:	MOVEI A,SIXTH*5
	MOVEM A,XOFS
	MOVEI A,SIXTH*3
	MOVEM A,YOFS
	MOVE I,NIN
	MOVEI S,0
	MOVE R,[10.0]
	MOVE X,[0.0]
	MOVE Y,[360.0]
	MOVE A,RATOC
	SUBI A,3777
	ASH A,-5
	PUSHJ P,FLT
	FMPR A,ETIME
	PUSHJ P,FIX
	ADDB A,ALTUD

BOTOM:	CAIL A,40
	JRST NOGND
	HRRZ A,BOTOM
	MOVEM A,ALTUD

NOGND:	IDIVI A,7777
	IMULI A,7777/10.
	PUSH P,B
	IDIVI B,10.
	ADD A,B
	POP P,B
	SKIPN DZON
	POPJ P,
	PUSHJ P,NEDL
	PUSHJ P,LINE
	MOVE A,B	; B IS REMBER BIG HAND
	MOVE R,LNTHN
	MOVE X,[0.0]
	MOVE Y,[360.0]
	PUSHJ P,NEDL
	PUSHJ P,LINE
	POPJ P,

MRPM:	MOVEI A,SIXTH*1
	MOVEM A,XOFS
	MOVEI A,SIXTH*1
	MOVEM A,YOFS
	MOVE R,LNTHN
	MOVE X,[200.0]
	MOVE Y,[520.0]
	MOVEI S,0
	MOVE I,NIN
	MOVE A,THRI
	SUB A,WRPM
	PUSHJ P,FLT
BRPM:	FMPR A,[0.1]
	FMPR A,ETIME
	PUSHJ P,FIX
	ADDB A,WRPM
	SKIPN DZON
	POPJ P,
	PUSHJ P,NEDL
	PUSHJ P,LINE
	POPJ P,

MTBK:	MOVEI A,SIXTH*3
	MOVEM A,XOFS
	MOVEI A,SIXTH*1
	MOVEM A,YOFS
	MOVE R,LNTHN
	MOVE I,NIN
	MOVEI S,0
	MOVE X,[270.0]
	MOVE Y,[450.0]
	MOVE B,RUDI1
	SUB B,RUDI
	ASH B,-2
	MOVE A,B
	PUSHJ P,FLT
	FMPR A,ETIME
	PUSHJ P,FIX
	MOVE B,A
	ADDB B,RUDI
	SUBI B,3777
	ASH B,-1
	MOVN A,ALRTIP
	FMPR A,[50.0]
	PUSHJ P,FIX
	ADD A,B
	MOVEM A,COMPI'
	ADDI A,3777
	SKIPN DZON
	JRST .+3
	PUSHJ P,NEDL
	PUSHJ P,LINE
	MOVE B,RUDI
	SUBI B,3777
	MOVN B,B
ASHB:	ASH B,-5
	MOVE A,ALRTIP
	PUSHJ P,FIX
	ASH A,0
	MOVN A,A
	ADD B,A
	MOVE A,SBALL
	ADD A,B
	DPB A,SBP
	POPJ P,
WFOO:	0
MRAT:	MOVEI A,SIXTH*5
	MOVEM A,XOFS
	MOVEI A,SIXTH*1
	MOVEM A,YOFS
	MOVE A,EVLI
	CAMGE A,STL2	;STALL POINT
	JRST STOL
	MOVE B,WAIS
	CAMG B,STL1
	JRST STOL
	MOVE B,CHIN
	CAIE B,3
	PUSHJ P,EXIN
	PUSHJ P,SETIN
	PUSHJ P,FLT
	FSBR A,[777'.0]
	FDVR A,[3584.0]
	MOVE B,[1.0]
	FSBR B,A
	MOVE A,WRPM
	PUSHJ P,FLT
	FDVR A,[4095.0]
	FMPR A,[2561.0]
	FADR A,[2000.0]
	FMPR A,B
	PUSHJ P,FIX
ST1:	MOVE R,LNTHN
	MOVE X,[90.0]
	MOVE Y,[450.0]
	MOVE I,NIN
	MOVEI S,0
	SUB A,WCLM
	PUSHJ P,FLT
	FMPR A,WFOO
	FMPR A,ETIME
	PUSHJ P,FIX
	ADDB A,WCLM
	SUBI A,3777
	MOVE B,[0.1]
	SKIPL A
	FSC B,2
	MOVEM B,WFOO
	SKIPL A
	ASH A,-2
	ADDI A,3777
	MOVEM A,RATOC'
	SKIPN DZON
	POPJ P,

	HRRZ A,BOTOM
	CAME A,ALTUD
	JRST NGOUT
	MOVE A,RATOC1
	PUSHJ P,FLT
	FSBR A,[2047.0]
	MOVN A,A
	FMPR A,[0.1]
	FMPR A,ETIME
	PUSHJ P,FIX
	ADDB A,RATOC1
	JRST NGOUT1
NGOUT:	MOVE A,RATOC
	MOVEM A,RATOC1
NGOUT1:	PUSHJ P,NEDL
	PUSHJ P,LINE
	POPJ P,

STOL:	AOS A,BSTOL
	CAIL A,3
	SETZM BSTOL
	SKIPN BSTOL
	PUSHJ P,EXIN
	PUSHJ P,SETIN
	MOVEI A,1777
	JRST ST1

SETIN:	PUSH P,BP
	PUSH P,A
	MOVE A,SBPC
	MOVEM A,BP
	MOVEI ZP%MD,CMD
	MOVE Z%IN,CHIN
	MOVEI Z%SC,1
	MOVEI Z%ST,0
	PUSHJ P,ZRMF
	POP P,A
	POP P,BP
	POPJ P,

EXIN:	EXCH A,CHIN
	EXCH A,CHIN1
	EXCH A,CHIN
	POPJ P,

RATOC1:	0

MIPS:	PUSH P,A
	PUSH P,BP
	PUSH P,SIPS
	MOVE A,SIPS
	MOVEM A,BP
	PUSHJ P,DIPS
	POP P,SIPS
	POP P,BP
	POP P,A
	POPJ P,

LINS:	PUSH P,A
	PUSH P,B
	PUSH P,C
	PUSH P,R
	PUSH P,X
	PUSH P,Y
	MOVEM A,TBP'
	MOVEM B,NOBL'
	MOVEM C,NOLL'
	MOVEM R,RADG'
	MOVEM X,STRG'
	MOVEM Y,STPG'
	SETZM LINL'
	SETZM NOTMS'
	PUSHJ P,STGR
	JRST RELINF



RELINF:	MOVEI ZP%MD,PMD
	MOVE Z%IN,I
	MOVE Z%SC,S
	MOVEI Z%ST,0
	PUSHJ P,ZRMF
	PUSHJ P,XYFIX
	ASH X,@S
	ASH Y,@S
	MOVEI LP%IF,0
	MOVEI ZP%MD,ZMD
	ADD X,XOFS
	ADD Y,YOFS
	PUSHJ P,PNTF
	PUSHJ P,LNTHL
	MOVEI ZP%MD,VMD
	MOVEI Z%SC,0
	PUSHJ P,ZRMF
	MOVEI LC%ES,1
	MOVEI LP%IF,1
	PUSHJ P,LIN
	MOVEI ZP%MD,ZMD
	MOVE Z%SC,S
	SKIPE LINL
	JRST NOCHR
	MOVEI ZP%MD,PMD
	MOVE Z%IN,I
	PUSHJ P,ZRMF
	PUSH P,A
	HRRZ A,TBP
	ADD A,NOTMS
	MOVE A,@A
	SKIPN A
	JRST POPAF
	POP P,A
	PUSHJ P,COMCP
	MOVEI LP%IF,0
	MOVEI ZP%MD,CMD
	PUSHJ P,PNTF
	PUSH P,A
	MOVE A,TBP
	ADD A,NOTMS
	HLRZ C,@A
	IDPB C,BP
	MOVEI C,373737
	IDPB C,BP
	POP P,A
NOCHR:	AOS LINL
	AOS NOTMS
	MOVE C,NOTMS
	CAML C,NOBL
	JRST OUTGR
	MOVE C,LINL
	CAMLE C,NOLL
	SETZM LINL
	MOVE X,XSAV1
	MOVE Y,YSAV1
	PUSHJ P,ROT1
	MOVEM X,XSAV1
	MOVEM Y,YSAV1
	JRST RELINF
OUTGR:	POP P,Y
	POP P,X
	POP P,R
	POP P,C
	POP P,B
	POP P,A
	POPJ P,
 
POPAF:	POP P,A
	JRST NOCHR

STGR:	PUSH P,C
	MOVE A,STRG
	MOVE Y,GAN
	FMPR Y,RADG
	SETZM X
	PUSHJ P,ANGXY
	PUSHJ P,ROT1
	MOVEM X,XSAV1'
	MOVEM Y,YSAV1'
	MOVE C,STPG
	FSBR C,STRG
	MOVE A,NOBL
	SKIPN 360DG
	SOS A
	PUSHJ P,FLT
	FDVRM C,A
	PUSHJ P,ANGXY
	POP P,C
	POPJ P,


LNTHL:	MOVE X,XSAV1
	MOVE Y,YSAV1
	SKIPE LINL
	JRST LITL
	FMPR X,[-0.4]
	FMPR Y,[-0.4]
	JRST BIGO
LITL:	FMPR X,[-0.2]
	FMPR Y,[-0.2]
BIGO:	PUSHJ P,XYFIX
	POPJ P,

COMCP:	PUSH P,A
	PUSH P,B
	MOVE X,XSAV1
	MOVE Y,YSAV1
	FMPR X,[1.4]
	FMPR Y,[1.4]
	PUSHJ P,XYFIX
	MOVE B,TBP
	ADD B,NOTMS
	HRRZ B,@B
	MOVE A,[-10]
	SKIPE B
	MOVE A,[-5]
	ASH A,@S
	ADD X,A
	MOVE A,[-3]
	ASH A,@S
	ADD Y,A
	ADD X,XOFS
	ADD Y,YOFS
	POP P,B
	POP P,A
	POPJ P,


TALY:	PUSH P,A
	HLRZ A,BP
	LSH A,-14
	SKIPE A
	JRST TAL3
	MOVEI A,3000
	REPEAT 6,IDPB A,BP
	POP P,A
	POPJ P,
TAL3:	MOVEI A,3000
	REPEAT 7,IDPB A,BP
	POP P,A
	POPJ P,

ANGXY:	MOVE B,A
	PUSHJ P,COSD
	EXCH A,B
	PUSHJ P,SIND
	POPJ P,

NEDL:	PUSH P,A
	PUSH P,B
	PUSHJ P,FLT
	FSBR Y,X
	FDVR Y,[4095.0]
	FMPR A,Y
	FADR A,X
	PUSHJ P,ANGXY
	MOVE X,[0.0]
	MOVE Y,GAN
	FMPR Y,R
	MOVEM A,SSIN'
	MOVEM B,SCOS'
	PUSHJ P,ROT1
	POP P,B
	POP P,A
	POPJ P,

ROT1:	PUSH P,X
	PUSH P,Y
	FMPR X,B
	FMPR Y,A
	FADR X,Y
	MOVEM X,SAVX'
	POP P,Y
	POP P,X
	FMPR Y,B
	FMPR X,A
	FSBR Y,X
	MOVE X,SAVX
	POPJ P,


LINE:	PUSH P,X
	PUSH P,Y
	MOVEI ZP%MD,PMD
	MOVE Z%IN,I
	MOVE Z%SC,S
	MOVEI Z%ST,0
	PUSHJ P,ZRMF
	MOVE X,XOFS
	MOVE Y,YOFS
	MOVEI LP%IF,0
	MOVEI ZP%MD,VMD
	PUSHJ P,PNTF
	MOVE X,-1(P)
	MOVE Y,(P)
	PUSHJ P,XYFIX
	MOVEI LC%ES,1
	MOVEI LP%IF,1
	PUSHJ P,LIN
	POP P,Y
	POP P,X
	POPJ P,



POSLIN:	PUSH P,A
	PUSH P,B
	PUSH P,X
	PUSH P,Y
	PUSH P,XOFS
	PUSH P,YOFS
	PUSHJ P,XYFIX
	ADDM X,XOFS
	ADDM Y,YOFS
	MOVE X,X2
	MOVE Y,Y2
	PUSHJ P,XYFIX
	ADD X,-1(P)
	ADD Y,(P)
	SUB X,XOFS
	SUB Y,YOFS
	ASH X,-1
	ASH Y,-1
	MOVE A,X
	MOVE B,Y
	PUSHJ P,XYFLT
	PUSHJ P,LINE
	ADDM A,XOFS
	ADDM B,YOFS
	PUSHJ P,LINE
	POP P,YOFS
	POP P,XOFS
	POP P,Y
	POP P,X
	POP P,B
	POP P,A
	POPJ P,


BOX:	PUSH P,A
	PUSH P,B
	PUSH P,X
	PUSH P,Y
	MOVEI ZP%MD,PMD
	MOVE Z%IN,I
	MOVE Z%SC,S
	MOVEI Z%ST,0
	PUSHJ P,ZRMF
	FMPR X,GAN
	FMPR Y,GAN
	PUSHJ P,XYFIX
	MOVEM X,XSAV'
	MOVEM Y,YSAV'
	ASH X,@S
	ASH Y,@S
	ASH X,-1
	ASH Y,-1
	ADD X,XOFS
	ADD Y,YOFS
	FMPR A,GAN
	FMPR B,GAN
	EXCH A,B
	PUSHJ P,FIX
	EXCH A,B
	PUSHJ P,FIX
	ASH A,@S
	ASH B,@S
	ADD X,A
	ADD Y,B
	MOVEI LP%IF,0
	MOVEI ZP%MD,VMD
	PUSHJ P,PNTF
	MOVEI LC%ES,0
	MOVEI LP%IF,1
	MOVEI X,0
	MOVN Y,YSAV
	PUSHJ P,LIN
	MOVN X,XSAV
	MOVEI Y,0
	PUSHJ P,LIN
	MOVEI X,0
	MOVE Y,YSAV
	PUSHJ P,LIN
	MOVE X,XSAV
	MOVEI Y,0
	MOVEI LC%ES,1
	PUSHJ P,LIN
	POP P,Y
	POP P,X
	POP P,B
	POP P,A
	POPJ P,


POS:	PUSH P,A
	PUSH P,X
	PUSH P,Y
	MOVEI ZP%MD,PMD
	MOVE Z%IN,I
	MOVE Z%SC,S
	MOVEI Z%ST,0
	PUSHJ P,ZRMF
	FMPR X,GAN
	FMPR Y,GAN
	PUSHJ P,XYFIX
	ASH X,@S
	ASH Y,@S
	ADD X,XOFS
	ADD Y,YOFS
	MOVE A,[-2]
	ASH A,@S
	ADD X,A
	MOVE A,[-3]
	ASH A,@S
	ADD Y,A
	MOVEI LP%IF,0
	MOVEI ZP%MD,ZMD
	PUSHJ P,PNTF
	POP P,Y
	POP P,X
	POP P,A
	POPJ P,



RING:	JRST RING1
	PUSH P,A
	PUSH P,B
	PUSH P,C
	PUSH P,X
	PUSH P,Y
	MOVEI ZP%MD,PMD
	MOVE Z%IN,I
	MOVE Z%SC,S
	MOVEI Z%ST,0
	PUSHJ P,ZRMF
	MOVEM X,XSAV'
	FSBR Y,X
	FDVR Y,NOPTS
	PUSHJ P,XYFIX
	MOVE C,Y
	MOVE A,XSAV
	PUSHJ P,ANGXY
	MOVE X,[0.0]
	MOVE Y,R
	FMPR Y,GAN
	PUSHJ P,ROT1
	MOVE A,NOPTS
	MOVEI LP%IF,1
	PUSHJ P,ANGXY
	JRST PLTP+1
PLTP:	PUSHJ P,ROT1
	PUSH P,X
	PUSH P,Y
	PUSHJ P,XYFIX
	ASH X,@S
	ASH Y,@S
	ADD X,XOFS
	ADD Y,YOFS
	PUSHJ P,PNTF
	POP P,Y
	POP P,X
	CAIN C,1
	MOVEI ZP%MD,ZMD
	SOJGE C,PLTP
	POP P,Y
	POP P,X
	POP P,C
	POP P,B
	POP P,A
	POPJ P,

RING1:	PUSH P,A
	PUSH P,B
	PUSH P,X
	PUSH P,Y
	PUSH P,R
	MOVEI ZP%MD,PMD
	MOVE Z%IN,I
	JFCL
	MOVE Z%SC,S
	MOVEI Z%ST,0
	PUSHJ P,ZRMF
	MOVE X,R
	MOVE Y,[0.0]
	FMPR X,GAN
	PUSHJ P,XYFIX
	ASH X,@S
	ASH Y,@S
	ADD X,XOFS
	ADD Y,YOFS
	MOVEI LP%IF,0
	MOVEI ZP%MD,IMD
	PUSHJ P,PNTF
	MOVE A,BP
	MOVEM A,SBPP'
	MOVE  A,R
	FMPR A,GAN
	PUSHJ P,FIX
	MOVE R,A
	MOVN B,S
	ASH A,(B)
	PUSHJ P,CIRCR
	POP P,R
	POP P,Y
	POP P,X
	POP P,B
	POP P,A
	POPJ P,


E==S
M==LC%ES
T==LP%IF


CIRCR:	MOVEM 15,ACBLK+15
	MOVEI 15,ACBLK
	BLT 15,ACBLK+14
	MOVSI A,265012	;.7071068
	MULI A,(R)
	DPB A,[014200,,B]	;EVEN PROD > 0
	MOVEI C,(R)
	IMUL C,C
	SUBI C,(A)	;R*R-Y
	IMUL A,B
	SUB C,A	;R*R-Y-2*Y*Y
	JUMPLE C,BGOOD
	SUBI C,(B)
	CAIGE C,3
	AOSA B
	ADDI B,2
BGOOD:	MOVNI E,1(B)
	ASH E,21
	IDIVI B,8
	HRRM B,BIGIN
	HRRM C,PHASIN
	MOVSI X,-4
	MOVEI Y,0
	MOVE M,BP
	MOVEI A,200000
	TLZE M,220000
	TROA Y,4
	AOSA M
	IDPB A,BP
	IDPB A,BP
	MOVE B,BP
	IDPB A,B
CRLUP:	HRRM M,BPA(X)
	MOVE T,PHASES(Y)
	HLLM T,BPA(X)
	ADD T,IDBS(X)
	MOVEM T,A(X)
BIGIN:	ADDI M,.
PHASIN:	ADDI Y,.
	TRZE Y,8
	AOS M
	AOBJN X,CRLUP
	SKIPE Y
	TLOA M,220000
	SOS M
	HRLS Y,BP
	ADDI Y,1
	BLT Y,(M)
	MOVEI Y,600000
	DPB Y,M
	MOVEM M,BP
	MOVNI Y,1	;14   10
	MOVEI T,0	;17 3 13
	MOVEI X,(R)
	PUSHJ P,CIRLP
	MOVSI 15,ACBLK
	BLT 15,15
	POPJ P,
CIRLP:	AOBJP E,CDUN
	ADDI Y,2
	ADDI T,(Y)
	CAIL T,(X)
	SOJA X,CDIAG
	4PUT 2 14 3 10
	PUSHJ P,CIRLP
	4PUT 14 3 10 2
	POPJ P,
CDIAG:	SUBI T,(X)	;.+2,DOUT+2=CAT; .+6,+8 MONK
	SUBI T,1(X)
	4PUT 16 17 13 12
	PUSHJ P,CIRLP
CDOUT:	4PUT 16 17 13 12
	POPJ P,
CDUN:	TRNN E,400000
	JRST CDOUT
	POPJ P,
ACBLK:	BLOCK 16
IRPC Q,,ABCD
IDB!Q:	REPEAT 4,[
	JSP Q,(Q)
	IDPB 0,BP!Q]
	MOVSI 0,-20000
	ADDM 0,BP!Q
JDB!Q:	REPEAT 4,[
	JSP Q,(Q)
	IDPB 0,BP!Q]
	MOVE 0,[420000,,1]
	ADDM 0,BP!Q
	JRST IDB!Q
TERMIN
IRPC Q,,ABCD
BP!Q:	0
TERMIN
IDBS:	IRPC Q,,ABCD
	IDB!Q
TERMIN
PHASES:	420400,,1
	360400,,3
	320400,,5
	260400,,7
	200400,,13
	140400,,15
	100400,,17
	040400,,21



PNT:	PUSH P,A
	ANDI Y,1777
	MOVEI A,200000+PMD_15(Y)
	IDPB A,BP
	LDB A,[1200,,X]
	DPB ZP%MD,[150300,,A]
	DPB LP%IF,[120100,,A]
	IDPB A,BP
	POP P,A
	POPJ P,


CHR:	PUSH P,A
	IDPB A,BP
	SKIPG LC%ES
	JRST CHROUT
	MOVEI A,373737
	IDPB A,BP
CHROUT:	POP P,A
	POPJ P,

PNTF:	PUSH P,A
	PUSH P,X
	PUSH P,Y
	ANDI X,1777
	ANDI Y,1777
	MOVEI A,PMD
	DPB A,[150300,,Y]
	MOVE A,ZP%MD
	DPB A,[150300,,X]
	TRZ Y,2000
	TRZ X,2000
	SKIPE LP%IF
	TRO X,2000
	TRZ X,200000
	TRO Y,200000
	IDPB Y,BP
	IDPB X,BP
	POP P,Y
	POP P,X
	POP P,A
	POPJ P,



LIN:	PUSH P,X
	PUSH P,Y
	TRNN X,400000
	JRST DOY
	MOVMS X
	TRO X,200
DOY:	TRNN Y,400000
	JRST DDOY
	MOVMS Y
	TRO Y,200
DDOY:	ANDI X,377
	ANDI Y,377
	LSH Y,10
	ADD X,Y
	TRZ X,600000
	SKIPLE LC%ES
	TRO X,400000
	SKIPLE LP%IF
	TRO X,200000
	IDPB X,BP
	POP P,Y
	POP P,X
	POPJ P,


ZRMF:	PUSH P,A
	PUSH P,B
	SETZM B
	MOVE A,ZP%MD
	DPB A,[150300,,B]
	MOVE A,Z%IN
	DPB A,[000300,,B]
	MOVE A,Z%SC
	DPB A,[040200,,B]
	TRZ B,3000
	SKIPLE Z%ST
	TRO B,3000
	TRO B,10
	TRO B,100
	TRZ B,10000
	IDPB B,BP
	POP P,B
	POP P,A
	POPJ P,


ZRM:	DPB ZP%MD,[150300,,Z%IN]
	TRO Z%IN,110
	IDPB Z%IN,BP
	POPJ P,

XYFIX:	PUSH P,Y+1
	MULI Y,400
	TSC Y,Y
	ASH Y+1,-243(Y)
	MULI X,400
	TSC X,X
	ASH X+1,-243(X)
	MOVE X,X+1
	MOVE Y,Y+1
	POP P,Y+1
	POPJ P,

XYFLT:	TLC X,232000
	FAD X,X
	TLC Y,232000
	FAD Y,Y
	POPJ P,

FIX:	PUSH P,A+1
	MULI A,400
	TSC A,A
	ASH A+1,-243(A)
	MOVE A,A+1
	POP P,A+1
	POPJ P,

FLT:	TLC A,232000
	FAD A,A
	POPJ P,


NOPTS:	2.0
FOOI:	0
GAN:	8.0
CHIN:	3
WRPM:	0
WAIS:	0
WCLM:	0

LNTHN:	14.0
ALTUD:	7777
XOFS:	0
YOFS:	0
PDL:	BLOCK PDS
DSL0:	BLOCK STDS
DSL1:	BLOCK MVDS
DSL2:	BLOCK MVDS
TCOM:	401640,,0
	0
	0
	406340,,0
	0
	0
	406640,,0
	0
	0
	400540,,0
	0
	0
	616240,,1
	0
	0
	616540,,1
	0
	0
	402340,,0
	0
	0
	626140,,1
	0
	0
	626440,,1
	0
	0
	402740,,0
	0
	0
	636040,,1
	0
	0
	636340,,1
	0
	0
	0


TCLC:	616240,,1
	0
	0
	0
	0
	406140,,0
	0
	0
	0
	0
	406240,,0
	0
	0
	0
	0
	406340,,0
	0
	0
	0
	0
	406440,,0
	0
	0
	0
	0
	406540,,0
	0
	0
	0
	0
	406640,,0
	0
	0
	0
	0
	406740,,0
	0
	0
	0
	0
	407040,,0
	0
	0
	0
	0
	407140,,0
	0
	0
	0
	0
	616040,,1
	0
	0
	0
	0
	616140,,1
	0
	0
	0
	0
	0

TAIS:	406040,,0
	0
	626040,,1
	0
	646040,,1
	0
	666040,,1
	0
	706040,,1
	0
	616060,,0
	0
	616260,,0
	0
	616460,,0
	0
	616660,,0
	0
	617060,,0
	0
	626060,,0
	0

TALT:	406040,,0
	0
	0
	0
	0
	406140,,0
	0
	0
	0
	0
	406240,,0
	0
	0
	0
	0
	406340,,0
	0
	0
	0
	0
	406440,,0
	0
	0
	0
	0
	406540,,0
	0
	0
	0
	0
	406640,,0
	0
	0
	0
	0
	406740,,0
	0
	0
	0
	0
	407040,,0
	0
	0
	0
	0
	407140,,0
	0
	0
	0
	0



TRAT:	406340,,0
	406440,,0
	406540,,0
	0
	406540,,0
	406440,,0
	406340,,0
	406240,,0
	406140,,0
	406040,,0
	406140,,0
	406240,,0

TRPM:	406040,,0
	0
	0
	0
	0
	406540,,0
	0
	0
	0
	0
	616040,,1
	0
	0
	0
	0
	616540,,1
	0
	0
	0
	0
	626040,,1
	0
	0
	0
	0
	626540,,1
	0
	0
	0
	0
	636040,,1
	0
	0
	0
	0
	636540,,1
	


END
