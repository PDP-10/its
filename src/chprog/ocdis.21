IFE TS+DECTS,[

DIS1:	0
	IDIVI A,10.
	JSR DISC
	IDIVI A,6.
	JSR DISC
	MOVEI B,":-"0
	JSR DISC
	JRST @DIS1

DISC:	0
	ADDI B,"0
	DPB B,DISIP
	MOVE B,DISIP
	ADD B,[60000,,]
	SKIPGE B
	SUB B,[440000,,1]
	MOVEM B,DISIP
	JRST @DISC

APREX:	MOVE A,CASAVE
	MOVE B,CBSAVE
APREX1:	CONO 1000+APRCHN
	JRST 12,@IAPRBRK

DISL2:	20156220054
	61200274040
WNUM:	607260607260
	605660343337
	20000
	61200024040
BNUM:	607260607260
	605660373737
	3000,,
]
IFN DSPLY,[
FONT:	1	;FONT ON TV. 0-> 50. WIDE BIG, 1-> 30. WIDE SMALL
FFANCY:	-2
FANCY:	PUSHJ P,GETNUM
FANSET:	MOVE A,FFANCY		;OLD VALUE
	MOVEM C,FFANCY
	CLEARM TOPVP
	CAILE C,2
	JRST FFANTV
	CAILE A,2
	PUSHJ P,TVBOFF		;FLUSH TV PAGES
	AOJL C,PSRET		;NO DISPLAY
	MOVEI C,11.
	MOVEM C,TOPVP
	PUSHJ P,TOS
	PUSHJ P,DISUP
	JRST PSRET

FFANTV:	PUSHJ P,TVDINI
	PUSHJ P,DBRD
	SKIPGE TVFLG
	.CALL [	SETZ		;FLUSH ECHOING OF CR, LF, AND TAB
		SIXBIT /TTYSET/
		1000,,TYIC
		[232323232323]
		SETZ [230323032323]]
	JFCL
	POPJ P,

TVBOFF:	CLEARM TVFLG
	.CALL [	SETZ		;ECHO CR, LF AND TAB AS NORMAL
		SIXBIT /TTYSET/
		1000,,TYIC
		[232323232323]
		SETZ [232323232323]]
	.VALUE
TVBCLR:	MOVE A,[-10,,TVBBAS/2000]	;FLUSH VIDEO BUFFER PAGES
	.CALL [	SETZ
		SIXBIT /CORBLK/
		1000,,0
		1000,,-1
		SETZ A]
	.VALUE
	POPJ P,

DISUP:	
IFN TS,[
	MOVE A,FFANCY
	CAILE A,2
	JRST INCD		;DO INCR UPDATE OF TV BOARD
	SKIPL TTYOFF
	SKIPL GETTY
	POPJ P,			;NO DISPLAY
	HRROI B,TYO1
	.IOT TYOC,[^P]
	.IOT TYOC,["S]	;SAVE CURSOR POSITION
	.IOT TYOC,[^P]
	.IOT TYOC,["T]
	.IOT TYOC,[^P]
	.IOT TYOC,["L]
	PUSHJ P,.DISBD
	.IOT TYOC,[^P]
	.IOT TYOC,["R]	;RESTORE CURSOR POSITION
	POPJ P,
]
.DISBD:	SKIPLE FFANCY
	SOUT [ASCII /			!/]	;3 TAB'S
	MOVE A,PLYN
	TRNE A,1
	SOUT [ASCII /BLACK!/]
	TRNN A,1
	SOUT [ASCII /WHITE!/]
	SOUT [ASCII / TO MOVE

!/]
.DISB1:	SKIPE FFANCY
	PUSHJ P,BDOUT
DISUP3:	POPJ P,


.INSRT OCDAGB >
]
