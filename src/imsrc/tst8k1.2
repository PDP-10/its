TITLE TST8K1 (PDS1 VERSION)

MAXADR=37777
PDS4=0
MOUSE=0
.LSTOF
.INSRT DSK:IMLAC;IMDEFS >
.LSTON
.ADDR.=1

; This program tests the DMS IMLAC 8K Display Addressing Mode.  If
; this mode is working, a small dot will appear near the center of
; the screen.  If the mode is not working, no dot will appear.  For
; this test to work the machine should also have suppressed grid
; hardware (or replace the DGD by a DJMS DWAIT routine).  This will
; be neccessary if the machine is an Alpha machine, in which case the
; dot will appear in the lower left as well.

; The program starts from location 10000 so it can be overlaid on top
; of SSV.

LOC 10000-100

; This part of the display list is in the lower 4K.
DLIST:	DHVS 1		; startup
MODE:	DADR		; turn on 8k addressing
	DGD		; part of set point
	DLXA 1000	; set point to center
	DLYA 1000	; of screen
        DJMP HI		; jump to high core
	DHLT		; halt

LOC 10000

START:	LAW DLIST	; display list address
	DLA		; load dpc
	DON		; start display
	DSN		; display off?
	JMP .-1		; no
	JMP START	; yes, loop

; This part of the display list is in the upper 4K.
HI:	DDSP		; display dot
	DHLT		; halt

END START
