TITLE Muddle Subsystem Bootstrap

IF1,[	DR==0
	PRINTC /Type in Subsystem (Save File) name: /
	.TTYMAC A
	SAVNAM=SIXBIT /A/
	TERMIN

	PRINTC /Type Y if you wish to have Save File directly restored: /
	.TTYMAC A
	IFSE A,Y,[
		PRINTC /Type Second Name of Save File: /
		.TTYMAC B
		SAVNM2==SIXBIT /B/
		TERMIN

		PRINTC /Type Sname of Save File: /
		.TTYMAC B
		SAVSNM==SIXBIT /B/
		TERMIN

		DR==1
		MUDSNM=SIXBIT /SYS1/
	TERMIN
]

IFE DR,[
	PRINTC /Type in MUDDLE, MUDnm or NMUDDL: /
	.TTYMAC A
	MUDNM2=SIXBIT /A/
	TERMIN

	PRINTC /Type in SYS or SYS1: /
	.TTYMAC A
	MUDSNM=SIXBIT /A/
	TERMIN

	SAVNM2=0
	SAVSNM=0
]
]

A=1
B=2
C=3
IN==1	;muddle channel
SV==2	;save file channel

; six-word area varies from boot to boot

ZERO:	IFE DR, SAVNAM		; arg to muddle in ac 0
	IFN DR, SV

MDLNM2:	IFE DR, MUDNM2		; nm2 of ts muddle file
	IFN DR, 0
MDLSNM:	MUDSNM			; sname of muddle file

SVFNAM:	SAVNAM			; nm1 of save file
SVFNM2:	SAVNM2			; nm2 of save file
SVFSNM:	SAVSNM			; snm of save file

MUD:

IFN DR,[.CALL	SAVOPN
	 .VALUE	[ASCIZ /: No SAVE file?

/]
	.IOT	2,[-1,,B]
	.ACCES	2,[0]
	MOVSI	C,'MUD
	ILDB	0,NUMPTR
	SUBI	0,40
	IDPB	0,NAMPTR
	ILDB	0,NUMPTR
	SUBI	0,40
	IDPB	0,NAMPTR
	MOVEM	C,MDLNM2
]
	.CALL	MUDOPN
	 .VALUE	[ASCIZ /: No TS MUDDLE ?

/]
	.SUSET	[.ROPTION,,A]
	TLNE	A,OPTBRK
	 SETOM	STUFF+BRKFLG
	MOVSI	17,STUFF
	BLT	17,17
	MOVE	ZERO
	JRST	1

IFN DR,[
SAVOPN:	SETZ
	SIXBIT /OPEN/
	[6,,SV]
	[SIXBIT /DSK/]
	SVFNAM
	SVFNM2
	SETZ SVFSNM
]
MUDOPN:	SETZ
	SIXBIT /OPEN/
	[4,,IN]
	[SIXBIT /DSK/]
	[SIXBIT /TS/]
	MDLNM2
	SETZ MDLSNM

NUMPTR:	440700,,B
NAMPTR:	220600,,C

STUFF:	OFFSET -.
	0
	.CALL	LOAD
	 .VALUE
	.IOT	IN,A
	.CLOSE	IN,
	SKIPE	BRKFLG
	 .BREAK	12,SSTART
	ADDI 	A,1
	JRST	@A

LOAD:	SETZ
	SIXBIT /LOAD/
	1000,,-1
	SETZI IN
BRKFLG:	0
SSTART:	..SSTA,,A
	OFFSET 0

	END MUD
