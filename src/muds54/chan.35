TITLE CHAN

RELOCATABLE

A"=1
B"=2
C"=3
D"=4
P"=17

.GLOBAL MOPEN,MIOT,MCLOSE

; MCHAN LOW-LEVEL I/O GARBAGE (PDL)-ORIGINAL (LIM)-ADDITIONS

;CALLING ROUTINE:	AC-A contains pointer to block of SIXBIT data
;			DIR ? DEV ? FNM1 ? FNM2 ? SNM
;RETURNED VALUE :	AC-A = <channel #, or -1 if no channel available>

MOPEN:	PUSH	P,C
	PUSH	P,D
CHNGET:	PUSH	P,B
	MOVE	C,[.STATUS 1,B]	; XXX patched after initialization
CHNLP:	XCT	C
	ANDI	B,77
	JUMPE	B,CHNFND	; found unused channel ?
	ADD	C,[40,,0]	; try another channel
	TLNE	C,740		; are all the channels used ?
	 JRST	CHNLP
	SETO	C,		; all channels used so C = -1
	POP	P,B
	JRST	CHNFUL
CHNFND:	LDB	C,[270400,,C]
	POP	P,B
	MOVE	D,OLAB
	DPB	C,BYTEM
	XCT	D
	SKIPA
	AOS	-2(P)		; successful  skip when returning
CHNFUL:	MOVE	A,C
	POP	P,D
	POP	P,C
	POPJ	P,

MIOT:	PUSH	P,D
	MOVE	D,ILAB
	DPB	A,BYTEM
	XCT	D
	POP	P,D
	POPJ	P,

MCLOSE:	PUSH	P,C
	PUSH	P,D
	MOVE	D,CLAB
	DPB	A,BYTEM
	XCT	D
	POP	P,D
	POP	P,C
	POPJ	P,

BYTEM:	270400,,D
OLAB:	.OPEN	0,(A)
ILAB:	.IOT	0,B
CLAB:	.CLOSE	0

END

