
<FLOAD "AR3:MUDGRF;BITS NBIN">

<USE "BITS">

<DEFINE GET-FILE ("OPTIONAL" (FILNME "SIG;PDP11 >") "AUX" (FILE <OPEN "READB" .FILNME>)) 
	<READB <SET TDATA <IUVECTOR <FILE-LENGTH .FILE>>> .FILE>
	<CLOSE .FILE>>   

<SET PDP11-SCALE </ 1.0 1000>>

<SET FREQ-DATA <RUCPX 16>>

<DEFINE NEXT-DATA-CHUNK () 
	#DECL ((FREQ-DATA VALUE) UCPX)
	<PUT .FREQ-DATA
	     2
	     <MAPR ,UVECTOR
		   <FUNCTION (I) 
			   #DECL ((I) <UVECTOR FLOAT>)
			   <PUT .I 1 0.0000000>
			   <* <FLOAT <NEXT-PDP11-WORD>> .PDP11-SCALE>>
		   <3 .FREQ-DATA>>>
	<CDFT .FREQ-DATA>>

<DEFINE FFTIZE () 
	#DECL ((END-FLAG) <OR FALSE T>)
	<SET END-FLAG <>>
	<SET FFTDATA <STACKFORM ,UVECTOR <NEXT-DATA-CHUNK> <NOT .END-FLAG>>>>    
 
<DEFINE NEXT-HALF-WORD () 
	#DECL ((TDTIME RIGHT) FIX)
	<COND (<0? .RIGHT>
	       <SET RIGHT 1>
	       <HLRE <NTH .TDATA <+ 1 .TDTIME>>>)
	      (ELSE
	       <SET RIGHT 0>
	       <HRRE <NTH .TDATA <SET TDTIME <+ 1 .TDTIME>>>>)>>

<DEFINE HALF-WORD-ACCESS (POS) 
	#DECL ((TDTIME RIGHT POS) FIX)
	<SET BYTE 0>
	<SET TDTIME </ .POS 2>>
	<SET RIGHT <MOD .POS 2>>>

<HALF-WORD-ACCESS 0>

<DEFINE HALF-WORD-POS () #DECL ((TDTIME RIGHT) FIX) <+ <* .TDTIME 2> .RIGHT>>

<DEFINE NEXT-PDP11-WORD ("AUX" (NEXT-SAMPLE <NEXT-HALF-WORD>)) 
	#DECL ((NEXT-SAMPLE) FIX)
	<COND (<G=? .NEXT-SAMPLE -4095> .NEXT-SAMPLE)
	      (<==? .NEXT-SAMPLE -31743>
	       <SET-PDP11-GAIN <NEXT-HALF-WORD>>
	       <NEXT-PDP11-WORD>)
	      (<==? .NEXT-SAMPLE -32511>
	       <SET-PDP11-RATE <NEXT-HALF-WORD>>
	       <NEXT-PDP11-WORD>)
	      (<==? .NEXT-SAMPLE -32768> <END-PDP11-DATA>)
	      (<==? .NEXT-SAMPLE -32255>
	       <PDP11-PAUSED <NEXT-HALF-WORD>>
	       <NEXT-PDP11-WORD>)
	      (<==? .NEXT-SAMPLE -31998>
	       <PDP11-PAUSED <HALVES <NEXT-HALF-WORD> <NEXT-HALF-WORD>>>
	       <NEXT-PDP11-WORD>)
	      (<==? .NEXT-SAMPLE -32756>
	       <PDP11-HEADER>
	       <NEXT-PDP11-WORD>)
	      (ELSE <ERROR UNRECOGNIZED-PDP11-FORMAT> <NEXT-PDP11-WORD>)>>    
 
<SET VERBOSE T>

<DEFINE PDP11-HEADER () 
	<OR <1? <HALF-WORD-POS>>
	    <ERROR PDP11-HEADER-NOT-AT-BEGINNING-OF-FILE>>
	<SET PDP11-DATE <ISTRING 9 '<ASCII <NEXT-BYTE>>>>
	<OR <0? <NEXT-BYTE>> <ERROR IMPROPER-PDP11-HEADER>>
	<SET PDP11-TIME <ISTRING 8 '<ASCII <NEXT-BYTE>>>>
	<SET PDP11-NAME <ISTRING 6 '<ASCII <NEXT-BYTE>>>>
	<AND .VERBOSE
	     <PRINC "PDP11 file ">
	     <PRINC .PDP11-NAME>
	     <PRINC "
produced at ">
	     <PRINC .PDP11-TIME>
	     <PRINC "
on ">
	     <PRINC .PDP11-DATE>>
	<NEXT-HALF-WORD>>

<DEFINE NEXT-BYTE () 
	#DECL ((BYTE) FIX)
	<CHTYPE <COND (<0? .BYTE>
		       <SET BYTE 1>
		       <GETBITS <SET OWORD <NEXT-HALF-WORD>> <BITS 8 8>>)
		      (ELSE <SET BYTE 0> <GETBITS .OWORD <BITS 8>>)>
		FIX>>

<DEFINE SET-PDP11-RATE (RATE) <SET PDP11-RATE .RATE>>  

<DEFINE END-PDP11-DATA () <HALF-WORD-ACCESS <- <HALF-WORD-POS> 1>> <SET END-FLAG T> 0>

<SET MAGNIT <IUVECTOR .LINE-WIDTH 0.0000000>>

<DEFINE PPLOT-LINE ("OPTIONAL" (FREQ-DATA .FREQ-DATA) "AUX" (CNT </ .CHUNK-SIZE 2>))
	<MAPR <>
	      <FUNCTION (MAGNP REALP IMAGP) 
		      <PUT .MAGNP
			   1
			   <SQRT <+ <* <1 .REALP> <1 .REALP>>
				    <* <1 .IMAGP> <1 .IMAGP>>>>>
		      <AND <0? <SET CNT <- .CNT 1>>> <MAPLEAVE>>>
	      .MAGNIT
	      <2 .FREQ-DATA>
	      <3 .FREQ-DATA>>
	<PUT .MAGNIT
	     <LENGTH .MAGNIT>
	     <NORMALIZE1 <NTH .MAGNIT .MAINF>>>
	<PLOT-NEW-LINE .MAGNIT>>
 
<DEFINE NORMALIZE1 (SIGL
		    "AUX" (NSIG-LEVEL
			   </ <- .SIGL .SIG-LOW> <- .SIG-HIGH .SIG-LOW>>))
	<SET SIG-DATA
	     <COND (<1? .SIG-DATA>
		    <COND (<L? .NSIG-LEVEL 0.5> 0)
			  (ELSE
			   <SET SIG-HIGH <AVERAGE .SIG-HIGH .SIGL .LEVEL-DAMP>>
			   1)>)
		   (<0? .SIG-DATA>
		    <COND (<G? .NSIG-LEVEL 0.5> 1)
			  (ELSE
			   <SET SIG-LOW <AVERAGE .SIG-LOW .SIGL .LEVEL-DAMP>>
			   0)>)>>>

<DEFINE AVERAGE (X Y "OPTIONAL" (WEIGHT 1)) 
	</ <+ <* .X .WEIGHT> .Y> <+ .WEIGHT 1>>>

<SET SIG-LOW 0.30000000>

<SET SIG-HIGH 1.2999999>

<SET LEVEL-DAMP 6>
