<PACKAGE "LSRHIL" ;"HI-Level interface to LSRTNS">

<ENTRY FULLNAME NICKNAME MAILBOX BIRTHDAY BD? BD-MAIL>

<USE "LSRTNS" "MFD" "NSTR" "TIMFCN">

;"Following message hasn't yet caused any changes in the code:

To:            SWG at MIT-DMS, PDL at MIT-DMS
From:          TAA at MIT-DMS (Timothy A. Anderson)
Subject:       LSRTNS
Message id:    <[MIT-DMS].76293>

Originated:    19 May 78 at 0133 EDT
Received:      19 May 78 at 0155 EDT

As I threatened, I've split out a couple of internal routines, which
might make some efficiencies possible in using lsrtns.  In particular,
LSR-ENTRY takes a string (or primtype word) and the FAST? switch; it
returns a false or a string which is the loser's data base entry.
LSR-EXTRACT takes a string as returned by LSR-ENTRY and a fix and returns
the fixth field as a string.  Both of these return string pointers into
pmap space, which are therefore only valid until the next call to FIELD
or LSR-ENTRY or whatever.
FIELD still returns a real string; it is essentially
<STRING <LSR-EXTRACT <LSR-ENTRY uname fast?> field>>."

<DEFINE NAME-PARTS (LUSER "OPTIONAL" (FAST? <>)
		  "AUX" (NM <FIELD .LUSER ,$NAME .FAST?>) CMA JR LFST LLST)
	#DECL ((VALUE) <VECTOR STRING [REST STRING]> (LUSER) STRING (FAST?) ANY
	       (NM CMA JR) <OR STRING FALSE> (LLST LFST) FIX)
	<COND (<OR <NOT .NM> <EMPTY? .NM>> [.LUSER])
	      (<SET CMA <MEMQ !\, .NM>>
	       <SET LLST <- <LENGTH .NM> <LENGTH .CMA>>>
	       <SET CMA <REST .CMA>>
	       <SET LFST <LENGTH .CMA>>
	       <COND (<SET JR <MEMQ !\, .CMA>>
		      <SET LFST <- .LFST <LENGTH .JR>>>)
		     (<SET JR "">)>
	       <REPEAT ()
		       <COND (<EMPTY? .CMA> <RETURN>)
			     (<MEMQ <1 .CMA> " 	">
			      <SET CMA <REST .CMA>>
			      <SET LFST <- .LFST 1>>)
			     (T <RETURN>)>>
	       <VECTOR <SUBSTRUC .CMA 0 .LFST>
		       <SUBSTRUC .NM 0 .LLST>
		       .JR>)
	      (T [.NM])>>

<DEFINE FULLNAME (LUSER "OPTIONAL" (FAST? <>) "AUX" (N <NAME-PARTS .LUSER .FAST?>))
	#DECL ((VALUE LUSER) STRING (FAST?) ANY)
	<COND (<LENGTH? .N 2> <STRING !.N>)
	      (T <STRING <1 .N> <ASCII 32> !<REST .N>>)>>

<DEFINE NICKNAME (LUSER "OPTIONAL" (FAST? <>)
		  "AUX" (NK <FIELD .LUSER ,$NICK .FAST?>))
	#DECL ((VALUE LUSER) STRING (FAST?) ANY)
	<COND (<AND .NK <NOT <EMPTY? .NK>>>
	       .NK)
	      (T
	       <NO-INITIAL <NO-INITIAL <1 <NAME-PARTS .LUSER .FAST?>>>>)>>

<DEFINE NO-INITIAL (NAME "AUX" INL LEN)
	#DECL ((VALUE NAME) STRING (INL) <OR STRING FALSE> (LEN) FIX)
	<COND (<AND <SET INL <MEMQ !\. .NAME>>	;"Look for initial."
		    <NOT <LENGTH? .NAME 5>>>	;"as in Wodehouse, P. G."
	       <SET LEN <- <LENGTH .NAME> <LENGTH .INL> 1>>
	       <COND (<AND <G? .LEN 0>
			   <==? <ASCII 32> <NTH .NAME .LEN>>>
		      <STRING <SUBSTRUC .NAME 0 <- .LEN 1>>
			      <REST .INL>>	;" X.")
		     (<AND <NOT <LENGTH? .INL 1>>
			   <==? <ASCII 32> <NTH .INL 2>>>
		      <STRING <SUBSTRUC .NAME 0 .LEN>
			      <REST .INL 2>>	;"X. ")
		     (T
		      <STRING <SUBSTRUC .NAME 0 .LEN>
			      <REST .INL 1>>	;"X.")>)
	      (<==? <ASCII 32> <2 .NAME>>
	       <REST .NAME 2>)
	      (<==? <ASCII 32> <NTH .NAME <- <LENGTH .NAME> 1>>>
	       <SUBSTRUC .NAME 0 <- <LENGTH .NAME> 2>>)
	      (T .NAME)>>

<DEFINE MAILBOX (LUSER "OPTIONAL" (FAST? <>) "AUX" N)
	#DECL ((VALUE LUSER) STRING (FAST?) ANY (N) <OR STRING FALSE>)
	<COND (<OR <NOT <SET N <FIELD .LUSER ,$NETA .FAST?>>>
		   <EMPTY? .N>
		   <=? .N "DM">>
	       .LUSER)
	      (<MEMQ !\@ .N> .N)
	      (T <STRING .LUSER !\@ .N>)>>

<DEFINE BIRTHDAY (LUSER "OPTIONAL" (FAST? <>)
		  "AUX" (BD <FIELD .LUSER ,$BRTH .FAST?>) M MV S X L)
	#DECL ((VALUE) <OR FALSE LIST> (LUSER) STRING (FAST? X) ANY (L) LIST
	       (BD S) <OR STRING FALSE> (M) FIX (MV) <OR FALSE VECTOR>)
	<AND .BD
	     <NOT <LENGTH? .BD 2>>
	     <OR <AND <SET MV <MEMBER <UPPERCASE <SUBSTRUC .BD 0 3>>
				      '["DEC" "NOV" "OCT" "SEP" "AUG" "JUL"
					"JUN" "MAY" "APR" "MAR" "FEB" "JAN"]>>
		      <SET M <LENGTH .MV>>
		      <SET S <MEMQ <ASCII 32> <REST .BD 3>>>>
		 <AND <SET S <MEMQ !\/ .BD>>
		      <TYPE? <SET L <LPARSE
				     <SUBSTRUC .BD 0 <- <LENGTH .BD> <LENGTH .S>>>>>
			     LIST>		;"for stupid compiler"
		      <TYPE? <SET X <1 .L>> FIX>
		      <SET M .X>
		      <SET BD <REST .S>>
		      <OR <AND <SET S <MEMQ !\/ .BD>>
			       <SET S <SUBSTRUC .BD 0 <- <LENGTH .BD> <LENGTH .S>>>>>
			  <SET S .BD>>>>
	     <TYPE? <SET L <LPARSE .S>> LIST>	;"for stupid compiler"
	     <TYPE? <SET X <1 .L>> FIX>
	     ((.M .X) ())>>

<DEFINE BD? ("OPTIONAL" (NAMVEC <OR <GET-MFD> '[]>)
			(DT <DTNOW>)
				  ;"Find birthday people of given date in given vector."
	     "AUX" (NAMLIS ()) MD)
	#DECL ((VALUE NAMLIS) <LIST [REST STRING]> (NAMVEC) <VECTOR [REST STRING]>
	       (DT) <LIST <LIST [REST FIX]>> (MD) <LIST [REST FIX]>)
	<SET DT <DTNORM .DT>>
	<SET MD <REST <1 .DT>>>
	<LSR-INIT>
	<MAPF <>
	      <FUNCTION (NAME "AUX" (B <BIRTHDAY .NAME T>))
	       <COND (<AND .B
			   <=? .MD <1 .B>>
			   <NOT <MEMBER .NAME .NAMLIS>>>
		      <SET NAMLIS <CONS .NAME .NAMLIS>>)>>
	      .NAMVEC>
	<LSR-FLUSH>
	.NAMLIS>

<DEFINE BD-MAIL	;"COMSAT-mail MSG to birthday people of given date."
	("OPTIONAL" (DT <DTNOW>)
		 (MSG '["
Happy birthday to you!

Happy birthday to you!

Happy birthday, dear " ",

Happy birthday to you!
"])
		 "AUX" OUTCHAN BDL C)
	#DECL ((DT) <LIST LIST> (MSG) VECTOR (BDL) <LIST [REST STRING]>
	       (C) <OR CHANNEL FALSE>)
      <SET DT <DTNORM .DT>>
      <MAPF <> <FUNCTION (DIR "AUX" N) #DECL ((N) <OR STRING FALSE>)
	<COND (<SET C <OPEN "PRINT" ".MAIL.;MAIL">>
	       <SET N <MAILBOX .DIR>>
	       <SET OUTCHAN .C>
	       <MAPF <>
		     ,PRINTSTRING
		     (
"FROM-PROGRAM:LSRHIL
FROM:"<XUNAME>"
RCPT:(".N")
TEXT;-1
" <1 .MSG> <NICKNAME .DIR> <2 .MSG>)>
	       <CLOSE .C>
	       <PRINT .DIR ,OUTCHAN>	;"informational Batch output")>>
       <SET BDL
	    <BD?
	     [!<GET-MFD "AI"> !<GET-MFD "DM"> !<GET-MFD "MC"> !<GET-MFD "ML">]
	     .DT>>>
      <COND (<TYPE? .C CHANNEL> <CLOSE .C>)>
      .BDL>

<ENDPACKAGE>