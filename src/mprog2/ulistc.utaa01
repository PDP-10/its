<SETGô! ·<CHANNEL "READB" "INQUIR;LSR1 1">
	  0	; "BUFFER IN CORE"
	  0	; "FIRST PAGE MAPPED IN"
	  0	; "AOBJN POINTER TO FIRST PAGE"]>

<GDECL (DB) <VECTOR CHANNEL FIX FIX FIX>>

"OFFSETS INTO DB FOR CRUFTIES"
<SETG DB-CH 1>
<SETG DB-PG 3>
<SETG DB-AC 5>
<SETG DB-PT 7>
<MANIFEST DB-CH DB-PG DB-AC DB-PT>

<TITLE ULIST-RESET>
	<DECLARE ("VALUE" VECTOR)>
	<PUSHJ	P* IRESET>
	<JRST	FINIS>

<INTERNAL-ENTRY IRESET 0>
	<SUBM	M* (P)>
	<MOVE	A* <MQUOTE <RGLOC DB T>>>
	<ADD	A* GLOTOP 1>
	<PUSH	TP* (A)>
	<PUSH	TP* 1(A)>
	<MOVE	A* DB-CH(A)>
	<DPUSH	TP* (A)>
	<MCALL	1 RESET>
	<MOVE	A* (TP)>
	<SKIPE	B* DB-PG(A)>
	 <JRST	DOMAP>
	<MOVEI	A* 2>
	<PUSHJ	P* PGFIND>
	<JUMPL	B* [<*VALUE>]>
	<MOVE	A* (TP)>
	<MOVEM	B* DB-PG(A)>		; "PAGE NUMBER IN CORE"
DOMAP	<LSH	B* 10>			; "MAKE AN ADDRESS"
	<HRLI	B* -1024>
	<MOVEM	B* DB-PT(A)>		; "SAVE IT AWAY"
	<MOVE	C* DB-CH(A)>		; "CHANNEL"
	<*CALL	[<SETZ>
		 <SIXBIT "ACCESS">
		 <(C) 1>
		 <SETZI	5>]>
	 <*LOSE	*1400*>
	<MOVE	D* [<(-1) E>]>
	<*CALL	[<SETZ>
		 <SIXBIT "IOT">
		 <(C) 1>
		 <SETZ	D>]>		; "PICK UP POINTER TO FIRST DATA PAGE"
	 <*LOSE	*1400*>
	<LSH	E* -10>			; "TURN INTO PAGE #"
	<MOVEM	E* DB-AC(A)>		; "SAVE IT AWAY"
	<MOVE	B* DB-PG(A)>		; "CORE PAGE"
	<HRLI	B* -2>
	<*CALL	[<SETZ>
		 <SIXBIT "CORBLK">
		 <MOVEI *10000*>	; "%CBNDR"
		 <MOVEI	-1>		; "%JSELF"
		 <B>
		 <(C) 1>		; "CHANNEL"
		 <SETZ	E>]>
	 <*LOSE	*1000*>
	<POP	TP* B>
	<POP	TP* A>
	<JRST	MPOPJ>

<SUB-ENTRY NEXT-ENTRY ("VALUE" <OR FALSE STRING>)>
	<PUSHJ	P* IENTRY>
	<JRST	FINIS>

<INTERNAL-ENTRY IENTRY 0>
	<SUBM	M* (P)>
	<MOVE	A* <MQUOTE <RGLOC DB T>>>
	<ADD	A* GLOTOP 1>
	<DPUSH	TP* (A)>
	<MOVE	B* (TP)>
	<SKIPE	DB-PG(B)>	; "NOTHING HERE?"
	 <JRST	GETCHN>
	<PUSHJ	P* IRESET>	; "SET EVERYTHING UP"
	<JRST	GETENT>
GETCHN	<MOVE	A* DB-CH(B)>
	<SKIPE	1(A)>		; "CHANNEL CLOSED?"
	 <JRST	GETENT>		; "NOPE"
	<PUSH	TP* DB-CH -1(B)>
	<PUSH	TP* A>
	<MCALL	1 RESET>
	<MOVE	B* (TP)>
GETENT	<MOVE	C* DB-PT(B)>	; "AOBJN POINTER TO NEXT ENTRY"
	<JUMPGE	C* NEXTPG>
	<HLRZ	A* (C)>		; "LENGTH IN WORDS"
	<JUMPE	A* [<MOVSI A* <TYPE-CODE FALSE>>
		    <MOVEI B* 0>
		    <JRST NEXTOT1>]>
	<SUBI	A* 1>
	<IMULI	A* 5>		; "LENGTH IN CHARS"
	<HRLI	A* <TYPE-CODE STRING>>
	<MOVE	D* C>
	<HRLI	D* *10700*>
	<PUSH	TP* A>
	<PUSH	TP* D>		; "SAVE ENTRY"
	<HLRZ	A* (C)>
	<HRLS	A>
	<ADDM	A* DB-PT(B)>	; "AOBJN"
NEXTOT	<POP	TP* B>
	<POP	TP* A>
NEXTOT1	<SUB	TP* [<(2) 2>]>
	<JRST	MPOPJ>
NEXTPG	<AOSN	E* DB-AC(B)>		; "SKIP IF STUFF LEFT"
	 <JRST	[<MOVSI	A* <TYPE-CODE FALSE>>
		 <MOVEI	B* 0>
		 <SUB	TP* [<(2) 2>]>
		 <JRST	NEXTOT1>	; "RETURN FALSE--ALL DONE"]>
	<SUB	C* [<(1024) 1024>]>	; "UPDATE POINTER"
	<MOVEM	C* DB-PT(B)>
	<MOVE	C* DB-PG(B)>
	<*CALL	[<SETZ>
		 <SIXBIT "CORBLK">
		 <MOVEI *10000*>
		 <MOVEI -1>
		 <MOVEI (C)>
		 <MOVEI -1>
		 <SETZI 1(C)>]>
	 <*LOSE	*1000*>		; "MAP PAGE 2 OF BUFFER TO PAGE 1"
	<MOVE	D* DB-CH(B)>
	<ADDI	E* 1>
	<*CALL	[<SETZ>
		 <SIXBIT "CORBLK">
		 <MOVEI	*10000*>
		 <MOVEI -1>
		 <MOVEI 1(C)>
		 <(D) 1>
		 <SETZ	E>]>	; "GET NEXT PAGE IN"
	 <JRST	ENDFIL>
	<JRST	GETENT>		; "DONE"
ENDFIL	<SETOM	DB-AC(B)>	; "THIS WAS THE LAST PAGE"
	<JRST	GETENT>

<SUB-ENTRY ULIST-FLUSH ("VALUE" ATOM)>
	<PUSHJ	P* IFLUSH>
	<JRST	FINIS>

<INTERNAL-ENTRY IFLUSH 0>
	<SUBM	M* (P)>
	<MCALL	0 ULIST-CLOSE>	; "CLOSE CHANNEL"
	<MOVE	C* <MQUOTE <RGLOC DB T>>>
	<ADD	C* GLOTOP 1>
	<MOVE	C* 1(C)>
	<SKIPN	B* DB-PG(C)>	; "PAGE # OF BUFFER"
	 <JRST	MPOPJ>		; "ALREADY FLUSHED"
	<MOVEI	A* 2>
	<SETZM	DB-PG(C)>	; "MARK AS FLUSHED"
	<PUSHJ	P* PGGIVE>
	<MOVSI	A* <TYPE-CODE ATOM>>
	<MOVE	B* <MQUOTE T>>
	<JRST	MPOPJ>