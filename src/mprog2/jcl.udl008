<PACKAGE "JCL">

<ENTRY JCL? READJCL>
		
<TITLE JCL?>
	<DECLARE ("VALUE" <OR FIX FALSE>)>

<PSEUDO <SETG *BREAK-12 #OPCODE *045500000000*>>
<PSEUDO <SETG *ROPTION *54*>>

	<PUSHJ	P* IJCLQ>
	<JRST	FINIS>

<INTERNAL-ENTRY IJCLQ 0>
	<SUBM	M* (P)>
	<*SUSET [<(*ROPTION) B>]>
	<MOVSI	A* <TYPE-CODE FIX>>
	<TLNN	B* *40000*>
	 <MOVSI	A* <TYPE-CODE FALSE>>
	<SETZM	B>
	<JRST	MPOPJ>

<SUB-ENTRY READJCL ("VALUE" <OR FALSE STRING>)>

; "Read the JCL line of a job into a string.  If an argument is
supplied and non-false, include the terminating character, whatever it
is."
	<JUMPGE	AB* RDJCL0>
	<PUSH	TP* (AB)>
	<PUSH	TP* 1(AB)>
	<PUSHJ	P* IREADJCL1>
	<JRST	FINIS>

RDJCL0	<PUSHJ	P* IREADJCL0>	
	<JRST	FINIS>

<INTERNAL-ENTRY IREADJCL0 0>
	<SUBM	M* (P)>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* [<0>]>
	<JRST	RDJCLC>

<INTERNAL-ENTRY IREADJCL1 1>
	<SUBM	M* (P)>
RDJCLC	<HRLZI	A* *ROPTION>	; "See if there is one"
	<*SUSET	A>
	<TLNN	*40000*>
	 <JRST	NOJCL>		; "No line there"
	<MOVEI	A* 16>
	<MOVEI	O* IBLOCK>
	<PUSHJ	P* RCALL>
	<PUSH	TP* [<80 (<TYPE-CODE STRING>)>]>
	<HRLI	B* *10700*>
	<SUBI	B* 1>
	<PUSH	TP* B>
	<ADDI	B* 1>
	<HRLI	B* 5>
	<PUSH	P* B>
	<HRLZI	A* *400005*>
	<PUSH	P* A>
	<MOVEI	A* -1 (P)>
	<HRLI	A* -2>
	<*BREAK-12  A>
	<SUB	P* [<2 (2)>]>
	<MOVE	A* (TP)>
	<SETZM	C>
CNTLP	<ILDB	B* A>		; "Look for end of string"
	<JUMPE	B* FNDTRM>	; "Stop on ^@, ^C or carriage return"
	<CAIE	B* 3>
	 <CAIN	B* 13>
	  <SKIPA>
	<AOJA	C* CNTLP>

FNDTRM	<GETYP	D* -3(TP)>
	<CAIE	D* <TYPE-CODE FALSE>>
	 <ADDI	C* 1>		; "yes, include it"

FNDEND	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* C>
	<MCALL	1 ISTRING>
	<PUSH	TP* A>
	<PUSH	TP* B>
	<MOVE	C* -2(TP)>	; "Now copy the string"
	<HRRZ	D* A>
	<JUMPE	D* JCLX>	; "If empty - done"

CPYLP	<ILDB	A* C>
	<IDPB	A* B>
	<SOJG	D* CPYLP>
	<MOVE	A* -1(TP)>	; "Done - return the string"
	<MOVE	B* (TP)>
	<SUB	TP* [<4(4)>]>
	<JRST	JCLX>

NOJCL	<MOVE	A* <TYPE-WORD FALSE>>	; "No line there - return false"
	<SETZ	B*>
JCLX	<SUB	TP* [<2(2)>]>
	<JRST	MPOPJ>

<END>

<ENDPACKAGE>
