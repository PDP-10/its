<RPACKAGE "EMONIT">

<USE-TOTAL "EDIT">

<USE "MONITOR">

<DEFINE MONEDIT (COMSTR WHERE POS
		 "AUX" (EDIT-ITEM <IN .WHERE>) ITM (JUNKSTR ,JUNKSTR) TEMP MODE HOLD) 
   #DECL ((VALUE) ANY (COMSTR JUNKSTR MODE) STRING (WHERE) LOCATIVE
	  (ITM EDIT-ITEM) <PRIMTYPE LIST> (POS) FIX (TEMP) ANY (HOLD) <OR BREAK LIST>)
   <COND
    (<OR <AND <=? .COMSTR "RM"> <SET MODE "READ">>
	 <AND <=? .COMSTR "WM"> <SET MODE "WRITE">>
	 <AND <=? .COMSTR "RW"> <SET MODE "RW">>>
     <SET TEMP <INTREAD .INCHAN>>
     <COND (<NOT <TYPE? .TEMP ATOM>>
	    <ERR "ERROR, BAD ARG TO MONITOR">)
	   (<==? .POS <LENGTH .EDIT-ITEM>>
	    <ERR "ERROR, CAN'T PUT MONITOR AT RIGHT EDGE">)
	   (T
	    <COND (<AND <TYPE? <SET ITM <NTH .EDIT-ITEM <+ .POS 1>>> BREAK>
			<==? <1 .ITM> EMONITOR>
			<==? <4 .ITM> .TEMP>>
		   <SET HOLD .ITM>
		   <PUT .HOLD 3 .MODE>
		   <PUTREST <REST .HOLD 3> ()>)
		  (T
		   <PUT .EDIT-ITEM
			<+ .POS 1>
			<SET HOLD
			     <CHTYPE (EMONITOR .ITM .MODE .TEMP) BREAK>>>
		   <PUTPROP .TOP-ITEM
			    BREAKS
			    <LIST <AT .EDIT-ITEM <+ 1 .POS>>
				  !<GETPROP .TOP-ITEM BREAKS>>>)>
	    <SET HOLD <REST .HOLD 3>>
	    <REPEAT ()
		    <READSTRING .JUNKSTR .INCHAN ,GOOD-CHRS>
		    <COND (<==? <NEXTCHR .INCHAN> !\> <RETURN>)>
		    <PUTREST .HOLD <SET HOLD (<READ .INCHAN>)>>>)>)>>

<DEFINE EMONITOR ('ITM 'MODE 'ATM "ARGS" FROBS)
	#DECL ((ITM) ANY (MODE) STRING (ATM) ATOM (FROBS) LIST)
	<EVAL <FORM MONITOR .MODE .ATM LVAL !.FROBS>>
	<EVAL .ITM>>

<SETG MONEDIT-TABLE
      <MAPF ,VECTOR
	    <FUNCTION (STR) <MAPRET .STR ,MONEDIT>>
	    '["RM" "WM" "RW"]>>

<COND (<GASSIGNED? EDIT-TABLE> <SETG EDIT-TABLE [!,EDIT-TABLE !,MONEDIT-TABLE]>)
      (<SETG EDIT-TABLE ,MONEDIT-TABLE>)>

<ENDPACKAGE>
