<PACKAGE "DDT-STYLE">

<ENTRY DDT-ADRL DDT-SENDER DDT-ADR-TO MSG-FROM DDT-ADR DDT-LIST
       DDT-TEXT-LIST QUICK-DATE>

<USE "USRUTI" "M.DAC" "M.DOUT">

<DEFINE DDT-ADRL (UUL CC "OPTIONAL" (H? T) (L? <>) "AUX" UU U)
	#DECL ((UUL) LIST (CC) CHANNEL (U UU) STRING)
	<REPEAT ()
		<COND (<EMPTY? .UUL> <RETURN>)>
		<SET UU <1 .UUL>>
		<COND (.H?
		       <SET U <EXTRACT-RCVR .UU>>
		       <COND (<==? 70 <EXTRACT-HOST .UU>>
			      <COND (.L? <SET UU <STRING .U "@DM">>)
				    (<SET UU .U>)>)>)>
		<COND (<G? <+ <14 .CC> <LENGTH .UU>> 70>
		       <TERPRI .CC>)>
		<PRINC .UU .CC>
		<COND (<NOT <LENGTH? .UUL 1>> <PRINC ", " .CC>)>
		<SET UUL <REST .UUL>>>>

<DEFINE DDT-ADR-TO (UU CC "AUX" (AC <DRD .ADR "ACTION-TO">))
	#DECL ((UU) <OR STRING VECTOR> (CC) CHANNEL)
	<COND (<OR <NOT .AC> <NOT <TYPE? .AC LIST>>>)
	      (<OR <NOT <LENGTH? .AC 1>>
		   <NOT <MEMBER .ADR .AC>>>
	       <PRINC "
To: " .CC>
	       <DDT-ADRL .AC .CC T T>)>>

<DEFINE DDT-SENDER (UUL CC "AUX" (FV <DRD .ADR "FROM">))
	#DECL ((UUL) <OR LIST STRING> (CC) CHANNEL)
	<COND (<TYPE? .UUL STRING> <SET UUL (.UUL)>)>
	<COND (<OR <NOT <LENGTH? .UUL 1>>
		   <NOT <MEMBER .FV .UUL>>>
	       <PRINC " (Sent by " .CC>
	       <DDT-ADRL .UUL .CC T T>
	       <PRINC ")" .CC>)>>

<DEFINE MSG-FROM (FF CC)
	#DECL ((FF) STRING (CC) CHANNEL)
	<DDT-ADRL (.FF) .CC T T>>

<DEFINE DDT-TEXT-LIST (TL CC)
	#DECL ((TL) LIST (CC) CHANNEL)
	<MAPF <>
	      <FUNCTION (TT)
		<COND (<NOT <0? <14 .CC>>> <CRLF>)>
		<PRINC .TT .CC>
		<COND (<NOT <0? <14 .CC>>> <CRLF>)>
		<PRINC "-----" .CC>>
	      .TL>>

<DEFINE DDT-LIST (LL CC)
	<DDT-ADRL .LL .CC <> <>>>

<DEFINE DDT-ADR (UU CH)
	#DECL ((UU) <OR STRING VECTOR> (CH) CHANNEL)
	<DDT-ADRL (.UU) .CH T T>>

<DEFINE QUICK-DATE (DT CHAN "AUX" (DAT <1 .DT>))
	#DECL ((DT) <LIST [2 <LIST [3 FIX]>]> (CHAN) CHANNEL
	       (DAT) <LIST [3 FIX]>)
	<PN <2 .DAT> .CHAN>
	<PRINC !\/ .CHAN>
	<PN <3 .DAT> .CHAN>
	<PRINC !\/ .CHAN>
	<PN <1 .DAT> .CHAN>
	<PRINC !\  .CHAN>
	<SET DAT <2 .DT>>
	<PN <1 .DAT> .CHAN>
	<PRINC !\: .CHAN>
	<PN <2 .DAT> .CHAN>
	<PRINC !\: .CHAN>
	<PN <3 .DAT> .CHAN>>

<DEFINE PN (N CHAN)
	#DECL ((N) FIX (CHAN) CHANNEL)
	<AND <L? .N 10> <PRINC !\0 .CHAN>>
	<PRINC .N .CHAN>>

<ENDPACKAGE>

