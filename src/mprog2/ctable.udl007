
<PACKAGE "CTABLE">

<ENTRY CTABLE>

<USE "SYMBOLS" "MACROS">

<DEFMAC CTABLE ('P1 'P2 'EXPL "ARGS" GOODIES "AUX" (DFL <>) (FH <>)) 
   #DECL ((P1 P2 EXPL FH) ANY (GOODIES) LIST (DFL) <OR FIX FALSE>)
   <REPEAT ((CNT 0) (TCNT 1) (CMD? T) (STABT (T)) (STAB .STABT) (CTABT (T))
	    (CTAB .CTABT) (OTABT (T)) (OTAB .OTABT) (PTABT (T)) (PTAB .PTABT))
	   #DECL ((STAB STABT CTAB CTABT OTABT OTAB PTABT PTAB) LIST
		  (CNT TCNT) FIX)
	   <COND (<EMPTY? .GOODIES>
		  <COND (<NOT .CMD?> <ERROR UNBALANCED-TABLE>)>
		  <RETURN <FORM PROG
				(TB SVALUE)
				#DECL ((SVALUE) ANY (TB) FIX)
				<FORM SETG FAIL-HOOK .FH>
				<FORM SET SVALUE
					     <FORM SYMBOL
						   .DFL
						   .P1
						   .P2
						   .EXPL
						   <FORM
						     QUOTE
						     <VECTOR !<REST .STABT>>>
						   !<REST .PTABT>>>
				<FORM COND
				      ('<TYPE? .SVALUE FIX>
				       '<SET TB
					     <CHTYPE <GETBITS .SVALUE
							      <BITS 18 18>>
						     FIX>>
				       '<SET SVALUE
					     <CHTYPE <ANDB .SVALUE *777777*>
						     FIX>>
				       <COND (<1? .TCNT>
					      <FORM CASE
						    ',==?
						    '.SVALUE
						    !<REST .CTABT>>)
					     (ELSE
					      <FORM CASE
						    ',==?
						    '.TB
						    (1 
						     <FORM CASE
							   ',==?
							   '.SVALUE
							   !<REST .CTABT>>)
						    !<REST .OTABT>>)>)
				      '(ELSE .SVALUE)>>>)>
	   <SET G <1 .GOODIES>>
	   <COND (.CMD?
		  <COND (<==? .G *>
			 <SET DFL
			      <CHTYPE <PUTBITS <+ .CNT 1>
					       <BITS 18 18>
					       .TCNT>
				      FIX>>)
			(<TYPE? .G LIST>
			 <COND (<==? <1 .G> FAIL-HOOK>
				<SET FH <2 .G>>)
			       (ELSE
				<PUTREST .PTAB <SET PTAB (<1 .G>)>>
				<PUTREST .OTAB
					 <SET OTAB
					      ((<SET TCNT <+ .TCNT 1>>
						!<REST .G>))>>)>)
			(<TYPE? .G ATOM STRING>
			 <PUTREST .STAB <SET STAB (.G)>>
			 <SET CNT <+ .CNT 1>>
			 <SET CMD? <>>)
			(ELSE <ERROR BAD-SYMBOL .G>)>)
		 (ELSE <PUTREST .CTAB <SET CTAB ((.CNT .G))>> <SET CMD? T>)>
	   <SET GOODIES <REST .GOODIES>>>>

<ENDPACKAGE>
