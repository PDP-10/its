"MAKE-DUV takes a vector of words represented as either strings or uvector whose
 first element is the string and an optional output file and writes a data file to
 the output file to be used by the CORRECT-ERROR package"

<PACKAGE "MAKE-DUV">

<ENTRY MDICT DUV MAKE-DUV>

<USE "MDTABLE" "CORRECT-ERROR" "HAVERFORM" "SORTX" "DATA-SAVE">

<SETG DUVV <IUVECTOR <+ 509 1> ''()>>

<SETG DUV  <IUVECTOR <+ 509 1> ''![]>>

<SETG TUV <UVECTOR 0 0>>

<GDECL (DUV) <UVECTOR [509 UVECTOR][REST UVECTOR]>
       (DUVV) <UVECTOR [509 LIST]  [REST LIST]>
       (TUV) <UVECTOR FIX FIX>>

<DEFINE ADD-ECITEM (HF1 HF2 DUVV "AUX" X Y)
	#DECL ((HF1 HF2 X Y) FIX (DUVV) <UVECTOR [REST LIST]>)
	<SET Y <+ <DMOD .HF1 .HF2 509> 1>>
	<SET X	  <DDIV .HF1 .HF2 509>>
	<OR <MEMQ .X <NTH .DUVV .Y>>
	    <PUT .DUVV .Y (.X !<NTH .DUVV .Y>)>>>

<DEFINE MAKE-DUV (WRDS "OPTIONAL" (OFILE <>) "AUX" HF1 HF2 (MOST 0) )
	#DECL ((WRDS) UVECTOR (OFILE) <OR FALSE STRING>
	       (HF2) <OR FIX UVECTOR> (MOST HF1) FIX)
	<SETG DUVV <IUVECTOR <+ 509 1> ''()>>
	<MAPF <>
	      <FUNCTION (W)
		   #DECL ((W) <OR STRING VECTOR>)
		   <COND (<TYPE? .W STRING>
			  <SET HF2 <DENSER <RLS-FOR-WORD .W> ,TUV>>)
			 (<LENGTH? .W 2>
			  <SET HF2 <DENSER <RLS-FOR-WORD <1 .W>> ,TUV>>)
			 (ELSE <SET HF2 <DENSER <3 .W> ,TUV>>)>
		   <ADD-ECITEM <SET HF1 <1 .HF2>>
			       <SET HF2 <2 .HF2>>
			       ,DUVV>>
	      .WRDS>
	<MAPR <>
	      <FUNCTION (U1 U2) #DECL ((U1) <UVECTOR UVECTOR> (U2) <UVECTOR LIST>)
			<PUT .U1 1 <UVECTOR !<1 .U2>>>
			<SORT <> <1 .U1>>
			<SET MOST <MAX .MOST <LENGTH <1 .U1>>>>
			<PUT .U2 1 ()>>
	      <SETG DUV  <IUVECTOR <+ 509 1> ''![]>>
	      ,DUVV>
		<GUNASSIGN DUVV>
	<SETG MDICT ,DUV>
	<GUNASSIGN DUVV>
	<COND (.OFILE <SAVE-DATA MDICT .OFILE>)>
	.MOST>

<ENDPACKAGE>
