<PACKAGE "UNDUMP">

<ENTRY START NAME SKIP LOD>

<USE "STR">

<SETG  TC <>>
<GDECL (TC) <OR FALSE CHANNEL>>

<SETG SBUF <IUVECTOR 4 '0>>
<SETG NBUF <IUVECTOR 6 '0>>
<SETG TBUF <IUVECTOR 1024 '0>>
<GDECL (SBUF NBUF TBUF) <UVECTOR FIX>>

<DEFINE START ()
	<COND (,TC <CLOSE ,TC>)>
	<COND (<NOT <SETG TC <OPEN "READB" "MT0:">>>
	       <ERROR CANT-OPEN-TAPE-CHANNEL START>)
	      (<NOT <==? 4 <READB ,SBUF ,TC>>>
	       <ERROR E-O-T START>)
	      (<NOT <==? <1 ,SBUF> *777774000000*>>
	       <ERROR BAD-FORMAT START>)
	      (T <PRINC " TAPE #"> <PRINC <CHTYPE <GETBITS <2 ,SBUF> <BITS 18 18>> FIX>>
		 <PRINC " REEL #"> <PRINC <CHTYPE <GETBITS <2 ,SBUF> <BITS 18  0>> FIX>>
		 <PRINC " (DATED "> <PRINC <SIXTOS <3 ,SBUF>>> <PRINC ") OF ">
		 <PRINC <COND (<1? <4 ,SBUF>> "INCREMENTAL")
			      (<0? <4 ,SBUF>> "RANDOM")
			      (<==? -1 <4 ,SBUF>> "FULL")>>
		 <PRINC " DUMP">
		 <CRLF>)>>

<DEFINE NAME ()
	<COND (<NOT ,TC>
	       <ERROR NO-TAPE-CHANNEL-OPEN NAME>)
	      (<NOT <==? 6 <READB ,NBUF ,TC>>>
	       <ERROR E-O-T START>)
	      (<NOT <==? <1 ,NBUF> *777772000000*>>
	       <ERROR BAD-FORMAT NAME>)
	      (T <PRINC !\<> <PRINC <SIXTOS <2 ,NBUF>>> <PRINC !\>>
		 <PRINC <SIXTOS <3 ,NBUF>>> <PRINC !\.> <PRINC <SIXTOS <4 ,NBUF>>>
		 <COND (<L? <5 ,NBUF> 100>
			<PRINC " PACK #"> <PRINC <5 ,NBUF>>)
		       (T <PRINC " (LINK)">)>
		 <PRINC " CREATED "> <PRINC <CDATE <6 ,NBUF>>>
			 <PRINC !\ > <PRINC <CTIME <6 ,NBUF>>>
		 <CRLF>)>>

<DEFINE SKIP ()
	<COND (<NOT ,TC>
	       <ERROR NO-TAPE-CHANNEL-OPEN SKIP>)
	      (T
	       <REPEAT ()
		       <COND (<NOT <==? <READB ,TBUF ,TC> <LENGTH ,TBUF>>>
			      <RESET ,TC>
			      <RETURN>)>>)>>

<DEFINE LOD ("AUX" DC)
	<COND (<NOT ,TC>
	       <ERROR NO-TAPE-CHANNEL-OPEN LOD>)
	      (<NOT <SET DC <OPEN "PRINTB" <SIXTOS <3 ,NBUF>> <SIXTOS <4 ,NBUF>>
					   "DSK" <SIXTOS <2 ,NBUF>>>>>
	       <ERROR CANT-OPEN-DISK-CHANNEL LOD>)
	      (T
	       <REPEAT (N)
		       <COND (<NOT <==? <SET N <READB ,TBUF ,TC>> <LENGTH ,TBUF>>>
			      <PRINTB <SUBSTRUC ,TBUF 0 .N> .DC>
			      <RESET ,TC>
			      <RETURN <CLOSE .DC>>)
			     (T <PRINTB ,TBUF .DC>)>>)>>

;"Following are snarfed from DIR package:"

<SETG MONTH <BITS 4 23>>

<SETG DAY <BITS 5 18>>

<SETG YEAR <BITS 7 27>>

<SETG CREATION-TIME <BITS 18>>

<DEFINE FIELD (W B)
	#DECL ((W) <PRIMTYPE WORD> (B) BITS (VALUE) FIX)
	<CHTYPE <GETBITS .W .B> FIX>>

<DEFINE CDATE (W)
	#DECL ((W) <PRIMTYPE WORD> (VALUE) <LIST [3 FIX]>)
	(<FIELD .W ,YEAR> <FIELD .W ,MONTH> <FIELD .W ,DAY>)>

<DEFINE CTIME (W "AUX" H M S)
	#DECL ((W) <PRIMTYPE WORD> (H M S) FIX (VALUE) <LIST [3 FIX]>)
	<SET W </ <FIELD .W ,CREATION-TIME> 2>>
	<SET H </ .W 3600>>
	<SET W <MOD .W 3600>>
	<SET M </ .W 60>>
	<SET S <MOD .W 60>>
	(.H .M .S)>

<ENDPACKAGE>
