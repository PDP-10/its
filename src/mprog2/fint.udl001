<PACKAGE "FINT">

<ENTRY FINT INT-INFO>

<DEFINE INT-INFO (I "AUX" (INT <>) (CHN <>) (OUTCHAN .OUTCHAN) IH H A)
	#DECL ((I) ANY (INT) <OR ATOM FALSE> (CHN) <OR CHANNEL FALSE>
	       (IH) <OR IHEADER FALSE> (H) HANDLER (A) ANY (OUTCHAN) CHANNEL)
	<COND (<TYPE? .I ATOM>
	       <SET INT <LOOKUP <SPNAME .I> <INTERRUPTS>>>)
	      (<TYPE? .I STRING>
	       <SET INT <LOOKUP .I <INTERRUPTS>>>)
	      (<TYPE? .I CHANNEL>
	       <SET CHN .I>
	       <SET INT <LOOKUP "CHAR" <INTERRUPTS>>>)>
	<CRLF>
	<PRINC <COND (.INT .INT) (ELSE .I)>>
	<PRINC !\ >
	<COND (.CHN
	       <PRINC !\">
	       <PRINC <9 .CHN>>
	       <PRINC !\:>
	       <PRINC <10 .CHN>>
	       <PRINC !\;>
	       <PRINC <7 .CHN>>
	       <PRINC !\ >
	       <PRINC <8 .CHN>>
	       <PRINC "\" ">)>
	<COND (.INT
	       <COND (<SET IH
			   <GETPROP <COND (.CHN .CHN) (ELSE .INT)>
				    INTERRUPT>>
		      <COND (<NOT <0? <CHTYPE <2 .IH> FIX>>>
			     <PRINC "(disabled) ">)>
		      <PRINC "Priority ">
		      <PRINC <4 .IH>>
		      <SET H <3 .IH>>
		      <REPEAT ()
			      <COND (<EMPTY? .H> <RETURN>)>
			      <CRLF>
			      <PRINC "  ">
			      <COND (<TYPE? <SET A <3 .H>> RSUBR RSUBR-ENTRY>
				     <PRIN1 <2 .A>>)
				    (ELSE
				     <PRINC !\[>
				     <PRIN1 <TYPE .A>>
				     <PRINC !\]>)>
			      <COND (<N==? <STATE <4 .H>> ILLEGAL>
				     <PRINC !\ >
				     <PRIN1 <4 .H>>)>
			      <SET H <1 .H>>>)
		     (ELSE
		      <PRINC "No IHEADER.">)>
	       <CRLF>)
	      (ELSE
	       <PRINC "No such interrupt exists.">
	       <CRLF>
	       <>)>>

<DEFINE FINT ("OPTIONAL" (I <>))
	#DECL ((I) ANY)
	<COND (<NOT .I>
	       <MAPF <>
		     <FUNCTION (L)
			  #DECL ((L) LIST)
			  <MAPF <>
				<FUNCTION (A)
				     #DECL ((A) ATOM) 
				     <COND (<GETPROP .A INTERRUPT>
					    <INT-INFO .A>)>>
				.L>>
		     <INTERRUPTS>>)>
	<COND (<OR <NOT .I>
		   <=? .I "CHAR">
		   <AND <TYPE? .I ATOM> <=? <SPNAME .I> "CHAR">>>
	       <MAPF <>
		     <FUNCTION (A)
			  #DECL ((A) CHANNEL)
			  <COND (<GETPROP .A INTERRUPT>
				 <INT-INFO .A>)>>
		     <CHANLIST>>)
	      (.I <INT-INFO .I>)>
	T>

<ENDPACKAGE>
