<PACKAGE "SMCOD">

<ENTRY SMCOD>

<USE "MDTYPE" "MDTABLE">

"Search a MCOD for a given Maude decoding.  Takes the MCOD, a STRING
to search for, and (optionally) a flag saying whether to pay attention to
the distinction between letter and word spaces (defaultly 'yes').  Returns
the MCOD rested down to the END of the string, or FALSE.
"

<DEFINE SMCOD (COD STR "OPTIONAL" (WRD? T)
	       "AUX" (LAST <>) MS L (DDC 0) (DDI 0))
   #DECL ((COD) MCOD (STR) STRING (DDI DDC) FIX (X) <PRIMTYPE UVECTOR> (MS) ELT)
   <REPEAT ()
     <COND (<EMPTY? .COD> <RETURN <>>)>
     <SET MS <MSD <1 .COD>>>
     <SET DDC <+ .DDC 1>>
     <SET DDI
	  <+ <* .DDI 2> <COND (<==? <MTYP .MS> DOT> 0) (1)>>>
     <COND (<OR <==? .DDC 6>
		<==? <STYP .MS> LTR>
		<==? <STYP .MS> WRD>
		<LENGTH? .COD 1>>
	    <SET L <CHAR-FOR-RLS .DDC .DDI>>
	    <SET DDC 0>
	    <SET DDI 0>
	    <COND (<==? <MEMBER .L .STR> .STR>
		   <SET STR <REST .STR <LENGTH .L>>>
		   <AND <EMPTY? .STR> <RETURN <MREST .COD>>>
		   <OR .LAST <SET LAST .COD>>)
		  (.LAST <SET COD .LAST> <SET LAST <>> <AGAIN>)
		  (ELSE <SET STR <TOP .STR>>)>
	    <COND (<AND .WRD? <OR <==? <STYP .MS> WRD> <LENGTH? .COD 1>>>
		   <COND (<==? <1 .STR> <ASCII 32>>
			  <SET STR <REST .STR>>
			  <AND <EMPTY? .STR> <RETURN <MREST .COD>>>
			  <OR .LAST <SET LAST .COD>>)
			 (.LAST <SET COD .LAST> <SET LAST <>> <AGAIN>)
			 (ELSE <SET STR <TOP .STR>>)>)>)>
     <SET COD <MREST .COD>>>>

<ENDPACKAGE>