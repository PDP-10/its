<PACKAGE "TN">

<ENTRY TELNET TN-REMOTE TN-CLOSE TNPAIR TNHOST TN-TTY MDL-TTY>

<ENTRY DEBUG? TNSLAVE? SILENT?>

<USE "NETFTP" "HOSTS" "TTY">

<SETG SILENT? <>>
<SETG DEBUG? <>>
<SETG TNSLAVE? <>>

<FLOAD "LIBRM2;IOT8 NBIN">

<SETG WILL 251>
<SETG WONT 252>
<SETG DO 253>
<SETG DONT 254>
<MANIFEST WILL WONT DO DONT>

<SETG TNPAIR <>>
<SETG TNHOST <HOST "MIT-XX">>
<GDECL (TNPAIR) <OR FALSE <LIST [2 CHANNEL]>>
       (TNHOST) FIX
       (OTTY NTTY) <UVECTOR [3 WORD]>>

<DEFINE TELNET ("OPTIONAL" (HST ,TNHOST) (SOCK 23)
		 "AUX" (TN ,TNPAIR) TNI TNO CHRI (OUTCHAN .OUTCHAN))
	#DECL ((SOCK HST) FIX (TN) <OR FALSE <LIST [2 CHANNEL]>>
	       (OUTCHAN TNI TNO) <CHANNEL FIX>)
	<PROG ()
	      <COND (.TN)
		    (<SET TN <ICP .HST .SOCK>>
		     <SETG TN-INT <ON "CHAR" ,TN-INPUT 10 0 ,INCHAN>>
		     <SETG TNPAIR .TN>)
		    (ELSE <RETURN .TN>)>
	      <SET TNI <1 .TN>>
	      <SET TNO <2 .TN>>
	      <TN-TTY>
	      <IAC .TNO ,DO 1>
	      <IAC .TNO ,WILL 3>
	      <IAC .TNO ,DO 3>
	      <COND (<NOT ,TNSLAVE?>
		     <REPEAT ()
			     <COND (<NOT <TN-OUTPUT .TNI .TNO>>
				    <RETURN>)>>
		     <MDL-TTY>
		     "CLOSED")
		    (ELSE .TN)>>>

<DEFINE MDL-TTY ()
	<OFF ,TN-INT>
	<SETG TN-INT <>>
	<TTY-SET ,OTTY>
	<TTYECHO ,INCHAN T>>

<DEFINE TN-REMOTE (SCRIPT "OPTIONAL" (TN ,TNPAIR)
		"AUX" (TNI <1 .TN>) (TNO <2 .TN>))
	#DECL ((SCRIPT) VECTOR (TN) <LIST [2 CHANNEL]>
	       (TNI TNO) CHANNEL)
	<MAPF <>
	      <FUNCTION (LINE "AUX" (GOAL <1 .LINE>) VERSE C O)
		   #DECL ((LINE) <VECTOR STRING STRING> (GOAL VERSE) STRING
			  (O) <OR FALSE FIX> (C) CHARACTER)
		   <REPEAT ()
			   <COND (<SET O <TN-OUTPUT .TNI .TNO>>
				  <SET C <CHTYPE .O CHARACTER>>)
				 (ELSE <MAPLEAVE T>)>
			   <PROG ()
				 <COND (<OR <EMPTY? .GOAL>
					    <G=? <CHTYPE .C FIX> 128>>)
				       (<==? .C <1 .GOAL>>
					<SET GOAL <REST .GOAL>>)
				       (<N==? .GOAL <TOP .GOAL>>
					<SET GOAL <TOP .GOAL>>
					<AGAIN>)>>
			   <COND (<EMPTY? .GOAL>
				  <OR <EMPTY? <SET VERSE <2 .LINE>>>
				      <PRINC .VERSE .TNO>>
				  <COND (<==? <LENGTH .LINE> 3>
					 <SETG SILENT? <3 .LINE>>)>
				  <RETURN>)>>>
	      .SCRIPT>
	T>

<DEFINE TN-OUTPUT (TNI TNO "AUX" C)
	#DECL ((TNI TNO) CHANNEL (C) FIX)
	<COND (<OR <0? <1 .TNI>> <0? <1 .TNO>>> <>)
	      (<G? <SET C <CHTYPE <READCHR .TNI> FIX>> 127>
	       <IACJUNK .TNI .TNO>
	       .C)
	      (ELSE
	       <OR ,SILENT? <SIOT <ASCII .C> .OUTCHAN>>
	       .C)>>

<DEFINE TN-INPUT (CHR CHAN)
	#DECL ((CHR) CHARACTER (CHAN) CHANNEL)
	<COND (<AND ,TNPAIR <NOT <0? <1 <2 ,TNPAIR>>>>>
	       <SIOT .CHR <2 ,TNPAIR>>
	       <COND (<==? <TYI .CHAN> !\>
		      <TN-COMMAND .CHAN>)> 
	       #DISMISS T)>>

<DEFINE TN-COMMAND (CHAN "AUX" CHR (OUTCHAN .OUTCHAN))
	#DECL ((CHAN OUTCHAN) CHANNEL (CHR) CHARACTER)
	<COND (<==? <SET CHR <TYI .CHAN>> !\?>
	       <PRINC "Help!">)
	      (<==? .CHR !\D> <SETG DEBUG? <NOT ,DEBUG?>>)
	      (<==? .CHR !\Q> <TN-CLOSE>)
	      (<==? .CHR !\P> <VALRET ":PROCED
">)
	      (<==? .CHR !\S> <SETG SILENT? <NOT ,SILENT?>>)
	      (ELSE <SIOT .CHR <2 ,TNPAIR>>)>>

<DEFINE TN-TTY ()
	<TTYECHO ,INCHAN <>>
	<SETG OTTY <TTY-GET>>
	<SETG NTTY
	      <UVECTOR #WORD *030303030303*
		       #WORD *030303030303*
		       <ORB <3 ,OTTY> *000200000000*>>>
	<TTY-SET ,NTTY>>

<DEFINE IAC (TN X "OPTIONAL" (Y <>))
	#DECL ((TN) CHANNEL (X) FIX (Y) <OR FIX FALSE>)
	<IOT8 255 .TN>
	<IOT8 .X .TN>
	<COND (.Y <IOT8 .Y .TN>)>
	<COND (,DEBUG?
	       <PROG ((OUTCHAN .OUTCHAN))
		     #DECL ((OUTCHAN) CHANNEL)
		     <PRINC "[S:IAC ">
		     <PRINC <COND (<==? .X 251> "WILL")
				  (<==? .X 252> "WONT")
				  (<==? .X 253> "DO")
				  (<==? .X 254> "DONT")
				  (ELSE .X)>>
		     <COND (.Y <PRINC .Y>)>
		     <PRINC !\]>
		     <CRLF>>)>>

<DEFINE IACJUNK (TNI TNO "AUX" C D (DEBUG? ,DEBUG?) (OUTCHAN .OUTCHAN))
	#DECL ((TNI TNO OUTCHAN) CHANNEL (C D) FIX)
	<AND .DEBUG? <PRINC "[R:IAC ">>
	<SET C <CHTYPE <READCHR .TNI> FIX>>
	<AND .DEBUG? <PRINC <COND (<==? .C 251> "WILL")
				  (<==? .C 252> "WONT")
				  (<==? .C 253> "DO")
				  (<==? .C 254> "DONT")
				  (ELSE .C)>>>
	<COND (<G? .C 250>
	       <SET D <CHTYPE <READCHR .TNI> FIX>>
	       <AND ,DEBUG?
		    <PRINC !\ >
		    <PRINC .D>
		    <PRINC !\]>
		    <CRLF>>
	       <COND (<AND <==? .D 6>
			   <OR <==? .C ,WILL>
			       <==? .C ,DO>>>
		      <IAC .TNO
			   <COND (<==? .C ,WILL> ,DO)
				 (<==? .C ,DO> ,WILL)>
			   6>)>)
	      (ELSE <AND ,DEBUG? <PRINC "]"> <CRLF>>)>>

<DEFINE TN-CLOSE ("OPTIONAL" (F ,TNPAIR))
	#DECL ((F) <LIST [2 CHANNEL]>)
	<AND <GASSIGNED? TN-INT> <OFF ,TN-INT>>
	<COND (.F
	       <CLOSE <1 .F>>
	       <CLOSE <2 .F>>
	       <SETG TNPAIR <>>)>>

<ENDPACKAGE>
