<PACKAGE "CHARAC">
<ENTRY LETTERP DIGITP UPPER-CASE LOWER-CASE ANY-LOWER-CASE?>

<TITLE ANY-LOWER-CASE?>
 <DECLARE ("VALUE" <OR FALSE FIX> STRING)>
	<DPUSH TP* <ARG 1>>
	<PUSHJ P* ALC0>
	<JRST FINIS>

	<INTERNAL-ENTRY ALC0 1>
	<HRRZ B* (TP) -1>
	<CAILE B* 20>
	<MOVEI B* 20>
	<MOVE D* (TP)>
	<MOVSI A* TFALSE>
ACL1	<ILDB C* D>
	<CAIL C* *141*>
	<CAILE C* *172*>
	<JRST ACL2>
	<MOVSI A* TFIX>
ACL2	<SOJGE B* ACL1>
	<MOVEI B* 0>
	<SUB TP* [<(2) 2>]>
	<POPJ P*>

<TITLE LETTERP>		;"CONVERTS TO LOWERCASE TOO"
 <DECLARE ("VALUE" <OR FALSE CHARACTER> CHARACTER)>
	<DPUSH TP* <ARG 1>>
	<PUSHJ P* LP0>
	<JRST FINIS>

	<INTERNAL-ENTRY LP0 1>
	<HRRZ  B* (TP)>
	<MOVSI A* TFALSE>
	<CAIGE  B* *133*>
	<MOVEI B* (B) 32>
	<CAIL  B* *141*>
	<CAIL  B* *173*>
	<SKIPA B* [0]>
	<MOVSI A* TCHARACTER>
	<SUB TP* [<(2) 2>]>
	<POPJ P*>

<TITLE DIGITP>
 <DECLARE ("VALUE" <OR FALSE CHARACTER> CHARACTER)>
	<DPUSH TP* <ARG 1>>
	<PUSHJ P* DP0>
	<JRST FINIS>

	<INTERNAL-ENTRY DP0 1>
	<HRRZ B* (TP)>
	<MOVSI A* TFALSE>
	<CAIL B* *60*>
	<CAIL B* *72*>
	<SKIPA B* [0]>
	<MOVSI A* TCHARACTER>
	<SUB TP* [<(2) 2>]>
	<POPJ P*>

<TITLE UPPER-CASE>
 <DECLARE ("VALUE" <PRIMTYPE STRING><PRIMTYPE STRING>)>
	<DPUSH TP* <ARG 1>>
	<PUSHJ P* UC0>
	<JRST FINIS>

	<INTERNAL-ENTRY UC0 1>
	<MOVE A* (TP) -1>
	<MOVE B* (TP)>
	<HRRZI C* (A)>
	<MOVE D* B>
UC1	<JUMPLE C* UC2>
	<ILDB E* D>
	<CAIGE E* *141*>
	<SOJA C* UC1>
	<MOVEI E* (E) -32>
	<CAIGE E* *133*>
	<DPB E* D>
	<SOJG C* UC1>
UC2	<SUB TP* [<(2) 2>]>
	<POPJ P*>

<TITLE LOWER-CASE>
 <DECLARE ("VALUE" <PRIMTYPE STRING><PRIMTYPE STRING>)>
	<DPUSH TP* <ARG 1>>
	<PUSHJ P* LC0>
	<JRST FINIS>

	<INTERNAL-ENTRY LC0 1>
	<MOVE A* (TP) -1>
	<MOVE B* (TP)>
	<HRRZI C* (A)>
	<MOVE D* B>
LC1	<JUMPLE C* LC2>
	<ILDB E* D>
	<CAIGE E* *101*>
	<SOJA C* LC1>
	<MOVEI E* (E) 32>
	<CAIGE E* *173*>
	<DPB E* D>
	<SOJG C* LC1>
LC2	<SUB TP* [<(2) 2>]>
	<POPJ P*>

	<END>

<ENDPACKAGE>
