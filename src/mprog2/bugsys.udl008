<PACKAGE "BUGGAGE">

<ENTRY BUGGER>

<USE "M.DAC" "USRUTI" "TOKENS">

"Do expansion if we can.  If not, send to BUG-frob@AI."

<DEFINE BUGGER (ADR DATUM
		"AUX" (XLIST <USE-DATUM .DATUM>)
		      (ADT <DRD .ADR "ADDRESSED-TO">)
		      (FROMHOST <DRD "" "RECEIVED-FROM-HOST">)
		      (TMP ()) TMP1)
	#DECL ((ADR DATUM) STRING (ADT) <OR FALSE STRING>
	       (FROMHOST) <OR FALSE FIX> (TMP) LIST (TMP1) ANY)
	<COND (<==? .FROMHOST 70> <SET FROMHOST <>>)>
	<COND (<AND .XLIST .ADT>
	       <SET ADT <UC-STR .ADT>>
	       <COND (<SET TMP1 <MEMBER "BUG-" .ADT>>
		      <SET TMP1 <REST .TMP1 4>>
		      <SET TMP <ADR-EXPAND (.TMP1) .XLIST .FROMHOST>>)
		     (<OR <SET TMP1 <MEMBER "(BUG" .ADT>>
			  <SET TMP1 <MEMBER "(FEATURE" .ADT>>>
		      <SET TMP1 <TOKENS <REST .ADT> " " ")">>
		      <SET TMP1 <REST .TMP1>>
		      <SET TMP <ADR-EXPAND .TMP1 .XLIST .FROMHOST>>)
		     (ELSE <SET TMP1 (.ADT)>)>
	       <COND (<EMPTY? .TMP>
		      <SET TMP <LIST <STRING "(BUG " <1 .TMP1> ")@AI">>>)>
	       <DWT .TMP "" "MORE-TO">
	       <PRCADD "" "EXPANSION">
	       <PRCADD "" "TRANSMISSION">
	       T)>>

<DEFINE ADR-EXPAND (ALIST XLIST LOCAL-ONLY?)
	#DECL ((ALIST) <LIST [REST STRING]> (XLIST) <VECTOR [REST STRING LIST]>
	       (LOCAL-ONLY?) <OR FIX FALSE> (VALUE) <LIST [REST <OR STRING ATOM>]>)
	<MAPF ,LIST
	      <FUNCTION (A "AUX" B)
		   #DECL ((A) STRING (B) <OR FALSE LIST STRING>)
		   <COND (<SET B <SYMLOOK <UC-STR .A> .XLIST>>
			  <COND (<STRUCTURED? .B>
				 <COND (.LOCAL-ONLY?
					<MAPRET !<MAPF ,LIST
						       <FUNCTION (A)
							  #DECL ((A) <OR STRING ATOM>)
							  <COND (<MEMQ !"@ .A> <MAPRET>)
								(ELSE <MAPRET .A>)>>
						       .B>>)
				       (ELSE <MAPRET !.B>)>)
				(ELSE <MAPRET .A>)>)
			 (<SET B <KEYGET .A>> <MAPRET .B>)
			 (ELSE <MAPRET>)>>
	      .ALIST>>

<DEFINE UC-STR (S)
	#DECL ((VALUE S) STRING)
	<MAPR <>
	      <FUNCTION (S "AUX" (C <1 .S>)) 
		   <COND (<OR <G? <ASCII .C> <ASCII !"z>>
			      <L? <ASCII .C> <ASCII !"a>>>)
			 (ELSE <PUT .S 1 <ASCII <- <ASCII .C> 32>>>)>>
	      .S>
	.S>

<DEFINE SYMLOOK (OBJ TABLE "AUX" (CNT 10) AGN)
	#DECL ((OBJ) STRING (TABLE) <VECTOR [REST STRING LIST]> (VALUE) <OR LIST FALSE>)
	<REPEAT ()
		<REPEAT ((TBL .TABLE))
			<COND (<LENGTH? .TBL 1> <RETURN <SET R <>>>)
			      (<=? .OBJ <1 .TBL>> <RETURN <SET R <2 .TBL>>>)
			      (ELSE <SET TBL <REST .TBL 2>>)>>
		<SET AGN <>>
		<SET R
		     <MAPF ,LIST
			   <FUNCTION (S)
				     <COND (<AND <TYPE? .S STRING> <NOT <MEMQ !"@ .S>>>
					    <SET CNT <- .CNT 1>>
					    <SET AGN T>
					    <MAPRET !<SYMLOOK .S .TABLE>>)
					   (ELSE <MAPRET .S>)>>
			   .R>>
		<COND (<OR <0? .CNT> <NOT .AGN>> <RETURN .R>)>>>

<ENDPACKAGE>
