TITLE MLIFE 21

IFNDEF TS, TS==1
DDTF==0
DLL==6000.-2000.*DDTF
FLL==4000.-500.*DDTF	; RELEVANT ONLY IF TS=0
PDP6MT==40000
DDTLOC==34000

	DEFINE	FBLK
	XCT	FLP
	EXCH	A,FLP
TERMIN
IFN TS,[
TYIC==1
TYOC==2
DKIC==3
DKOC==4
PDPCH==5
]
IFE TS,[PTQCHN==3
TTYCHN==4
]
FLGCHN==5
DISCHN==6
APRCHN==7
APROFF==1000\1_<7-APRCHN>
APRON==2000\1_<7-APRCHN>
X0=0
A=1
B=2
C=3
D=4
E=5
X=6
Y=7
J=10
K=12
R=14
S=15
T=16
P=17

F=6
G=7
H=10
I=11
P0=12
L0=13
L1=14
L2=15
L3=16
FSK==2

ISCALE==20
ISCSHIF==0

LPDL==20

CRLF==15_7\12

LOC 41
	JSR UUOH
IFN TS,	TSINT
IFE TS,[
LOC 40+2*PTQCHN
	JSR PTQBRK
LOC 40+2*TTYCHN
	JSR TTYBRK
]
LOC 40+2*FLGCHN
	JSR FLGBRK
LOC 40+2*DISCHN
	BLKO DIS,DISPTR
	JSR DISBRK
LOC 40+2*APRCHN
	JSR APRBRK
LOC 60
IFE TS,JRST GO	; NON TS ENTRY POINT

SCALE:	ISCALE
SCSHIF:	,ISCSHIF
XOFF:	0
YOFF:	0
XORG:	0
YORG:	0
TIME:	0
SIXF:	0	; NON-ZERO MEANS DUAL PROCESSORS; COUNT POINTS

COUNTM:	SETZM	N'
	MOVEI	I,LP-1
COUNTI:	SKIPN	I,1(I)
	POPJ	P,
	MOVE	J,I
COUNTJ:	HRRZ	J,(J)
	CAIN	J,DUMMY
	JRST	COUNTI
	LDB	X,[14200,,1(J)]
	SKIPN	Y,X
	JRST	COUNTJ
	LSH	Y,-1
	AND	Y,[333333,,333333]
	SUB	X,Y
	LSH	Y,-1
	AND	Y,[333333,,333333]
	SUBB	X,Y
	LSH	Y,-3
	ADD	X,Y
	AND	X,[070707,,070707]
	IDIVI	X,77
	ADDM	Y,N
	JRST	COUNTJ
; DISPLAY LIFE SPACE

DS:IFN TS,[SKIPN SIXF
	.DSTOP
]	AOS	DISINH	; INHIBIT THE DISPLAY
IFN TS,SKIPE	SIXF
	CONO	DIS,100	; STOP THE DISPLAY
	MOVE A,LP
	MOVE	L1,[002200,,DLST-1]
	MOVEI	L3,EDL1-18.
INTEN:	MOVEI	B,020157
	IDPB	B,L1
	MOVE B,SCALE
	ASH B,@SCSHIF
	SKIPN B
	SKIPA B,[HLRE L2,(C)]
	MOVE B,[JRST DSBG1]
	MOVEM B,DSSM
DSA:	JUMPE	A,DSX
	HRRZ	C,(A)
	HLRE	G,(A)
	IMULI	G,34.
	ADD	G,XOFF
	MOVE	D,G
	ADDI	D,33.
	IMUL	D,SCALE
	ASH	D,@SCSHIF
	ADDI	D,1000
	TRNN	D,776000
	JRST	DSC
	JUMPL	D,DSB
	MOVE	D,G
	IMUL	D,SCALE
	ASH	D,@SCSHIF
	ADDI	D,1000
	TRNE	D,776000
	JRST	DSX
DSC:	MOVE	E,1(C)
	TDZ	E,[400000,,000001]
	JUMPE	E,DSH
	HLRE	D,(C)
	MOVNS	D
	ADD	D,YOFF
	IMUL	D,SCALE
	ASH	D,@SCSHIF
	ADDI	D,1000
	TRZE	D,776000
	JRST	DSG	;Y WORD OFF SCREEN
DSSN:	HRRZ C,(C)
DSSM:	HLRE L2,(C)\JRST DSBG1
	MOVNS L2
	ADD L2,YOFF
	IMUL L2,SCALE
	ASH L2,@SCSHIF
	CAIE L2,-1000(D)
	JRST DSBG
	IOR E,1(C)	;MERGE SUPERPOSED DOTS
	JRST DSSN

DSBG:	TRZ E,1
DSBG1:	TRO	D,220000
	CAIGE	L3,(L1)
DLOVFL:	JRST	DSX	; COULD PRINT MESAGE OR SOMETHING
	IDPB	D,L1
	MOVE	D,G
	HLLZS LASTX
DSD:	LSH	E,1
	SKIPLE	E
	AOJA	D,DSD
	JUMPE	E,DSHA
	MOVE	F,D
	IMUL	F,SCALE
	ASH	F,@SCSHIF
	ADDI	F,1000
	TRZE	F,776000
	JRST	DSE
	TRO	F,022000
LASTX:	CAIN F,.
	AOJA D,DSD
	IDPB	F,L1
	HRRM F,LASTX
DSF:	AOJA	D,DSD

DSG:	JUMPL	D,DSB	; IF ABOVE, QUIT COLUMN
DSH:	HRRZ	C,(C)
DSHA:	CAIE	C,DUMMY
	JRST	DSC
DSB:	HRRZ	A,1(A)
	JRST	DSA

DSE:	JUMPL	F,DSF
	JRST	DSHA

DSX:	MOVEM	L1,DSDP
	MOVEI	A,220000
	IDPB	A,L1
	POPJ	P,

DSDP:	0
; PROCESS LIFE SPACE LIST

PLST:	HRRZ	L1,LP
	MOVEI	A,LP-1
	MOVEM	A,CNLP
	JUMPE	L1,PSLX

PSL:	HLRE	A,(L1)
	HRLI	A,DUMMY
	MOVSM	A,MHB
	HRRI	A,-1(A)
	MOVSM	A,LHB
	HRRI	A,2(A)
	MOVSM	A,RHB

PSLA:	MOVEM	L1,MOC
	PUSHJ	P,PCOL
	MOVE	L1,LOC.
	HLRE	A,(L1)
	HRRZ	L1,1(L1)
	JUMPE	L1,PSLB
	HLRE	H,(L1)
	SUB	H,A
	SOJE	H,PSLA
	PUSHJ	P,OAS	; TRANSPARENT TO H
	SOJE	H,PSLA
	PUSHJ	P,OAS
	JRST	PSL

PSLB:	PUSHJ	P,OAS
	PUSHJ	P,OAS
PSLX:	MOVE	A,CNLP
	SETZM	1(A)
	POPJ	P,

PCOL:	MOVEI	A,LHB
	MOVEM	A,LCL
	MOVEI	A,MHB
	MOVEM	A,MCL
	MOVEI	A,RHB
	MOVEM	A,RCL
	HRRZ	L2,(L1)

; L2 HAS PNTR TO FIRST WORD

PCA:	MOVE	L1,(L2)	; 001
	HLRE	P0,L1
	SOS	P0
	PUSHJ	P,P1W
	AOS	P0
PCJ:	MOVE	L0,(L1)
	HLRE	A,L0
	SUB	A,P0
	SOJE	A,PCC	; (011 N)
	PUSHJ	P,P1W	; 010 N
	AOS	P0
PCI:	HLRE	A,L0
	SUB	A,P0
	SOJE	A,PCD	; (101)
	PUSHJ	P,P1W	; 100
	MOVE	L2,L1
	CAME	L0,DUMMY
	JRST	PCA
			; DONE
	MOVE	A,RCL
	HRRM	L0,(A)
	JRST	OAS

PCC:	PUSHJ	P,P2W	; 011 N
	EXCH	L2,L1
	AOS	P0
PCL:	MOVE	L3,L0
	MOVE	L0,(L3)
	HLRE	A,L0
	SUB	A,P0
	SOJE	A,PCH	; (111 N)
	PUSHJ	P,P2W
	MOVE	L1,L3
	AOJA	P0,PCI

PCD:	PUSHJ	P,P101W	; 101
	MOVE	L2,L1
	MOVE	L1,L0
	AOJA	P0,PCJ

PCH:	PUSHJ	P,P3W	; 111 N
	MOVE	L1,L2
	MOVE	L2,L3
	AOJA	P0,PCL

OAS:	MOVE	C,RHB
	MOVS	D,C
	AOS	D
	HRLI	D,DUMMY
	MOVSM	D,RHB
	EXCH	C,MHB
	EXCH	C,LHB
	HRRZ	B,C
	CAIN	B,DUMMY
	JRST	OASA	; NO NEW LINE, FLUSH OLD LINE
	FBLK
	MOVEM	C,(A)
	HRRZ	B,A
	EXCH	B,CNLP
	HRRM	A,1(B)
OASA:	MOVEI	A,0
	EXCH	A,MOC
	EXCH	A,LOC.
	JUMPE	A,CPOPJ

	MOVE	B,FLP
OSA:	HRLI	A,(MOVE A,)
	CAMN	A,[MOVE A,DUMMY]
	JRST	OSB
	EXCH	B,(A)
	HRLI	B,(MOVE A,)
	CAMN	B,[MOVE A,DUMMY]
	JRST	OSC
	EXCH	A,(B)
	JRST	OSA

OSB:	MOVEM	B,FLP
	POPJ	P,
OSC:	MOVEM	A,FLP
	POPJ	P,
; L2 PNTS TO CENTER - L1,L3 TO EDGES

P3W:	MOVE	A,1(L1)
	MOVE	B,A
	MOVE	C,1(L2)
	AND	B,C
	IORB	A,C
	MOVE	D,B
	LSHC	A,-1
	LSHC	C,1
	MOVE	E,A
	MOVE	F,B
	IOR	A,C	; 1AB
	ANDB	C,E	; 1A & 1B
	IOR	B,D	; 2A ' 2B
	AND	C,B	; 3AB
	IOR	B,E	; 2AB
	AND	D,F	; 4AB

	MOVE	I,1(L3)
	MOVE	G,I
	ROT	G,2
	MOVE	H,G
	IOR	G,I
	AND	H,I
	ROTC	G,-1	; G=1D, H=2D
	MOVE	E,1(L1)
	MOVE	F,E
	AND	F,I	; 2C
	IORB	E,I	; 1C
	IOR	I,G
	AND	A,I	; 1AB & 1CD
	MOVE	I,F
	ANDB	G,E	; 1C & 1D
	AND	I,H
	IOR	D,I	; 4AB ' 4CD
	IOR	F,H	; 2C ' 2D
	AND	G,F	; 3CD
	IORB	F,E	; 2CD
	IOR	C,G	; 3CD ' 3AB

	IOR	E,B	; 2AB ' 2CD
	MOVE	I,E
	IOR	I,A
	AND	I,1(L2)	; ABCD2 & X
	IOR	I,C
	AND	E,A
	IOR	I,E	; (ABCD2 & X) ' ABCD3
	ANDCM	I,D
	AND	C,A
	ANDCM	I,C
	AND	B,F
	ANDCM	I,B	; ((ABCD2 & X) ' ABCD3) & -ABCD4
	AND	I,[377777,,777776]
	JUMPN	I,SWD
	POPJ	P,
; L2 PNTS TO CENTER - L1 TO EDGE

P2W:	MOVE	A,1(L1)
	MOVE	B,A
	MOVE	C,1(L2)
	AND	B,C	; A2
	IORB	A,C	; A1, B1
	MOVE	D,B	; B2
	LSHC	A,-1
	LSHC	C,1
	MOVE	E,A	; A1
	MOVE	I,B	; A2
	IOR	A,C	; 1AB
	ANDB	C,E	; 1A & 1B
	IOR	B,D	; 2A ' 2B
	AND	C,B	; 3AB
	IOR	B,E	; 2AB
	AND	I,D	; 4AB

	AND	A,1(L1)
	IOR	A,B	; ABC2
	AND	B,1(L1)
	IOR	B,C	; ABC3
	AND	C,1(L1)
	IOR	I,C	; ABC4
	AND	A,1(L2)	; ABC2 & X
	IOR	A,B	; (ABC2 & X) + ABC3
	ANDCA	I,A	; ((ABC2 & X) + ABC3) & -ABC4
	AND	I,[377777,,777776]
	JUMPN	I,SWD
	POPJ	P,
; L1 AND L2 POINT TO EDGES

P101W:	MOVE	A,1(L1)
	MOVE	B,A
	MOVE	C,1(L2)
	AND	B,C	; A2
	IORB	A,C	; A1, B1
	MOVE	D,B	; B2
	MOVE	G,B	; C2
	MOVE	H,A	; C1
	LSHC	A,-1
	LSHC	C,1
	MOVE	E,A	; A1
	MOVE	I,B	; A2
	IOR	A,C	; 1AB
	ANDB	C,E	; 1A & 1B
	IOR	B,D	; 2A ' 2B
	AND	C,B	; 3AB
	IOR	B,E	; 2AB
	AND	I,D	; 4AB

	AND	A,G
	IOR	A,B
	AND	A,H
	IOR	A,C	; ABC3
	AND	B,G
	IOR	B,C
	AND	B,H
	IOR	I,B	; ABC4
	ANDCA	I,A
	AND	I,[377777,,777776]
	JUMPN	I,SWD
	POPJ	P,
P1W:	MOVE	I,1(L2)
	MOVE	A,I
	LSH	I,1
	AND	I,A
	LSH	A,2
	AND	I,A
	LSH	I,-1
	JUMPE	I,CPOPJ
SWD:	TRNE	I,2	; I HAS WORD TO STORE
	JRST	SWDA
	SKIPA	B,MCL
SWDC:	MOVE	B,C
SWDB:	MOVE	C,(B)
	HLRE	D,(C)
	SUB	D,P0
	JUMPL	D,SWDC
	JUMPE	D,SWDD
	FBLK
	HRRM	A,(B)
	HRL	C,P0
	MOVEM	C,(A)
	MOVEM	I,1(A)
	MOVEM	A,MCL
	TLNN	I,200000
CPOPJ:	POPJ	P,
SWDE:	SKIPA	B,LCL
SWDH:	MOVE	B,C
SWDF:	MOVE	C,(B)
	HLRE	D,(C)
	SUB	D,P0
	JUMPL	D,SWDH
	JUMPE	D,SWDG
	FBLK
	HRRM	A,(B)
	HRL	C,P0
	MOVEM	C,(A)
	MOVEI	D,1
	MOVEM	D,1(A)
	MOVEM	A,LCL
	POPJ	P,

SWDD:	IORM	I,1(C)
	MOVEM	C,MCL
	TLNN	I,200000
	POPJ	P,
	JRST	SWDE

SWDG:	AOS	1(C)
	MOVEM	C,LCL
	POPJ	P,

SWDA:	MOVE	B,RCL	; CHAIN PREVIOUS WORD
	FBLK		; STORE RIGHT WORD
	HRRM	A,(B)
	MOVEM	A,RCL
	HRLZM	P0,(A)	; SETUP WORD
	HRLZI	B,400000
	MOVEM	B,1(A)
	MOVE	B,MCL
	JRST	SWDB

CNLP:	0	; CURRENT NEW COLUMN POINTER
LCL:	0	; PNTR TO LAST, LEFT COL
MCL:	0	; PNTR TO LAST, MIDDLE COL
RCL:	0	; PNTR TO LAST, RIGHT COL (INIT TO HEADER BLOCK)
LHB:	DUMMY	; LEFT HEADER BLOCK PROTOTYPE
MHB:	DUMMY
RHB:	DUMMY
LOC.:	0	; LEFT OLD COLUMN POINTER, OR 0
MOC:	0
LP:	0	; LIST POINTER
FLP:	MOVE	A,FL	; FREE LIST POINTER

DLIST:	20157
	BLOCK DLL
EDL1:	BLOCK 16
EDLIST:	0

SCOBUF:	BLOCK 16	;SCOPE COMMENT BUFFER

DLST=DLIST
IFE TS,[
TTYBRK:	0
	MOVEM A,TTYAM'
	CONSO TTY,40
	JRST TTO
TTI:	DATAI TTY,A
	ANDI A,177
	IDPB A,TTIP
	AOS TTICNT
	CAIE A,15
	JRST TTI1
	MOVE A,TTIP
	CAMN A,TTIPND
	HRRI A,TTIBUF-1
	HRRM A,TTIP
	MOVEI A,12
	IDPB A,TTIP
	AOS TTICNT
TTI1:	MOVE A,TTIP
	CAMN A,TTIPND
	HRRI A,TTIBUF-1
	HRRM A,TTIP
TTO:	CONSZ TTY,20
	JRST TTYBK1
	SOSGE TTOCNT
	JRST TTO1
	MOVE A,TTOP
	CAMN A,TTOPND
	HRRI A,TTOBUF-1
	HRRM A,TTOP
	ILDB A,TTOP
	DATAO TTY,A
TTYBK1:	MOVE A,TTYAM
	JRST 12,@TTYBRK

TTO1:	SETZM TTOCNT
	CONO TTY,200\TTYCHN
	JRST TTYBK1

TTIL==10
TTOL==10

TTICNT:	0
TTOCNT:	0

TTIBUF:	BLOCK TTIL
TTOBUF:	BLOCK TTOL

TTOPND:	10700,,TTOBUF+TTOL-1
TTOP:	10700,,TTOBUF-1
TTIPND:	10700,,TTIBUF+TTIL-1
TTIP:	10700,,TTIBUF-1
OTTIP:	10700,,TTIBUF-1
ITTOP:	10700,,TTOBUF-1

TYI:	SKIPGE TYIF
	JRST PTYI
	SKIPE	A,RCHR
	JRST	TYI1
	SKIPG TTICNT
	JRST .-1
	MOVE A,OTTIP
	CAMN A,TTIPND
	HRRI A,TTIBUF-1
	HRRM A,OTTIP
	ILDB A,OTTIP
	SOS TTICNT
TYO:	PUSH P,A
	MOVEI A,5*TTOL
	CAMG A,TTOCNT
	JRST .-1
	MOVE A,ITTOP
	CAMN A,TTOPND
	HRRI A,TTOBUF-1
	HRRM A,ITTOP
	MOVE A,(P)
	ANDI A,177
	CAIGE A,40
	JRST CNTRL
TYO1:	POP P,A
TYO2:	IDPB A,ITTOP
	SKIPG TTOCNT
	AOSA TTOCNT
	AOSA TTOCNT
	CONO PI,4000\1_<7-TTYCHN>
	POPJ P,

TYI1:	SETZM	RCHR
	POPJ	P,

CNTRL:	CAIE A,15
	CAIN A,12
	JRST TYO1
	MOVEI A,"^
	PUSHJ P,TYO2
	MOVE A,(P)
	ADDI A,100
	PUSHJ P,TYO
	POP P,A
	POPJ P,
]

FLGBRK:	0
	CONSZ DIS,400
	JRST FLGBK1
	CONO DIS,200\FLGCHN_3\DISCHN
	JRST 12,@FLGBRK
FLGBK1:	CONO DIS,0
	JRST 12,@FLGBRK

DISBRK:	0
	CONO DIS,0
	JRST 12,@DISBRK	; GO AWAY HAPPY

APRBRK:	0
	CONSO APR,1000
	JRST APRBK1
	AOS TIME
	CONSO DIS,7
	JRST APRDIS	;RESTART THE DISPLAY
APRBK1:	CONO APR,453440+APRCHN
	JRST 12,@APRBRK

APRDIS:	PUSH P,DISIPT
	POP P,DISPTR
	SKIPN	DISINH
	CONO DIS,100\FLGCHN_3\DISCHN
	JRST APRBK1

DISIPT:	-1,,EDLIST-1
DISPTR:	-1,,EDLIST-1
DISINH:	1	; ZERO MEANS DISPLAY LIST IS OK TO RUN
IFE TS,[
PTQBRK:	0
	MOVEM A,PTQAM'
	CONSO PTR,10
	JRST PTO
PTI:	DATAI PTR,A
	JUMPE A,PTQBK1
	IDPB A,PTIP
	AOS A,PTICNT
	CAIL A,5*PTIL
	CONO PTR,0
	MOVE A,PTIP
	CAMN A,PTIPND
	HRRI A,PTIBUF-1
	HRRM A,PTIP
PTO:	CONSZ PTP,20
	JRST PTQBK1
	SOSGE PTOCNT
	JRST PTO1
	MOVE A,PTOP
	CAMN A,PTOPND
	HRRI A,PTOBUF-1
	HRRM A,PTOP
	ILDB A,PTOP
	DATAO PTP,A
PTQBK1:	MOVE A,PTQAM
	JRST 12,@PTQBRK

PTO1:	SETZM PTOCNT
	CONO PTP,PTQCHN
	JRST PTQBK1

PTROFF:	SETZM TYIF
	CONO PTR,400
	JRST TYI

PTIL==20
PTOL==20

PTICNT:	0
PTOCNT:	0

PTIBUF:	BLOCK PTIL
PTOBUF:	BLOCK PTOL

PTOPND:	10700,,PTOBUF+PTOL-1
PTOP:	10700,,PTOBUF-1
PTIPND:	10700,,PTIBUF+PTIL-1
PTIP:	10700,,PTIBUF-1
OPTIP:	10700,,PTIBUF-1
IPTOP:	10700,,PTOBUF-1

PTYI:	SKIPLE PTICNT
	JRST PTYI1
	CONSO PTR,7
	CONO PTR,10+PTQCHN
	MOVSI A,1
	SOJL A,PTROFF
	SKIPG PTICNT
	JRST .-2
PTYI1:	MOVE A,OPTIP
	CAMN A,PTIPND
	HRRI A,PTIBUF-1
	HRRM A,OPTIP
	ILDB A,OPTIP
	SOS PTICNT
	POPJ P,

PTYO:	PUSH P,A
	MOVEI A,5*PTOL
	CAMG A,PTOCNT
	JRST .-1
	MOVE A,IPTOP
	CAMN A,PTOPND
	HRRI A,PTOBUF-1
	HRRM A,IPTOP
PTYO1:	POP P,A
PTYO2:	IDPB A,IPTOP
	SKIPG PTOCNT
	AOSA PTOCNT
	AOSA PTOCNT
	CONO PI,4000+1_<7-PTQCHN>
	POPJ P,

IFN DDTF,[
PWAT:	SKIPLE PTOCNT
	JRST .-1
	JRST DDTLOC
]
]

; THIS ROUTINE IS UNTESTED AS OF VERSION 21, JUNE 72

FRAMES:	MOVEI A,	;FRAMES OF FILM PER TICK OF LIFE
	JUMPE A,FRAM2
	AOS	DISINH
	CONO	DIS,100	; STOP, BY GULLY
FRAM1:	PUSHJ P,FROPEN
NNDISS:	MOVEI B,1	;NUMBER TO .NDIS (CYCLES OF DISPLAY)
FRAM3:	SETZM	DISINH
	CONSO	DIS,7	; WAIT FOR DIS TO START
	 JRST	.-1
	AOS	DISINH
	CONSZ	DIS,7	; WAIT FOR DIS TO STOP
	 JRST	.-1
	SOJG	B,FRAM3
	PUSHJ P,FROPEN
	SOJG A,FRAM1
	AOS	DISINH	;START DISPLAY UP AGAIN
FRAM2:	POPJ P,

LEADER:	MOVEI P,PDL-1
	MOVEI A,310	;5 FEET
	PUSHJ P,FROPEN
	PUSHJ P,FROPEN
	SOJG A,.-2
	JRST .

;CCW = FWD = 0, 100, 300, 200, 0, ...
FROPEN:	PUSH P,A
	PUSH P,B
	MOVEI A,62	;25. COMPLETE CYCLES = 1/2 REVOLUTION
OPEN2:	MOVEI B,100
	XORB B,MOTOR
	DATAO 760,B
	MOVEI B,1400	;1400 IN PDP-10 DROPS STEP OCCASIONALLY
	SOJG B,.
	MOVEI B,200
	XORB B,MOTOR
	DATAO 760,B
	MOVEI B,1400
	SOJG B,.
	SOJG A,OPEN2
	POP P,B
	POP P,A
	POPJ P,

MOTOR:	0	;LAST DATAO TO MOTOR
UUOH:	0
	MOVEM A,AM'
IFN TS,[
	MOVE	A,UUOH
	JFCL	1,.+1
	JRST	.+1
	JFCL	1,SIXIN	; RESTART IF 6
]	JRST	UUOH2

IFN TS,[
SIXIN:	MOVEI	P,PDL6-1
	CONO	APR,653440+APRCHN
	CONO	PI,12200\1_<7-APRCHN>\1_<7-FLGCHN>\1_<7-DISCHN>
	PUSH	P,[.]
	MOVEI	A,SIXGO
	HRRM	A,SIXGO
SIXGO:	JRST	.

PDL6:	BLOCK	LPDL
]
DISF:	0
STEPC:	0	; PROCEED CYCLE COUNTER
STEPK:	0	; TEMPORY STORAGE FOR K, THE GENERATION COUNTER

STEP6:	PUSHJ	P,FRAMES
IFE TS,	SKIPN	TTICNT
IFN TS,	SKIPN	STOPF'
	SOSGE	STEPC
	POPJ	P,
	PUSHJ	P,PLST
	AOS	K,STEPK
; CALCULATE N
	SKIPE	STEPC
	SKIPGE	DISF
	JRST	.+2
	JRST	STEP6
	PUSHJ	P,DS
	MOVE	D,DSDP
	SETZM	N
	PUSHJ	P,DISSUB
	PUSHJ	P,DSRT6
	JRST	STEP6
DISSUB:	MOVEM D,PTDEND
	PUSH D,[221747,,61617]
	MOVE A,K
	MOVEI C,5
	MOVE I,[600,,(D)]
	PUSHJ P,DISNUM
	MOVEI A,37
	IDPB A,I
	ADD D,[1,,1]
	PUSH D,[20000]
	PUSH D,[220000,,61617]
	MOVE I,[600,,(D)]
	MOVEI C,5
	MOVE A,N
	PUSHJ P,DISNUM
	MOVEI A,34	;340 CR
	MOVE B,[440700,,SCOBUF]
	MOVEI	C,40	; SPACE INSTEAD OF 0
	SKIPN	N
	DPB	C,I
DISGO2:	IDPB A,I
	ILDB A,B
	JUMPN A,DISGO2
	MOVEI A,37
	MOVEI B,5
	IDPB A,I
	SOJG B,.-1
	POPJ	P,

DSRT6:
IFN TS,[SKIPE	SIXF
	JRST	DSRT6A
	.DSTART	[DLIST-EDLIST-2,,DLIST-1]
	 .VALUE
	POPJ	P,
DSRT6A:]
	MOVE A,[DLIST-EDLIST-2,,DLIST-1]
	MOVEM A,DISIPT
	AOS	DISINH
	CONSZ	DIS,7
	 JRST	DSRT6B
	MOVEM	A,DISPTR
	CONO	DIS,100\FLGCHN_3\DISCHN
DSRT6B:	SETZM	DISINH
	POPJ	P,

DISNM1:	SOJLE C,CPOPJ
	SKIPN A
	SKIPA B,[20]
DISNUM:	IDIVI A,10.
	HRLM B,(P)
	PUSHJ P,DISNM1
	HLRZ A,(P)
	TRC A,60
	IDPB A,I
	POPJ P,

PTDEND:	DLIST-EDL1+1+5,,DLIST	;END OF PT MODE DISPLAY LIST

FIRST:	-1

IFN TS,[
VARIABLES
CONSTANTS

SIXWT:	0
	JFCL
	JRST	SIXIN	; MUST FOLLOW SIXIN
]
	BLOCK 1&.
DUMMY:	377777,,.?	0
IFN TS,FLL==<PDP6MT-.>/FSK-1
FL:	BLOCK <FLL+1>*FSK
.TYOI=1_33
.DKOI=2_33

UUOH2:	LDB A,[331100,,40]
	CAIN A,.TYOI_-33
	JRST ATYOI
	CAIN A,.DKOI_-33
	JRST ADKOI
IFE TS,[FSEXP:	JRST 4,.]
ILUUO:	.VALUE

ATYOI:	LDB A,[70700,,40]
	SKIPE A
IFN TS,	.IOT TYOC,A
IFE TS,	PUSHJ P,TYO
	LDB A,[700,,40]
IFN TS,	.IOT TYOC,A
IFE TS,	PUSHJ P,TYO
UUEXIT:	MOVE A,AM
	JRST 2,@UUOH

ADKOI:	LDB A,[70700,,40]
	SKIPE A
IFN TS,	.IOT DKOC,A
IFE TS,	PUSHJ P,PTYO
	LDB A,[700,,40]
IFN TS,	.IOT DKOC,A
IFE TS,	PUSHJ P,PTYO
	JRST UUEXIT
IFN TS,[
FSEXP:	MOVEI A,NBLKS
	.CORE 1(A)
NOCORE:	.VALUE 0
	AOS FSEXP
	LSH A,10.
	HRLI A,(MOVE A,)
	MOVEM A,FLP
	MOVEM I,IM'
	MOVEI I,2000/FSK-1
	ADDI A,FSK
	MOVEM A,-FSK(A)
	SOJG I,.-2
	MOVE I,[PUSHJ P,FSEXP]
	MOVEM I,(A)
	MOVE I,IM
	XCT	FLP
	POPJ	P,
]

SAVERG:	BLOCK 16.
PDL:	BLOCK LPDL
IFN TS,[
OP6B:	20,,(SIXBIT /USR/)
	0
	SIXBIT /PDP6/

WAIT6:	SKIPN	SIXF
	POPJ	P,
	MOVEI	SIXGO
CALL6:	SKIPN	SIXF
	JRST	@
	PUSH	P,A
	MOVE	A,[JRST SIXGO]
	CAME	A,SIXGO
RUN6:	.HANG	; HUNG ON LAST REQUEST
	HRRM	SIXGO
	POP	P,A
	POPJ	P,

NO6:	SETZM	SIXF
	HLLZS	FRAMES
	JRST	REQ0
]

IFE TS,[
CALL6:	JRST	@
WAIT6:	POPJ	P,
]; MAIN ENTRY POINT IS GO; GO10 SUPPRESSES THE ATTEMPT TO USE THE PDP6
GO:
IFN TS,	AOSA	SIXF	; NON-ZERO MEANS DUAL PROCESSORS
GO10:	SETZM	SIXF	; START HERE FOR TEN ONLY OPERATION
	SETZM TYIF'
	SETOM DISF
	MOVEI	15
	MOVEM	RCHR'
	MOVSI X0,200000
	MOVEI P,PDL-1
IFN TS,[.OPEN TYIC,[SIXBIT \   TTY\]
	.VALUE
	.OPEN TYOC,[SIXBIT \  !TTY\]
	.VALUE
	.SUSET	[.SMSK2,,[1_TYIC]]
	SKIPE	SIXF
	.OPEN	PDPCH,OP6B
	 JRST	NO6
	MOVEI	A,PDP6MT/2000
	MOVE	B,[400,,500000]
	MOVE	C,[2400+PDPCH,,400000]
GOC1:	.CBLK	B,
	 .VALUE
	ADDI	B,1001
	.CBLK	C,
	 .VALUE
	ADDI	C,1001
	SOJN	A,GOC1
	SETZM	SIXWT
	SETOM	SIXWT+200000
	SKIPE	SIXWT
	JRST	GOC2
	.RESET	PDPCH,	; CLEARS 6 MEMORY
	MOVE	B,[CONO APR,635550]
	MOVEM	B,SIXWT+1
	MOVE	A,[JSR SIXWT]
	MOVEM	A,SIXWT+2
	MOVEM	A,41
	SKIPN	SIXWT
STRT6:	.HANG
	MOVE	[200020,,20]
	BLT	PDP6MT-1
GOC2:	.CORE	NBLKS
	.VALUE
REQ0:]
IFE TS,[CONO APR,653440+APRCHN
	CONO PI,12200\1_<7-APRCHN>\1_<7-FLGCHN>\1_<7-DISCHN>\1_<7-TTYCHN>\1_<7-PTQCHN>
	CONO TTY,3600\TTYCHN
	CONO PTP,PTQCHN
REQ0:	CONO PTR,0
]
	AOSG	FIRST
	PUSHJ	P,INIT	; FIRST TIME ONLY
REQ:	.TYOI CRLF
	.TYOI "_
	JRST GOI

GO1:	PUSHJ P,DISGO
GOI:	PUSHJ P,TYI
	CAIN A," 
	JRST SPACE
	SETZM PFLAG
	CAIN A,"E
	JRST INPUT2
	CAIN A,"R
	JRST READA
	CAIN A,"W
	JRST WRITE
IFN TS,[CAIN A,"I
	JRST FILEI
	CAIN A,"O
	JRST FILEO
	CAIN A,"C
	JRST CLOSE
]	CAIN A,"K
	JRST KPRINT
	CAIN A,"T
	JRST TPRINT
	CAIN A,"S
	JRST SPRINT
	CAIN A,"X
	JRST XPRINT
	CAIN A,"Y
	JRST YPRINT
	CAIN	A,"H
	JRST	HERE
	CAIN A,"D
	JRST IN
	CAIN A,"V
	JRST INVIEW
	CAIN A,^S
IFN TS,	.VALUE
IFE TS,[
IFN DDTF,JRST PWAT
IFE DDTF,JRST QUEST
]	CAIN A,^D
	JRST SDISF
	CAIN A,^E
	JRST CDISF
	CAIE A,15
	CAIN A,12
	JRST GOI
	CAIN A,14
	JRST GOI
	CAIN A,";
	JRST COMMNT
	CAIN A,":
	JRST SCOMNT
	CAIN A,"P
	JRST PROCED
	PUSHJ P,NUMGT0
	JRST QUEST
	CAIN A,^F
	JRST SETFRM
	CAIN A,"S
	JRST SETSCL
	CAIN A,"X
	JRST SETXF
	CAIN A,"Y
	JRST SETYF
	CAIN	A,^X
	JRST	SXF
	CAIN	A,^Y
	JRST	SYF
	CAIN	A,^I
	JRST	SIF
	CAIN A,"R
	JRST READ
	CAIE A,"P
	JRST QUEST
	JUMPGE B,GO1A
	ADD B,K
	MOVNS B
GO1A:	JUMPLE	B,DISGN0
	SETOM PFLAG
STEP:	MOVEM	K,STEPK
	MOVEM	B,STEPC
	SETZM	STOPF'
	MOVEI	STEP6
	PUSHJ	P,CALL6
	PUSHJ	P,WAIT6
	MOVE	K,STEPK
	MOVE	D,PTDEND
	PUSHJ	P,DISGO	; SETUP POINT COUNT
	SKIPE	STOPF	; COMPLETED NORMALLY TEST
	JRST	TTYRD	; NO
	AOSE PFLAG
	JRST GOI
	JRST REQ
SETFRM:
IFN TS,[SKIPN	SIXF
	 JRST	QUEST	; ILLEGAL IF NO PDP6
]	HRRM B,FRAMES
	JRST GOI

SETSCL:	JUMPLE B,SETSC2
	MOVEM B,SCALE
	JRST DISGN0
SETXF:	ADDM B,XOFF
	JRST DISGN0
SETYF:	ADDM B,YOFF
	JRST DISGN0
SXF:	MOVEM	B,XOFF
	JRST	DISGN0
SYF:	MOVEM	B,YOFF
	JRST	DISGN0
SIF:	DPB	B,[300,,INTEN]
	JRST	DISGN0
SETSC2:	HRRZM B,SCSHIF
	JRST DISGN0

PROCED:	MOVSI B,1
	SETOM DISF
	JRST GO1A

SPACE:	AOSN PFLAG'
	JRST REQ
	MOVEI	B,1
	JRST	STEP

SDISF:	SETOM DISF
	JRST GOI

CDISF:	SETZM DISF
	JRST GOI

QUEST:	.TYOI "?
TTYRD:
IFN TS,[SKIPN TYIF
	JRST GOI
]	SETZM TYIF
IFN TS,	SOS RCNT
	JRST GOI

KPRINT:	.TYOI CRLF
	MOVE A,K
	PUSHJ P,DECP
	.TYOI 40
	MOVE A,N
	PUSHJ P,DECP
	.TYOI CRLF
	JRST GOI

SPRINT:	.TYOI CRLF
	MOVE A,SCALE
	PUSHJ P,DECP
	HRRE A,SCSHIF
	JUMPE A,SPRIN1
	.TYOI "_
	PUSHJ P,SDECP
SPRIN1:	.TYOI CRLF
	JRST GOI

XPRINT:	.TYOI CRLF
	MOVE A,XOFF
	ADD	A,XORG
	PUSHJ P,SDECP
	.TYOI CRLF
	JRST GOI

YPRINT:	.TYOI CRLF
	MOVE A,YOFF
	ADD	A,YORG
	PUSHJ P,SDECP
	.TYOI CRLF
	JRST GOI

HERE:	PUSHJ P,HR
	JRST GOI

HR:	.TYOI	CRLF
	MOVN	A,XOFF
	MOVEM	A,XORG
	PUSHJ	P,SDECP
	.TYOI	40
	MOVN	A,YOFF
	MOVEM	A,YORG
	PUSHJ	P,SDECP
	.TYOI	CRLF
	POPJ	P,

TPRINT:	.TYOI CRLF
	MOVE A,TIME
	IDIVI A,6
	PUSHJ P,DECP
	.TYOI CRLF
	JRST GOI
DISGO:	MOVEI	COUNTM
	PUSHJ	P,CALL6
	PUSHJ	P,WAIT6
	PUSHJ	P,DISSUB
DISGO1:	MOVEI	DSRT6
	JRST	CALL6

DISG0:	SETZM PFLAG
	SETZM TIME
DISGN0:	PUSH P,[GO1]
DISGEN:	MOVEI	DS
	PUSHJ	P,CALL6
	PUSHJ	P,WAIT6
	MOVE	D,DSDP
	POPJ	P,

DISAD:	JUMPG D,CPOPJ
	HLRE B,A
	HRRE C,A
	ADD B,XOFF
	ADD C,YOFF
	IMUL B,SCALE
	IMUL C,SCALE
	ASH B,@SCSHIF
	ASH C,@SCSHIF
	ADDI B,1000
	ADDI C,1000
	HRL B,C
	TDZE B,[776000,,776000]
	POPJ P,
	TDO B,[220000,,22000]
	PUSH D,B
	POPJ P,
COMMNT:	PUSHJ P,TYI
	SKIPGE TYIF
	.TYOI (A)
	CAIE A,15
	JRST COMMNT
	SKIPGE TYIF
	.TYOI 12
	JRST GOI

SCOMNT:	MOVE B,[440700,,SCOBUF]
SCOMN1:	PUSHJ P,TYI
	CAIN A,15
	MOVEI A,0
	IDPB A,B
	JUMPN A,SCOMN1
	JRST DISGN0

READA:	MOVEI B,1
READ:	SETCMB A,TYIF
	JUMPE A,REQ0
	JUMPLE B,UNREAD
READ1:
IFN TS,	AOS RCNT
	SOJLE B,GOI
READ2:	PUSHJ P,TYI
	CAIE A,";
	CAIN A,":
	JRST RCOM
	CAIE A,"R
	JRST READ2
	JRST READ1

IFE TS,	UNREAD==QUEST
IFN TS,[
UNREAD:	XCT OPEN
	JRST QUEST
	ADD B,RCNT
	SETZM RCNT
	JUMPG B,READ1
	JRST QUEST
]
RCOM:	PUSHJ P,TYI
	CAIE A,15
	JRST RCOM
	JRST READ2

WDECP:	JUMPGE A,WDECP1
	.DKOI "-
	MOVNS A
WDECP1:	IDIVI A,10.
	HRLM B,(P)
	SKIPE A
	PUSHJ P,WDECP1
	HLRZ A,(P)
	.DKOI "0(A)
	POPJ P,

WRITE:	.DKOI	"D
	.DKOI	CRLF
	MOVE	A,SCALE
	PUSHJ	P,WDECP
	.DKOI	"S
	HRRE	A,SCSHIF
	PUSHJ	P,WDECP
	.DKOI	"S
	MOVE A,K
	PUSHJ P,WDECP
	.DKOI "G
	MOVEI I,LP-1
W1:	SKIPN I,1(I)
	JRST WDONE
	HLRE	X,(I)
	IMULI	X,34.
	SUB	X,XORG
	MOVE	J,I
W1A:	HRRZ	J,(J)
	CAIN	J,DUMMY
	JRST	W1
	LDB	R,[14200,,1(J)]
	JUMPE	R,W1A
	HLRE	Y,(J)
	MOVNS	Y
	SUB	Y,YORG
	MOVE	A,X
	PUSHJ	P,WDECP
	.DKOI	",
	MOVE	A,Y
	PUSHJ	P,WDECP
	.DKOI	"C_7\";
	.DKOI	CRLF
	MOVEI	D,34.
	LSH	R,1
W1B:	LSH	R,1
	MOVEI	A," 
	SKIPGE	R
	MOVEI	A,".
	.DKOI	(A)
	SOJN	D,W1B
	JRST	W1A

WDONE:	MOVN A,XOFF
	SUB	A,XORG
	PUSHJ P,WDECP
	.DKOI ",
	MOVN A,YOFF
	SUB	A,YORG
	PUSHJ P,WDECP
	.DKOI "C_7\"E
	.DKOI "R
	JRST GOI

IFN TS,[
CLOSE:	.CLOSE DKOC,
	JRST GOI

FILEI:	TDZA I,I
FILEO:	MOVEI I,4
	.TYOI " 
FIN1:	PUSHJ P,SYLRD
	JUMPE B,OPENIO
	MOVEM B,NAME2(I)
	JUMPL A,OPENIO
	PUSHJ P,SYLRD
	JUMPE B,OPENIO
	EXCH B,NAME2(I)
	MOVEM B,NAME1(I)
	JUMPGE A,FIN1
OPENIO:	CAMN A,[15-40]
	XCT OPEN(I)
	JRST QUEST
	JUMPN I,GOI
	SETZM RCNT'
	JRST GOI

ISPACE:	JUMPN B,CPOPJ
SYLRD:	MOVEI B,0
	MOVE C,[440600,,B]
SYLRD1:	PUSHJ P,TYI
	SUBI A,40
	JUMPL A,CPOPJ
	JUMPE A,ISPACE
	CAIN A,":-40
	JRST ICOLON
	CAIN A,";-40
	JRST ISEMI
	CAIN A,137
	JRST IRUB
	TLNE C,770000
	IDPB A,C
	JRST SYLRD1

IRUB:	.TYOI "?
	JRST SYLRD

ISEMI:	MOVEM B,SYSNAM
	.SUSET [.SSNAME,,SYSNAM]
	MOVSI B,(SIXBIT \DSK\)
ICOLON:	HLRM B,DEVICE(I)
	JRST SYLRD

OPEN:	.OPEN DKIC,.+1
DEVICE:	SIXBIT \   DSK\
NAME1:	SIXBIT \LIFE\
NAME2:	SIXBIT \DATA\
	.OPEN DKOC,.+1
	SIXBIT \  !DSK\
	SIXBIT \LIFE\
	SIXBIT \SAVED\
SYSNAM:	SIXBIT \MS\
]
INIT:	PUSHJ P,TYI
	CAIE A,15
	JRST QUEST
IFN TS,[.CORE NBLKS
	.VALUE
	SKIPGE TYIF
]	PUSHJ P,TYI
	SETZM	LP
	MOVEI I,FLL
	MOVE T,[MOVE A,FL]
	MOVEM T,FLP
	ADDI T,FSK
	MOVEM T,-FSK(T)
	SOJG I,.-2
	MOVE I,[PUSHJ P,FSEXP]
IFN TS,[SKIPE	SIXF
	MOVE	I,[JRST 4,SIXIN]
]	MOVEM I,(T)
IFN TS,[MOVEI I,NBLKS
	HRRM I,FSEXP
]	MOVEI J,3000
	MOVEM J,DLIST+1
	MOVE D,[DLIST+1,,DLIST+2]
	BLT D,EDLIST
	PUSHJ P,DISGO1
	MOVE D,[DLIST-EDLIST+1,,DLIST]
	SETZB K,N
	MOVEI	B,ISCSHIF
	MOVEM	B,SCSHIF
	MOVEI B,ISCALE
	MOVEM B,SCALE
	SETZM	XORG
	SETZM	YORG
	POPJ P,

IN:	PUSHJ P,INIT	; D COMMAND
	SETZB X,Y
INPUT:	MOVNM X,XOFF
	MOVNM Y,YOFF
INPUT0:	SKIPGE TYIF
	JRST INPUT1
	PUSHJ P,DISGEN
	PUSH D,[220764,,60770]
	PUSH D,[173737,,20000]
	PUSHJ P,DISGO
	MOVN X,XOFF
	MOVN Y,YOFF
INPUT1:	PUSHJ P,TYI
	CAIN A,177
	JRST RUB
	CAIN A,"-
	JRST IN1
	MOVEI B,1
	CAIL A,"0
	CAILE A,"9
	JRST IN2
IN1:	PUSHJ P,NUMGT0
	JRST INRUB
IN2:	MOVMM B,COUNT'
	CAIN A,",
	JRST XSET
	CAIN A," 
	JRST SPACES
	CAIN A,".
	JRST DOTS
	CAIN A,"D
	JRST DELES
	CAIN A,15
	JRST NLINES
	CAIN A,"_
	JRST BACK
	CAIN A,"^
	JRST UP
	CAIN A,12
	JRST DOWN
	CAIGE A,40
	JRST INPUT1
	CAIN A,"X
	JRST SETXOF
	CAIN A,"Y
	JRST SETYOF
	CAIN A,"H
	JRST DHERE
	CAIN A,"S
	JRST SETSC
	CAIE A,":
	CAIN A,";
	JRST INCOM
	CAIN	A,"G
	SKIPA	K,B
	CAIE A,"E
	JRST INPUT1
	JRST DISG0

DOTS:	JUMPLE B,MDOT
DOT:	PUSHJ P,STORE0
	SOSLE COUNT
	AOJA X,DOT
	AOJA X,INPUT

MDOT:	PUSHJ P,STORE0
	SOSL COUNT
	SOJA X,MDOT
	JRST INPUT

DELES:	JUMPLE B,MDELE
DELE:	PUSHJ P,DELE0
	SOSLE COUNT
	AOJA X,DELE
	AOJA X,INPUT

MDELE:	PUSHJ P,DELE0
	SOSL COUNT
	SOJA X,MDELE
	JRST INPUT

SPACES:	ADD X,B
	JRST INPUT

UP:	ADD Y,B
	JRST INPUT

NLINES:	IFN TS,SKIPGE TYIF
	PUSHJ P,TYI
	MOVEI X,0
DOWN:	SUB Y,B
	JRST INPUT

BACK:	SUB X,B
	JRST INPUT

RUB:	SKIPL TYIF
	.TYOI "#
	SOS X
RUBOUT:	PUSHJ P,DELE0
	JRST INPUT

DHERE:	PUSHJ P,HR
	JRST INPUT0

XSET:	MOVE X,B
	PUSHJ P,NUMGET
	JRST INRUB
	MOVE Y,B
	CAIN A,15
	JRST XYSETI
	CAIE A,40
	CAIN A,11
	JRST XYSET
	CAIN A,"D
	JRST XYDEL
	CAIE A,":
	CAIN A,";
	JRST XYSETC
	ADD	X,XORG
	ADD	Y,YORG
	CAIN A,"C
	JRST INPUT
INRUB:	SKIPGE TYIF
	JRST INPUT2
	.TYOI "?
	.TYOI CRLF
	JRST INPUT2

XYSETC:	PUSHJ P,TYI
	CAIE A,15
	JRST XYSETC
XYSETI:	IFN TS,SKIPGE TYIF
	PUSHJ P,TYI
XYSET:	SUB X,XOFF
	SUB Y,YOFF
	PUSHJ P,STORE0
INPUT2:	MOVN X,XOFF
	MOVN Y,YOFF
	JRST INPUT0

XYDEL:	SUB	X,XOFF
	SUB Y,YOFF
	PUSHJ P,DELE0
	JRST INPUT2

SETXOF:	ADDM B,XOFF
	JRST INPUT2
SETYOF:	ADDM B,YOFF
	JRST INPUT2

SETSC:	JUMPLE B,SETSH
	MOVEM B,SCALE
	JRST INPUT0
SETSH:	HRRZM B,SCSHIF
	JRST INPUT0

INCOM:	PUSHJ P,TYI
	CAIE A,15
	JRST INCOM
	IFN TS,SKIPGE TYIF
	PUSHJ P,TYI
	JRST INPUT1

INVIEW:	PUSHJ P,INIT	; V COMMAND
	SETZM XOFF
	SETZM YOFF
INV1:	PUSHJ P,TYI
	CAIE A,15
	CAIN A,12
	JRST INV1
	CAIN A," 
	JRST INV1
	CAIE A,":
	CAIN A,";
	JRST ICOM
	PUSHJ P,NUMGT0
	JRST INV0
	CAIE A,15
	CAIN A," 
	JRST XYSTO
	CAIN A,",
	JRST XSTO
	CAIN A,"G
	JRST GENSTO
	CAIE A,"E
	JRST INV1
	JRST DISG0

ICOM:	PUSHJ P,TYI
	CAIE A,15
	JRST ICOM
	JRST INV1

INV0:	SKIPGE TYIF
	JRST INV1
	.TYOI "?
	.TYOI CRLF
	JRST INV1

XYSTO:	ADD	B,YORG
	HRL B,X
	MOVE A,B
	PUSHJ P,STORE
	PUSHJ P,DISAD
	JRST INV1

XSTO:	MOVE X,B
	ADD	X,XORG
	JRST INV1

GENSTO:	MOVE K,B
	JRST INV1
IFN TS,[
TYI:	SKIPGE TYIF
	JRST DKI
	SKIPN	A,RCHR
	.IOT TYIC,A
	SETZM	RCHR
	POPJ P,

DKI:	.IOT DKIC,A
	POPJ P,

TSINT:	0
	0
	SKIPL	TYIF
	SETOM	STOPF
	MOVEM	A,INTA'
	MOVEI	A,TYIC
	.ITYIC	A,
	 JFCL
	MOVE	A,INTA
	.DISMIS	TSINT+1
]
IFE TS,	DKI=PTYI

SDECP:	JUMPGE A,DECP
	MOVNS A
	.TYOI "-
DECP:	IDIVI A,10.
	HRLM B,(P)
	SKIPE A
	PUSHJ P,DECP
	HLRZ A,(P)
	.TYOI "0(A)
	POPJ P,

NUMGET:	PUSHJ P,TYI
NUMGT0:	MOVEI B,0
	CAIN A,"-
	TROA X0,1
	TRZA X0,1
NUMGT1:	PUSHJ P,TYI
	CAIL A,"0
	CAILE A,"9
	JRST NUMGT2
	IMULI B,10.
	ADDI B,-"0(A)
	JRST NUMGT1
NUMGT2:	CAIE A,177
	AOS (P)
	TRZN X0,1
	POPJ P,
	MOVNS B
	SKIPN B
	MOVNI B,1
	POPJ P,
STORE0:	MOVE A,Y
	HRL A,X
STORE:	MOVE	X0,[A,,SAVERG]
	BLT	X0,SAVERG+P-A
	HLRE	B,A	; X
	HRRE	G,A	; Y
	MOVNS	G
	ADD	B,[34.,,]
	IDIVI	B,34.
	HRRES	B
	PUSHJ	P,STRW
	HRLZI	A,200000
	MOVN	F,C
	ROT	A,(F)
	IORM	A,1(E)
	JUMPE	C,STRA
	CAIE	C,33.
	JRST	STRB
	AOS	B
	PUSHJ	P,STRW
	HRLZI	A,400000
	IORM	A,1(E)
	JRST	STRB
STRA:	SOS	B
	PUSHJ	P,STRW
	MOVEI	A,1
	IORM	A,1(E)
STRB:	MOVE	X0,[SAVERG,,A]
	BLT	X0,P
	POPJ	P,

STRW:	MOVEI	E,LP-1
STR1:	MOVE	D,E	; FIND COL
	SKIPN	E,1(D)
	JRST	STR2
	HLRE	F,(E)
	CAMGE	F,B
	JRST	STR1
	CAMN	F,B
	JRST	STR3	; FOUND IT
STR2:	FBLK	; START NEW COL
	MOVE	F,B
	HRLI	F,DUMMY
	MOVSM	F,(A)
	HRRZM	E,1(A)
	HRRM	A,1(D)
	MOVE	E,A
STR3:	MOVE	D,E	; FIND ROW WORD
	HRRZ	E,(D)
	HLRE	F,(E)
	CAMGE	F,G
	JRST	STR3
	CAMN	F,G
	POPJ	P,
	FBLK	; START NEW WORD
	SETZM	1(A)
	HRL	E,G
	MOVEM	E,(A)
	HRRM	A,(D)
	MOVE	E,A
	POPJ	P,

DELE0:	MOVE A,Y
	HRL A,X
DELETE:	MOVE	X0,[A,,SAVERG]
	BLT	X0,SAVERG+P-A
	HLRE B,A
	HRRE	G,A
	MOVNS	G
	ADD	B,[34.,,]
	IDIVI	B,34.
	HRRES	B
	PUSHJ	P,STRW
	HRLZI	A,200000
	MOVN	F,C
	ROT	A,(F)
	PUSHJ	P,DBIT
	JUMPE	C,DELA
	CAIE	C,33.
	JRST	DELB
	AOS	B
	PUSHJ	P,STRW
	HRLZI	A,400000
	PUSHJ	P,DBIT
	JRST	DELB
DELA:	SOS	B
	PUSHJ	P,STRW
	MOVEI	A,1
	PUSHJ	P,DBIT
DELB:	MOVE	X0,[SAVERG,,A]
	BLT	X0,P
	POPJ	P,

DBIT:	ANDCAB	A,1(E)
	JUMPN	A,CPOPJ
	HRR	A,(E)	; FLUSH WORD
	HRRM	A,(D)
	MOVE	A,FLP
	MOVEM	A,(E)
	HRRM	E,FLP
	POPJ	P,

CONSTANTS
VARIABLES

NBLKS==<.+1777>_<-10.>
MEMTOP==NBLKS_10.
IFE TS,[
MEMTOP==PDP6MT-DDTF*10000
IFN DDTF,PATCH=MEMTOP
]
IFN TS,PATCH:

END	GO
