.mllit==1

TITLE PICTURE DISPLAY

;OUTPUTS ON TV'S

;tv's have 455. vertical lines of 576. dots each (262080. bits out 'o 262144).

;memory is organized as 9 64.-bit words (equiv to 18. 32.-bit words) per line.
;the pdp10 accesses half of such a word (or two 16.-bit chunks) at once.
;these 32. bits are packed left justified into the 36. bits.

;tvend (or the last word of the tv-memory) has two functions:
;bit  200000 when on, complements the black/white output.
;bits 177760 are a word-counter for which 64.-bit word the frame is to start on.
;for winnage the number ought to be a multiple of 9.

;Characters are 10. lines high and 5 points wide (right and top justified).
;Line-pitch is 12. TV-lines, Character-pitch is 6 TV-points.
;Thats 96. chrs/line exactly and 37. and 11./12. lines (3552. chrs).

A=1
B=2
C=3
D=4
E=5
F=6
G=7

T=10
U=11
V=12
W=13
X=14
Y=15
Z=16

P=17

tv==100*2000			;address of beg of tv buffer
tvend==100*2000+10*2000-1	;black/white bit and frame start number

dtyoc==2
ttyc==3

	jrst exit

BEG:	MOVEI P,PDL
	MOVE A,[-10,,100]
	MOVEI B,0
	.CALL [SETZ
		SIXBIT /CORBLK/
		1000,,600000
		1000,,-1
		A
		1000,,-2
		SETZ B]
	.VALUE
	.break 12,[5,,jcl]	;get job control language
	move c,[440700,,jcl]
	ildb a,c
	movei b,0
	jumpe a,bull	;nothing there, default
	jrst gobsub

goblp:	rot b,3
	ior b,a
	ildb a,c
gobsub:	subi a,60
	jumpge a,goblp
	movem b,octnum

bull:	.open dtyoc,[sixbit /  1TTY/]
	.value
	.iot dtyoc,[^P]
	.iot dtyoc,["T]	;home cursor up
	.iot dtyoc,[^P]
	.iot dtyoc,["B]	;home cursor up
	.close dtyoc,
 	pushj p,spdset	;set up fast loop in acs
	pushj p,displ
	pushj p,sidcpy	;copy side-ways
	pushj p,dwncpy	;copy down-wards

surlp:	.open ttyc,[sixbit /   TTY/]
	.value
	.iot ttyc,a
	cain a,177
	.value	[asciz /:bkph;tvwrit
:vp
/]
	pushj p,clr
	jrst exit

roll:	setzm z		;roll it up the screen
rollp:	move a,rsleep
	.sleep a,
	move a,rolinc
	imuli a,220	;9.*20
	add z,a
	cail z,200000
	setzm z		;reset
	movem z,tvend
	jrst rollp

ssleep:	24.	;sleep interval between blinkage
rsleep:	10.	;sleep interval between rollage

rolinc:	12.	;lines increment in roll

exit:	setzm tvend
	pushj p,clr
	movei a,0
	dpb a,[041400,,tvend]
	.value [asciz /:killî/]

clr:	movsi f,-10*2000+1
	move a,[setzm tv(f)]
	move b,[aobjn f,a]
	move c,[popj p,]
	jrst a

set:	movsi f,-10*2000+1
	movni e,20
	move a,[movem e,tv(f)]
	move b,[aobjn f,a]
	move c,[popj p,]
	jrst a

displ:	movei d,tv	;ptr to tv-screen
	movei b,227.	;y
thrlp:	movni a,288.	;x-1
	pushj p,lndis	;once per line
	jrst twolp

onert:	movem z,(d)	;put up on screen
	aos d		;advance pointer
twolp:	movei y,32.	;32. bits per word
	setzm z		;start with clear word
	jumpl a,e	;more words on this line?
	addi d,9.	;skip over 9 words to fill in later
	sosl b		;y = y-1
	jrst thrlp	;yes
	popj p,

sidcpy:	movei x,tv	;beginning of line
	movei y,tv+17.	;end of line
	movei w,228.	;number of lines to do
sidlop:	hrli x,-9.	;9. words to do per line
	movei c,0	;right most bit on screen will be zero
sidinl:	move a,(x)	;get word
	lsh a,-5	;squezze out four blanks and one bit
	circ a,44	;reverse and put in b
	trne c,20	;test saved bit
	tro b,20	;put it in
	movem b,(y)	;put in generated word
	move c,(x)	;save the bit
	sos y
	aobjn x,sidinl
	movei x,9.(x)	;to start of next line
	movei y,27.(y)	;to end of next line
	sojg w,sidlop
	popj p,

dwncpy:	move b,[hrli y,-18.]
	move c,[move a,(x)]
	move d,[movem a,(y)]
	move e,[aos x]
	move f,[aobjn y,c]
	move g,[subi x,36.]
	move t,[sojg w,b]
	move u,[popj p,]
	movei x,tv+226.*18.
	movei y,tv+228.*18.
	movei w,227.	;that many lines to copy
	jrst b
	
lndis:	move c,b
	imul c,c		; y^2
	addi c,288.*288.	; + (x-1)^2
	popj p,

spdset:	move e,[add c,a]
	move f,[aos a]
	move g,[add c,a]	; + 2x + 1
	move t,[trne c,1000]	;even or odd?
	move u,[tro z,10]	;set the bit on
	move v,[rot z,1]	;shift in another bit
	move w,[sojg y,e]
	move x,[jrst onert]	;finish 32. bits
	hrr t,octnum
	popj p,

octnum:	1000	;bit mask

jcl:	block (72./5+1)
pat:	block 100
pdl:	block 100

	end beg
