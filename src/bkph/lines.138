TITLE LINES

FLEFLG==1
STEREO==1
LIBRAR==1
OUTALL==1
INPALL==1
STMPIT==1
CNTRLX==1
COMMND==1

.INSRT SUPPRT >

START:	.CORE CORSIZ
	.VALUE
	JSR INIT
	SZ FRAMSW
.I RSTART=#ACCEPT, MESSX=12.
	SETOM DSPARY-YDISP+DSPIB
	SETOM DSPARY-YDISP+DSPIB+LDSPIB
	Q STRSWD	;GENERATE THE TWO DISPLAY ARRAYS
	Q DINNE1
	Q DINYY
	Q ADISON	;SWITCH ON DISPLAY
.I DISAAD<DSPARY>	;PUT ARRAY 2 ON DISLIS
	SETZB Y,TYPED
.I PTDTA1<-1>
	J ACCEPT

NMDATA=3000

MDATA:	BLOCK NMDATA
MSAVE:	BLOCK NMDATA

DELTA:	4	;<77	.0025" * DELTA
RATE:	7	;<7	200/(RATE+1) PER SECOND
EPSIL:	30.	;ERROR ACCEPTABLE IN DELETE OPS-.0025"*EPSIL
SHTOVR:	100.	;LESS THAN THIS  WILL NOT BE SQUARED-.0025"*SHTOVR
PERLN:	5	;ITEMS PER LINE IN FOOD
DSRAT:	6	;LOOKS PER DISPLAY IN STRGT


SNGFLG:	1	;NON-ZERO TO CAUSE SINGLE POINTS TO BE DELETED
SLPFLG:	1	;NON-ZERO TO CAUSE SLOPPINESS TO BE REMOVED
SQRFLG:	1	;NON-ZERO TO SQUARE UP VERTICALS AND HORIZONTALS
DELFLG:	1	;NON-ZERO TO ALLOW DELETING LINES BY REDRAWING THEM

NOPINT:	0	;NON-ZERO TO GENERATE NO DECIMAL POINTS ON OUTPUT
CRSFLG:	1	;NON-ZERO TO MAP TABLET ON WHOLE SCREEN IN WINDOW OPS
COMFLG:	1	;NON-ZERO TO CAUSE COMPACTING FOR EACH DSUFR
ONEFLG:	1	;NON-ZERO SO OUTPUT AND INPUT IS SCOPE COORDS RATHER THAN TABLET
ESYFLG:	1	;NON-ZERO TO CALL ESYPLT EACH FOOD
EPSFLG:	1	;NON-ZERO TO SCALE EPSIL TOO

BUTFLG:	0	;NON-ZERO TO ENABLE 'PEN-BUTTONS'
WNDFLG:	0	;NON-ZERO TO ENABLE WINDOWING
FIGFLG:	0	;NON-ZERO TO MOVE FIGURES,NOT SEGMENTS
LSTFLG:	-1	;-1 => LIST OF QUADRUPLES, 0 => LIST OF LISTS OF DOTTED PAIRS, 1 => LIST OF LISTS OF LIST PAIRS IN OUTPUT


TBLC==7

TBDATA:	 BLOCK 100.

TBLOP:	2,,(SIXBIT/TAB/)
	0
	0

NGRX:	10.	;NUMBER OF INTERVALS IN X DIRECTION
NGRY:	10.	;NUMBER OF INTERVALS IN Y DIRECTION

GRDINT:	0	;GRID INTENSITY
DRWINT:	2	;DRAWING INTENSITY


GRIDS:	GET NGRX,NGRY,GRDINT
DINNE1:
.I GXD0=GYD0=0,GXD=4099./(NGRX+1),GYD=4099./(NGRY+1)
DINNER:
.I DISINS<DSPSEL<0>>	;GO TO FRAME ETC DISLIS
	Q DSCRT
	Q DISHAD
	Q GRID
	Q DISGO		;OK FOR ARRAY 1 TO DO A DISGO
.I DSPSEL<1>		;GO TO DRAWING DISLIS
	R

GRID:	SKIPN W,GRDINT
	R
	Q DSPBRT	;CHANGE INTENSITY
.I 1,1
	SKIPN WNDFLG
	J NOMGT
	HLRZ A,WXFC
	HLRZ B,WYFC
NOMGT:
.I ,,GRXD=GXD/A,GRYD=GYD/B
.I GRGX=GXD0
ZPGRLX:
.I GHCPX<GRGX>
.I GRGX=GRGX+GRXD
	CAIGE A,4096.
	J ZPGRLX
.I GRGY=GYD0
ZPGRLY:
.I GHCPY<GRGY>
.I GRGY=GRGY+GRYD
	CAIGE A,4096.
	J ZPGRLY
	R
GHCPX:	SKIPN WNDFLG
	J NOWNDX
	IMUL A,WXFC
	HLRES A
	ADD A,WXOF
NOWNDX:	LSH A,-2	;TO SCOPE COORDS
	SKIPLE A
	CAIL A,1777
	R
	MM A,CRNG'
.I DSPPNI<,CRNG,0>
.I DSPVCA<,CRNG,1777>
	R

GHCPY:	SKIPN WNDFLG
	J NOWNDY
	IMUL A,WYFC
	HLRES A
	ADD A,WYOF
NOWNDY:	LSH A,-2	;TO SCOPE COORDS
	SKIPLE A
	CAIL A,1777
	R
	MM A,CRNG'
.I DSPPNI<,0,CRNG>
.I DSPVCA<,1777,CRNG>
	R

GX0:	0
GX1:	0
GXD0:	0
GYD0:	0
GY0:	0
GY1:	0
GXD:	0
GYD:	0
GRGX:	0
GRGY:	0
GRXD:	0
GRYD:	0
	
BYTOLD==FOOADD	;IN PROTECTED STORAGE

CMWNGR:
.I BYTOLD=BYTPNT,MODOLD'=DSPMOD
	Q GTSGX
	Q CHOP	;REMOVE TAIL END OF DISPLAY ARRAY AGAIN
	M X,YBVR
	M B,Y
	M Y,X	;RESTORE Y TO BEFORE GRID SPEC
	SUB B,X	;NUMBER OF NEW POINTS
	R

GRIDT:.I APOUSP=#WNDPS
	Q CMWNGR
	CAIGE B,2
	J GRIDW1	;ONE POINT TOUCHED ONLY
	MI A,1
	SKIPN GRDINT
	MM A,GRDINT	;IN CASE ITS OFF
	M A,MDATA(X)
	HLRZM A,GX0
	HRRZM A,GY0
	M A,MDATA+1(X)
	HLRZM A,GX1
	CAIG B,2
	J GLYTP		;ONLY TWO POINTS TOUCHED
	M A,MDATA+2(X)
	HRRZM A,GY1
GMLYT:	Q GTGFTA
GMLAT:	J DINNER	;MAKE NEW GRID ETC.

CHOP:	MI A,403737	;WIPE OUT DISPLAY GOODIES FROM BYTOLD TO BYTPNT
	M B,BYTOLD
	IDPB A,B
	CAME B,BYTPNT
	J .-2
.I BYTPNT=BYTOLD,DSPMOD=MODOLD
	SO XDISP
	SO YDISP
	SO XLST
	SO YLST
	R

GRIDW1:	SKIPN GRDINT
	J GROFFN
	SZ GRDINT
	J GMLAT

GROFFN:
.I GRDINT=1
	J GMLAT

GLYTP:
.I GY1=GY0+ABS<GX1-GX0>
	J GMLYT

NGRTX:
.I GX1=GX0,GX0=GX1
	J GTGFTA

NGRTY:	
.I GY1=GY0,GY0=GY1

GTGFTA:
.I GX1-GX0,GY1-GY0
	JUMPL A,NGRTX
	JUMPL B,NGRTY
	CAIL B,120.
	CAIGE A,120.
	J BDGRPR
.I GXD=,GYD=
	SKIPE WNDFLG
	Q FXUPCF
.I GXD0=GX0-(GX0/GXD)*GXD,GYD0=GY0-(GY0/GYD)*GYD
	R

FXUPCF:	HLRZ A,WXFC
	HLRZ B,WYFC
.I ,,GXD=GXD*A,GYD=GYD*B
	R

BDGRPR:	STRIKE _GRID TOO FINE
	R	;USE OLD VALUES

OPNTAB:	SKIPE TBLOPN
	J INGSUT
.I (DELTA*8+RATE)*8+2
	HRLM A,TBLOP
	.OPEN TBLC,TBLOP
	STOP TABLET WON'T OPEN
	SO TBLOPN
	SZ Z
INGSUT:
.I IUPWAT'=IUPCNT
	M A,IGNTMS
	CAILE A,1
	SOS IGNTMS	;INCREASE RATE
	R

WXFC:	2,,0
WYFC:	2,,0
WXOF:	-2048.
WYOF:	-2048.

MASK1:	7777,,7777
TBLOPN:	0	;NON-ZERO IF TABLET OPEN
IGNTMS:	1	;DIVIDES RATE TO GIVE TRUE RATE READ AT
IGNCNT:	0
IUPCNT:	250.	;TIMES ROUND BEFORE WE INCREASE RATE AGAIN

DEFINE ZUM XX
	AND XX,MASK1
TERMIN
DISHAD:	Q FRAME
	SKIPN HEADSW
	J NOLSD
.I TALK<#STAMP,MESSX,MESSY>
NOLSD:	SKIPN BUTFLG
	R
.I TALK<#TPBTMS,TBTXL,TBTYL>
.I TALK<#BTBTMS,TBTXL,BTBYL>
	R

TPBTMS:	PRINT _&0.MOVE&12.MOVE&24.COPY&36.COPY&48.FLUSH&60.FLUSH&72.FLUSH
	PRINT _&0.SEGMENTS&12.FIGURES&24.SEGMENTS&36.FIGURES&48.POINTS&60.SEGMENTS&72.FIGURES
	R

BTBTMS:	PRINT _&0.FINISH&12.LINE&24.CURVE&36.WINDOW&48.GRID&60.GENERATE&72.MOVE
	PRINT _&0.     &12.DRAW&24.DRAW&36.DEFINE&48.DEFINE&60.POLYGONS&72.POINTS
	R

TBTXL:	115.	;X TO PUT BUTTON MESS ON SCOPE

TBTYL:	970.	;Y TO PUT TOP BUTTON MESS
BTBYL:	100.	;Y TO PUT BOTTOM BUTTON MESS

NBUT:	7	;NUMBER OF BUTTONS IN EACH ROW

XBTHG:	1000.	;RIGHT END OF RIGHT MOST BUTT
XBTLW:	-50.	;LEFT END OF LEFTMOST BUTT

YBTHG:	930.	;Y ON TABLET OF UPPER BUTTONS
YBTLW:	90.	;Y ON TABLET OF LOWER BUTTONS

TSTBTT:	SKIPN BUTFLG
	R		;BUTTONS NOT ENABLED
	HRRZ B,PRSVAL	;USE UNMASKED VALUE SAVED BY INSTANT DISPLAY HACK
	CAML B,YBTLW
	CAML B,YBTHG
	SKIPA
	R		;BUTTONS NOT PRESSED
	SZ TOBBUT
	CAML B,YBTLW
	SO TOBBUT'
	HLRZ A,PRSVAL	;PICK UP X VALUE
.I (A-XBTLW)*NBUT/(XBTHG-XBTLW)-TOBBUT*NBUT
	MM A,LASJOB'
	M B,CARNUM	;MAY NOT ALWAYS WIN
	CAIL B,50.
	STRIKE _
	M P,[-PDLL+1,,PDL]	;RESTORE PDL
	TYP3 PPCHCH(A)	;TYPE APPROPRIATE CHARACTER
	SZ BUTFLG	;AVOID GETTING BACK INTO THIS ROUTINE
	Q REMOS		;AWAIT LIFT-OFF
	SO BUTFLG	;HAD TO BE ON TO GET IN HERE
.I PTDTA1<-1>
	M A,LASJOB
	JRST @DPCHTB(A)	;GO TO APPROPRIATE ROUTINE
GOST:	POP P,CARNUM
GO:	Q OPNINT
.I APOUSP=#POPJP
.I BUTFLV'=BUTFLG
	SZ FRAMSW
	SO BUTFLG
	SKIPN BUTFLV
	Q DINNER	;DISPLAY BUTTONS
	Q REDTAB	;AWAIT BUTTON PRESSURE
	J .-1

WINDOX:	PUSH P,CARNUM
	Q WINDOW
	J GOST

GRIDTX:	PUSH P,CARNUM
	Q GRIDT
	J GOST

DPCHTB:	APPLES?DRAWMR?CONTMR?WINDOX?GRIDTX?CPYPOL?STRPNT
	STRSEG?STRFIG?CPYSEG?CPYFIG?FLUSH?FLSSEG?FLSFIG

PPCHCH:	SIXBIT / R!/
	SIXBIT / D!/
	SIXBIT / C!/
	SIXBIT / W!/
	SIXBIT / G!/
	SIXBIT / P!/
	SIXBIT / MP!/
	SIXBIT / MS!/
	SIXBIT / MF!/
	SIXBIT / CS!/
	SIXBIT / CF!/
	SIXBIT / FP!/
	SIXBIT / FS!/
	SIXBIT / FF!/

DINYY:	SO XLST		;INITIALISE ALL DISPLAY GOODIES
	SO YLST
	M A,DSPARY
	M B,ARYORG(A)
	M C,[403737,,403737]
	MM C,(B)
	HRRZ C,BYTPNT
	AOS C
	CAMGE C,B
	J DINX1
	HRLS B
	AOS B
	BLT B,-1(C)	;CLEAN OUT DISPLAY ARRAY
DINX1:	Q DISIN2
.I DSPPNZ<,0,0>		;TO PREVENT FOLLOWING FROM BEING ORED IN 7
	M W,DRWINT	;SET INTENSITY
	J DSPBRT
REDTAB:
.I IGNCNT=IGNTMS
REDTA0:	SKIPE TYPED	;TEST TYPE 'INTERUPT'
	J APPLES	;FINISH
	SOSG IUPWAT
	Q INGSUT	;INCREASE RATE AGAIN
	AOBJN Z,.+2	
	Q GTOMP		;GET A GULP
	M A,(Z)
	M B,A
	.RBTC B,	;SHOW IT
	TLZE A,400000
	J REDTA1	;MISSED DATA 
	M B,A		;DISPLAY ONE POINT SPECIAL
	ZUM B
	Q ADJXYW
	MM B,PRSVAL'	;SAVE FOR BUTTON TEST
	MOVSS B
	SUB B,CROSSZ
	M C,@SECRT	;DISPLAY CURRENT PEN LOCATION
	AND C,[776000,,776000]
	IOR B,C
	MM B,@SECRT
	TLNN A,340000	;IN CONTACT ?
	Q TSTBTT	;YES,TEST BUTTONS
	TRNN A,760000	;IS REPEAT COUNT >1 ?
	SOSG IGNCNT
	R
	J REDTA0	;IGNORE, READ NEXT WORD

REDTA1:	Q INGSLW
	J REDTA0

INGSLW:	M A,IGNTMS
	CAIG A,5
	AOS IGNTMS	;DECREASE RATE
	R

APPLES:	.CLOSE TBLC,
	SZ TBLOPN
.I PTDTA1<-1>
	Q @APOUSP	;FINAL REQUEST
	SZ BUTFLG	;DISABLE BUTTONS
	M P,[-PDLL,,PDL]	;RESTORE PDL
	Q DINNER	;REMOVE BUTTON DISPLAY
	J @RSTART	;RETURN TO COMMAND INTERPRETOR

ADJXYW:	HRRE C,B	;MAP POINT IN B ON 340  - WITH LIMITS
	HLRES B
	SKIPE WNDFLG
	SKIPE CRSFLG
	SKIPA
	Q AEDG
	ASH B,-2
	ASH C,-2
	SKIPGE B
	SZ B
	CAIL B,2000
	MI B,1777
	SKIPGE C
	SZ C
	CAIL C,2000
	MI C,1777
	HRLS B
	HRR B,C
	R

ADJWYX:	SKIPE CRSFLG
	SKIPN WNDFLG
	R
EADG:	HRRE B,A	;UNMAP POINT IN A
	HLRES A
	SUB A,WXOF
	SUB B,WYOF
	HRLES A
	HRLES B
	IDIV B,WYFC
	PUSH P,B
	IDIV A,WXFC
	POP P,B
	HRLS A
	HRR A,B
	R

APOUSP:	0		;WHERE TO GO WHEN DONE

SQRITN:	M A,MDATA(Y)
	SUB A,MDATA-1(Y)
	HRLZ B,SHTOVR	;SHORTEST SEGMENT SQUARED UP
	Q SAMGA		;IS IT TOO SHORT ?
	SKIPA
	R		;YES
	HRLZ B,EPSIL
	Q SCLEPS
	HLRZ C,B
	HLRE A,MDATA(Y)	;SQUARE UP VERTICALS AND HORIZONTALS
	HLRE B,MDATA-1(Y)
	SUB A,B
	MOVMS A
	CAMGE A,C
	HRLM B,MDATA(Y)	;COPY IN OLD X
	HRRE A,MDATA(Y)
	HRRE B,MDATA-1(Y)
	SUB A,B
	MOVMS A
	CAMGE A,C
	HRRM B,MDATA(Y)	;COPY IN OLD Y

	M A,MDATA(Y)
	R

SCLEPS:	SKIPE EPSFLG
	SKIPN WNDFLG
	R
	IDIV B,WXFC	;RANDOM CHOICE OF X FACTOR-KILLS C
	HRLZS B
	R

POUSP:	SKIPN SLPFLG	;EXIT ROUTINE FRO DRAMR
	R
SLOPFX:	CAIG Y,4
	R
	SKIPE SNGFLG
	Q SNGKLL
	Q SLOP		;FIX UP SLOP AND DISPLAY AGAIN
	SKIPN SNGREM
	SKIPE SLPREM	;DID ANYTHING CHANGE
	J DSUFRB		;YES, NEED TO REDISPLAY
	R

GTOMP:	M B,[-100.,,TBDATA]	;READ A GULP FROM TABLET
	.IOT TBLC,B
	SUB B,[-100.,,TBDATA]
	CAML B,[20.,,20.]
	Q INGSLW	;REDUCE RATE IF READ MORE THAN 20. POINTS
	HLLZS B
	MOVN Z,B
	HRRI Z,TBDATA	;GEN NEW AOBJ POINTER
	R

DSCRT:	MI T,117		;GENERATE PLACE FOR PEN-POINT IMMEDIATE DISPLAY
	Q DSPPTZ
.I DSPPNI<1,1>
	HRRZ A,BYTPNT
	MM A,SECRT
	JRST DCROST

EPSDRW:	40.	;.075"

OPNINT:	Q OPNTAB
	SZ TYPED
.I PTDTA1<-1>
	R

DM:DRAWMR:
.I APOUSP=#POUSP,EPSIL=EPSDRW
	Q OPNINT
	J DRAWL6
DRAWL1:	
.I PTDTA1<-1>
	Q POUSP	;FIX UP SLOP-MAY REDISPLAY
DRAWL6:	SZ T
	Q CONTAC	;AWAIT CONTACT
RCRDL1:	ZUM A
	Q ADJWYX
	Q PTDTA1	;STORE DATA AND DISPLAY IT
	Q TOUCHD	;AWAIT UP AND THEN DOWN
	J DRAWL5	;REMOVED FAR
	J RCRDL1	;CONTACT AGAIN, RECORD IT

TOUCHD:	Q REDTAB
	TLZE A,40000	;REMOVED FAR ?
	R		;YES-UNUSUAL
	TLZN A,200000	;IN CONTACT ?
	J TOUCHD	;YES
TOUCHT:	Q REDTAB	;NOW WE ARE IN LIGHT CONTACT ONLY
	TLZE A,40000	;REMOVED FAR ?
	R		;YES
	TLZE A,340000	;IN CONTACT ?
	J TOUCHT	;NO
	AOS (P)
	R

FCRUSH:	ZUM A
	Q ADJWYX
UPYPL:	MM A,MDATA(Y)	;STORE IT-WITHOUT HAIR TYPICAL OF PTDTA
	Q PYUP
	SKIPA
PTDTA1:	Q PTDTA
	SKIPL C,MDATA-1(Y)
	J DDTPIR	;DISPLAY IT
	SZ T		;
	R

PTDTA:	CAMN A,MDATA-1(Y)
	R		;SAME AS LAST POINT
	MM A,MDATA(Y)	;STORE DATA
	SKIPL A
	SKIPN EPSIL
	J PYUP		;FORGET ALL THE HAIR
	SUB A,MDATA-1(Y)
	Q SAMGB
	SKIPA	
	R		;'SAME' AS LAST POINT
	SKIPE SQRFLG
	Q SQRITN	;SQUARE UP VERTICAL AND HORIZONTAL LINES
	SKIPN DELFLG
	J PYUP		;DELETIONS NOT PERMITTED
	SKIPL E,MDATA(Y)
	SKIPGE F,MDATA-1(Y)
	J PYUP		;EITHER CURRENT OR PREVIOUS -1
	SUB A,MDATA-2(Y)
	Q SAMGB		;EQUAL TO SECOND LAST POINT ?
	J SUB2YD	
	SOS Y		;YES, DELETE
	J DSUFRA	;END OF SNAKE BIT IT - COULD BE AVOIDED
TOUFT:	Q REMOS
CONTAC:	Q REDTAB
	TLZE A,340000	;IN CONTACT ?
	J CONTAC	;NO
	R

REMOS:	Q REDTAB
	TLZN A,200000	;WAIT FOR LIFT-OFF
	J REMOS
	R

GTSGX:	Q OPNINT	;GET ONE SEGMENT WITHOUT A LOT OF HAIR
	MM Y,YBVR'
	Q TOUFT		;WAIT FOR LIFT-OFF AND CONTACT

FCRDL1:	Q FCRUSH	;TRANSFORM AND RECORD
	Q TOUCHD
	R		;LIFTED FAR, DONE
	J FCRDL1	;MORE TO COME


WINDOW:	;READ WINDOW AND MAGNIFY THE WORLD
.I APOUSP=#WNDPS,  WNDFLV'=WNDFLG
	SZ WNDFLG
	SKIPE WNDFLV
	Q RESTRM
	Q CMWNGR
	CAIGE B,2
	J WNDW1		;ONLY ONE POINT TOUCHED
	M A,MDATA(X)
	HLRZM A,XR0
	HRRZM A,YR0
	M A,MDATA+1(X)
	HLRZM A,XR1
	CAIG B,2
	J NLYTP		;ONLY TWO POINTS
	M A,MDATA+2(X)
	HRRZM A,YR1
CMLYT:	Q GTCFTA	;CALCULATE TRANSFORMATION
OLMAG:	SO WNDFLG
ALMAG:	J RESTRM	;CHANGE GRID TOO

WNDW1:	SKIPN WNDFLV	;ONE POINT TOUCHED ONLY
	J OLMAG
	R		;NOTHING MORE TO DO

OPEN:	SKIPN WNDFLG	;KILL MAGNIFICATION
	R
OPEN1:	SZ WNDFLG
	J RESTRM

WNDPS:	CAMN Y,YBVR
	R
	M Y,YBVR
	J RESTRM

RESET:	.I WNDFLV'=WNDFLG
	SETZB Y,WNDFLG	;KILL ALL DATA
.I PTDTA<-1>
RESTRA:	SKIPE WNDFLV
RESTRM:	Q DINNER	;REDO GRID ETC.
	J DSUFRA	;REDO DRAWING

NLYTP:		;USE A SQUARE WINDOW
.I YR1=YR0+ABS<(XR1-XR0)>
	J CMLYT

;GET HERE IF LOOSE CONTACT IN DRAW
DRAWL5:	SKIPGE MDATA-1(Y);WAS LAST A -1
	J DRAWL6	;UNUSUAL
	Q DROWNT	;INVESTIGATE WHAT REALLY HAPPENED
	J DRAWL1
.I PTDTA1<-1>		;JUST IN CASE
	J DRAWL6

DROWNT:	SKIPL MDATA-2(Y)	;WAS LAST A POINT ?
	R		;NO, A SEGMENT
	SKIPE SNGFLG	;YES, ARE WE REMOVING SINGLE POINTS ANYWAY ?
	J SUTN2		;YES, RESULTING DSUFRA COULD BE AVOIDED
	CAIG Y,3	;ENOUGH DATA TO WARRANT THIS FUSS
	R		;NO
	M C,MDATA-1(Y)
	Q KLSPPN	;SEE IF CAN KILL SPECIFIC POINT INC 
	SKIPN SRFNIT	;DID ANYTHING HAPPEN
	R		;NO
SUTN2:	SOS Y		;YES, NEED TO REDISPLAY
	Q DSUFRA
	AOS (P)
	R

KLLTWO:	SUBI Y,2
	SKIPG Y
	J FOOKL2
	MI A,MDATA-1(X)
	HRLI A,MDATA+1(X)
	CAMGE X,Y
	BLT A,MDATA-1(Y)
	R

FOOKL2:	MI Y,1
	R

KLSPPN:	MI X,2		;NOW SEE IF POINT ALREADY EXCISTS
	SZ SRFNIT'
	HRLZ B,EPSIL
	Q SCLEPS
RTLPSR:	SKIPGE MDATA-2(X)	;SINGLE POINT ?
	SKIPL MDATA(X)
	J NDLPSR		;NO
	M A,MDATA-1(X)
	SUB A,C
	Q SAMG		;'SAME' POINT ?
	J NDLPSR	;NO
	SO SRFNIT	;YES, LETS DELETE IT
	Q KLLTWO
	SKIPA
NDLPSR:	AOS X
	CAMGE X,Y
	J RTLPSR
	R
SUB2YD:	Q PYUP		;NO TROUBLE AT END SO TRY DELETING SEGMENT
	MI W,-1(Y)
	Q ASSOXY	;TRY KILLING THIS SEGMENT
	SKIPN DELPHN	;DID IT HAPPEN
	R		;NOTHING HAPPENED
	M A,MDATA-1(Y)
	SO MDATA-1(Y)	;PUT IN A RECORD MARK
	MM A,MDATA(Y)	;MAKE AS IF POINTED HERE
	PUSHJ P,PYUP
	J DSUFRA	;DISPLAY ANEW

PYUP:	AOS Y		;INCREMENT Y
	CAIGE Y,NMDATA
	R
	STOP _TOO MUCH DATA-PYUP

SNGKLL:	SZ SNGREM'
	CAIG Y,4	;KILL SINGLE POINTS
	R
	MI X,2
SNGK1:	SKIPGE MDATA(X)
	SKIPL MDATA-2(X)
	J NOGATP
	Q KLLTWO
	SO SNGREM	;INDICATE SOMETHING HAPPENED
	SKIPA
NOGATP:	AOS X
	CAMGE X,Y
	J SNGK1
	R

FOOD:	SKIPE ESYFLG
	Q ESYPLT	;REARRANGE DATA
.I FRCNPT=NOPINT
	SZ X		;TYPE OUT DATA
	PRINT _(_
FOOD1:	SKIPL MDATA(X)
	Q SEGMT
	AOS X
	CAMGE X,Y
	J FOOD1
	PRINT _)_
	SZ FRCNPT
	R

SEGMT:	PRINT _(
	SZ U		;TYPE OUT SEGMENT
	SETOM COMETH'	;TELL BEGINNING OF SEGMENT
SEGL1:	SKIPGE B,MDATA(X)
	J NDSEY1
	Q DOTPIR
	AOS X
	CAMGE X,Y
	J SEGL1

NDSEY1:	PRINT )_
	R

DOTPIR:	HLRE A,B	;TYPE OUT DOTTED PAIR
	HRRES B
	SKIPE ONEFLG
	ASH A,-2
	SKIPE ONEFLG
	ASH B,-2
	AOS U
	SKIPL LSTFLG
	CAMGE U,PERLN	;ENOUGH FOR THIS LINE OF OUTPUT
	J PRUPR
	PRINT _ 
	MI U,1
PRUPR:	SKIPLE COMETH
	Q TERQUA
	SKIPGE LSTFLG
	AOS COMETH
.I NONE'=,NTWO'=
	SKIPL LSTFLG
	PRINT (
	PRINT #1 
	SKIPN LSTFLG
	PRINT . 
	PRINT #2
	SKIPL LSTFLG
	PRINT )
	SKIPGE LSTFLG
	PRINT  
	R

TERQUA:	PUSH P,A
	PUSH P,B
	PRINT )
	CAMGE U,PERLN
	J POOPS
	PRINT _
	MI U,2
POOPS:
.I NONE,NTWO
	PRINT (#1 #2 
	POP P,B
	POP P,A
	POPJ P,

DFOOD:	Q DSUFR		;DISPLAY DATA
	J DISGO		;DANGER !

DSUFR:	SKIPE SNGFLG
	Q SNGKLL	;KILL SINGLE POINTS
DSUFRB:	SKIPE COMFLG
	Q COMPCT	;COMPACTIFY
DSUFRA:	Q DINYY		;DISPLAY IT ALL
	SETZB X,T
DFOOD1:	SKIPL C,MDATA(X)
	Q DSEGL3
	AOS X
	CAMGE X,Y
	J DFOOD1
	R

DSEGL1:	SKIPGE C,MDATA(X)
	J DSEGL2
DSEGL3:	Q DDTPIR
	AOS X
	CAMGE X,Yî
	J DSEGL1
	R

DSEGL2:	SZ T
	R

DDTPIR:	HLRE B,C	;DISPLAY A DOTTED PAIR
	HRRES C
	JUMPE T,PNTFRS
	PUSH P,X
	Q VVECT
	POP P,X
	R

PNTFRS:	SO T		;FIRST ONE IN POINT MODE
	PUSH P,X
	Q VPOIN
	POP P,X
	R
AEDG:	IMUL B,WXFC	;APPLY WINDOWING TRANSFORMATIONS
	HLRES B
	ADD B,WXOF
	IMUL C,WYFC
	HLRES C
	ADD C,WYOF
	R

VPOIN:	SKIPN WNDFLG
	J DSPPN5	;NO WINDOWING NEEDED
	Q AEDG
	ASH B,-2
	ASH C,-2
	J DPOIN

DSPPN5:	ASH B,-2
	ASH C,-2
	SKIPN EXTFLG
	J DSPPNI
	J DPOIN

VVECT:	SKIPN WNDFLG
	J DSPVC5	;NO WINDOWING REQUIRED
	Q AEDG
	ASH B,-2
	ASH C,-2
	J DVECT

DSPVC5:	ASH B,-2
	ASH C,-2
	SKIPN EXTFLG
	J DSPVCA
	J DVECT

EXTFLG:	0	;NON-ZERO TO ALLOW SOME WINDOWING WITHOUT WNDFLG ON
		;NOTE-NEGATIVE X'S ARE IMPOSSIBLE

NVRTX:	
.I XR0=XR1,XR1=XR0
	J GTCFTA

NVRTY:
.I YR0=YR1,YR1=YR0

GTCFTA:		;CALCULATE WINDOWING COEFFICIENTS
.I XR1-XR0,YR1-YR0
	JUMPL A,NVRTX
	JUMPL B,NVRTY
	CAIL B,120.
	CAIGE A,120.
	J BDWNPR
.I WXFC=4096.*1000000/(XR1-XR0)
.I WYFC=4096.*1000000/(YR1-YR0)
.I WXOF=-XR0*WXFC/1000000
.I WYOF=-YR0*WYFC/1000000
	R

BDWNPR:	STRIKE _WINDOW TOO SMALL
	R	;USE OLD VALUES

XR0:	0
YR0:	0
XR1:	0
YR1:	0

ST:STRGT:STRPNT:	;STRAIGHTEN FUNCTION-MOVES POINTS
.I APOUSP=#POPJP, EPSIL=EPSDRW
	Q OPNINT
	SETZB U,ADJSW'
STRG2:	Q CONTRS
	Q ADJST1	;ADJUST NEARBY POINTS
	Q DSUFRA	;DISPLAY IT AGAIN - OFTEN SKIPPED
	J STRG2

ADJST1:	M E,A
	SZ X
	HRLZ B,EPSIL
	Q SCLEPS
ADJLP1:	SKIPGE A,MDATA(X)
	J NADJL1
	SUB A,E
	JUMPE A,NADJL1	;VERY UNLIKELY, BUT ...
	Q SAMG
	J NADJL1
	MM E,MDATA(X)	;UPDATE
	SO ADJSW
NADJL1:	AOS X
	CAMGE X,Y
	J ADJLP1

	AOS (P)
	AOS U
	CAME U,DSRAT	;DISPLAY EVERY DSRAT'TH TIME
	R
	SZ U
	SKIPN ADJSW
	R
	SZ ADJSW
	SOS (P)
	R


EPSFLS:	20.	;.05"

FLUSH:			;FLUSH VERTICES INSTEAD OF LINES
.I APOUSP=#POPJP, EPSIL=EPSFLS
	Q OPNINT
	SKIPA
CUMLA:	Q DSUFRA	;RE-DISPLAY
	M W,Y
CUML2:	Q CONTRS
	M E,A
	Q ASSEXY
	SKIPN DELPHN	;DID IT HAPPEN
	J CUML2		;NO
	J CUMLA		;YES, NEED TO REDISPLAY
ASSEXY:	SZ DELPHN'	;DELETE POINT IN E
	CAIG W,3
	R
	SZ X
	HRLZ B,EPSIL
	Q SCLEPS
ASSEX0:	SKIPL A,MDATA(X)
	Q SESSY		;COMPARE POINTS TO PRESENT ONE
	AOS X
	CAMGE X,W
	J ASSEX0
	R

SESSY:	SUB A,E
	Q SAMG
	R
	SO DELPHN	;SAME POINT, SO DELETE IT
	SKIPL MDATA+1(X)
	SKIPGE MDATA-1(X)
	J MUDW1		;IS AT EITHER END OF SEGMENT
	SO MDATA(X)	;IS INSIDE SEGMENT
	R

KLLONE:	SOSG Y
	J FOOKL2
	MI A,MDATA(X)
	HRLI A,MDATA+1(X)	;FROM,,TO
	CAMGE X,Y
	BLT A,MDATA-1(Y)
	R

EAT:		;READ INPUT DATA-APPEND TO INTERNAL STUFF
.I PTDTA1<-1>
EAT1:	Q TYI
	CAIE A,"(
	J EAT1

ESEG1:	Q TYI
	CAIN A,"(
	J EDPTR
	CAIE A,")
	J ESEG1
	R		;END OF INPUT

EDPTR:	Q TYI	;READ A DOTTED PAIR
	CAIN A,"(
	J EDPTR0
	CAIN A,")
	J EDPTR9
	CAIL A,"0
	CAILE A,"9
	J EDPTR
	SETOM UNRCHF	;QUADS
.I PTDTA1<-1>
	Q EDPTR8
	SETZM UNRCHF
	Q EDPTR8
	J EDPTR

EDPTR0:	Q EDPTR8
EDPTR7:	Q TYI
	CAIE A,")
	J EDPTR7
	J EDPTR

EDPTR9:
.I PTDTA1<-1>
	J ESEG1		;END OF SEGMENT

EDPTR8:	Q REDDED
	PUSH P,A
EDPTR1:
	Q TYI
	CAIL A,"0
	CAILE A,"9
	J EDPTR1
	SETOM UNRCHF
	Q REDDED
	POP P,B
	HRL A,B
	SKIPE SWPFLG
	MOVSS A
	Q UPYPL
	SETOM UNRCHF
	POPJ P,

SCLFCT:	1,,0	;SCALE TO BE APPLIED TO INPUT
SWPFLG:	0	;SHOULD X,Y BE SWAPPED
INVTX:	0	;NON-ZERO TO INVERT X WRT 2000
INVTY:	0	;NON-ZERO TO INVERT Y WRT 2000

REDDED:	Q REDNN
	SKIPN INVTX
	R
INVRTT:	SUBI A,1777
	MOVNS A
	R

REDNN:	Q RDNUM
	SKIPE ONEFLG
	LSH A,2
	IMUL A,SCLFCT
	HLRES A
	R

SAMGB:	HRLZ B,EPSIL
SAMGA:	Q SCLEPS
SAMG:	MOVMS A		;TEST IF BOTH HALVES < EPSIL IN B
	CAML A,B
	R
	MOVSS A
	MOVMS A
	CAMGE A,B
	AOS (P)
	R

ASSOX:	M W,Y		;SEGMENT IN E,F FIND AND DELETE IT
ASSOXY:	SZ DELPHN'
	SKIPE EPSIL
	CAIG W,3
	R
	SZ X
	HRLZ B,EPSIL
	Q SCLEPS
ASSOX0:	SKIPL A,MDATA(X)
	Q SASSY
	AOS X
	CAMGE X,W
	J ASSOX0
	R		;GIVE UP

SASSY:	SUB A,E		;FIRST ERROR TERM
	Q SAMG
	R		;LOSSAGE
	M A,MDATA-1(X)
	SUB A,F
	Q SAMG
	J OTHRS
;X AND X-1
XAXM1:	SKIPGE MDATA+1(X)	;IS THERE AN END OF SEGMENT NEARBY
	J MVDW1
	SKIPGE MDATA-2(X)
	J MVDW2
	Q PYUP			;NOPE, NEED TO EXPAND

	MI A,MSAVE
	HRLI A,MDATA(X)
	MI C,MSAVE(Y)
	SUB C,X
	BLT A,-1(C)

	SO MDATA(X)

	MI A,MDATA+1(X)
	HRLI A,MSAVE
	BLT A,MDATA-1(Y)

	SUB P,[1,,1]
	SO DELPHN
	R

OTHRS:	M A,MDATA+1(X)	;TRY OTHER DIRECTION OF SEGMENT
	SUB A,F
	Q SAMG
	R		;FOOEY
;X AND X+1
	AOS X
	J XAXM1

MVDW2:	SOS X
MVDW1:	SO DELPHN
MUDW1:	Q KLLONE
	AOS (P)		;SKIP THE AOS X
	R

DELET:	Q RDNUM		;DELET SEGMENT SPECIFIED BY X1,Y1,X2,Y2
	HRL E,A
	Q RDNUM
	HRR E,A
	Q RDNUM
	HRL F,A
	Q RDNUM
	HRR F,A
	Q ASSOX
	J DSUFR

DELAT:	;DELETE SEGMENT SPECIFIED BY N,M
.I AXAXM'=#XAXM1
FNDPLC:	Q RDNUM
	MM A,SEGTN'
	Q RDNUM
	MM A,TERNT'
	SZ X 
DELA1:	SKIPL MDATA(X)
	Q ASECT
	AOS X
	CAMGE X,Y
	J DELA1
	R		;NOT FOUND

ASECT:	SOSGE SEGTN
	J RGHSEG
ASEC1:	SKIPGE MDATA(X)
	R
	AOS X
	J ASEC1

RGHSEG:	ADD X,TERNT
	AOS X
	Q @AXAXM
	Q DSUFR
	SUB P,[1,,1]
	R
MODIF:	;MODIFY N,M TO X,Y	
.I AXAXM'=#HAIRM
	J FNDPLC
HAIRM:	Q RDNUM
	M B,A
	Q RDNUM
	HRL A,B
	MM A,MDATA-1(X)
	J DSUFRA

SLOP:	SZ SLPREM'
	SKIPE EPSIL
	CAIG Y,3
	R
	SZ X		;REMOVE SLOP IN DATA
	HRLZ B,EPSIL
	Q SCLEPS
TRYWUS:	MI W,1(X)
TRYXUS:	SKIPGE C,MDATA(W)
	J AOSTWS
	SKIPGE A,MDATA(X)
	J AOSTXS
	SUB A,C
	Q SAMG
	J AOSTWS
	M A,MDATA(X)
	MM A,MDATA(W)	;COPY UP
	SO SLPREM	;INDICATE SOMETHING CHANGED
AOSTXS:	AOS X
	CAMGE X,Y
	J TRYWUS
	R

AOSTWS:	AOS W
	CAMGE W,Y
	J TRYXUS
	J AOSTXS


COMPCT:	CAIG Y,3	;COMPACTIFY EXTRA -1'S ETC.
	R
	SO MDATA(Y)	;JUST IN CASE
	SZ X		;REMOVE EXTRA -1'S AND LINK
	SKIPA
COMPC0:	AOS X
COMPC3:	SKIPGE A,MDATA(X)
	J COMPC2
	CAME A,MDATA+1(X)
	J COMPC0
	Q KLLONE
	CAML X,Y
	J BLUBT
	J COMPC3

COMPC2:	CAML X,Y
	J BLUBT
	SKIPGE B,MDATA+1(X)
	J MURT1
	CAMN B,MDATA-1(X)
	Q KLLTWO
	CAML X,Y
	J BLUBT
	J COMPC0

MURT1:	Q KLLONE
	J COMPC2

BLUBT:	AOS Y
	SO MDATA(Y)
	R

FIXMAG:	Q FIXMA1	;FIX MAGNIFICATION
.I WNDFLV'=WNDFLG
	SZ WNDFLG
	SKIPN WNDFLV
	J RESTRM
	R

FIXMA1:	SZ X
ASHLP1:	SKIPGE C,MDATA(X)
	J ASHT1
	HLRE B,C
	HRRE C,C
	Q AEDG		;MAGNIFY
	JUMPL B,LOSTPA	;KILL THINGS OUT OF RANGE
	JUMPL C,LOSTPA
	CAIGE B,4096.
	CAIL C,4096.
	J LOSTPA
	HRL C,B
	MM C,MDATA(X)
ASHT1:	AOS X
ASHT2:	CAMGE X,Y
	J ASHLP1
	R

LOSTPA:	Q KLLONE
	J ASHT2

KLLOUT:	Q FIXMA1
UNMAG:	Q UNMA1		;INVERSE MAGNIFICATION
.I WNDFLV'=WNDFLG
	SZ WNDFLG
	J RESTRA

UNMA1:	SZ X
USHLP1:	SKIPGE A,MDATA(X)
	J USHT1
	Q EADG		;UNMAGNIFY
	MM A,MDATA(X)
USHT1:	AOS X
	CAMGE X,Y
	J USHLP1
	R
ESYPLT:	Q COMPCT	;MAKE IT EASY TO PLOT
	SZ X
	SKIPA
ESYPL0:	AOS X
	SKIPL MDATA(X)
	J ESYPL0
	CAML X,Y
	J COMPCT	;DONE-UNUSUAL
	M U,X
ESYPL1:	AOS U
	SKIPL MDATA(U)
	J ESYPL1
	CAML U,Y
	J COMPCT	;DONE
	SOS U
	HLRE E,MDATA(U)
	HRRE F,MDATA(U)
	M W,U
	SZ WNEW'
.I MINDS'=1000000
	Q ESYPL2	;FIND OPTIMAL NEXT LINE
	SKIPN W,WNEW
	J COMPCT	;WE  DIND'T GET TOO FAR !
	SKIPL MDATA-1(W)
	Q RVRDW		;REVERSES SEGMENT AND SKIPS
	Q GTTPND
	ADDI U,2
	Q MVDWN
	MI X,-2(U)
	J ESYPL0

MVDWN:	CAMGE W,V
	CAML U,W
	R
	MI A,MSAVE
	HRLI A,MDATA(U)
	MI B,MSAVE(W)
	SUB B,U
	BLT A,-1(B)

	MI A,MDATA(U)
	HRLI A,MDATA(W)
	MI B,MDATA(U)
	ADD B,V
	SUB B,W
	BLT A,-1(B)

	MI A,MDATA(U)
	ADD A,V
	SUB A,W
	HRLI A,MSAVE
	BLT A,MDATA-1(V)
	R
;U POINTS TO FIRST IN LOWER BLOCK
;W POINTS TO FIRST IN TOP BLOCK
;V POINTS TO LAST + 1 IN TOP BLOCK
ESYPL2:	AOS W
	SKIPL MDATA(W)
	J ESYPL2
	SOS W
	CAME W,U
	Q CALCDS
	ADDI W,2
	CAML W,Y
	R		;DONE
	Q CALCDS
	J ESYPL2

CALCDS:	HLRE A,MDATA(W)
	HRRE B,MDATA(W)
	SUB A,E
	SUB B,F
	MOVMS A
	MOVMS B
	CAMG A,B
	M A,B		;RIGHT METRIC FOR PLOTTER
	CAML A,MINDS
	R		;NOT BETTER
	MM W,WNEW
	MM A,MINDS
	CAMG A,ESYLIM	;WHY FUXX ABOUT SHORTER STUFF
	SUB P,[1,,1]	;POP EXTRA LEVEL UP
	R

ESYLIM:	100.

RVRDW:	SKIPL MDATA+1(W)
	.VALUE		;WHAT THE HELL, W NOT NEAR GAP
	M V,W
	Q GTTPW1
	PUSH P,V
	PUSH P,W
EXRVRS:	M A,MDATA(W)
	EXCH A,MDATA(V)
	MM A,MDATA(W)
	AOS W
	SOS V
	CAMGE W,V
	J EXRVRS
	POP P,W
	POP P,V
	ADDI V,2
	AOS (P)
	R

GTTPND:	M V,W
GTTPN1:	AOS V	;MOVE V TO ABOVE NEXT -1 UP
	SKIPL MDATA(V)
	J GTTPN1
	AOS V
	R

GTTPW1:	SOS W	;MOVE W TO ABOVE NEXT -1 DOWN
	SKIPL MDATA(W)
	J GTTPW1
	AOS W
	R

PLOTS:	Q ESYPLT
	PUSH P,GRDINT
	PUSH P,FRAMSW
	PUSH P,BUTFLG
	SZ GRDINT
	SO FRAMSW
	SZ BUTFLG
.I PLOT<DFOOD<DINNER<>>>
	POP P,BUTFLG
	POP P,FRAMSW
	POP P,GRDINT
	J RESTRM
SGX:	0	;RUNNING TOTALS
SGY:	0
SGX2:	0
SGXY:	0
SGY2:	0

SDGX2:	0	;SUM OF PRODUCTS WRT CG
SDGXY:	0
SDGY2:	0

SGMDV:	0	;VARIANCE

CGX:	0	;CG
CGY:	0

LSIN:	0	;SLOPE OF CURRENT LINE
LCOS:	0

CSX:	0	;SAVED CGX,CGY
CSY:	0

SSIN:	0	;SAVED LSIN,LCOS
SCOS:	0

XDX0:	0	;FIRST POINT IN SEGMENT
YDY0:	0

XDXS:	0	;FIRST POINT IN CURRENT LINE
YDYS:	0

XDX:	0	;CURRENT POINT
YDY:	0

INCRCV:	AOS NPNT	;UPDATE TOTALS
.F SGX=SGX+XDX,SGY=SGY+YDY,SGX2=SGX2+XDX^2 ,SGXY=SGXY+XDX*YDY,SGY2=SGY2+YDY^2
	R

INTCV:			;INITIALISE TOTALS
.F NPNT'=SGX=SGY=SGX2=SGXY=SGY2=0
	R

OFLNPN:	;DISTANCE^2 OF POINT XDX,YDY FROM LINE CGX,CGY;LSIN,LCOS
.F (LSIN*(XDX-CGX)-LCOS*(YDY-CGY))^2 /(LSIN^2 +LCOS^2 )-(OFSLIM/FLTSCL)^2
	SKIPGE A
	AOS (P)
	R	;SKIPS IF DISTANCE SMALL

PRJLNP:	;PROJECTION OF POINT XDX,YDY ON LINE CGX,CGY;LSIN,LCOS
.F DENOM'=LSIN^2 +LCOS^2
.F (LSIN^2 *CGX+LSIN*LCOS*(YDY-CGY)+LCOS^2 *XDX)/DENOM,$
.F (LCOS^2 *CGY+LSIN*LCOS*(XDX-CGX)+LSIN^2 *YDY)/DENOM
	R

INTLNS:	;INTERSECTION CGX,CGY;LSIN,LCOS AND CSX,CSY;SSIN,SCOS
.F DENOM'=LSIN*SCOS-LCOS*SSIN
	SKIPN A
	J PRLLSS	;PARALLEL LINES
.F (LSIN*SCOS*CGX+LCOS*SCOS*(CSY-CGY)-LCOS*SSIN*CSX)/DENOM,$
.F (LSIN*SCOS*CSY-LSIN*SSIN*(CSX-CGX)-LCOS*SSIN*CGY)/DENOM
;IF A,B TOO FAR FROM CGX,CGY OR CSX,CSY USE AVERAGE
.F ,,(CGX-CSX)^2 +(CGY-CSY)^2 ,(A-CGX)^2 +(B-CGY)^2 +(A-CSX)^2 +(B-CSY)^2
	CAMG D,C
	R
PRLLSS:
.F (CGX+CSX)/2.0,(CGY+CSY)/2.0
	R

TSTCRV:	M A,NPNT	;GET OPTIMAL LINE AND RMS
	CAIG A,1
	STOP _TOO FEW POINTS TSTCRV
.F FNPT'=FFLOAT<NPNT>
.F CGX=SGX/FNPT,CGY=SGY/FNPT
.F SDGX2=SGX2-FNPT*CGX^2, SDGXY=SGXY-FNPT*CGX*CGY, SDGY2=SGY2-FNPT*CGY^2
.F LSIN2'=2.0*SDGXY/FNPT, LCOS2'=(SDGX2-SDGY2)/FNPT
	SKIPN B
	Q FXBSN
	SKIPN A
	Q FXASN
.F LSIN=SQRT<LSIN2^2 +LCOS2^2>-LCOS2, LCOS=LSIN2
.F /(FNPT*(LSIN^2 +LCOS^2))*(LSIN^2 *SDGX2-2.0*LSIN*LCOS*SDGXY+$
.F LCOS^2 *SDGY2)-(SGMLIM/FLTSCL)^2
;VARIANCE - IE. RMS ^ 2
	SKIPGE A
	AOS (P)
	R	;SKIPS IF RMS SMALL

FXBSN:.F ,LCOS2=0.0001
	R

FXASN:.F LSIN2=.0001*LCOS2
	R

CHKTMS:	7	;REPEAT BEFORE CHECK RMS

OFSLIM:	10.0	;OFFSET LIMIT
SGMLIM:	10.0	;RMS LIMIT
FLTSCL:	1.0	;SCALING TO TO WINDOWING

FLABC:	HRRE B,A
	HLRE A,A
	FLOAT A
	FLOAT B
	R
CONTRS:	Q CONTAC
	ZUM A
	J ADJWYX

FXABC:	FIX B
	FIX A
	HRL A,B
	HRR A,C
	R

CM:CONTMR:
.I APOUSP=#POPJP
	Q OPNINT
.F FLTSCL=1.0
	SKIPE EPSFLG
	SKIPN WNDFLG
	J CONL1
.F FLTSCL=FFLOAT<WXFC>/FFLOAT<1000000>
CONL1:
.I PTDTA1<-1>
	Q CONTRS
	Q FLABC		;SEPERATE AND FLOAT
.I XDX0=XDXS=XDX=,YDY0=YDYS=YDY=	;SAVE BEGINNING OF SEGMENT
	SO FRSGD'	;INDICATE START OF SEGMENT
	Q INTCV		;RESET ACS
MYGDNS:	SO ALSFRS'	;INDICATE NO GUESS AVAILABLE YET
CONL3A:	SZ U
CONL3:	Q REDTAB
	TLZE A,200000	;IN CONTACT ?
	J CONL9		;NO
	ZUM A
	Q ADJWYX
	Q FLABC
.I XDX=,YDY=
	SKIPE ALSFRS
	J NOTYTS	;CANNOT TEST OFFSET YET
	Q OFLNPN	;SKIPS IF OFFSET OK
	J CONL11	;NO

NOTYTS:	Q INCRCV	;UPDATE ACS
	AOS U
	CAME U,CHKTMS	;WANT TO CALC LINES AND RMS ?
	J CONL3		;NO
	Q TSTCRV	;SKIPS IF RMS OK
	J CONL10
	SZ ALSFRS	;NOW HAVE GUESS AT LINE
	J CONL3A

CONL9:	SKIPN FRSGD	;LOST CONTACT
	J GDND		;PROPER END
.F XDX-XDX0,YDY-YDY0
	JUMPN A,.+3
	SKIPN B
	J COMNDL	;A SINGLE POINT
.I UPYPL<FXABC<XDX0,YDY0>>
COMNDL:
.I UPYPL<FXABC<XDX,YDY>>
	J CONL1

GDND:	SKIPN ALSFRS
	J VGDND	;VERY PROPER ENDING
.I UPYPL<FXABC<XDXS,YDYS>>
	J COMNDL

VGDND:	;REALLY COOL ENDING
.I UPYPL<FXABC<INTLNS<>>>	;FIND INTERSECTION WITH LAST
.I UPYPL<FXABC<PRJLNP<>>>	;AND PROJECTION
	J CONL1

CONL11:CONL10:	SKIPN FRSGD
	J RGDND
	SZ FRSGD
	PUSH P,XDX
	PUSH P,YDY
.I XDX=XDX0,YDY=YDY0
.I UPYPL<FXABC<PRJLNP<>>>
	POP P,YDY
	POP P,XDX
MYCMNS:
.I XDXS=XDX,YDYS=YDY
.I CSX=CGX,CSY=CGY,SSIN=LSIN,SCOS=LCOS
;.F NPNT=1,SGX=XDX,SGY=YDY,SGX2=XDX^2,SGXY=XDX*YDY,SGY2=YDY^2
;LAST POINT HERE GETS PUT IN WITH NEW LOT-MAY BE WORSE THAN INTCV
	Q INTCV
	J MYGDNS

RGDND:
.I UPYPL<FXABC<INTLNS<>>>	;INTERSECTION WITH PREVIOUS
	J MYCMNS

NSIDS:	6	;NUMBER OF SIDES IN POLYGON
NSIZS:	120.	;RADIUS OF POLYGON
TWPIE:	6.2831852

KM:CPYPOL:	SO OLDXY'
.I APOUSP=#POPJP, EPSIL=EPSDRW
	Q OPNINT
KLUDG1:	
.I PTDTA1<-1>
	Q CONTRS
	M E,A
	SUB A,OLDXY
	MM E,OLDXY
	Q SAMGB
	SKIPA
	J KLUDG4	;NEAR PREVIOUS ONE
	HRRE B,E
	HLRE A,E
.F XDX=,YDY=
	Q INSRTK
KLUDG3:	Q REMOS
	J KLUDG1	

KLUDG4:	SUB Y,NSIDS
	SOS Y
	SOSG Y
	MI Y,1
	Q DSUFRA
	SO OLDXY
	J KLUDG3

INSRTK:
.F FNSIDS'=FFLOAT<NSIDS>,FNSIZS'=FFLOAT<NSIZS>
	M G,NSIDS
INSRL1:
.F FFIX<COS<ANG'=TWPIE*(FFLOAT<G>/FNSIDS)>*FNSIZS>,FFIX<SIN<ANG>*FNSIZS>
	ADD A,XDX
	ADD B,YDY
	HRLS A
	HRR A,B
	Q UPYPL
	SOJGE G,INSRL1
	R

CPYNEW:	;COPY UP SEGMENT FROM VYLOW TO VYABS MOVE FROM (CXYSV) TO (G)
	SKIPG V,VYABS
	R
.I PTDTA1<-1>	;JUST IN CASE
	SUB G,CXYSV
	M X,VYLOW
MVUPMR:	SKIPL A,MDATA(X)
	ADD A,G
	Q UPYPL
	AOS X
	CAME X,V
	J MVUPMR
.I PTDTA1<-1>	;JUST IN CASE
	R

MOVFRS:	;MOVE SEGMENT FROM VYLOW TO VYABS FROM (CXYSV) TO (C)
	SKIPG V,VYABS
	R
	SUB C,CXYSV
	M X,VYLOW
MOVFR1:	SKIPL MDATA(X)
	ADDM C,MDATA(X)
	AOS X
	CAMGE X,V
	J MOVFR1
	R

KLLLST:	SKIPG V,VYABS	;KILL LAST COPY CREATED
	R
	SUB Y,V
	ADD Y,VYLOW
	J DSUFRA
FNDSIM:	;FIND FIRST NEAR POINT TO ONE IN F
	SZ X
	HRLZ B,EPSIL
	Q SCLEPS
NAOSLP:	SKIPGE A,MDATA(X)
	J NAOSXL
	SUB A,F
	Q SAMG
	SKIPA
	R		;MATCH
NAOSXL:	AOS X
	CAMGE X,Y
	J NAOSLP
	AOS (P)		;SKIPS IF NOT FOUND
	R

SPRWNG:	M V,W		;PUT W AT START, V AT END +1 OF SEGMENT
	Q GTTPW1	;MOVE W DOWN
	J GTTPN1	;MOVE V UP

FNDEQ1:	CAMN A,MDATA(W)	;FIND POINT EQUAL TO A, START AT W
	R		;FOUND IT
	AOS W
	CAMGE W,Y
	J FNDEQ1
	AOS (P)		;SKIPS IF NOT FOUND
	R

CRFT1:	M F,A
	Q FNDSIM	;FIND NEAR ONE
	SKIPA
	R		;NO LUCK
	M A,MDATA(X)
	MM A,CXYSV'	;NEAREST POINT IN OBJECT
	MM F,DXYSV'	;PEN POSITION
	M W,X
	Q SPRWNG	;SPREAD WINGS TO END OF CURRENT SEGMENT
	MM V,VYABS'
	MM W,VYLOW'
	AOS (P)
	SKIPN FIGFLG
	R		;SEGMENTS ONLY
	MI U,1
	Q MVDWN		;COPY DOWN THE WORLD
.I VYABS=VYABS-VYLOW+1,VYLOW=1
	SZ SLPREM	;CLOSE SET WRT TOUCHING OF SEGMENTS
	SKIPE COMFLG	;BETTER BE ON
	Q COMPCT
	SKIPE SLPFLG	;BETTER BE ON
	Q SLOP
	SKIPE SLPREM
	SO ADJSW	;TO ENSURE GETS DISPLAYED SOMETIME
	SZ X
ALLSE1:	SKIPL A,MDATA(X)
	Q ALSECT
	AOS X
	CAMGE X,VYABS
	J ALLSE1
	R
ALSECT:	M W,VYABS
	Q FNDEQ1
	SKIPA
	R		;FOUND NONE
	Q SPRWNG	;TOUCH NEAREST -1'S
	M U,VYABS
	Q MVDWN		;COPY IT DOWN
.I VYABS=U+V-W
	CAMGE A,Y
	J ALSECT	;TRY FOR MORE
	SUB P,[1,,1]	;ALL DONE-POP EXTRA LEVEL UP
	R		


STRFIG:	SO FIGFLG	;MOVE FIGURES AROUND
	SKIPA
STRSEG:	SZ FIGFLG	;MOVE SEGMENTS AROUND
.I APOUSP=#POPJP, EPSIL=EPSDRW
	Q OPNINT
	SETZB G,ADJSW'
STRSE3:	SO CXYSV'
STRSE2:	Q REDTAB
	TLZE A,340000
	J STRSE3	;TO ALLOW FIGURES TO GROW EASILY
	ZUM A
	Q ADJWYX
	Q ADJST2
	Q DSUFRA	;OFTEN SKIPPED
	J STRSE2

ADJST2:	SKIPL C,CXYSV
	J HAVONE	;WE ARE POINTING TO ONE
CRFT2:	Q CRFT1
	SKIPA		;NOTHING
	J ALLBLL	;HERE IF WE FOUND ONE
NOTNER:	AOS (P)
	AOS G
	CAME G,DSRAT
	R
	SZ G
	SKIPN ADJSW
	R
	SZ ADJSW
	SOS (P)
	R

ALLBLL:	M C,DXYSV	;PEN POSITION
	J APSDUF
HAVONE:	M D,A		;SAVE
	SUB A,C
	Q SAMGB		;NEAR ONE WE ARE KEEPING TABS ON ?
	J NOTNRL	;NOPE
	M C,D
APSDUF:	Q MOVFRS	;MOVE
	SO ADJSW
	ADDM C,CXYSV
	J NOTNER

NOTNRL:	M A,D
	SO CXYSV
	J CRFT2


CPYFIG:	SO FIGFLG	;COPY FIGURES
	SKIPA
CPYSEG:	SZ FIGFLG
.I APOUSP=#POPJP, EPSIL=EPSDRW
	Q OPNINT
CPYSE2:	SO OLDXY'
	Q CONTRS
	Q PCKUP1	;SEE IF FOUND ONE
	J CPYSE2	;NO
CPYDG3:	Q REMOS
	Q CONTRS	;WAIT FOR PUT DOWN
	M G,A
	SUB A,OLDXY
	MM G,OLDXY
	Q SAMGB
	SKIPA
	J DELTG4	;GO DELETE LAST COPY
	Q CPYNEW
	J CPYDG3	;MORE TO COME

PCKUP1:	Q CRFT1		;SEE IF ONE NEAR
	J PCKST		;NO
	AOS (P)
	STRIKE  * 
	R		;NOW READY TO GEN COPIES

PCKST:	SKIPE SLPREM
	J DSUFRA	;REDISPLAY
	R

DELTG4:	Q KLLLST
	SO OLDXY
	J CPYDG3

FLSFIG:	SO FIGFLG	;FLUSH FIGURES
	SKIPA
FLSSEG:	SZ FIGFLG
.I APOUSP=#POPJP,EPSIL=EPSDRW
	Q OPNINT
	SO OLDXY'
FLSSE3:	Q CONTRS
	Q CRFT1
	SKIPA		;NO LUCK
	J DELNEW	;DELETE IT
	SKIPGE OLDXY
	J FLSSE3	;NO OLD POSITION AVAILABLE
	M A,F		;PEN POSITION
	SUB A,OLDXY
	HRLI B,100.
	Q SAMGA		;NEAR LAST ONE ?
	J FLSSE3	;NO (FLLSE2 ?)
	Q OVBRTH	;BRING BACK TO LIFE
REFLS4:	Q REMOS		;AWAIT LIFT-OFF
	J FLSSE3

DELNEW:	MM F,OLDXY'
	SKIPGE W,VYABS
	R
	M U,VYLOW
	M V,Y
	Q MVDWN
.I VYLOW-Y=VYLOW+Y-VYABS,VYABS=Y
COMSUF:	Q DSUFRA
	J REFLS4


OVBRTH:	;REINSTATE LAST ONE DELETED
	SZ T
OVBRT0:	SKIPGE C,MDATA(Y)	;?
	J OVBRT1
	Q DDTPIR
OVBRT2:	AOS Y
	CAMGE Y,VYABS
	J OVBRT0
	SZ T
	J REFLS4

OVBRT1:	SZ T
	J OVBRT2


TSTWAT:	SETZM DSKYES'
	CAIN A,(SIXBIT /DSK/)
	SETOM DSKYES'
	LSH A,-3
	CAIN A,(SIXBIT / UT/)
	SETOM DSKYES'
	POPJ P,

WRITE:	MOVEI A,1		;OUTPUT ASCII MODE
	HRLM A,ONOP
	PUSHJ P,TTINW
	HRRZ A,ONOP
	PUSHJ P,TSTWAT
	PUSHJ P,OPNDO
	POPJ P,
	SKIPE DSKYES
	EXCH ZR,DSKONL
.I QITADR=#WRITEQ

	PUSHJ P,FOOD

WRITEQ:	SKIPE DSKYES
	EXCH ZR,DSKONL
	SETZM QITADR
	JRST CLONDO

READ:	PUSHJ P,RESET
READMR:	MOVEI A,0	;INPUT ASCII MODE
	HRLM A,INOP
	PUSHJ P,TTINR
	HRRZ A,INOP
	PUSHJ P,TSTWAT
	PUSHJ P,OPNDI
	POPJ P,
	SKIPE DSKYES
	EXCH ZR,DSKONL
.I QITADR=#READQ

	MOVE A,DSKONL
	TRNE A,DISFLG
	TRO ZR,DISFLG	;COPY IN DISFLG

	PUSHJ P,EAT

READQ:	TRZ ZR,DISFLG
	SKIPE DSKYES
	EXCH ZR,DSKONL
	SETZM QITADR
	JRST CLONDI

	

CONSTANTS
VARIA:
VARIABLES
PAT:
PATCH:	BLOCK 200
FS:
CORSIZ==<.+1777>_-10.

END START
