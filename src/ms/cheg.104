;;;END GAME INTERPRETER

EGRTTY=ERRTTY 1,
EGLOGT==1
EGINTT==2
EGSETT==3
EGPCT==4
EGSQT==5
EGMOVT==6	;MOVE FORMAT:	2.9-2.7 FOR SPECIAL MOVES;	2.6-2.1 PIECE,	1.7-1.1 DESTINATION

EGNT==0	;NUL
EGVT==1	;READ VARIABLE
EGWT==2	;WRITTEN VARIABLE
EGUT==3	;READ & WRITTEN VARIABLE
EGTT==4	;TAG
EGCT==5	;CONDITION
EGOT==6	;ORDERING

NIEGV==0
NIEGL==0
NEGI==0
NEGC==0
NEGO==0
;;INSTRUCTIONS

DEFINE	EGIDEF A1,A2,A3,A4,A5
	CONC EIN,\NEGI,==SIXBIT \A1\
	CONC EIT,\NEGI,==EG!A2!T_6+EG!A3!T_3+EG!A4!T
IF2,	CONC EGI,\NEGI,==A5
	NEGI==NEGI+1
TERMIN

EGILIN==[ASCIZ \ILLEGAL EGINSTRUCTION\]	;SO IF2 IN EGIDEF DOESN'T CAUSE ICS
	EGIDEF ,N,N,N,[EGRTTY EGILIN]
	EGIDEF BREAK,V,V,V,[PUSHJ P,EGBRK]
;CONTROL
	EGIDEF RETURN,V,N,N,[JRST EGRET]

	EGIDEF COND,C,T,T,[PUSHJ P,EGCND]
	EGIDEF CONDV,C,V,T,[PUSHJ P,EGCNDV]
	EGIDEF CONDT,T,V,T,[PUSHJ P,EGCNDT]
	EGIDEF CONDF,T,V,T,[PUSHJ P,EGCNDF]
EGJMPI==NEGI
	EGIDEF JUMP<,V,V,T,[PUSHJ P,EGJUMP]
	EGIDEF JUMP=,V,V,T,[PUSHJ P,EGJUMP]
	EGIDEF JUMP>,V,V,T,[PUSHJ P,EGJUMP]
	EGIDEF JUMP#<,V,V,T,[PUSHJ P,EGJUMP]
	EGIDEF JUMP#=,V,V,T,[PUSHJ P,EGJUMP]
	EGIDEF JUMP#>,V,V,T,[PUSHJ P,EGJUMP]
;VARIABLE SETTING
	EGIDEF SET,W,T,V,[PUSHJ P,EGSET]
	EGIDEF SETV,W,V,N,[PUSHJ P,EGSETV]
	EGIDEF SETSQ,W,V,V,[PUSHJ P,EGSTSQ]
	EGIDEF SETMOV,W,V,V,[PUSHJ P,EGSTMV]
	EGIDEF EXCH,U,U,N,[PUSHJ P,EGEXCH]
	EGIDEF ORDER,U,U,O,[PUSHJ P,EGORD]
;SET OPERATIONS
	EGIDEF SELECT,W,T,V,[PUSHJ P,EGSELT]
	EGIDEF SELECC,W,C,V,[PUSHJ P,EGSELC]
	EGIDEF MAP,W,T,V,[PUSHJ P,EGMAP]
	EGIDEF PAIR,W,V,V,[PUSHJ P,EGPAIR]
	EGIDEF MEMBER,W,V,V,[PUSHJ P,EGMEMP]
	EGIDEF MEMNUM,W,V,V,[PUSHJ P,EGMEMN]
	EGIDEF CARD,W,V,N,[PUSHJ P,EGCARD]
	EGIDEF GETEL,W,V,V,[PUSHJ P,EGGETE]
	EGIDEF SETEL,V,U,V,[PUSHJ P,EGSETE]
	EGIDEF INSERT,V,U,V,[PUSHJ P,EGINSE]
	EGIDEF UNSERT,W,U,V,[PUSHJ P,EGUNSE]
	EGIDEF EXCHWE,U,U,V,[PUSHJ P,EGXCWE]
	EGIDEF EXCHEL,U,V,V,[PUSHJ P,EGXCHL]
	EGIDEF INTERS,W,V,V,[PUSHJ P,EGCAP]
	EGIDEF UNION,W,V,V,[PUSHJ P,EGCUP]
	EGIDEF DIFFER,W,V,V,[PUSHJ P,EGDIFF]
	EGIDEF APPEND,W,V,V,[PUSHJ P,EGAPP]
;CHESS PLAYING
	EGIDEF PLAY,V,C,O,[PUSHJ P,EGPLAY]
	EGIDEF PLAYB,V,C,O,[PUSHJ P,EGPLAB]
	EGIDEF TRY,V,C,O,[PUSHJ P,EGTRY]
	EGIDEF PLAN,T,V,V,[PUSHJ P,EGPLAN]
	EGIDEF UNPLAN,N,N,N,[PUSHJ P,EGUPLN]
	EGIDEF DONE,N,N,N,[JRST EGDONE]

	EGIDEF VALRET,V,V,V,[JRST EGVRET]
	EGIDEF VALRUT,V,V,V,[PUSHJ P,EGVRUT]
	EGIDEF VALON,V,V,V,[PUSHJ P,EGVCVT]
	EGIDEF RETBET,V,V,V,[PUSHJ P,EGRETB]
	EGIDEF LEAVE,N,N,N,[JRST EGLEAV]
;CHESS DATA MANIPULATION
	EGIDEF PIECE,W,V,N,[PUSHJ P,EGPIEC]
	EGIDEF SQUARE,W,V,N,[PUSHJ P,EGSQUA]
	EGIDEF SIDE,W,V,N,[PUSHJ P,EGSSID]
	EGIDEF RANK,W,V,N,[PUSHJ P,EGRANK]
	EGIDEF FILE,W,V,N,[PUSHJ P,EGFILE]
	EGIDEF SCOLOR,W,V,N,[PUSHJ P,EGSCOL]
	EGIDEF PCOLOR,W,V,N,[PUSHJ P,EGPCOL]
	EGIDEF MVDIST,W,V,V,[PUSHJ P,EGMVDS]
	EGIDEF INSQ,W,V,V,[PUSHJ P,EGINSQ]
	EGIDEF OPP,W,V,V,[PUSHJ P,EGOPPI]
	EGIDEF SQ>RNK,W,V,V,[PUSHJ P,EGSQGR]
;LOGICAL OPERATIONS
	EGIDEF NOR,W,V,V,[PUSHJ P,EGNOR]
	EGIDEF AND,W,V,V,[PUSHJ P,EGAND]
	EGIDEF OR,W,V,V,[PUSHJ P,EGOR]
;INTEGER ARITHMETIC
	EGIDEF ADD,W,V,V,[PUSHJ P,EGADD]
	EGIDEF SUB,W,V,V,[PUSHJ P,EGSUB]
	EGIDEF MUL,W,V,V,[PUSHJ P,EGMUL]
	EGIDEF DIV,W,V,V,[PUSHJ P,EGDIV]
	EGIDEF MOD,W,V,V,[PUSHJ P,EGMOD]
	EGIDEF DIF,W,V,V,[PUSHJ P,EGDIF]
	EGIDEF MIN,W,V,V,[PUSHJ P,EGMIN]
	EGIDEF MAX,W,V,V,[PUSHJ P,EGMAX]
	EGIDEF ABS,W,V,N,[PUSHJ P,EGABS]
;;VARIABLES

DEFINE	EGVDEF A1,A2
	CONC EVN,\NIEGV,==SIXBIT \A1\
IF2,	CONC EGV,\NIEGV,==A2
	NIEGV==NIEGV+1
TERMIN
	EGVDEF X,0
IF2,EGLM=EGIVAR-1+NIEGV
	EGVDEF LM,0
IRP A1,,[WK,WQ,WR,WR2,WB,WB2,WN,WN2,WP,WP2,WP3,WP4,WP5,WP6,WP7,WP8,BK,BQ,BR,BR2,BB,BB2,BN,BN2,BP,BP2,BP3,BP4,BP5,BP6,BP7,BP8]
	IF2,EG!A1=EGIVAR-1+NIEGV
	EGVDEF A1,0
TERMIN
IF2,EGPG=EGIVAR-1+NIEGV
	EGVDEF PG,EGSETT_36
IF2,EGDEPT==EGIVAR-1+NIEGV
	EGVDEF DEPTH,EGINTT_36
NIEGW=NIEGV
IF2,EGSIDE=NIEGV
	EGVDEF SIDE,0

;;LITERALS

DEFINE	EGLDEF A1,A2
	CONC ELN,\NIEGL,==SIXBIT \A1\
IF2,	CONC ELV,\NIEGL,==A2
	NIEGL==NIEGL+1
TERMIN
	EGLDEF #,0
IF2,EGF=EGLIT+NIEGL
	EGLDEF F,EGLOGT_36
IF2,EGT=EGLIT+NIEGL
	EGLDEF T,EGLOGT_36+1
;;CONDITIONS

DEFINE	EGCDEF A1,A2
	CONC ECN,\NEGC,==SIXBIT \A1\
IF2,	CONC EGC,\NEGC,==PUSHJ P,A2
	NEGC==NEGC+1
TERMIN

	EGCDEF ,POPJ1
	EGCDEF DEFD,EGDEFD
;INTEGER
	EGCDEF =0,EGCEQ0
	EGCDEF <0,EGCL0
	EGCDEF >0,EGCG0
	EGCDEF ODD,EGCODD
;LOGICAL
	EGCDEF T,EGTRUE
;SET
	EGCDEF EMPTY,EGMT
;CHESS
	EGCDEF FAR,EGFAR
	EGCDEF RF,EGRKF
	EGCDEF NF,EGNTF
	EGCDEF NNF,EGNNTF
	EGCDEF SAFE,EGSAFE
	EGCDEF WPC,EGWPC
	EGCDEF WSQ,EGWSQ
	EGCDEF KING,EGKING
	EGCDEF PAWN,EGPAWN
	EGCDEF QUEEN,EGQUEEN
	EGCDEF ROOK,EGROOK
	EGCDEF NIGHT,EGNIGHT
	EGCDEF BISH,EGBISH
	EGCDEF CAPT,EGCAPT
	EGCDEF CHECK,EGCHK
	EGCDEF OPP,EGOPPC
	EGCDEF PASSD,EGPASS
	EGCDEF PDP,EGPDP
	EGCDEF RWK=,EGRWKE
;;ORDERING OPERATIONS

DEFINE	EGODEF A1,A2
	CONC EON,\NEGO,==SIXBIT \A1\
IF2,	CONC EGO,\NEGO,==PUSHJ P,A2
	NEGO==NEGO+1
TERMIN

	EGODEF ,CPOPJ
	EGODEF DEEP,EGDEEP
;;INTERPRETER

EGL==3000	;MAX EGPROG SIZE
LEGFS==1000	;EG FREE STORAGE SIZE
MXEGV==400	;MAX # EG VARS (#>400)
MXEGL==400	;MAX # EG LITS (#>400)
MXEGDS==100	;MAX # DISTINGUISHED SETS (FOR SET LITERAL STORAGE EFFICIENCY)
MXEGT==400	;MAX # EG TAGS (#>1000)
EGDTBL==200	;MAXIMUM # EG DISPATCHES

EGPC:	1100,,EGPROG-1	;END GAME PROGRAM PC

EGITAB:	REPEAT NEGI,CONC EGI,\.RPCNT	;END GAME INSTRUCTION TABLE
EGCTAB:	REPEAT NEGC,CONC EGC,\.RPCNT	;END GAME CONDITION TABLE
EGOTAB:	REPEAT NEGO,CONC EGO,\.RPCNT	;END GAME ORDERING TABLE

EGPROG:	BLOCK EGL	;END GAME PROGRAM
	1100,,EGPROG-1
EGSTAT:	BLOCK MXEGT	;STATEMENT TAG TABLE, 1100 IN LEFT HALVES

EGVARA:	.(A)
EGVARB:	.(B)
EGVARC:	.(C)
EGVARX:	.

EGXM:	0
	0

EGNVAR:	0	;NUMBER OF EGVARS - 1 (IN BOTH HALVES)


;BEGIN:
EGBEG:	SKIPE B,EGTSW
	PUSHJ P,EGBEGT
	SKIPN @EGLM
	JRST EGMATE	;NO LEGAL MOVES
	MOVE A,LMGD
	HRRM A,EGDEPT
		;ORDER PAWNS
	PUSHJ P,EGFSP	;GENERATE PG (SET OF PAWN GROUPS)
	MOVEI T1,1
	MOVEM T1,1(TT)
	HRRM TT,EGPG
	MOVE T,TT
	MOVEI TT,0
	MOVE C,[-100,,BOARD+2*BW+1]
EGPGL:	SKIPE A,(C)
	JRST EGPG1
EGPGLE:	ADDI C,BW-1
	AOBJN C,EGPGL
	SKIPE TT
	SETZM (TT)
	SETZM (T)
	PUSH P,WPLAN(I)
	PUSH P,EGXM(I)
	PUSH P,P
	PUSH P,EGXM(I)
	MOVEI A,1(P)
	HRLI A,EGIVAR
	MOVEI B,NIEGW+1(P)
	HRRM P,EGVARA
	HRRM P,EGVARB
	HRRM P,EGVARC
	HRRM P,EGVARX
	ADD P,EGNVAR
	JUMPG P,[ERRTTY [ASCIZ \PDL OVERFLOW IN EG\]]
	BLT A,-2(B)
	SETZM -1(B)
	HRLI B,-1(B)
	BLT B,(P)
	SETZM EGMVC	;SET MOVE COUNTER TO 0
	MOVE Q,STVT1(I)
	MOVE R,Q
	SETZM CVPR
	MOVEI T2,-1
EGLP:	SKIPGE QTF
	JRST EGLEAV
	SKIPE B,EGTSW
	PUSHJ P,EGTRAC
	ILDB T,EGPC
	ILDB A,EGPC
	ILDB B,EGPC
	ILDB C,EGPC
	XCT EGITAB(T)
	JRST EGLP

EGPG1:	JUMPL A,EGPG2	;GENERATING PAWN GROUPS
	SKIPL LMGT-PIECE(A)
	JRST EGPGLE
	JUMPN TT,EGPG1A
	PUSHJ P,EGFSP
	MOVEM TT,(T)
	MOVE T,TT
	PUSHJ P,EGFSP
	HRLI TT,EGSETT_14
	MOVEM T1,1(TT)
	MOVEM TT,1(T)
EGPG1A:	MOVNI D,1
	PUSHJ P,EGFSP
	SUBI A,PIECE
	HRLI A,EGPCT_14
	MOVEM A,1(TT)
	JRST EGPGLE
EGPG2:	SUBI C,10*BW-1
	JUMPE TT,EGPGL
	AOJLE D,EGPGL
	SETZB TT,(TT)
	JRST EGPGL

EGMATE:	AOS (P)		;NO LEGAL MOVES, RETURN TO MAIN PROGRAM WITHOUT EVALUATION
	MOVE A,EGLM
	JRST EGGC1
;;FREE STORAGE

EGFSP:	MOVE TT,EGFSS	;FREE STORAGE POINTER
	EXCH TT,EGFSP
	POPJ P,
EGFSS:	REPEAT LEGFS,[MOVE TT,.+2
		0
		]
	EGRTTY [ASCIZ \EG FREE STORAGE EXHAUSTED\]
	0

EGGCIB:	TLOA B,EGINTT_14
EGGCIA:	TLOA A,EGINTT_14
EGGCBA:	EXCH A,B
EGGCAB:	EXCH A,@EGVARB
EGGC:	LDB B,[360600,,A]	;GARBAGE COLLECT
	CAIE B,EGSETT
	POPJ P,		;NOT A SET
EGGC1:	SOSE 1(A)
	POPJ P,
	PUSH P,EGFSP
	HRLI A,(MOVE TT,)
	MOVEM A,EGFSP
	PUSH P,A
	JRST EGGC3
EGGC2:	MOVEM A,(P)
	MOVE A,1(A)
	PUSHJ P,EGGC	;RECURSE
EGGC3:	SKIPE A,@(P)
	JRST EGGC2
	POP P,A
	POP P,(A)
	POPJ P,

;;TRACE

EGTSW:	0		;0 OR POINTER TO OUTPUT ROUTINE

EGBEGT:	PUSHJ P,FORMF
	HRLM I,(P)
	PUSHJ P,BDOUT	;PRINT BOARD
	PUSHJ P,DCRR
	MOVE T,PLYN
	SUB T,LMGD
	AOS T
	TRZ T,1
	MOVEI S,GAME-1(T)
	MOVEM T,PGMCNT
	SETZB T,PMRDF
	PUSHJ P,APG5	;PRINT CURRENT MOVE SEQUENCE
	HLRZ I,(P)
	JRST DCRR

EGTRAC:	MOVE TT,EGPC
EGTRI:	PUSHJ P,EGPLOC
	SOUT [ASCIZ \/	\]
	PUSHJ P,EGPINS
	JRST DCRR

EGBRK:	MOVEI B,TYO
	MOVE TT,EGPC
	SOS TT
	PUSHJ P,EGTRI
	.VALUE
	POPJ P,

EGPLOC:	MOVNI D,1		;PRINT LOCATION
	MOVE T1,EGSTAT-1
	HRLZ C,EGTCNT
EGPLCL:	CAML TT,EGSTAT(C)
	CAMLE T1,EGSTAT(C)
	JRST EGPLC1
	MOVE T1,EGSTAT(C)
	MOVE D,C
EGPLC1:	AOBJN C,EGPLCL
	SUBM TT,T1
	MOVE D,EGTAG(D)
	PUSHJ P,EGSPNT
	JUMPE T1,CPOPJ
	MOVEI A,"+
	PUSHJ P,(B)
	JRST DPT2

EGPINS:	ILDB T,TT		;PRINT INSTRUCTION
	MOVE D,EGINST(T)
	PUSHJ P,EGSPNT
	MOVEI A," 	;SPACE
	PUSHJ P,(B)
	HLLZ T,EGITYP(T)
	JUMPE T,CPOPJ
EGPIN1:	ROT T,3
	ILDB A,TT
	XCT EGPNT(T)
	TRZ T,-1
	JUMPE T,CPOPJ
	MOVEI A,",
	PUSHJ P,(B)
	PUSHJ P,MCRR
	JRST EGPIN1

EGPNT:	JFCL	;TABLE FOR PRINTING ARGUMENTS OF INSTRUCTION
	PUSHJ P,EGVARP
	PUSHJ P,EGVAWP
	PUSHJ P,EGUVWP
	PUSHJ P,EGTAGP
	PUSHJ P,EGCNDP
	PUSHJ P,EGORDP
	SOUT [ASCIZ \ITYPE7?\]

EGVARP:	TRZE A,400		;PRINT READ-ONLY VARIABLE
	JRST EGLITP
EGUVWP:	PUSH P,@EGVARA
	PUSHJ P,EGVAWP
	SOUT [ASCIZ \:=\]
	POP P,C
EGVP1:	LDB A,[360600,,C]
	XCT EGVPNT(A)
	POPJ P,

EGLITP:	SOUT [ASCIZ \"\]		;PRINT LITERAL
	MOVE C,EGLIT(A)
	JRST EGVP1

EGVAWP:	MOVE D,EGVAR(A)		;PRINT WRITABLE VARIABLE
	JRST EGSPNT

EGTAGP:	MOVE D,EGTAG(A)		;PRINT TAG
	JRST EGSPNT

EGCNDP:	TRZE A,400		;PRINT CONDITION
	SOUT [ASCIZ \#\]
	MOVE D,EGCOND(A)
	JRST EGSPNT

EGORDP:	TRZE A,400		;PRINT ORDER
	SOUT [ASCIZ \#\]
	MOVE D,EGORDR(A)
	JRST EGSPNT

EGVPNT:	SOUT [ASCIZ \#\]	;TABLE FOR PRINTING VARIABLES
	PUSHJ P,EGLOGP
	PUSHJ P,EGINTP
	PUSHJ P,EGSETP
	PUSHJ P,EGPCP
	PUSHJ P,EGSQP
	PUSHJ P,EGMOVP
	SOUT [ASCIZ \VTYPE7?\]

EGLOGP:	MOVEI A,"?		;PRINT LOGICAL VARIABLE
	CAMN C,EGT
	MOVEI A,"T
	CAMN C,EGF
	MOVEI A,"F
	JRST (B)

EGINTP:	MOVE A,C		;PRINT INTEGER
	LSH A,6
	ASH A,-6
	JRST DPT

EGSETP:	MOVEI A,"(		;PRINT SET
	PUSHJ P,(B)
	SKIPN D,(C)
	JRST EGSTP2
	JRST EGSTP1
EGSTPL:	MOVEI A,",
	PUSHJ P,(B)
	PUSHJ P,MCRR
EGSTP1:	PUSH P,D
	MOVE C,1(D)
	PUSHJ P,EGVP1
	POP P,D
	SKIPE D,(D)
	JRST EGSTPL
EGSTP2:	MOVEI A,")
	JRST (B)

EGPCPC:	MOVEI A,"*
	PUSHJ P,(B)
	SUBI C,PIECE
	PUSHJ P,EGPCP
	JRST EGMVP1

EGPCP:	MOVE A,PLYN		;PRINT PIECE
	AOS A
	MOVEM A,PGMCNT
	MOVE A,C
	PUSHJ P,PCOUTP
	MOVEI A,"/
	PUSHJ P,(B)
	SKIPA A,PIECEL(C)
EGSQP:	MOVE A,C		;PRINT SQUARE
	JRST SQOUT

EGMOVP:	LDB D,[170300,,C]	;PRINT MOVE
	HRRM D,EGMVP1
	CAIG D,2
	JUMPN D,EGCASP
	TRNE C,200
	JRST EGCAS1	;CASTLE
	LDB D,[700,,C]
	LDB C,[110600,,C]
	PUSHJ P,EGPCP
	SKIPLE C,BOARD(D)
	JRST EGPCPC
	CAMN D,GHLOC
	JRST EGPEP
	MOVEI A,"-
	PUSHJ P,(B)
EGMVP2:	MOVE A,D
	PUSHJ P,SQOUT
EGMVP1:	MOVEI A,.
	CAIL A,4
	SOUT PROTXT-4(A)
	POPJ P,

EGPEP:	SOUT [ASCIZ \P EP/\]
	JRST EGMVP2

EGCAS1:	MOVEI D,-375(C)
EGCASP:	SOUT [ASCIZ \O-O\]
	CAIN D,2
	SOUT [ASCIZ \-O\]
	POPJ P,

EGSPNT:	JUMPE D,CPOPJ		;PRINT SIXBIT SYMBOL
	MOVEI C,0
	LSHC C,6
	MOVEI A,40(C)
	PUSHJ P,(B)
	JRST EGSPNT

EGSACS:	0
	PUSH P,A
	PUSH P,B
	PUSH P,C
	PUSH P,D
	PUSH P,T
	PUSH P,TT
	PUSH P,S
	PUSH P,T1
	PUSH P,T2
	JRST 2,@EGSACS

EGRACS:	0
	POP P,T2
	POP P,T1
	POP P,S
	POP P,TT
	POP P,T
	POP P,D
	POP P,C
	POP P,B
	POP P,A
	JRST 2,@EGRACS

;;INPUT ROUTINES

EGSYLR:	MOVE TT,[440600,,B]
	MOVEI B,0
EGSYL1:	PUSHJ P,TYI
	CAIN A,":
	JRST EGTAGD	;DEFINE TAG
EGSYL3:	CAIE A," 	;SPACE
	CAIN A,",
	POPJ P,
	CAIE A,"(
	CAIN A,")
	POPJ P,
	CAIE A,"&
	CAIN A,^I	;TAB
	POPJ P,
	CAIN A,""
	POPJ P,
	CAIN A,";
	JRST EGSYL2
	SUBI A,40
	JUMPL A,CPOPJ
	TLNE TT,770000
	IDPB A,TT
	JRST EGSYL1

EGSYL2:	PUSHJ P,TYI	;IGNORE REST OF LINE
EGSYL4:	CAIE A,"	;TAB
	SUBI A,40
	JUMPGE A,EGSYL2
	POPJ P,

EGREAD:	SETZB Q,EGDTBP
	MOVNI T,NIEGV	;NUMBER OF INITIAL VARS
	MOVEM T,EGVCNT
	SETZM EGTCNT
	MOVNI T,NIEGL
	EXCH T,EGLCNT
	HRLZS T	;GARBAGE COLLECT SET LITERALS FROM PREVIOUS READ
	ADD T,[NIEGL,,NIEGL]
	JUMPGE T,EGRD0
EGRD0L:	MOVE A,EGLIT(T)
	PUSHJ P,EGGC
	AOBJN T,EGRD0L
EGRD0:	SETZM EGSCNT
	SETZM EGSTAT
	MOVE T,[EGSTAT,,EGSTAT+1]
	BLT T,EGSTAT+MXEGT-1
	MOVE T,[1100,,EGPROG-1]
EGRD1:	PUSHJ P,EGSYLR
	JUMPE B,EGRD1
	CAMN B,[SIXBIT \ENTRY\]
	JRST EGENTR
	CAMN B,[SIXBIT \END\]
	JRST EGRCHK
	CAML T,[1100,,EGPROG+EGL-1]
	ERRTTY [ASCIZ \EGPROG STORAGE EXHAUSTED\]
	PUSHJ P,EGIGET	;GET INSTRUCTION
	IDPB C,T
	MOVE D,EGITYP(C)
EGRD2:	JUMPL A,EGSTI
	PUSHJ P,EGSYLR
	ROT D,3
	XCT EGSYME(D)
	IDPB C,T
	TRZ D,777777
	TLNE T,770000
	JRST EGRD2
	PUSHJ P,EGSYL4
	JRST EGRD1

EGSTI:	MOVEI C,0
EGSTI1:	TLNN T,770000
	JRST EGRD1
	IDPB C,T
	JRST EGSTI1
;DISPATCHING

EGDSPT:	IRP A1,,[QB,QN,Q,R2B,RBN,R2N,2R,RB,RN,2BN,B2N,2B,BN,2N,R,B,N,K]
	SIXBIT \A1\
TERMIN

EGDTB:	BLOCK 3*EGDTBL	;PIECES,PAWNS,EGPC DISPATCH
EGDTBP:	0	;# ENTRIES IN EGDTB

EGGDSP:	MOVSI T1,-22
EGGDPL:	CAMN B,EGDSPT(T1)
	JRST EGGDP1
	AOBJN T1,EGGDPL
	JSP C,EGALOC
	ASCIZ \UNDEFINED DISPATCH TYPE\
EGGDP1:	MOVEI C,1
	LSH C,(T1)
	POPJ P,

EGENTR:	CAIL Q,3*EGDTBL
	ERRTTY [ASCIZ \ENTRY STORAGE EXHAUSTED\]
	MOVEI T2,0
	PUSHJ P,EGSYLR
	PUSHJ P,EGGDSP
	MOVSM C,EGDTB(Q)
	PUSHJ P,EGGDPN
	DPB C,[WPWNN,,EGDTB+1(Q)]
	PUSHJ P,EGSYLR
	PUSHJ P,EGGDSP
	HRRM C,EGDTB(Q)
	PUSHJ P,EGGDPN
	DPB C,[BPWNN,,EGDTB+1(Q)]
	JUMPE T2,EGENT1
	MOVEI T2,0
	JUMPL A,EGENT1
	PUSHJ P,TYI
	CAIL A,"<
	CAILE A,">
	JRST EGENT1
	MOVEI T2,-"<+1(A)
	MOVEI D,0
EGENT2:	PUSHJ P,TYI
	CAIL A,"0
	CAIL A,"9
	JRST EGENT3
	MOVEI C,-"0(A)
	TRNE D,1
	MOVNS C
	DPB C,[WRLNUM,,EGDTB+1(Q)]
EGENT1:	DPB T2,[WPRLOP,,EGDTB+1(Q)]
	MOVEM T,EGDTB+2(Q)
	ADDI Q,3
	SOS EGDTBP
	PUSHJ P,EGSYL4
	JRST EGRD1
EGENT3:	CAIE A," 
	CAIN A,"+
	JRST EGENT2
	CAIN A,"-
	AOJA D,EGENT2
	MOVEI T2,0
	JRST EGENT1

EGGDPN:	SETZB C,TT
	CAIE A,"&
	AOJA T2,CPOPJ
	PUSHJ P,TYI
	CAIL A,"0
	CAIL A,"9
	JRST EGGDN1
	MOVEI C,-"0+20(A)
	JRST EGSYL1	;ADVANCE TO BREAK CHAR
EGGDN1:	CAIE A,"P
	AOJA T2,EGSYL3	;ADVANCE TO BREAK CHAR
	MOVEI C,21
	JRST EGSYL1	;ADVANCE TO BREAK CHAR
;SYMBOL TABLE HACKING

EGIGET:	MOVSI C,-NEGI	;LOOK UP EG INST
EGIGTL:	CAMN B,EGINST(C)
	POPJ P,
	AOBJN C,EGIGTL
	JSP C,EGALOC
	ASCIZ \UNDEFINED INSTRUCTION\

EGTAGD:	TLNE T,770000
	JRST EGBTAG
	PUSHJ P,EGTAGG	;DEFINE TAG
	MOVE B,EGSTAT(C)
	MOVEM T,EGSTAT(C)
	JUMPE B,EGSYLR
	PUSHJ P,EGTGPS
	PTTY [ASCIZ \REDEFINED\]
	JRST EGSYLR

EGBTAG:	PUSHJ P,EG6PNT
	PTTY [ASCIZ \ILLEGALLY PLACED TAG\]
	JRST EGSYLR

EGSYME:	MOVEI C,0
	PUSHJ P,EGVARG
	PUSHJ P,EGVAWG
	PUSHJ P,EGVAWG
	PUSHJ P,EGTAGG
	PUSHJ P,EGCNDG
	PUSHJ P,EGORDG
	PTTY [ASCIZ \UNDEFINED SYMBOL TYPE\]

EGVARG:	HRLZ C,EGVCNT	;GET VARIABLE NUMBER
	JUMPE B,EGVRG1
EGVRGL:	CAMN B,EGVAR(C)
	POPJ P,
	AOBJN C,EGVRGL
	HRRZS C
	CAIL C,MXEGV
	ERRTTY [ASCIZ \VARIABLE STORAGE EXHAUSTED\]
	MOVEM B,EGVAR(C)
	SOS EGVCNT
	POPJ P,

EGVRG1:	CAIE A,""
	POPJ P,
	PUSHJ P,EGLG	;LITERAL
	MOVE B,C
	HRLZ C,EGLCNT
EGVLGL:	CAMN B,EGLIT(C)
	JRST EGVLG1
	AOBJN C,EGVLGL
	HRRZS C
	CAIL C,MXEGL
	ERRTTY [ASCIZ \LITERAL STORAGE EXHAUSTED\]
	MOVEM B,EGLIT(C)
	SOS EGLCNT
EGVLG1:	TRO C,400
	POPJ P,

EGLG:	PUSHJ P,EGLITR	;GET A LITERAL
	JUMPE B,EGLTG0
	JUMPE T1,EGLNM
EGLG0:	PUSHJ P,EGLITG
	MOVE C,EGLIT(C)
	POPJ P,

EGLTG0:	CAIN A,"(
	JRST EGLSET
	CAIE A,")
	JUMPGE A,EGLG0
	MOVEI C,0
	POPJ P,

EGLSET:	PUSHJ P,EGFSP
	MOVEI T1,1
	MOVEM T1,1(TT)
	PUSH P,TT
EGLSTL:	PUSH P,TT
	PUSHJ P,EGLG
	POP P,T1
	JUMPE C,EGLST1
EGLST2:	PUSHJ P,EGFSP
	MOVEM TT,(T1)
	MOVEM C,1(TT)
	CAIE A,")
	JUMPGE A,EGLSTL
	SETZM (TT)
EGLST3:	POP P,C
	PUSH P,A	;SEE IF THIS SET = ONE READ IN BEFORE
	HRLZ TT,EGSCNT
	JUMPGE TT,EGLSTE
EGLSTS:	MOVE A,C
	MOVE B,EGSLIT(TT)
	PUSHJ P,EGSEQ
	AOBJN TT,EGLSTS
	JUMPGE TT,EGLSTD
	MOVE A,C
	PUSHJ P,EGGC1
	MOVE C,EGSLIT(TT)
	JRST EGLSTF

EGLSTD:	HRRZS TT
	CAIL TT,MXEGDS
	JRST EGLSTF
EGLSTE:	MOVEM C,EGSLIT(TT)
	SOS EGSCNT
EGLSTF:	POP P,A
	HRLI C,EGSETT_14
	JUMPL A,CPOPJ
	SETZB T1,TT
	AOJA T1,EGLR0	;GO TO NEXT BREAK CHAR

EGLST1:	JUMPN B,EGLST2
	CAIE A,")
	JUMPGE A,EGLST2
	SETZM (T1)
	JRST EGLST3

EGLITG:	MOVSI C,-NIEGL	;LOOK UP LITERAL
EGLTGL:	CAMN B,EGLITN(C)
	POPJ P,
	AOBJN C,EGLTGL
	JSP C,EGALOC
	ASCIZ \UNDEFINED LITERAL\

EGLNM:	SKIPE EGSGNF
	MOVNS C
	LSH C,6
	TRO C,EGINTT
	ROT C,-6
	POPJ P,

EGLITR:	MOVE TT,[440600,,B]	;READ A LITERAL
	SETZB B,T1
	SETZB C,EGSGNF'
EGLR0:	PUSHJ P,TYI
	CAIE A," 
	CAIN A,",
	POPJ P,
	CAIE A,"(
	CAIN A,")
	POPJ P,
	CAIN A,^I
	POPJ P,
	CAIN A,";
	JRST EGLR1
	SUBI A,40
	JUMPL A,CPOPJ 
	TLNE TT,770000
	IDPB A,TT
	JUMPN T1,EGLR0
	CAIN A,'-
	JUMPE C,EGLMIN
	CAIN A,'+
	JUMPE C,EGLPLU
	CAIL A,'0
	CAILE A,'9
	AOJA T1,EGLR0
	IMULI C,10.
	ADDI C,-'0(A)
	JRST EGLR0

EGLPLU:	AOSA EGSGNF
EGLMIN:	SOS EGSGNF
	JRST EGLR0

EGLR1:	PUSHJ P,TYI	;IGNORE REST OF LINE
	CAIE A,^I
	SUBI A,40
	JUMPGE A,EGLR1
	POPJ P,

EGVAWG:	PUSHJ P,EGVARG
	HRRZS C
	TRNE C,400
	JRST EGVWG1
	CAIL C,NIEGW
	POPJ P,
EGVWG0:	JSP C,EGALOC
	ASCIZ \STORED INTO\

EGVWG1:	MOVE B,[SIXBIT \"\]
	JRST EGVWG0

EGTAGG:	HRLZ C,EGTCNT	;GET TAG VALUE
	JUMPE C,EGTGG1
EGTGGL:	CAMN B,EGTAG(C)
	POPJ P,
	AOBJN C,EGTGGL
	HRRZS C
	CAIL C,MXEGT
	ERRTTY [ASCIZ \TAG STORAGE EXHAUSTED\]
EGTGG1:	MOVEM B,EGTAG(C)
	SOS EGTCNT
	POPJ P,

EGCNDG:	LDB C,[360600,,B]
	CAIN C,'#
	JRST EGCNG1
	MOVSI C,-NEGC	;LOOK UP CONDITION
EGCNGL:	CAMN B,EGCOND(C)
	POPJ P,
	AOBJN C,EGCNGL
	JSP C,EGALOC
	ASCIZ \UNDEFINED CONDITION\

EGCNG1:	LSH B,6
	PUSHJ P,EGCNDG
	TRC C,400
	POPJ P,

EGORDG:	LDB C,[360600,,B]
	CAIN C,'#
	JRST EGORG1
	MOVSI C,-NEGO	;LOOK UP ORDERING OPERATION
EGORGL:	CAMN B,EGORDR(C)
	POPJ P,
	AOBJN C,EGORGL
	JSP C,EGALOC
	ASCIZ \UNDEFINED ORDERING OPERATION\

EGORG1:	LSH B,6
	PUSHJ P,EGORDG
	TRC C,400
	POPJ P,

EGRCHK:	MOVNI D,1	;CHECK IF ALL TAGS DEFINED
	HRLZ C,EGTCNT
EGRCKL:	SKIPN EGSTAT(C)
	PUSHJ P,EGTAGU
	AOBJN C,EGRCKL
	MOVEI B,TYO
	SOUT [ASCIZ \
EGPROG SIZE = \]
	MOVE T1,T
	SUB T1,EGSTAT-1
	PUSHJ P,DPT2
	SOUT [ASCIZ \
# EGVARS = \]
	MOVN T1,EGVCNT
	PUSHJ P,DPT2
	SOUT [ASCIZ \
# EGLITS = \]
	MOVN T1,EGLCNT
	PUSHJ P,DPT2
	SOUT [ASCIZ \
# EGTAGS = \]
	MOVN T1,EGTCNT
	PUSHJ P,DPT2
	SOUT [ASCIZ \
EGDISP SIZE = \]
	MOVN T1,EGDTBP
	PUSHJ P,DPT2
	SETCM A,EGVCNT	;SET EGNVAR
	HRLS A
	MOVEM A,EGNVAR
	HRLZS EGDTBP	;SET EGDTBP
	JRST CRR

EGTAGU:	AOJN D,EGTGU1
	PTTY [ASCIZ \UNDEFINED TAGS:\]
	MOVNI TT,110-6
EGTGU1:	PUSHJ P,EGTGPS
	JUMPL TT,CPOPJ
	MOVNI TT,110-6
	JRST CRR

EGTGPS:	MOVE B,EGTAG(C)	;PRINT OUT TAG FOLLOWED BY SPACE
EG6PNT:	MOVEI A,0
	LSHC A,6
	ADDI A,40
	PUSHJ P,TYO
	CAIE A,40
	AOJA TT,EG6PNT
	AOJA TT,CPOPJ

EGALOC:	PUSH P,A
	PUSHJ P,EG6PNT
	MOVEI B,TYO
	SOUT (C)
	MOVE TT,T
	TLZE TT,770000
	SOS TT
	SOUT [ASCIZ \ @ \]
	PUSH P,D
	PUSHJ P,EGPLOC
	POP P,D
	PUSHJ P,CRR
	MOVEI C,0
	JRST POPAJ
;SYMBOL TABLES

EGITYP:	REPEAT NEGI,<CONC EIT,\.RPCNT,>_33
	;TABLE OF INSTRUCTION TYPES
	;4.9-4.7,4.6-4.4,4.3-4.1 FOR ARGUMENT TYPES
	;1=VAR,2=VAW,3=TAG,4=COND,5=ORDER,6=KONSTANT
	;TABLE OF EGINSTRUCTION NAMES:
EGINST:	REPEAT NEGI,CONC EIN,\.RPCNT 
EGTCNT:	0	;-NUMBER OF EGTAGS
	SIXBIT \EGPROG\
EGTAG:	BLOCK MXEGT	;TABLE OF EGTAG NAMES
EGVCNT:	-NIEGV	;-NUMBER OF EGVARS
	;TABLE OF EGVAR NAMES:
EGVAR:	REPEAT NIEGV,CONC EVN,\.RPCNT 
	BLOCK MXEGV-NIEGV
EGIVAR:	REPEAT NIEGW-1,CONC EGV,\1+.RPCNT	;TABLE OF INITIAL VARIABLE VALUES
EGCOND:	REPEAT NEGC,CONC ECN,\.RPCNT	;TABLE OF CONDITIONS
EGORDR:	REPEAT NEGO,CONC EON,\.RPCNT	;TABLE OF ORDERING OPERATIONS
EGLCNT:	-NIEGL	;-NUMBER OF LITERALS
EGLITN:	REPEAT NIEGL,CONC ELN,\.RPCNT	;TABLE OF INITIAL LITERAL NAMES
EGLIT:	REPEAT NIEGL,CONC ELV,\.RPCNT	;TABLE OF LITERAL VALUES
	BLOCK MXEGL-NIEGL
EGSCNT:	0	;-NUMBER OF DISTINGUISHED SETS (FORMED BY LITERAL READ)
EGSLIT:	BLOCK MXEGDS
;;INSTRUCTION INTERPRETING

;CONTROL

EGRET:	TRZE A,400		;RETURN
	SKIPA A,EGLIT(A)
	MOVE A,@EGVARA
	POPJ P,

EGCND:	PUSH P,EGPC		;COND
	PUSH P,A
	PUSH P,EGSTAT(C)
	MOVE A,EGSTAT(B)
	MOVEM A,EGPC
	PUSHJ P,EGLP
	POP P,C
	POP P,B
	POP P,EGPC
EGCND1:	TRZN B,400
	EXCH C,EGPC
	XCT EGCTAB(B)
	MOVEM C,EGPC
	POPJ P,

EGCNDV:	EXCH A,B		;CONDV
	TRZE A,400
	SKIPA A,EGLIT(A)
	MOVE A,@EGVARA
	MOVE C,EGSTAT(C)
	JRST EGCND1

EGCNDT:	TRZE B,400		;CONDT
	SKIPA B,EGLIT(B)
	MOVE B,@EGVARB
	LDB D,[360600,,B]
	CAIN D,EGSETT
	AOS 1(B)
	EXCH B,@EGVARX
	PUSH P,B
	PUSH P,EGPC
	PUSH P,EGSTAT(C)
EGCDT1:	MOVE A,EGSTAT(A)
	MOVEM A,EGPC
	PUSHJ P,EGLP
	POP P,B
	POP P,C
	PUSHJ P,EGTRUE
	EXCH B,C
	MOVEM B,EGPC
	POP P,A
	EXCH A,@EGVARX
	JRST EGGC

EGCNDF:	TRZE B,400		;CONDF
	SKIPA B,EGLIT(B)
	MOVE B,@EGVARB
	LDB D,[360600,,B]
	CAIN D,EGSETT
	AOS 1(B)
	EXCH B,@EGVARX
	PUSH P,B
	PUSH P,EGSTAT(C)
	PUSH P,EGPC
	JRST EGCDT1

EGJUMP:	TRZE A,400		;JUMP?
	SKIPA A,EGLIT(A)
	MOVE A,@EGVARA
	TRZE B,400
	SKIPA B,EGLIT(B)
	MOVE B,@EGVARB
	LDB T1,[360600,,A]
	LDB T2,[360600,,B]
	CAIE T,EGJMPI+1
	CAIN T,EGJMPI+4
	JRST EGJMPE
	CAIN T1,EGINTT
	CAIE T2,EGINTT
	EGRTTY [ASCIZ \NON-NUMERIC ARG\]
	LSH A,6
	LSH B,6
EGJMP1:	MOVE C,EGSTAT(C)
	XCT EGJMPT-EGJMPI(T)
	MOVEM C,EGPC
	POPJ P,
EGJMPT:	CAMGE A,B	;<
	CAMN A,B	;=
	CAMLE A,B	;>
	CAML A,B	;#<
	CAME A,B	;#=
	CAMG A,B	;#>

EGJMPE:	CAIN T1,EGSETT
	CAIE T2,EGSETT
	JRST EGJMP1
	CAMN A,B
	JRST EGJMP1
	PUSHJ P,EGSEQ	;TWO SETS IN A AND B
	JRST EGJMP1	;#= [NOTE A #= B]
	JRST EGJMP1	;= [NOTE A = B]

EGEQ:	CAMN A,B
	JRST POPJ1
	LDB T1,[360600,,A]
	LDB T2,[360600,,B]
	CAIN T1,EGSETT
	CAIE T2,EGSETT
	POPJ P,
EGSEQ:	MOVEM P,EGSEQP
EGSEQ0:	SKIPN A,(A)
	JRST EGSEQA
	SKIPN B,(B)
	JRST EGSNEQ
	HRL A,B
	PUSH P,A
	MOVE A,1(A)
	MOVE B,1(B)
	CAMN A,B
	JRST EGSEQ1
	LDB T1,[360600,,A]
	LDB T2,[360600,,B]
	CAIN T1,EGSETT
	CAIE T2,EGSETT
	JRST EGSNEQ
	PUSHJ P,EGSEQ0
	JRST EGSNEQ	;SHOULD NEVER GET HERE
EGSEQ1:	POP P,A
	HLRZ B,A
	JRST EGSEQ0

EGSEQA:	SKIPN B,(B)
	AOSA (P)
EGSNEQ:	MOVE P,EGSEQP
	POPJ P,

EGSEQP:	0	;P FOR RETURN
;VARIABLE SETTING

EGSET:	PUSH P,EGPC		;SET
	PUSH P,A
	PUSH P,@EGVARX
	TRZE C,400
	SKIPA A,EGLIT(C)
	MOVE A,@EGVARC
	LDB D,[360600,,A]
	CAIN D,EGSETT
	AOS 1(A)
	MOVEM A,@EGVARX
	MOVE A,EGSTAT(B)
	MOVEM A,EGPC
	PUSHJ P,EGLP
	EXCH A,(P)
	EXCH A,@EGVARX
	PUSHJ P,EGGC
	POP P,A
	POP P,B
	POP P,EGPC
EGSET1:	LDB D,[360600,,A]
	CAIN D,EGSETT
	AOS 1(A)
	JRST EGGCAB

EGSETV:	EXCH A,B		;SETV
	TRZE A,400
	SKIPA A,EGLIT(A)
	MOVE A,@EGVARA
	JRST EGSET1

EGEXCH:	MOVE D,@EGVARA		;EXCH
	EXCH D,@EGVARB
	MOVEM D,@EGVARA
	POPJ P,

EGORD:	PUSHJ P,EGOGET		;ORDER
	MOVEM C,EGORDI
	MOVEI T,@EGVARA
	MOVEI TT,@EGVARB
	MOVE A,(T)
	MOVE B,(TT)
EGORDI:	.
	MOVEM A,(T)
	MOVEM B,(TT)
	POPJ P,

EGOGET:	TRZN C,400
	JRST EGOGT1
	MOVE C,EGOTAB(C)
	MOVEM C,EGREVO
	SKIPA C,[PUSHJ P,EGREVO]
EGOGT1:	MOVE C,EGOTAB(C)
	POPJ P,

EGREVO:	.
	JUMPE C,CPOPJ	;C=0 FOR TIE
	EXCH A,B
	POPJ P,

EGSTSQ:	TRZE B,400			;SETSQ
	SKIPA B,EGLIT(B)
	MOVE B,@EGVARB
	TRZE C,400
	SKIPA C,EGLIT(C)
	MOVE C,@EGVARC
	LDB T,[360600,,B]
	LDB TT,[360600,,C]
	CAIN T,EGINTT
	CAIE TT,EGINTT
	EGRTTY [ASCIZ \NON-NUMERIC ARG\]
	SKIPE I
	MOVE B,REVERS(B)
	MOVEI B,1(B)
	IMULI B,BW
	ADDI B,(C)
	HRLI B,EGSQT_14
	JRST EGGCBA

EGSTMV:	TRZE B,400			;SETMOV
	SKIPA B,EGLIT(B)
	MOVE B,@EGVARB
	LDB D,[360600,,B]
	CAIN D,EGSQT
	JRST EGSMV1
	CAIE D,EGPCT
	EGRTTY [ASCIZ \WRONG TYPE ARG1\]
EGSMV2:	TRZE C,400
	SKIPA C,EGLIT(C)
	MOVE C,@EGVARC
	LDB D,[360600,,C]
	CAIN D,EGPCT
	JRST EGSMV3
	CAIE D,EGSQT
	EGRTTY [ASCIZ \WRONG TYPE ARG2\]
EGSMV4:	DPB B,[61100,,C]
	HRLI C,EGMOVT_14
	EXCH C,@EGVARA
	MOVE A,C
	JRST EGGC
EGSMV1:	MOVE B,BOARD(B)
	JRST EGSMV2
EGSMV3:	MOVE C,PIECEL(C)
	JRST EGSMV4
;SET OPERATIONS

EGSELT:	PUSH P,EGPC			;SELECT
	PUSH P,A
	PUSHJ P,EGFSP
	MOVEI A,1
	MOVEM A,1(TT)
	PUSH P,TT
	TRZE C,400
	SKIPA A,EGLIT(C)
	MOVE A,@EGVARC
	LDB D,[360600,,A]
	CAIE D,EGSETT
	EGRTTY [ASCIZ \NON-SET ARG\]
	AOS 1(A)
	PUSH P,A
	PUSH P,@EGVARX
	PUSH P,TT
	PUSH P,A
	PUSH P,EGSTAT(B)
EGSLT1:	SKIPN T,@-1(P)
	JRST EGSLTD
	MOVEM T,-1(P)
	MOVE A,1(T)
	MOVEM A,@EGVARX
	MOVE A,(P)
	MOVEM A,EGPC
	PUSHJ P,EGLP
	MOVE B,@EGVARX
	CAME A,EGT
	JRST EGSLT1
	LDB A,[360600,,B]
	CAIN A,EGSETT
	AOS 1(B)
	PUSHJ P,EGFSP
	MOVEM B,1(TT)
	MOVEM TT,@-2(P)
	MOVEM TT,-2(P)
	JRST EGSLT1
EGSLTD:	SETZM @-2(P)
	SUB P,[3,,3]
	POP P,@EGVARX
	POP P,A
	PUSHJ P,EGGC1
	POP P,A
	HRLI A,EGSETT_14
	POP P,B
	POP P,EGPC
	JRST EGGCAB

EGSELC:	SETZM EGSCNT			;SELECC
	PUSH P,A
	PUSHJ P,EGFSP
	PUSH P,TT
	MOVEI A,1
	MOVEM A,1(TT)
	TRZE C,400
	SKIPA T,EGLIT(C)
	MOVE T,@EGVARC
	LDB D,[360600,,T]
	CAIE D,EGSETT
	EGRTTY [ASCIZ \NON-SET ARG\]
	TRZN B,400
	JRST EGSLC0
	MOVE A,EGCTAB(B)
	MOVEM A,EGREVC
	SKIPA A,[PUSHJ P,EGREVC]
EGSLC0:	MOVE A,EGCTAB(B)
	MOVEM A,EGSLC
EGSLC1:	SKIPN T,(T)
	JRST EGSLCD
	MOVE A,1(T)
EGSLC:	.		;SHOULD NOT CLOBBER T OR TT
	JRST EGSLC1
	AOS EGSCNT
	MOVE A,1(T)
	LDB B,[360600,,A]
	CAIN B,EGSETT
	AOS 1(A)
	PUSHJ P,EGFSP
	MOVEM A,1(TT)
	JRST EGSLC1
EGSLCD:	SETZM (TT)
	POP P,A
	HRLI A,EGSETT_14
	POP P,B
	JRST EGGCAB

EGREVC:	.
	AOS (P)
	POPJ P,

EGMAP:	PUSH P,EGPC			;MAP
	PUSH P,A
	PUSHJ P,EGFSP
	MOVEI A,1
	MOVEM A,1(TT)
	PUSH P,TT
	TRZE C,400
	SKIPA A,EGLIT(C)
	MOVE A,@EGVARC
	LDB D,[360600,,A]
	CAIE D,EGSETT
	EGRTTY [ASCIZ \NON-SET ARG\]
	AOS 1(A)
	PUSH P,A
	PUSH P,@EGVARX
	PUSH P,TT
	PUSH P,A
	PUSH P,EGSTAT(B)
EGMAP1:	SKIPN T,@-1(P)
	JRST EGSLTD
	MOVEM T,-1(P)
	MOVE A,1(T)
	MOVEM A,@EGVARX
	MOVE A,(P)
	MOVEM A,EGPC
	PUSHJ P,EGLP
	LDB B,[360600,,A]
	CAIN B,EGSETT
	AOS 1(A)
	PUSHJ P,EGFSP
	MOVEM A,1(TT)
	MOVEM TT,@-2(P)
	MOVEM TT,-2(P)
	JRST EGMAP1

EGPAIR:	TRZE C,400
	SKIPA C,EGLIT(C)
	MOVE C,@EGVARC
	TRZE B,400
	SKIPA D,EGLIT(B)
	MOVE D,@EGVARB
	PUSHJ P,EGFSP
	MOVE B,TT
	MOVEI T1,1
	MOVEM T1,1(TT)
	PUSHJ P,EGFSP
	LDB T,[360600,,D]
	CAIN T,EGSETT
	AOS 1(D)
	MOVEM D,1(TT)
	PUSHJ P,EGFSP
	LDB T,[360600,,C]
	CAIN T,EGSETT
	AOS 1(C)
	MOVEM C,1(TT)
	SETZM (TT)
	HRLI B,EGSETT_14
	JRST EGGCBA

EGMEMP:	PUSHJ P,EGMEM			;MEMBER
	MOVSI B,EGLOGT_14
	JUMPE D,EGGCBA
	AOJA B,EGGCBA

EGMEMN:	PUSHJ P,EGMEM			;MEMNUM
	MOVE B,D
	TLO B,EGINTT_14
	JRST EGGCBA

EGMEM:	TRZE B,400
	SKIPA T,EGLIT(B)
	MOVE T,@EGVARB
	LDB D,[360600,,T]
	CAIE D,EGSETT
	EGRTTY [ASCIZ \NON-SET ARG\]
	TRZE C,400
	SKIPA C,EGLIT(C)
	MOVE C,@EGVARC
	MOVEI D,1
	LDB TT,[360600,,C]
	CAIN TT,EGSETT
	JRST EGMEM2
EGMEM1:	SKIPN T,(T)
	JRST EGMEM0
	CAME C,1(T)
	AOJA D,EGMEM1
	POPJ P,

EGMEMZ:	HLRZ A,(P)
EGMEM0:	MOVEI D,0
	POPJ P,

EGMEM2:	HRLM A,(P)
EGMEM3:	SKIPN T,(T)
	JRST EGMEMZ
	MOVE A,1(T)
	MOVE B,C
	PUSHJ P,EGEQ
	AOJA D,EGMEM3
	HLRZ A,(P)
	POPJ P,

EGCARD:	TRZE B,400			;CARD
	SKIPA C,EGLIT(B)
	MOVE C,@EGVARB
	LDB D,[360600,,C]
	CAIE D,EGSETT
	EGRTTY [ASCIZ \NON-SET ARG\]
	MOVSI B,EGINTT_14
	SKIPE C,(C)
	AOJA B,.-1
	JRST EGGCBA

EGGETE:	TRZE B,400			;GETEL
	SKIPA T,EGLIT(B)
EGGTE0:	MOVE T,@EGVARB
	LDB D,[360600,,T]
	CAIE D,EGSETT
	EGRTTY [ASCIZ \NON-SET ARG\]
	TRZE C,400
	SKIPA C,EGLIT(C)
	MOVE C,@EGVARC
	LDB D,[360600,,C]
	CAIE D,EGINTT
	EGRTTY [ASCIZ \NON-NUMERIC ARG\]
	LSH C,6
	ASH C,-6
	MOVEI B,0
	JUMPG C,EGGTE1
	MOVE S,T
	SKIPE S,(S)
	AOJA C,.-1
	AOJLE C,EGGCBA
EGGTE1:	SKIPN T,(T)
	JRST EGGCBA
	SOJN C,.-2
	MOVE B,1(T)
	LDB D,[360600,,B]
	CAIN D,EGSETT
	AOS 1(B)
	JRST EGGCBA

EGSETE:	PUSHJ P,EGSTE			;SETEL
	JUMPLE C,CPOPJ
EGSTE0:	MOVE D,1(T)
	SOJN D,EGSTEC	;MUST MAKE COPY
EGSTEL:	MOVE D,T
	SKIPN T,(T)
	JRST EGSTEX	;MUST EXTEND SET
	SOJN C,EGSTEL
	EXCH A,1(T)
	JRST EGGC

EGXCLX:	MOVEI A,0	;EXTEND
	EXCH A,1(B)
EGSTEX:	PUSHJ P,EGFSP	;EXTEND SET
	MOVEM TT,(D)
	SOJE C,EGSEX1
EGSEX0:	SETZM 1(TT)
	PUSHJ P,EGFSP
	SOJN C,EGSEX0
EGSEX1:	MOVEM A,1(TT)
	SETZM (TT)
	POPJ P,

EGSTEC:	PUSHJ P,EGFSP	;COPY
	MOVEI T1,1
	MOVEM T1,1(TT)
	MOVE D,TT
	SOJE C,EGSEC2
EGSECL:	PUSHJ P,EGFSP
	SKIPN T,(T)
	JRST EGSEC0
	MOVE T1,1(T)
	LDB T2,[360600,,T1]
	CAIN T2,EGSETT
	AOS 1(T1)
	MOVEM T1,1(TT)
	SOJN C,EGSECL
EGSEC2:	PUSHJ P,EGFSP
	MOVEM A,1(TT)
	SKIPE T,(T)
EGSEC3:	SKIPN T,(T)
	JRST EGSECD
	PUSHJ P,EGFSP
	MOVE T1,1(T)
	LDB T2,[360600,,T1]
	CAIN T2,EGSETT
	AOS 1(T1)
	MOVEM T1,1(TT)
	JRST EGSEC3

EGSEC0:	SETZM 1(TT)
	PUSHJ P,EGFSP
	SOJN C,EGSEC0
	MOVEM A,1(TT)
EGSECD:	SETZM (TT)
	MOVE A,D
	HRLI A,EGSETT_14
	JRST EGGCAB

EGINSE:	PUSHJ P,EGSTE			;INSERT
	JUMPLE C,CPOPJ
	MOVE D,1(T)
	SOJE D,EGINS1
EGINSC:	PUSHJ P,EGFSP	;COPY
	MOVEI T1,1
	MOVEM T1,1(TT)
	MOVE D,TT
	SOJE C,EGINC0
EGINCL:	PUSHJ P,EGFSP
	SKIPN T,(T)
	JRST EGSEC0
	MOVE T1,1(T)
	LDB T2,[360600,,T1]
	CAIN T2,EGSETT
	AOS 1(T1)
	MOVEM T1,1(TT)
	SOJN C,EGINCL
EGINC0:	PUSHJ P,EGFSP
	MOVEM A,1(TT)
	JRST EGSEC3

EGINSL:	MOVE D,T
	SKIPN T,(T)
	JRST EGINSX
EGINS1:	SOJN C,EGINSL
	PUSHJ P,EGFSP
	MOVEM A,1(TT)
	EXCH TT,(T)
	MOVEM TT,@(T)
	POPJ P,

EGINSX:	PUSHJ P,EGFSP	;EXTEND
	MOVEM TT,(D)
EGINXL:	SETZM 1(TT)
	PUSHJ P,EGFSP
	SOJN C,EGINXL
	MOVEM A,1(TT)
	SETZM (TT)
	POPJ P,

EGUNSE:	PUSHJ P,EGSTES			;UNSERT
	JUMPLE C,EGUNSU
	MOVE D,1(T)
	SOJN D,EGUNSC	;MUST MAKE COPY
EGUNS0:	MOVE D,T
	SKIPN T,(T)
	JRST EGUNSU
	SOJN C,EGUNS0
	MOVE B,1(T)
	EXCH T,EGFSP
	EXCH T,@EGFSP
	MOVEM T,(D)
	JRST EGGCBA

EGUNSU:	MOVEI B,0
	JRST EGGCBA

EGUNSC:	PUSHJ P,EGFSP
	MOVEI T1,1
	MOVEM T1,1(TT)
	MOVE D,TT
EGUSC0:	SKIPN T,(T)
	JRST EGUNSA	;CAN ABORT
	MOVE S,1(T)
	LDB T2,[360600,,S]
	CAIN T2,EGSETT
	AOS 1(S)
	SOJE C,EGUSC1
	PUSHJ P,EGFSP
	MOVEM S,1(TT)
	JRST EGUSC0

EGUSCL:	PUSHJ P,EGFSP
	MOVE T1,1(T)
	LDB T2,[360600,,T1]
	CAIN T2,EGSETT
	AOS 1(T1)
	MOVEM T1,1(TT)
EGUSC1:	SKIPE T,(T)
	JRST EGUSCL
	SETZM (TT)
	EXCH A,D
	HRLI A,EGSETT_14
	PUSHJ P,EGGCAB
	MOVE A,S
	MOVE B,D
	JRST EGGCAB

EGUNSA:	SETZB B,(TT)	;ABORT
	PUSHJ P,EGGCBA
	MOVE A,D
	JRST EGGC1

EGXCWE:	CAMN A,B			;EXCHWE
	JRST EGGTE0
	PUSHJ P,EGSTES
	JUMPLE C,EGUNSU
	MOVE D,1(T)
	SOJN D,EGXWEC	;MUST MAKE COPY
EGXCW0:	MOVE D,T
	SKIPN T,(T)
	JRST EGXWEE	;MUST EXTEND
	SOJN C,EGXCW0
	MOVE S,1(T)
	EXCH S,@EGVARA
	MOVEM S,1(T)
	POPJ P,

EGXWEE:	MOVEI S,0	;EXTEND
	EXCH S,@EGVARA
	MOVE A,S
	JRST EGSTEX

EGXWEC:	PUSHJ P,EGFSP	;COPY
	MOVE D,TT
	MOVEI T1,1
EGXWCL:	MOVEM T1,1(TT)
	PUSHJ P,EGFSP
	SKIPN T,(T)
	JRST EGXWC1
	MOVE T1,1(T)
	LDB T2,[360600,,T1]
	CAIN T2,EGSETT
	AOS 1(T1)
	SOJN C,EGXWCL
	EXCH T1,@EGVARA
	MOVEM T1,1(TT)
	JRST EGSEC3

EGXWC0:	SETZM 1(TT)
	PUSHJ P,EGFSP
EGXWC1:	SOJN C,EGXWC0
	MOVEI S,0
	EXCH S,@EGVARA
	MOVEM S,1(TT)
	JRST EGSECD

EGSTE:	PUSHJ P,EGSTES
	JUMPLE C,CPOPJ
	TRZE A,400
	SKIPA A,EGLIT(A)
	MOVE A,@EGVARA
	LDB D,[360600,,A]
	CAIN D,EGSETT
	AOS 1(A)
	MOVE D,1(T)
	POPJ P,

EGSTES:	TRZE C,400
	SKIPA C,EGLIT(C)
	MOVE C,@EGVARC
	LDB D,[360600,,C]
	CAIE D,EGINTT
	EGRTTY [ASCIZ \NON-NUMERIC ARG\]
	LSH C,6
	ASH C,-6
	MOVE T,@EGVARB
	LDB D,[360600,,T]
	CAIE D,EGSETT
	EGRTTY [ASCIZ \NON-SET ARG\]
	JUMPG C,CPOPJ
	MOVE S,T
	SKIPE S,(S)
	AOJA C,.-1
	AOJA C,CPOPJ

EGXCHL:	MOVE T,@EGVARA			;EXCHEL
	LDB T1,[360600,,T]
	CAIE T1,EGSETT
	EGRTTY [ASCIZ \NON-SET ARG\]
	PUSHJ P,EGINTU
	LSH B,6
	ASH B,-6
	LSH C,6
	ASH C,-6
	CAMLE B,C
	EXCH B,C
	JUMPG B,EGXL2
	MOVEI D,1
	MOVE S,T
	SKIPE S,(S)
	AOJA D,.-1
	JUMPG C,EGXL1
	ADD C,D
	JUMPLE C,CPOPJ
EGXL1:	ADD B,D
	JUMPLE B,EGXCL1
EGXL2:	SUB C,B
	JUMPE C,CPOPJ
	MOVE D,1(T)
	SOJN D,EGXCLC	;MUST DO COPY
EGXLL:	SKIPN T,(T)
	POPJ P,
	SOJN B,EGXLL
	MOVE B,T
EGXLL2:	MOVE D,T
	SKIPN T,(T)
	JRST EGXCLX	;MUST EXTEND
	SOJN C,EGXLL2
	MOVE D,1(B)
	EXCH D,1(T)
	MOVEM D,1(B)
	POPJ P,

EGXCL1:	LSHC A,-44	;MOVE B,A
			;MOVEI A,0
	JRST EGSTE0

EGXCLC:	PUSHJ P,EGFSP	;COPY
	MOVEI T1,1
	MOVEM T1,1(TT)
	MOVE D,TT
EGXLCL:	SKIPN T,(T)
	JRST EGXLCA	;CAN ABORT
	PUSHJ P,EGFSP
	MOVE T1,1(T)
	LDB T2,[360600,,T1]
	CAIN T2,EGSETT
	AOS 1(T1)
	MOVEM T1,1(TT)
	SOJN B,EGXLCL
	MOVE B,TT
EGXLCM:	SKIPN T,(T)
	JRST EGXLC2
	PUSHJ P,EGFSP
	MOVE T1,1(T)
	LDB T2,[360600,,T1]
	CAIN T2,EGSETT
	AOS 1(T1)
	MOVEM T1,1(TT)
	SOJN C,EGXLCM
	EXCH T1,1(B)
	MOVEM T1,1(TT)
	MOVE B,A
	JRST EGSEC3

EGXLCA:	SETZM (TT)	;ABORT
	MOVE A,D
	JRST EGGC1

EGXLC1:	SETZM 1(TT)
EGXLC2:	PUSHJ P,EGFSP
	SOJN C,EGXLC1
	EXCH T,1(B)	;T HAS 0
	MOVEM T,1(TT)
	MOVE B,A
	JRST EGSECD

EGCAP:	PUSHJ P,EGSETU			;INTERS(ECTION)
	SKIPE C,(C)
	JRST EGCAP2
EGCAP0:	SETZM (TT)
	JRST EGGCBA
EGCAP1:	PUSHJ P,EGFSP
	MOVEM T,1(TT)
EGCAP2:	SKIPN D,(D)
	JRST EGCAP0
	MOVE S,C
	MOVE T,1(D)
	LDB T1,[360600,,T]
	CAIN T1,EGSETT
	JRST EGCAP4
EGCAP3:	CAMN T,1(S)
	JRST EGCAP1
	SKIPE S,(S)
	JRST EGCAP3
	JRST EGCAP2

EGCAP4:	HRLM A,(P)
	PUSH P,B
EGCAP5:	MOVE A,T
	MOVE B,1(S)
	PUSHJ P,EGEQ
	JRST EGCAP6
	POP P,B
	HLRZ A,(P)
	AOS 1(T)
	JRST EGCAP1

EGCAP6:	SKIPE S,(S)
	JRST EGCAP5
	POP P,B
	HLRZ A,(P)
	JRST EGCAP2

EGCUP:	PUSHJ P,EGSETU			;UNION
	MOVE S,D
	JRST EGCUP2
EGCUP1:	PUSHJ P,EGFSP
	MOVE T,1(S)
	LDB T1,[360600,,T]
	CAIN T1,EGSETT
	AOS 1(T)
	MOVEM T,1(TT)
EGCUP2:	SKIPE S,(S)
	JRST EGCUP1
EGCUP3:	EXCH C,D
	JRST EGDIF1

EGDIFF:	PUSHJ P,EGSETU			;DIFFER(ENCE)
EGDIF1:	SKIPN D,(D)
	JRST EGCAP0
	MOVE T,1(D)
	LDB T1,[360600,,T]
	CAIN T1,EGSETT
	JRST EGDIF4
	SKIPN S,(C)
	JRST EGDIF3
EGDIF2:	CAMN T,1(S)
	JRST EGDIF1
	SKIPE S,(S)
	JRST EGDIF2
EGDIF3:	PUSHJ P,EGFSP
	MOVEM T,1(TT)
	JRST EGDIF1

EGDIF4:	SKIPN S,(C)
	JRST EGDIF7
	HRLM A,(P)
	PUSH P,B
EGDIF5:	MOVE A,T
	MOVE B,1(S)
	PUSHJ P,EGEQ
	JRST EGDIF6
	POP P,B
	HLRZ A,(P)
	JRST EGDIF1

EGDIF6:	SKIPE S,(S)
	JRST EGDIF5
	POP P,B
	HLRZ A,(P)
EGDIF7:	PUSHJ P,EGFSP
	AOS 1(T)
	MOVEM T,1(TT)
	JRST EGDIF1

EGAPP:	PUSHJ P,EGSETU			;APPEND
EGAPP1:	SKIPN D,(D)
	JRST EGAPP2
	PUSHJ P,EGFSP
	MOVE T,1(D)
	LDB T1,[360600,,T]
	CAIN T1,EGSETT
	AOS 1(T)
	MOVEM T,1(TT)
	JRST EGAPP1
EGAPP2:	SKIPN C,(C)
	JRST EGCAP0
	PUSHJ P,EGFSP
	MOVE T,1(C)
	LDB T1,[360600,,T]
	CAIN T1,EGSETT
	AOS 1(T)
	MOVEM T,1(TT)
	JRST EGAPP2

EGSETU:	TRZE B,400	;SET CHECK
	SKIPA D,EGLIT(B)
	MOVE D,@EGVARB
	TRZE C,400
	SKIPA C,EGLIT(C)
	MOVE C,@EGVARC
	LDB T,[360600,,C]
	LDB TT,[360600,,D]
	CAIN T,EGSETT
	CAIE TT,EGSETT
	EGRTTY [ASCIZ \NON-SET ARG\]
	PUSHJ P,EGFSP
	MOVEI T1,1
	MOVEM T1,1(TT)
	MOVE B,TT
	HRLI B,EGSETT_14
	POPJ P,
;CHESS PLAYING

EGPLAY:	PUSHJ P,EGOGET			;PLAY
	MOVEM C,EGPL1
	MOVE C,A
	PUSH P,@EGVARX
	SETZB A,@EGVARX
	PUSHJ P,EGSELC
	POP P,A
	EXCH A,@EGVARX
	SKIPN T,(A)
	JRST EGGC1
	MOVE A,1(T)
EGPLP:	SKIPN T,(T)
	JRST EGPLA
	MOVE B,1(T)
EGPL1:	.
	JRST EGPLP
EGPLA:	LDB B,[110600,,A]	;PLAY MOVE IN A
	ANDI A,700177
	PUSHJ P,TMOVE
	EGRTTY [ASCIZ \PLAYED ILLEGAL MOVE\]
	SKIPE C,CVPR
	PUSHJ P,LFSRC
	SETZM CVPR
	MOVSI C,40000
	PUSHJ P,RECMH
	PUSHJ P,UNMOVE
	MOVE A,EGVAL
	MOVEI D,CPOPJ
	JRST EGVLRP

EGPLAB:	PUSHJ P,EGTRYA			;PLAYB
	SKIPN T,(A)
	JRST EGGC1
	MOVE TT,A
	JSP D,EGPOP
	MOVE A,TT
	PUSHJ P,EGTRYP
	SKIPN EGMVC
	AOS (P)
	POPJ P,

EGPLAN:	MOVE A,EGSTAT(A)		;PLAN
	MOVEM A,WPLAN(I)
	TRZE B,400
	SKIPA A,EGLIT(B)
	MOVE A,@EGVARB
	LDB T,[360600,,A]
	CAIE T,EGSETT
	EGRTTY [ASCIZ \NON-SET ARG\]
	AOS 1(A)
	TRZE C,400
	SKIPA C,EGLIT(C)
	MOVE C,@EGVARC
	MOVEM C,EGXM(I)
	JRST EGTRY1

EGTRY:	PUSHJ P,EGTRYA			;TRY
EGTRY1:	SKIPN T,(A)
	JRST EGGC1
	PUSH P,EGVARX
	PUSH P,EGPC
	PUSHJ P,EGTRYP
	POP P,EGPC
	POP P,A
	HRRM A,EGVARA
	HRRM A,EGVARB
	HRRM A,EGVARC
	HRRM A,EGVARX
	POPJ P,

EGTRYA:	PUSHJ P,EGOGET
	MOVEM C,EGTR1
	MOVE C,A
	PUSH P,@EGVARX
	SETZB A,@EGVARX
	PUSHJ P,EGSELC
EGTRLP:	MOVE T,@EGVARX
	MOVE T,(T)
	SOSG TT,EGSCNT
	JRST EGTRA
EGTRL:	MOVE A,1(T)
	MOVE B,(T)
	MOVE B,1(B)
EGTR1:	.		;SHOULDN'T CLOBBER T OR TT
	MOVEM A,1(T)
	MOVE T,(T)
	MOVEM B,1(T)
	SOJG TT,EGTRL
	JRST EGTRLP
EGTRA:	POP P,A
	EXCH A,@EGVARX	;RESTORE OLD X
	POPJ P,

EGTRYP:	PUSH P,A	;TRY MOVES IN SET IN ORDER
	PUSH P,LGLMST
	PUSH P,HSADR
	PUSH P,EGMVC	;MOVE COUNTER
EGMOVC==-5
	PUSH P,R	;MAX VAL
EGBMX==-4
	PUSH P,CVPR	;PRINCIPAL VARIATION
EGBPV==-3
	PUSH P,Q	;MIN VAL
EGBMN==-2
	PUSH P,T2	;HASH ADR OF PRINC VAR
EGBHSH==-1
EGTRAL:	PUSH P,(T)
	MOVE A,1(T)
	LDB B,[110600,,A]	;TRY MOVE IN A
	ANDI A,700177
	SKIPL QTF
	PUSHJ P,TMOVE
	JRST EGTRA1	;ILLEGAL OR IMPOSSIBLE MOVE
	AOS EGMOVC(P)
	TRC I,1
	PUSHJ P,LMG
	TRC I,1
	XCT EGBMNC(I)
	XCT EGNBMN(I)
	XCT EGBSFC(I)
	JRST EGNBSF	;NEW BEST SO FAR
	SKIPE C,CVPR
	PUSHJ P,LFSRC
	CLEARM CVPR
EGTRA2:	PUSHJ P,UNMOVE
EGTRA1:	POP P,T
	JUMPN T,EGTRAL
EGTRA3:	POP P,T2	;HASH ADDRESS OF PRINCIPAL VARIATION
	POP P,Q		;MIN VAL
	POP P,CVPR	;PRINCIPAL VARIATION
	POP P,R		;MAX VAL
	POP P,EGMVC
	POP P,HSADR	;HASH ADDRESS OF CURRENT POSITION
	POP P,LGLMST	;SAVED PDL
	POP P,A
	JRST EGGC1

EGBSFC:	CAMLE R,EGBMX(P)	;SKIP ON DONT WANT MOVE
	CAMGE Q,EGBMN(P)

EGBMNC:	CAMLE Q,EGBMN(P)	;BEST MIN?
	CAMGE R,EGBMX(P)

EGNBMN:	MOVEM Q,EGBMN(P)	;NEW BEST MIN
EGNBMX:	MOVEM R,EGBMX(P)	;NEW BEST MAX
	MOVEM Q,EGBMN(P)

EGNBSF:	MOVE A,POSHSA
	MOVEM A,EGBHSH(P)
	SKIPE C,EGBPV(P)
	PUSHJ P,LFSRC
	MOVSI C,40000
	PUSHJ P,RECMH
	MOVE C,CVPR
	MOVE A,EGMOVC(P)
	DPB A,[PUSNM,,(C)]
	MOVEM C,EGBPV(P)
	XCT ABTST(I)
	JRST EGABC
	XCT EGNBMX(I)
	XCT ABUPT(I)
	PUSHJ P,BPRVS
	JRST EGTRA2

EGABC:	XCT ABVLI(I)	;CLOBBER LEADING VALUE TO INFINITY
	XCT EGNBMX(I)
	PUSHJ P,UNMOVE
	POP P,T
	JRST EGTRA3

EGUPLN:	SETZM EGXM(I)			;UNPLAN
	SETZM WPLAN(I)
	POPJ P,

EGVRET:	PUSHJ P,EGVRUT			;VALRET
	JRST EGLEAV

EGRB:	CAMG R,A
	CAML Q,A

EGRETB:	PUSH P,EGVAL	;RETURN CURRENT BEST SO FAR IF BETTER THAN SPECIFIED VALUE		;RETBET
	PUSHJ P,EGVCVT
	POP P,EGVAL
	XCT EGRB(I)	;SKIPS IF BEST SO FAR GOOD ENOUGH
	POPJ P,

EGDONE:	MOVEI D,CPOPJ			;DONE
	SKIPN EGMVC
EGLEAV:	MOVEI D,POPJ1			;LEAVE
	JRST EGPOP

EGVRUT:	PUSHJ P,EGVCVT			;VALRUT
	MOVE TT,LMGD
	SOJE TT,CPOPJ
	MOVEI D,CPOPJ
EGVALR:	SKIPE C,CVPR
	PUSHJ P,LFSRC
	SETZM CVPR
EGVLRP:	MOVEI T2,-1
	MOVE R,A
	MOVE Q,R
EGPOP:	HRLZ C,EGVCNT	;UNBIND ALL VARS
	HRR C,EGVARX
	AOBJP C,EGPOPE	;EXCEPT X
EGPOPL:	MOVE A,(C)
	PUSHJ P,EGGC
	AOBJN C,EGPOPL
EGPOPE:	MOVEI A,-1	;RESTORE PDL
	MOVE P,@EGVARA
	POP P,EGXM(I)
	POP P,WPLAN(I)
	SKIPN B,EGTSW
	JRST (D)
	CAIN D,CPOPJ
	ISOUT [.ASCII ?*U!Q 	*U!R?]
	SOUT [ASCIZ \
				**********
\]
	JRST (D)

EGVCVT:	TRZE A,400	;CONVERT CHEG VALUE TO MAIN PROGRAM VALUE
	SKIPA A,EGLIT(A)
	MOVE A,@EGVARA
	LDB D,[360600,,A]
	CAIE D,EGINTT
	EGRTTY [ASCIZ \NON-NUMERIC ARG\]
	PUSHJ P,EGINTU
	SUB A,B
	CAIGE C,500.
	JRST EGVCVA
	JUMPE A,EGVSTO	;CERTAIN WIN, LOSS, OR DRAW
	CAILE A,500.
	MOVEI A,500.
	CAMGE A,[-500.]
	MOVNI A,500.
	MOVEI B,67400-500._5
	SUB B,LMGD
	SKIPG A
	MOVNS B
	ASH A,5
	ADD A,B
EGVCV1:	JUMPE I,EGVSTO
	MOVNS A
EGVSTO:	MOVEM A,EGVAL
	POPJ P,

EGVCVA:	IMULI A,PWNV	;NOT CERTAIN
	IDIVI A,100.
	JRST EGVCV1

EGVAL:	0		;SAVED VALUE FOR PLAY

EGMVC:	0	;MOVE COUNTER
;CHESS DATA MANIPULATION

EGGTSA:	TRZE A,400
	SKIPA A,EGLIT(A)
	MOVE A,@EGVARA
EGGTSQ:	LDB D,[360600,,A]	;GET SQUARE VALUE OF ARG
	ANDI A,177
	CAIE D,EGSQT
	CAIN D,EGMOVT
	POPJ P,
	CAIE D,EGPCT
	EGRTTY [ASCIZ \EGGTSQ: WRONG TYPE ARG\]
	MOVE A,PIECEL(A)
	POPJ P,

EGGTPA:	TRZE A,400
	SKIPA A,EGLIT(A)
	MOVE A,@EGVARA
EGGTPC:	LDB D,[360600,,A]	;GET PIECE VALUE OF ARG
	CAIN D,EGPCT
	POPJ P,
	CAIE D,EGMOVT
	JRST EGGTP1
	LDB A,[110600,,A]
	POPJ P,
EGGTP1:	CAIE D,EGSQT
	EGRTTY [ASCIZ \EGGTPC: WRONG TYPE ARG\]
	SKIPG A,BOARD(A)
	EGRTTY [ASCIZ \EGGTPC: NO PIECE ON SQUARE\]
	SUBI A,PIECE
	POPJ P,

EGPIEC:	EXCH A,B			;PIECE
	PUSHJ P,EGGTPA
	HRLI A,EGPCT_14
	JRST EGGCAB

EGSQUA:	EXCH A,B			;SQUARE
	PUSHJ P,EGGTSA
	HRLI A,EGSQT_14
	JRST EGGCAB

EGSSID:	EXCH A,B			;SIDE
	PUSHJ P,EGGTSA
	LDB A,[BFILE,,BPREL(A)]
	SOS A
	LSH A,-2
	JRST EGGCIA

EGFILE:	EXCH A,B			;FILE
	PUSHJ P,EGGTSA
	LDB A,[BFILE,,BPREL(A)]
	JRST EGGCIA

EGRANK:	EXCH A,B			;RANK
	PUSHJ P,EGGTSA
	LDB A,[BRANK,,BPREL(A)]
	JUMPE I,EGGCIA
	MOVE A,REVERS(A)
	JRST EGGCIA

EGSCOL:	EXCH A,B			;SCOLOR
	PUSHJ P,EGGTSA
	LDB C,[BCOLR,,BPREL(A)]
	JRST EGGCIA

EGPCOL:	EXCH A,B			;PCOLOR
	PUSHJ P,EGGTPC
	LDB A,[430100,,PIECE(A)]
	XOR A,I
	JRST EGGCIA

EGMVDS:	EXCH A,B			;MVDIST
	PUSHJ P,EGGTSA
	LDB T,[BFILE,,BPREL(A)]
	LDB TT,[BRANK,,BPREL(A)]
	MOVE A,@EGVARC
	PUSHJ P,EGGTSQ
	LDB C,[BFILE,,BPREL(A)]
	LDB A,[BRANK,,BPREL(A)]
	SUB A,TT
	SUB C,T
	MOVMS A
	MOVMS C
	CAMGE A,C
	MOVE A,C
	JRST EGGCIA

EGINSQ:	EXCH A,B			;INSQ
	PUSHJ P,EGGTPA
	EXCH A,C
	PUSHJ P,EGGTPA
	SKIPGE RPIECE(C)
	SKIPL LMGT(A)
	EGRTTY [ASCIZ \INSQ: ARGS NOT K,P\]
	MOVE D,PIECE(A)
	HLR D,PIECE(C)
	MOVE T,PIECEL(C)
	MOVE TT,PIECEL(A)
	LDB A,[BFILE,,BPREL(T)]
	LDB C,[BFILE,,BPREL(TT)]
	SUB C,A
	MOVMS C
	LDB T,[BRANK,,BPREL(T)]
	LDB TT,[BRANK,,BPREL(TT)]
	JUMPL D,EGISQ1		;D<0 IF PAWN BLACK
	MOVE T,REVERS(T)
	MOVE TT,REVERS(TT)
EGISQ1:	CAIN TT,7
	MOVEI TT,6
	MOVSI A,EGLOGT_14
	TLC D,(D)
	JUMPGE D,EGISQ2		;D<0 IF PAWN AND KING OF OPPOSING COLORS
	TLC D,(D)
	SKIPE I
	TLC D,400000
	SKIPGE D		;D<0 IF KING TO MOVE
	AOS TT
EGISQ2:	CAMLE T,TT		;SEE IF KING CLOSER TO 8TH RANK
	JRST EGGCAB
	CAMGE C,TT		;SEE IF KING CLOSER TO PAWN THAN PAWN IS TO QUEENING
	AOJA A,EGGCAB
	JRST EGGCAB

EGOPPI:	EXCH A,B			;OPP
	PUSHJ P,EGGTSA
	EXCH A,C
	PUSHJ P,EGGTSA
	MOVE C,BPREL(C)
	XOR C,BPREL(A)
	MOVSI A,EGLOGT_14
	TLNE C,1001
	JRST EGGCAB
	AOJA A,EGGCAB

EGSQGR:	EXCH A,B			;SQ>RNK
	PUSHJ P,EGGTSA
	TRZE C,400
	SKIPA C,EGLIT(C)
	MOVE C,@EGVARC
	LDB D,[360600,,C]
	CAIE D,EGINTT
	EGRTTY [ASCIZ \NON-NUMERIC ARG\]
	LSH C,6
	ASH C,-6
	SKIPE I
	MOVNS C
	IMULI C,BW
	ADD A,C
	HRLI A,EGSQT_14
	JRST EGGCAB
;LOGICAL OPERATIONS

EGNOR:	MOVE D,[PUSHJ P,EGNORI]			;NOR
	JRST EGANOR
EGNORI:	IOR B,C
	TRC B,1
	POPJ P,

EGAND:	SKIPA D,[AND B,C]			;AND
EGOR:	MOVE D,[IOR B,C]			;OR
EGANOR:	TRZE B,400
	SKIPA B,EGLIT(B)
	MOVE B,@EGVARB
	TRZE C,400
	SKIPA C,EGLIT(C)
	MOVE C,@EGVARC
	LDB T,[360600,,B]
	LDB TT,[360600,,C]
	CAIN T,EGLOGT
	CAIE TT,EGLOGT
	EGRTTY [ASCIZ \NON-LOGICAL ARG\]
	XCT D
	JRST EGGCBA
;INTEGER ARITHMETIC

EGADD:	SKIPA D,[ADD B,C]			;ADD
EGSUB:	MOVE D,[SUB B,C]			;SUB
EGADSB:	PUSHJ P,EGINTU
	XCT D
	TLZ B,770000
	JRST EGGCIB

EGMUL:	SKIPA D,[IMUL B,C]			;MUL
EGDIV:	MOVE D,[IDIV B,C]			;DIV
EGMD:	PUSHJ P,EGINTU
	LSH B,6
	LSH C,6
	ASH B,-6
	ASH C,-6
	XCT D
	TLZ B,770000
	JRST EGGCIB

EGMOD:	MOVE D,[PUSHJ P,EGMOD1]			;MOD
	JRST EGMD

EGMOD1:	IDIV B,C
	MOVE B,C
	POPJ P,

EGDIF:	PUSHJ P,EGINTU				;DIF [ABS OF SUB]
	SUB B,C
	JRST EGABS1

EGMIN:	SKIPA D,[CAMLE B,C]			;MIN
EGMAX:	MOVE D,[CAMGE B,C]			;MAX
EGMNX:	PUSHJ P,EGINTU
	LSH B,6
	LSH C,6
	XCT D
	MOVE B,C
	LSH B,-6
	JRST EGGCIB

EGABS:	TRZE B,400				;ABS
	SKIPA B,EGLIT(B)
	MOVE B,@EGVARB
	LDB D,[360600,,B]
	CAIE D,EGINTT
	EGRTTY [ASCIZ \NON-NUMERIC ARG\]
EGABS1:	LSH B,6
	ASH B,-6
	MOVMS B
	JRST EGGCIB

EGINTU:	TRZE B,400
	SKIPA B,EGLIT(B)
	MOVE B,@EGVARB
	TRZE C,400
	SKIPA C,EGLIT(C)
	MOVE C,@EGVARC
	LDB T1,[360600,,B]
	LDB T2,[360600,,C]
	CAIN T1,EGINTT
	CAIE T2,EGINTT
	EGRTTY [ASCIZ \NON-NUMERIC ARG\]
	POPJ P,
;;CONDITION INTERPRETING

EGDEFD:	LDB D,[360600,,A]			;DEFD
	JUMPN D,POPJ1
	POPJ P,

;INTEGER CONDITIONS

EGCEQ0:	PUSHJ P,EGINCK				;=0
	EGRTTY [ASCIZ \=0: NON-NUMBERIC ARG\]
	JUMPE A,POPJ1
	POPJ P,

EGCL0:	PUSHJ P,EGINCK				;<0
	EGRTTY [ASCIZ \<0: NON-NUMERIC ARG\]
	JUMPL A,POPJ1
	POPJ P,

EGCG0:	PUSHJ P,EGINCK				;>0
	EGRTTY [ASCIZ \>0: NON-NUMERIC ARG\]
	JUMPG A,POPJ1
	POPJ P,

EGCODD:	PUSHJ P,EGINCK				;ODD
	EGRTTY [ASCIZ \ODD: NON-NUMERIC ARG\]
	TRNE A,1_6
	AOS (P)
	POPJ P,

EGINCK:	LDB D,[360600,,A]	;CHECK FOR INTEGER IN A
	LSH A,6
	CAIN D,EGINTT
	AOS (P)
	POPJ P,

;LOGICAL CONDITIONS

EGTRUE:	CAMN A,EGT				;TRUE
	AOSA (P)
	CAMN A,EGF
	POPJ P,
	EGRTTY [ASCIZ \T: NON-LOGICAL ARG\]

;SET CONDITIONS

EGMT:	LDB D,[360600,,A]			;EMPTY
	CAIE D,EGSETT
	EGRTTY [ASCIZ \EMPTY: NON-SET ARG\]
	SKIPN (A)
	AOS (P)
	POPJ P,
;CHESS CONDITIONS

EGFAR:	PUSHJ P,EGGTSQ				;FAR
	LDB A,[BFILE,,BPREL(A)]
	SOS D,A
	LSH D,-2
	MOVEI A,EGSIDE
	SKIPN A,@EGVARA
	EGRTTY [ASCIZ \FAR: SIDE UNDEFINED\]
	CAIE D,(A)
	AOS (P)
	LDB D,[360600,,A]
	CAIE D,EGINTT
	EGRTTY [ASCIZ \FAR: SIDE NON-NUMERIC\]
	POPJ P,

EGRKF:	PUSHJ P,EGGTSQ				;RF
	LDB A,[BFILE,,BPREL(A)]
	CAIE A,1
	CAIN A,8
	AOS (P)
	POPJ P,

EGNTF:	PUSHJ P,EGGTSQ				;NF
	LDB A,[BFILE,,BPREL(A)]
	CAIE A,2
	CAIN A,7
	AOS (P)
	POPJ P,

EGNNTF:	PUSHJ P,EGNTF				;NNF
	POPJ P,
	SOS D,A
	LSH D,-2
	MOVEI A,EGSIDE
	SKIPN A,@EGVARA
	EGRTTY [ASCIZ \NNF: SIDE UNDEFINED\]
	CAIN D,(A)
	AOS (P)
	LDB D,[360600,,A]
	CAIE D,EGINTT
	EGRTTY [ASCIZ \NNF: SIDE NON-NUMERIC\]
	POPJ P,

EGKING:	PUSHJ P,EGGTPC				;KING
	SKIPGE RPIECE(A)
	AOS (P)
	POPJ P,

EGPAWN:	PUSHJ P,EGGTPC				;PAWN
	SKIPGE LMGT(A)
	AOS (P)
	POPJ P,

EGQUEE:	PUSHJ P,EGGTPC				;QUEEN
	LDB A,[PTID,,PIECE(A)]
	CAIN A,QID
	AOS (P)
	POPJ P,

EGROOK:	PUSHJ P,EGGTPC				;ROOK
	LDB A,[PTID,,PIECE(A)]
	CAIN A,RID
	AOS (P)
	POPJ P,

EGBISH:	PUSHJ P,EGGTPC				;BISH
	LDB A,[PTID,,PIECE(A)]
	CAIN A,BID
	AOS (P)
	POPJ P,

EGNIGH:	PUSHJ P,EGGTPC				;(K)NIGHT
	LDB A,[PTID,,PIECE(A)]
	CAIN A,KNID
	AOS (P)
	POPJ P,

EGCAPT:	LDB D,[360600,,A]			;CAPT
	CAIE D,EGMOVT
	EGRTTY [ASCIZ \CAPT: NON-MOVE ARG\]
	LDB D,[110600,,A]
	ANDI A,177
	SKIPE BOARD(A)
	JRST POPJ1
	SKIPGE LMGT(D)
	CAME A,GHLOC
	POPJ P,
	JRST POPJ1

EGPASS:	PUSHJ P,EGGTPC				;PASSD
	SKIPL LMGT(A)
	EGRTTY [ASCIZ \PASSD: NON-PAWN ARG\]
	SKIPE PPASED(A)
	AOS (P)
	POPJ P,

EGCHK:	LDB D,[360600,,A]			;CHECK
	CAIE D,EGMOVT
	EGRTTY [ASCIZ \CHECK: NON-MOVE ARG\]
	LDB B,[110600,,A]
	ANDI A,700177
	PUSH P,T
	PUSH P,TT
	PUSHJ P,TMOVE
	JRST EGCHK1	;NOT LEGAL, SO NOT A CHECK
	MOVE A,@GAMP
	TRNE A,CHKBT
	AOS -2(P)
	PUSHJ P,UNMOVE
EGCHK1:	POP P,TT
	POP P,T
	POPJ P,

EGPDP:	PUSHJ P,EGGTPC				;PDP
	SKIPL LMGT(A)
	EGRTTY [ASCIZ \PDP: NON-PAWN ARG\]
	SKIPGE PPTP(A)
	AOS (P)
	POPJ P,

EGKMV:	LDB D,[360600,,A]	;KING MOVE?
	CAIE D,EGMOVT
	EGRTTY [ASCIZ \EGKMV: NON-MOVE ARG\]
	LDB D,[110600,,A]
	SKIPGE RPIECE(D)
	AOS (P)
	POPJ P,

EGRWKE:	PUSHJ P,EGKMV				;RWK=
	JRST POPJ1
	ANDI A,177
	LDB A,[BRANK,,BPREL(A)]
	MOVE D,PIECEL(D)
	LDB D,[BRANK,,BPREL(D)]
	CAMN A,D
	AOS (P)
	POPJ P,

EGWPC:	PUSHJ P,EGGTPC				;WPC
	JUMPN I,EGWPC1
	SKIPL PIECE(A)
	AOS (P)
	POPJ P,
EGWPC1:	SKIPGE PIECE(A)
	AOS (P)
	POPJ P,

EGWSQ:	PUSHJ P,EGGTSQ				;WSQ
	TRNN A,1
	AOS (P)
	POPJ P,

EGSAFE:	PUSHJ P,EGKMV				;SAFE
	JRST EGSF1
	ANDI A,177
EGSF1A:	MOVEI B,EGWP-EGIVAR+1
	MOVE B,@EGVARB
	MOVE B,PIECEL(B)
	JRST EGSF2
EGSF1:	SKIPL LMGT(D)
	JRST EGSF3
	ANDI A,177
	MOVEI B,EGWK-EGIVAR+1
	MOVE B,@EGVARB
	MOVE B,PIECEL(B)
	EXCH A,B
EGSF2:	MOVEI C,EGBK-EGIVAR+1
	MOVE C,@EGVARC
	MOVE C,PIECEL(C)
	LDB D,[BRANK,,BPREL(B)]
	LDB B,[BFILE,,BPREL(B)]
	LDB T1,[BRANK,,BPREL(C)]
	LDB C,[BFILE,,BPREL(C)]
	SUB C,B
	MOVMS C
	SUB T1,D
	MOVMS T1
	CAMGE C,T1
	MOVE C,T1
	LDB T1,[BRANK,,BPREL(A)]
	LDB A,[BFILE,,BPREL(A)]
	SUB A,B
	MOVMS A
	SUB T1,D
	MOVMS T1
	CAMGE A,T1
	MOVE A,T1
	CAMG A,C
	AOS (P)
	POPJ P,

EGSF3:	MOVEI A,EGWK-EGIVAR+1
	MOVE A,@EGVARA
	MOVE A,PIECEL(A)
	JRST EGSF1A

EGOPPC:	PUSHJ P,EGKMV				;OPP
	JRST EGOPC1
	ANDI A,177
	JRST EGOPC2
EGOPC1:	MOVEI A,EGWK-EGIVAR+1
	MOVE A,@EGVARA
	MOVE A,PIECEL(A)
EGOPC2:	MOVE A,BPREL(A)
	MOVEI B,EGBK-EGIVAR+1
	MOVE B,@EGVARB
	MOVE B,PIECEL(B)
	XOR A,BPREL(B)
	TLNN A,1001
	AOS (P)
	POPJ P,
;;ORDERING OPERATION INTERPRETING

EGDEEP:	LDB C,[360600,,A]			;DEEP
	LDB D,[360600,,B]
	CAIN C,EGMOVT
	CAIE D,EGMOVT
	EGRTTY [ASCIZ \DEEP: NON-MOVE ARG\]
	LDB C,[700,,A]
	LDB D,[700,,B]
	LDB C,[BRANK,,BPREL(C)]
	LDB D,[BRANK,,BPREL(D)]
	SUB C,D
	SKIPE I
	MOVNS C
	JUMPGE C,CPOPJ
	EXCH A,B
	POPJ P,		;RANK(A)#<RANK(B)

