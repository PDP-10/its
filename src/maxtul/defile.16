;;;;;;;;;;;;;;;;;;; -*- Mode: Lisp; Package: Macsyma -*- ;;;;;;;;;;;;;;;;;;;
;;;     (c) Copyright 1980 Massachusetts Institute of Technology         ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; The macsyma source files. This documentation is used 
;;; during compiliation of the macsyma system, during the loading
;;; of the macsyma system, and during runtime by the error message
;;; system.  -GJC, 9:00pm  Friday, 24 October 1980

(INCLUDEF "LIBMAX;GPRELUDE >")

(HERALD DEFMFILE)

(DEFSTRUCT (MACSYMA-SOURCE-FILE NAMED-HUNK CONC-NAME default-pointer)
  ;; The following are relevant to all files:
  DIR             ;; The ITS directory it may be found on. 
  NAME            ;; The ITS first file name.
  VERSION         ;; The ITS version number of latest version.
  MAINTAINERS     ;; The primary loosers hacking this file.
  UNFASL-HEADER   ;; of installed version.
  CODESIZE        ;; in machine words.
  (SYSTEMS-FOR '(ITS MACLISP PDP-10 FRANZ LISPM NIL))
  (SYSTEMS-NOT-FOR NIL)

  ;; The following are relevant for runtime files:
  GOEDEL-NUMBER    ;; used by the error message system.
  GENPREFIX        ;; prefix to generated symbols in the file,
  ;; generated in the compiler, which must be
  ;; unique at load time.
  (IN-CORE  NIL)    ;; precedence order of loading for IN-CORE PDP10,
  ;; NIL if not IN-CORE.
  (LANGUAGE 'LISP) ;; for TAGS.
  TAGS-PRECEDENCE   ;; also for TAGS.

  ;; The following is relevant mainly on MC-ITS for installed files.
  (FASL-DIR 'MACSYM)   ;; Directory to put the compiled FASL code,
  (UNFASL-DIR 'MUNFAS) ;; for the compiler comments,
  (ERRMSG-DIR 'MAXERR) ;; for error messages and documentation
  ;; strings.
  (SPLIT NIL)          ;; Uses SPLITfile on PDP10.
  ;; This also implies something about FASL-DIR.
  
  ;; The following are relevant for macro packages:
  GLOBAL-COMPILE    ;; T if for all of runtime macsyma.
  SELECTIVE-COMPILE ;; A list of files which reference.
  MACRO-COMPILE     ;; A list of macro files which use this
  ;; macro file. If there is circularity
  ;; refer to the documentation in the files
  ;; for how to resolve it.
  )

(DEFMACRO DEF-DISP-STRUCT (NAME)
  (DO ((L (DEFSTRUCT-DESCRIPTION-SLOT-ALIST 
	    (OR (GET NAME 'DEFSTRUCT-DESCRIPTION)
		(ERROR "No DEFSTRUCT description." NAME 'FAIL-ACT)))
	  (CDR L))
       (ARGS NIL)
       (STRING (EXPLODEN "~&")))
      ((NULL L)
       (SETQ STRING `',(MAKNAM STRING))			; should be LIST-TO-STRING
       (SETQ ARGS (NREVERSE ARGS))
       `(DEFUN ,(SYMBOLCONC 'DISP- NAME) (EXP STREAM)
	  (FORMAT STREAM
		  ,STRING
		  ,@ARGS)))
    (PUSH `(,(DEFSTRUCT-SLOT-DESCRIPTION-REF-MACRO-NAME (CDAR L)) EXP)
	  ARGS)
    (SETQ STRING
	  (NCONC STRING (EXPLODEN (CAAR L)) (EXPLODEN " : ~S~%")))))

(DEF-DISP-STRUCT MACSYMA-SOURCE-FILE)

#-LISPM ; useful lisp machine function.

(PROGN 'COMPILE

(IF (STATUS FEATURE COMPLR)
    (*LEXPR DEL))

(DEFUN DEL (PREDICATE ITEM LIST &OPTIONAL (N #.(LSH -1 -1))) ; maximum FIXNUM.
       (COND ((ATOM PREDICATE)
	      (LET ((FUNCTION (GETL PREDICATE '(SUBR LSUBR EXPR))))
		   (CASEQ (CAR FUNCTION)
			  (SUBR
			   (DEL*1 (CADR FUNCTION) ITEM LIST N))
			  (LSUBR
			   (DEL*2 (CADR FUNCTION) ITEM LIST N))
			  (EXPR
			   (DEL*3 (CADR FUNCTION) ITEM LIST N))
			  (T
			   (ERROR "Unbound function as arg to DEL" PREDICATE)))))
	     ('ELSE
	      (DEL*3 PREDICATE ITEM LIST N))))

(DEFMACRO DEFDEL* (NUMB &REST CALLER &AUX (NAME (SYMBOLCONC 'DEL* NUMB)))
	  `(DEFUN ,NAME (PREDICATE ITEM LIST N)
		  (COND ((OR (NULL LIST) (ZEROP N)) LIST)
			((,@CALLER PREDICATE ITEM (CAR LIST))
			 (,NAME PREDICATE ITEM (CDR LIST) (1- N)))
			('ELSE
			 (RPLACD LIST (,NAME PREDICATE ITEM (CDR LIST) N))))))
(DEFDEL* 1 SUBRCALL NIL)
(DEFDEL* 2 LSUBRCALL NIL)
(DEFDEL* 3 FUNCALL)

(IF (STATUS FEATURE COMPLR) (*EXPR ASS))

(DEFUN ASS (PREDICATE ITEM LIST)
       (DO ()
	   ((NULL LIST) NIL)
	   (LET ((A (POP LIST)))
		(IF (FUNCALL PREDICATE ITEM A)
		    (RETURN A)))))

; end of #-LISPM
)

(DEFMACRO DEFMFILE (DIR NAME &REST OPTIONS)
	  `(SETQ MACSYMA-SOURCE-FILES
		 (ADD-MACSYMA-SOURCE-FILE
		  (MAKE-MACSYMA-SOURCE-FILE DIR ',DIR
					    NAME ',NAME
					    ,@OPTIONS)
		  MACSYMA-SOURCE-FILES)))

;(DEFMACRO DEFMFILES (DIR &REST CLAUSES))
	
(DEFVAR MACSYMA-SOURCE-FILES NIL)

(DEFUN ADD-MACSYMA-SOURCE-FILE (SOURCE-STRUCT LIST)
       (SETQ SOURCE-STRUCT
	     (DEFAULTY-MACSYMA-SOURCE-FILE SOURCE-STRUCT))
       (CONS SOURCE-STRUCT
	     (DEL #'(LAMBDA (S-MATCH S)
			    (COND ((EQ (MACSYMA-SOURCE-FILE-NAME S)
				       (MACSYMA-SOURCE-FILE-NAME S-MATCH))
				   (FORMAT MSGFILES
					   "~&(COMMENT *REDEFINING SOURCE FILE*~%")
				   (DISP-MACSYMA-SOURCE-FILE S MSGFILES)
				   (FORMAT MSGFILES "~&; as")
				   (DISP-MACSYMA-SOURCE-FILE S-MATCH MSGFILES)
				   (TYO #/) MSGFILES)
				   T)
				  (T NIL)))
		  SOURCE-STRUCT
		  LIST)))

(DEFVAR MACSYMA-SOURCE-FILES-GOEDEL-INDEX 0)

(DEFUN INIT-MACSYMA-SOURCE-FILES ()
       (SETQ MACSYMA-SOURCE-FILES-GOEDEL-INDEX 0
	     MACSYMA-SOURCE-FILES NIL))

(DEFUN DEFAULTY-MACSYMA-SOURCE-FILE (S)
       (LET ((IND MACSYMA-SOURCE-FILES-GOEDEL-INDEX))
	    (SETQ MACSYMA-SOURCE-FILES-GOEDEL-INDEX (1+ IND))
	    ; would could do all sorts of consistency checking here.
	    (ALTER-MACSYMA-SOURCE-FILE
	     S
	     GOEDEL-NUMBER IND
	     GENPREFIX (MAKE-GENPREFIX IND)
	     TAGS-PRECEDENCE (IF (MACSYMA-SOURCE-FILE-IN-CORE S)
				 ; JPG likes this option.
				 (MACSYMA-SOURCE-FILE-IN-CORE S)
				 (+ 10000. IND)))
	    (IF (MACSYMA-SOURCE-FILE-SPLIT S)
		(ALTER-MACSYMA-SOURCE-FILE
		 S
		 FASL-DIR 'MAXOUT
		 UNFASL-DIR 'MAXOUT))
	    (IF (MACSYMA-SOURCE-FILE-IN-CORE S)
		(SETF (MACSYMA-SOURCE-FILE-FASL-DIR S) 'MAXDMP))
	    (IF (NOT (MACSYMA-SOURCE-FILE-MAINTAINERS S))
		(SETF (MACSYMA-SOURCE-FILE-MAINTAINERS S)
		      (LIST (MACSYMA-SOURCE-FILE-DIR S))))
	    (IF (MACRO-FILE-P S)
		(ALTER-MACSYMA-SOURCE-FILE
		 S
		 FASL-DIR (MACSYMA-SOURCE-FILE-DIR S)
		 UNFASL-DIR (MACSYMA-SOURCE-FILE-UNFASL-DIR S)))
	    S))


(DEFUN MAKE-GENPREFIX (GOEDEL-NUMBER)
       (IF (GREATERP GOEDEL-NUMBER
		     (1- (LSH 1 (* 7 4))))
	   ; thats (TIMES 2.68 (EXP 10. 8.)) source files!
	   (ERROR "Goedel number is too large" GOEDEL-NUMBER 'FAIL-ACT))
       ; be a compact as possible, be obscure to be unique,
       ; keep the symbol pname under one PDP10 word in size.
       (DO ((N #\BS ; thats Back-Space, not Bull-Shit.
	       (LOGIOR (LSH N 7) (LOGAND K #o177)))
	    (K GOEDEL-NUMBER (LSH K -7))
	    (J 0 (1+ J)))
	   ((= J 4.)
	    ; am only taking 4 least significant ASCII's.
	    (SETQ N (LSH N 1)) ; LSB is not part of the game.
	    ; PNPUT takes a list of FIXNUMs in the internal representation
	    ; of Maclisp pnames. Trailing ^@ are ignored by EXPLODEN
	    ; FLATC, PRINT.
	    (PNPUT (LIST N) NIL))))

(IF (STATUS FEATURE COMPLR) (*EXPR MACRO-FILE-P))

(DEFUN MACRO-FILE-P (X)
       (OR (MACSYMA-SOURCE-FILE-GLOBAL-COMPILE X)
	   (MACSYMA-SOURCE-FILE-SELECTIVE-COMPILE X)
	   (MACSYMA-SOURCE-FILE-MACRO-COMPILE X)))