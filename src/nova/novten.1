TITLE TEN-NOVA TESTER

TENI==6
TENO==7
;READ A CHAR FROM THE TEN
RTEN:	SKPDN TENI
	JMP .-1
	DIAS 0,TENI
	JMP (3)

;WRITE A CHAR TO THE TEN
WTEN:	SKPBZ TENO
	JMP .-1
	DOAS 0,TENO
	JMP (3)

;THIS ROUTINE JUST ECHOES
SECHO:	IORST
	INTDS
ECHO:	JSR RTEN
	JSR WTEN
	JMP ECHO

;THIS ROUTINE SENDS CHARACTERS TO THE TEN AND EXPECTS
;	THEM TO BE ECHOED.  IT CHECKS THEM FOR ACCURACY.

STEST:	IORST	;I/O RESET
	INTDS	;DISABLE INTEREUPTS
	LDA 2,[ERRLST-1]
	STA 2,21
	LDA 2,[CHRLST-1]
	STA 2,20
	LDA 0,[40]
	LDA 1,[-100]
	STA 0,@20
	INC 0,0
	INC 1,1(SZR)
	JMP .-3
	LDA 0,[0]
	STA 0,@20	;TERMINATE CHAR LIST
	STA 0,CT1H
	STA 0,CT1L
	STA 0,CT2L
	STA 0,CT2H
	STA 2,20	;REINIT AUTO INC LOC
TEST:	READS 0	;READ CONSOLE SWITCHES
	MOVZL% 0,0(SNC)	;IF SIGN BIT ON USE CHAR IN SWITCHES
	LDA 0,@20	;OTHERWISE GET CHAR FROM LIST
	LDA 1,[177]
	AND 1,0(SNR)	;MASK OFF HIGH BITS
	STA 2,20	;IF CHAR IS ZERO, RESET LOC 20
	JSR WTEN	;SEND CHAR TO TEN
	MOV 0,1	;REMEMBER CHAR SENT
	ISZ CT2L
	JMP TEST1
	ISZ CT2L
	JMP TEST1
	HALT
TEST1:	JSR RTEN	;RECIEVE CHAR FROM TEN
	LDA 3,[177]
	AND 3,0	;MASK OFF PARITY BIT
	SUB% 0,1(SNR)	;IS IT THE ONE WE SENT?
	JMP TEST	;YES
	HALT	;NO
	LDA 2,21	;SAVE THE GOOD AND BAD CHARS
	LDA 3,[16776]
	SUBZ 2,3	;CARRY=1 IF (3) >= (2)
	LDA 3,[ERRLST-1]
	MOV 3,3(SNC)
	STA 3,21
	STA 0,@21
	STA 1,@21
	LDA 2,[CHRLST-1]
	ISZ CT1L
	JMP TEST
	ISZ CT1H
	JMP TEST
	HALT


	CONSTANTS
CT1L:	0	;COUNT OF ERRORS
CT1H:	0
CT2L:	0	;COUNT OF CHARS SENT
CT2H:	0
CHRLST:	BLOCK 200.
ERRLST:	0
	END STEST
;THIS ROUTINE SENDS CHARACTERS TO THE TEN AND EXPECTS
;	THEM TO BE ECHOED.  IT CHECKS THEM FOR ACCURACY.

STEST:	IORST	;I/O RESET
	INTDS	;DISABLE INTEREUPTS
	LDA 2,[ERRLST]
	STA 2,21
	LDA 2,[CHRLST]
	STA 2,20
	LDA 0,[40]
	LDA 1,[-100]
	STA 0,@20
	INC 0,0
	INC 1,1(SZR)
	JMP .-2
	LDA 0,[0]
	STA 0,CT1H
	STA 0,CT1L
TEST:	READS 0	;READ CONSOLE SWITCHES
	MOVZL% 0,0(SNC)	;IF SIGN BIT ON USE CHAR IN SWITCHES
	MOV 0,@20	;OTHERWISE GET CHAR FROM LIST
	LDA 1,[377]
	AND 1,0(SNR)	;MASK OFF HIGH BITS
	STA 2,20	;IF CHAR IS ZERO, RESET LOC 20
	JSR WTEN	;SEND CHAR TO TEN
	MOV 0,1	;REMEMBER CHAR SENT
	JSR RTEN	;RECIEVE CHAR FROM TEN
	SUB% 0,1(SNR)	;IS IT THE ONE WE SENT?
	JMP TEST	;YES
	HALT	;NO
	LDA 2,21	;SAVE THE GOOD AND BAD CHARS
	LDA 3,[16776]
	SUBZ 1,3
	LDA 3,[ERRLST]
	MOV 3,3(SZC)
	STA 3,21
	STA 0,@21
	STA 1,@21
	LDA 2,[CHRLST]
	ISZ CT1L
	JMP TEST
	ISZ CT1H
	JMP TEST
	HALT


	CONSTANTS
CT1L:	0	;COUNT OF ERRORS
CT1H:	0
CHRLST:	BLOCK 200.
ERRLST:	0
	END STEST
