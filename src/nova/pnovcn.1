
TITLE	NOVA CONSOLE
;console program using the plasma panel
DSP=21
TENI=6
TENO=7

LOC	17000

BEG:	INTDS
	LDA	0,[414]	;FORM FEED WITH 400 BIT
	DOAS	0,DSP
CLOOP:	SKPDZ	TENI
	JMP	RCH10
	SKPDN	TTI
	JMP	CLOOP
TTYRCH:	DIAS	0,TTI
	SKPBZ	TENO
	JMP	.-1
	DOAS	0,TENO
	JMP	CLOOP

RCH10:	DIAS	0,TENI
	LDA	1,[177]
	AND	1,0
	SUB%	0,1(SNR)
	JMP	CLOOP
	LDA	1,[3]
	SUB%	0,1(SNR)
	JMP	BLK
	LDA	1,[15]
	SUBZ%	0,1(SNR)
	JMP	DCH
	LDA	1,[40]
	SUBZ%	1,0(SNC)
	JMP	CLOOP
	LDA	1,[140]
	SUBZ%	0,1(SZC)
	JMP	DCH
	LDA	1,[173]
	SUBZ%	0,1(SBN)
	JMP	CLOOP
	LDA	1,[40]
	SUB	1,0
DCH:	LDA	1,[400]
	ADD	1,0
	JSR	WTD
	DOAS	0,DSP
	LDA	1,[15+400]
	SUB%	1,0(SZR)
	JMP	CLOOP
	LDA	0,[12+400]
	JSR	WTD
	DOAS	0,DSP
	JSR	CLINE
	DIAS	0,TENI
	STA	0,SAV
	LDA	1,[-36]
	JSR	CLINE+1
	LDA	0,[15+400]
	JSR	WTD
	DOAS	0,DSP
	LDA	0,SAV
	JMP	RCH10+1

SAV:	0
CLINE:	LDA	1,[-44]
	LDA	0,[40+400]
	STA	3,CLINR
CLRLN:	JSR	WTD
	DOAS	0,DSP
	INC	1,1(SZR)
	JMP	CLRLN
	JMP	@CLINR
CLINR:	0

WTD:	STA	3,WTDR
	LDA	3,TIMEC	;TIME OUTCONSTANT
	STA	3,TIMOTC
WTD1:	ISZ	TIMOTC
	JMP	.+2
	JMP	TIMOT
	SKPBZ	DSP
	JMP	WTD1
	JMP	@WTDR
TIMOT:	JMP	.+1	;IN CASE WANT TO PATCH WITH HALT
	JMP	@WTDR

TIMOTC:	0
TIMEC:	176000
WTDR:	0

CONSTANTS

BLK:	JSR	GET
	MOV	3,1
	JSR	GET
	MOV	3,2
	MOVL%	1,1(SNC)
	JMP	STBLK
L:	JSR	GET
	STA	3,(2)
	INC	2,2
	INC	1,1(SZR)
	JMP	L
	JMP	BLK

STBLK:	MOVZR	1,1(SNR)
	MOV	1,1(SNC)
	HALT
	LDA	0,[^G]
	DOAS	0,TTO
	MOVL%	2,2(SZC)
	HALT
	JMP	(2)

GET:	STA	3,GX
	STA	2,G2
	JSR	CH
	MOVS	3,2
	ADDZL	2,2
	JSR	CH
REPEAT 3,ADDZL	3,3
	ADD	3,2
	JSR	CH
	ADD	2,3
	LDA	2,G2
	JMP	@GX
GX:	0
G2:	0

CH:	STA	3,CX
	SKPDN	6
	JMP	.-1
	DIAS	3,6
	LDA	0,[177]
	AND	0,3
	MOVZR	0,0
	AND	3,0
	SUB%	0,3(SNR)
	HALT
	MOV	0,3
	JMP	@CX
CX:	0

CONSTANTS
DLIST:	14
LOC	17640
DLEND:	0
	123456

END	BEG
