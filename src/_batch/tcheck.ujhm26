
<PACKAGE "TCHECK">

<ENTRY TASK-CHECK>

<USE	"BATCHN"		;"DELETE-FROM-DISK-Q NIGHT? WRITE-TASK"
	"BATCHQ"		;"task descriptor field names"
	"TASKM"			;"ACKNOWLEDGE"
	"TIMFCN"		;"DTADD DTADD-2NORM DTNORM DTNOW">


<DEFINE TASK-CHECK (TSK "AUX" FIELD H-LIM T-LIM C)
    #DECL ((VALUE) <OR FALSE VECTOR> (TSK) VECTOR
	   (FIELD H-LIM T-LIM) ANY (C) <OR CHANNEL FALSE>)

  <PROG ()

    <COND (<DECL? <FILE-TO-RUN .TSK>		     ;"FILE-TO-RUN & TASK-NAME"
		  '<VECTOR STRING STRING STRING [REST STRING]>>
	   <COND (<NOT <TYPE? <TASK-NAME .TSK> STRING>>
		  <PUT .TSK ,TASK-NAME "NAME">)>)
	  (<TYPE? <SET FIELD <TASK-NAME .TSK>> STRING>
	   <PUT .TSK ,FILE-TO-RUN ["SYS" <ORIGINATOR .TSK> "TS" .FIELD]>)
	  (ELSE
	   <PUT .TSK ,TASK-NAME "NULL">
	   <PUT .TSK ,TASK-STATE "REMOVED">
	   <DELETE-FROM-DISK-Q .TSK>
	   <RETURN %<>>)>


    <COND (<NOT <SET FIELD <TASK-STATE .TSK>>>		;"TASK-STATE"
	   <PUT .TSK ,TASK-STATE "RUNNABLE">)
	  (<NOT <TYPE? .FIELD STRING>>
	   <PUT .TSK ,TASK-STATE "UNRUNNABLE">)
	  (<=? .FIELD "RUNNING">
	    <COND (<RESTART-SWITCH .TSK>
		   <PUT .TSK ,TASK-STATE "RUNNABLE">
		   <ACKNOWLEDGE	.TSK
				ON-COMPLETION:SYSTEM-CRASH
				"System or batch crash while job was running">
		   <WRITE-TASK .TSK>)
		  (ELSE
		   <PUT .TSK ,TASK-STATE "UNRUNNABLE">
		   <ACKNOWLEDGE	.TSK
				ON-COMPLETION:SYSTEM-CRASH
				"System or batch crash while job was running">
		  <WRITE-TASK .TSK>)>)
	  (<=? .FIELD "REMOVED">
	    <DELETE-FROM-DISK-Q .TSK>)>

    <COND (<TYPE? <SET FIELD <TIME-OF-NEXT-RUN .TSK>> LIST> ;"TIME-OF-NEXT-RUN"
	   <PUT .TSK ,TIME-OF-NEXT-RUN <DTNORM .FIELD>>)
	  (<MEMBER .FIELD '["NOW" "ASAP" "now" "asap"]>
	   <PUT .TSK ,TIME-OF-NEXT-RUN <DTNOW>>)
	  (<NIGHT?>
	   <PUT .TSK ,TIME-OF-NEXT-RUN <DTNOW>>)
	  (ELSE						;"USE 1:00 TOMORROW"
	   <PUT .TSK
		,TIME-OF-NEXT-RUN
		<DTADD '(() ()) '((0 0 1) (1 0 0))>>)>


    <COND (<TYPE? <SET H-LIM <HANG-LIMIT .TSK>> FIX>)		;"HANG-LIMIT"
	  (<TYPE? .H-LIM FLOAT>
	   <PUT .TSK ,HANG-LIMIT <SET H-LIM <FIX .H-LIM>>>)
	  (.H-LIM
	   <PUT .TSK ,HANG-LIMIT <SET H-LIM %<>>>)>
    ;<COND (<AND .H-LIM <G? .H-LIM 25>>
	   <PUT .TSK ,HANG-LIMIT <SET H-LIM 25>>)>

    <COND (<TYPE? <SET T-LIM <TIME-LIMIT .TSK>>FLOAT>);"TASK-TYPE & TIME-LIMIT"
	  (<TYPE? .T-LIM FIX>
	   <PUT .TSK ,TIME-LIMIT <SET T-LIM <FLOAT .T-LIM>>>)
	  (.T-LIM
	   <PUT .TSK ,TIME-LIMIT <SET T-LIM %<>>>)>

    <COND (<NOT <TYPE? <TASK-TYPE .TSK> STRING>>	;"NEEDS A TASK TYPE"
	   <COND (<NOT .T-LIM>
		  <COND (<OR <NOT .H-LIM> <L=? .H-LIM 5>>
			 <PUT .TSK ,TIME-LIMIT 5.0>
			 <PUT .TSK ,TASK-TYPE "SHORT">)
			(ELSE
			 <PUT .TSK ,TIME-LIMIT 5.0>
			 <PUT .TSK ,TASK-TYPE "LONG">)>)
		 (<OR <G? .T-LIM 5.0> <G? .H-LIM 5>>
		  <PUT .TSK ,TASK-TYPE "LONG">)
		 (ELSE
		  <PUT .TSK ,TASK-TYPE "SHORT">)>)
	  (<NOT .T-LIM>					;"NEEDS A TIME-LIMIT"
	   <PUT .TSK
		,TIME-LIMIT
		<COND (<=? <TASK-TYPE .TSK> "SHORT">
			   5.0)
		      (ELSE 30.0)>  >)>

    <COND (<NOT <TYPE? <WHEN-ORIGINATED .TSK> LIST>>	;"WHEN-ORIGINATED"
	   <PUT .TSK ,WHEN-ORIGINATED <DTNOW>>)>


    <COND (<TYPE? <SET FIELD <ORIGINATOR .TSK>> STRING>)	;"ORIGINATOR"
	  (<TYPE? .FIELD ATOM>
	   <PUT .TSK ,ORIGINATOR <PNAME .FIELD>>)
	  (ELSE
	   <PUT .TSK ,ORIGINATOR "GUEST">)>


    <COND (<TYPE? <SET FIELD <OUTPUT-FILE .TSK>> VECTOR>	;"OUTPUT-FILE"
	   <PUT .TSK
		,OUTPUT-FILE
		<OUTPUT-FILE-FIXUP .FIELD .TSK>>)
	  (<=? .FIELD "NUL:">
	   <PUT .TSK ,OUTPUT-FILE "">)
	  (<TYPE? .FIELD STRING>
	   <AND <SET C <CHANNEL "PRINT" .FIELD>>
		<=? <5 .C> "NUL">
		<PUT .TSK ,OUTPUT-FILE "">>)
	  (.FIELD
	   <PUT .TSK ,OUTPUT-FILE %<>>)>

    <COND (<NOT <TYPE? <DELIMITER-STRING .TSK> STRING>>	;"DELIMITER-STRING"
	    <PUT .TSK ,DELIMITER-STRING %<STRING !\ <ASCII 13>> >)>
				;"DEFAULT DELIMITER STRING IS [ESC] [CR] "

    <COND (<TYPE? <SET FIELD <WHEN-ORIGINATED .TSK>> LIST>   ;"WHEN-ORIGINATED"
	   <COND (<LENGTH? .FIELD 3>
		  <PUT .TSK ,WHEN-ORIGINATED <DTNORM .FIELD>>)>)
	  (ELSE
	   <PUT .TSK ,WHEN-ORIGINATED <DTNOW>>)>


    <COND (<NOT <SCHEDULER .TSK>>			;"RESCHEDULE-INFO"
	   <COND (<TYPE? <SET FIELD <RESCHEDULE-INFO .TSK>> LIST>
		  <PUT .TSK ,RESCHEDULE-INFO <DTADD-2NORM .FIELD>>)
		 (<TYPE? .FIELD VECTOR>
		  <PUT .TSK
		       ,RESCHEDULE-INFO
		       <MAPF ,VECTOR ,DTNORM .FIELD>>)
		 (.FIELD
		  <PUT .TSK ,RESCHEDULE-INFO %<>>) > )>

						;"WHEN-TO-STOP-RESCHEDULING"
    <COND (<TYPE? <SET FIELD <WHEN-TO-STOP-RESCHEDULING .TSK>> LIST>
	   <PUT .TSK ,WHEN-TO-STOP-RESCHEDULING <DTNORM .FIELD>>)
	  (.FIELD
	   <PUT .TSK ,WHEN-TO-STOP-RESCHEDULING %<>>)>

    <COND (<AND <SET FIELD <MAXIMUM-NUMBER-OF-RUNS .TSK>>
						;"MAXIMUM-NUMBER-OF-RUNS"
		<NOT <TYPE? .FIELD FIX>> >
	   <PUT .TSK ,MAXIMUM-NUMBER-OF-RUNS %<>>)>

    <COND (<AND <SET FIELD <JCL-LINE .TSK>>		;"JCL-LINE"
		<NOT <TYPE? .FIELD STRING>> >
	   <PUT .TSK ,JCL-LINE %<>>)>

    <COND (<AND <SET FIELD <WORKING-DIRECTORY .TSK>>
		<NOT <TYPE? .FIELD STRING>> >
	   <PUT .TSK ,WORKING-DIRECTORY %<>>)>

						;"ACKNOWLEDGEMENT-INFO"
    <COND (<NOT <DECL? <ACKNOWLEDGEMENT-INFO .TSK>
		   '<VECTOR [REST <OR ATOM <VECTOR [REST <OR FIX STRING>]>>]>>>
	   <PUT .TSK ,ACKNOWLEDGEMENT-INFO %<>>)>
    .TSK>>

<DEFINE OUTPUT-FILE-FIXUP (FSPEC TSK)
   #DECL ((VALUE) <OR STRING FALSE VECTOR>
	  (TSK FSPEC) VECTOR)
   <COND (<=? .FSPEC '["NUL:"]>
	  "")
	 (<NOT <DECL? .FSPEC '<VECTOR STRING [REST STRING]>>>
	  %<>)
	 (<SHITS-IN .FSPEC>
	  <MAPF ,STRING
		#FUNCTION ((S) #DECL ((S) STRING) <MAPRET .S !\ >)
		.FSPEC>)
	 (<G=? <LENGTH .FSPEC> 4>
	  .FSPEC)
	 (<==? <LENGTH .FSPEC> 1>
	  ["DSK" <ORIGINATOR .TSK> <1 .FSPEC> "OUTPUT"])
	 (<==? <LENGTH .FSPEC> 2>
	  ["DSK" <ORIGINATOR .TSK> <1 .FSPEC> <2 .FSPEC>])
	 (<==? <LENGTH .FSPEC> 3>
	  ["DSK" <1 .FSPEC> <2 .FSPEC> <3 .FSPEC>])> >

<DEFINE SHITS-IN (FIL)
   #DECL ((VALUE) ANY (FIL) <VECTOR STRING [REST STRING]>)
   <MAPF ,OR? ;OR1
	 #FUNCTION ((S) #DECL ((S) STRING)
		    <OR <MEMQ !\; .S> <MEMQ !\: .S>>)
	 .FIL> >

;<DEFINE OR1 ("TUPLE" PQR)
   #DECL ((VALUE) ANY (PQR) TUPLE)
   <PROG ()
	<COND (<EMPTY? .PQR> %<>)
	      (<1 .PQR>)
	      (ELSE
	       <SET PQR <REST .PQR>>
	       <AGAIN>)>>>

<ENDPACKAGE>
