<PACKAGE "COLUMNS">

<ENTRY COLFILE COLUMNS>

<DEFINE COLFILE (IT OF "AUX" (OC <OPEN "PRINT" .OF>) OUTCHAN)
	#DECL ((OUTCHAN) <SPECIAL CHANNEL>)
	<COND (.OC <SET OUTCHAN .OC> <PUT .OUTCHAN 13 120>
	       <PRINC <ASCII 12> .OUTCHAN>
	       <COLUMNS .IT 50 8>
	       <CLOSE .OC>
	       "DONE")>>

"COLUMNS -- print a vector of strings in columns HIGH high and WIDE wide"

<DEFINE COLUMNS (IT
		 "OPTIONAL" (HIGH 50) (WIDE 12)
		 "AUX" (H 0) (WIDTH </ <13 .OUTCHAN> .WIDE>) (LAST <>))
	#DECL ((IT) <VECTOR [REST <OR STRING ATOM>]> (HIGH WIDE H WIDTH) FIX)
	<REPEAT ((II .IT) (I .II))
		#DECL ((II I) <VECTOR [REST <OR STRING ATOM>]>)
		<SET I .II>
		<COND (<EMPTY? .I> <TERPRI> <PRINC <ASCII 12>> <RETURN "DONE">)>
		<REPEAT ((W 0))
			#DECL ((W) FIX)
			<PRINC <1 .I>>
			<COND (<==? <SET W <+ .W 1>> .WIDE> <RETURN>)
			      (ELSE <INDENT-TO <* .WIDTH .W>>)>
			<COND (<G? <LENGTH .I> .HIGH> <SET I <REST .I .HIGH>>)
			      (ELSE <SET LAST T> <RETURN>)>>
		<TERPRI>
		<COND (<==? <SET H <+ .H 1>> .HIGH>
		       <SET H 0>
		       <PRINC <ASCII 12>>
		       <AND .LAST <RETURN "DONE">>
		       <TERPRI>
		       <SET II <REST .I>>)
		      (ELSE <SET II <REST .II>>)>>>

<ENDPACKAGE>
\