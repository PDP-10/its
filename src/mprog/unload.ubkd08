<PACKAGE "UNLOAD">
<ENTRY UNLOAD>

<DEFINE UNLOAD (PS "EXTRA" P IP OBL) 
	#DECL ((PS) STRING (P IP) <OR FALSE ATOM>
	       (OBL) <OR FALSE OBLIST>)
	<COND (<AND <SET P <LOOKUP .PS <GETPROP PACKAGE OBLIST>>>
		    <SET IP <PUTPROP .P IOBLIST!-IPKG!-PKG!-PACKAGE>>>
	       <DROP .PS>
	       <IOBK <GETPROP .IP OBLIST
			  '<ERROR NO-INTERNAL-OBLIST!-ERRORS .IP UNLOAD>>>
	       <COND (<SET OBL <GETPROP .P OBLIST>>
		      <IOBK .OBL>)>
	       <ATOMK .P>
	       <ATOMK .IP>
	       "DONE")
	      (ELSE <ERROR NO-PACKAGE!-ERRORS .PS UNLOAD>)>>

<DEFINE OBK (OBNM)
	    #DECL ((OBNM) ATOM)
	    <IOBK <GETPROP .OBNM OBLIST
			   '<ERROR NO-OBLIST!-ERRORS .OBNM>>>>

<DEFINE IOBK (OB "EXTRA" (DC <DECL-CHECK <>>)) 
	#DECL ((OB) OBLIST)
	<MAPF <>
	      <FUNCTION (B) 
		      #DECL ((B) <LIST [REST ATOM]>)
		      <MAPF <> ,ATOMK .B>>
	      .OB>
	<PUTPROP <PUTPROP .OB OBLIST> OBLIST>
	<DECL-CHECK .DC>
	"DONE">

<DEFINE ATOMK (A)
	#DECL ((A) ATOM)
	<REPEAT ((AS <ASSOCIATIONS>))
		#DECL ((AS) <OR FALSE ASOC>)
		<COND (<NOT .AS> <RETURN>)
		      (<OR <==? <ITEM .AS> .A>
			   <==? <INDICATOR .AS> .A>>
		       <PUTPROP <ITEM .AS> <INDICATOR .AS>>)>
		<SET AS <NEXT .AS>>>
	<REMOVE .A>
	<GUNASSIGN .A>
	<UNASSIGN .A>>

<ENDPACKAGE>
