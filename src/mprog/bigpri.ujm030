
<PACKAGE "BIGPRINT">

<ENTRY BIGPRINT 5X7-MASK>

<DEFINE REP-PRINC (CH "OPTIONAL" (RP 1)) 
	#DECL ((CH) ANY (RP) FIX)
	<REPEAT ()
		<COND (<G? <SET RP <- .RP 1>> -1> <PRINC .CH>)
		      (T <RETURN .CH>)>>>

<DEFINE BIGPRINT (INSTRING
		  "OPTIONAL" (RP 1) (OFFS 0)
		  "AUX" (OUTCHAN .OUTCHAN)
			(OFFSTR <COND (<0? .OFFS> "") (T <ISTRING .OFFS !\ >)>)
			(VECT <MAPF ,UVECTOR ,5X7-MASK .INSTRING>))
   #DECL ((VALUE INSTRING OFFSTR) STRING (RP OFFS) FIX
	  (VECT) <UVECTOR [REST FIX]> (OUTCHAN) CHANNEL)
   <COND
    (<0? <LENGTH .INSTRING>> <REP-PRINC "
" .RP>)
    (T
     <REPEAT ((STRIP <* 32 32 32 32 32 32>))
       #DECL ((STRIP) FIX)
       <COND (<0? .STRIP> <RETURN .INSTRING>)>
       <REPEAT ((ROW-IN-STRIP 0))
	       #DECL ((ROW-IN-STRIP) FIX)
	       <COND (<G? <SET ROW-IN-STRIP <+ 1 .ROW-IN-STRIP>> .RP>
		      <RETURN>)>
	       <PRINC .OFFSTR>
	       <REPEAT ((JTH 1) CHAR COLUMN-STRIP)
		       #DECL ((JTH COLUMN-STRIP) FIX (CHAR) CHARACTER)
		       <SET CHAR <NTH .INSTRING .JTH>>
		       <SET COLUMN-STRIP </ <NTH .VECT .JTH> .STRIP>>
		       <REPEAT ((COLUMN 32))
			       #DECL ((COLUMN) FIX)
			       <COND (<0? <SET COLUMN </ .COLUMN 2>>> <RETURN>)
				     (<0? <CHTYPE <ANDB .COLUMN .COLUMN-STRIP>
						  FIX>>
				      <REP-PRINC " " .RP>)
				     (T <REP-PRINC .CHAR .RP>)>>
		       <COND (<G? <SET JTH <+ 1 .JTH>> <LENGTH .INSTRING>>
			      <RETURN>)>
		       <REP-PRINC "  " .RP>>
	       <PRINC "
">>
       <SET STRIP </ .STRIP 32>>>)>>

<DEFINE 5X7-MASK (CHAR "AUX" (N <ASCII .CHAR>))
	#DECL ((VALUE N) FIX (CHAR) CHARACTER)
	<COND (<OR <L? .N *40*> <G? .N *177*>>
	       <ERROR BAD-ASCII-CHARACTER .CHAR .N 5X7>)
	      (T <NTH ,5X7TB <- .N *37*>>)>>

<DEFINE 5BY7MASKGEN (AA AB AC AD AE AF AG)
       #DECL ((VALUE AA AB AC AD AE AF AG) FIX)
	<+ .AG <* 32
		  <+ .AF <* 32
			    <+ .AE <* 32
				      <+ .AD <* 32
						<+ .AC
						   <* 32
						      <+ .AB
							 <* 32 .AA>>>>>>>>>>>>>

"5x7 matrix for ASCII non-control characters"
<GDECL (5X7TB) <UVECTOR [REST FIX]>>
<SETG 5X7TB <UVECTOR
	0							;"SP"
	%<5BY7MASKGEN   4    4    4    4    4    0    4 >	;"!"
	%<5BY7MASKGEN *12* *12* *12*   0    0    0    0 >	;""
	%<5BY7MASKGEN *12* *12* *37* *12* *37* *12* *12*>	;"#"
	%<5BY7MASKGEN   4  *17* *24* *16*   5  *36*   4 >	;"$"
	%<5BY7MASKGEN *36* *31*   2    4  *10* *23*   3 >	;"%"
	%<5BY7MASKGEN   4  *12*   4  *10* *25* *22* *15*>	;"&"
	%<5BY7MASKGEN   4    4    4    0    0    0    0 >	;"'"
	%<5BY7MASKGEN   2    4  *10* *10* *10*   4    2 >	;"("
	%<5BY7MASKGEN *10*   4    2    2    2    4  *10*>	;")"
	%<5BY7MASKGEN   0  *25* *16* *33* *16* *25*   0 >	;"*"
	%<5BY7MASKGEN   0    0    4  *33*   4    0    0 >	;"+"
	%<5BY7MASKGEN   0    0    0    0  *14*   4  *10*>	;","
	%<5BY7MASKGEN   0    0    0  *16*   0    0    0 >	;"-"
	%<5BY7MASKGEN   0    0    0    0    0  *14* *14*>	;"."
	%<5BY7MASKGEN   0    1    2    4  *10* *20*   0>	;"/"
	%<5BY7MASKGEN *16* *21* *23* *25* *31* *21* *16*>	;"0"
	%<5BY7MASKGEN   4  *14*   4    4    4    4  *16*>	;"1"
	%<5BY7MASKGEN *16* *21*   1    2    4  *10* *37*>	;"2"
	%<5BY7MASKGEN *16* *21* 1 6 1 *21* *16* >	;"3"
	%<5BY7MASKGEN 2 6 *12* *37* 2 2 2 >	;"4 . . . *OK*  BEELER?"
	%<5BY7MASKGEN *37* *20* *36* 1 1 *21* *16* >	;"5"
	%<5BY7MASKGEN *16* *21* *20* *36* *21* *21* *16* >	;"6"
	%<5BY7MASKGEN *37* 1 2 4 *10* *20* *20* >	;"7"
	%<5BY7MASKGEN *16* *21* *16* *21* *21* *21* *16* >	;"8"
	%<5BY7MASKGEN *16* *21* *21* *17* 1 *21* *16* >	;"9"
	%<5BY7MASKGEN 0 *14* *14* 0 *14* *14* 0 >	;":"
	%<5BY7MASKGEN 0 *14* *14* 0 *14* 4 *10* >	;" ;"
	%<5BY7MASKGEN 0 2 4 *10* 4 2 0 >	;"<"
	%<5BY7MASKGEN 0 0 *37* 0 *37* 0 0 >	;"="
	%<5BY7MASKGEN 0 *10* 4 2 4 *10* 0 >	;">"
	%<5BY7MASKGEN *16* *21* 2 4 4 0 4 >	;"?"
	%<5BY7MASKGEN 0 *16* *33* *25* *33* *16* 0 >	;"@"
	%<5BY7MASKGEN *16* *21* *21* *37* *21* *21* *21* >	;"A"
	%<5BY7MASKGEN *36* *21* *21* *36* *21* *21* *36* >	;"B"
	%<5BY7MASKGEN *16* *21* *20* *20* *20* *21* *16* >	;"C"
	%<5BY7MASKGEN *36* *21* *21* *21* *21* *21* *36* >	;"D"
	%<5BY7MASKGEN *37* *20* *20* *36* *20* *20* *37* >	;"E"
	%<5BY7MASKGEN *37* *20* *20* *36* *20* *20* *20* >	;"F"
	%<5BY7MASKGEN *16* *21* *20* *20* *23* *21* *16* >	;"G"
	%<5BY7MASKGEN *21* *21* *21* *37* *21* *21* *21* >	;"H"
	%<5BY7MASKGEN *16* 4 4 4 4 4 *16* >	;"I"
	%<5BY7MASKGEN 7 1 1 1 1 *21* *16* >	;"J"
	%<5BY7MASKGEN *21* *22* *24* *34* *22* *21* *21* >	;"K"
	%<5BY7MASKGEN *20* *20* *20* *20* *20* *20* *37* >	;"L"
	%<5BY7MASKGEN *21* *33* *25* *21* *21* *21* *21* >	;"M"
	%<5BY7MASKGEN *21* *21* *31* *25* *23* *21* *21* >	;"N"
	%<5BY7MASKGEN *16* *21* *21* *21* *21* *21* *16* >	;"O"
	%<5BY7MASKGEN *36* *21* *21* *36* *20* *20* *20* >	;"P"
	%<5BY7MASKGEN *16* *21* *21* *21* *25* *22* *15* >	;"Q"
	%<5BY7MASKGEN *36* *21* *21* *36* *21* *21* *21* >	;"R"
	%<5BY7MASKGEN *16* *21* *20* *16* 1 *21* *16* >	;"S"
	%<5BY7MASKGEN *37* 4 4 4 4 4 4 >	;"T"
	%<5BY7MASKGEN *21* *21* *21* *21* *21* *21* *16* >	;"U"
	%<5BY7MASKGEN *21* *21* *21* *21* *21* *12* 4 >	;"V"
	%<5BY7MASKGEN *21* *21* *21* *21* *21* *25* *12* >	;"W"
	%<5BY7MASKGEN *21* *21* *12* 4 *12* *21* *21* >	;"X"
	%<5BY7MASKGEN *21* *21* *12* 4 4 4 4 >	;"Y"
	%<5BY7MASKGEN *37* 2 4 *16* 4 *10* *37* >	;"Z"
	%<5BY7MASKGEN 6 4 4 4 4 4 6 >	;"OPEN SQUARE BRACKET"
	%<5BY7MASKGEN 0 *20* *10* 4 2 1 0 >	;"BACKSLASH"
	%<5BY7MASKGEN *14* 4 4 4 4 4 *14* >	;"CLOSE SQUARE BRACKET"
	%<5BY7MASKGEN 4 *16* *25* 4 4 4 4 >	;"UPARROW"
	%<5BY7MASKGEN 0 4 *10* *37* *10* 4 0 >	;"LEFT ARROW"
%<5BY7MASKGEN 8 4 2 0 0 0 0>	;"`"
%<5BY7MASKGEN 0 0 14 18 18 19 13>	;"a"
%<5BY7MASKGEN 16 16 30 17 17 17 30>
%<5BY7MASKGEN 0 0 15 16 16 16 15>
%<5BY7MASKGEN 1 1 15 17 17 17 15>
%<5BY7MASKGEN 0 0 14 17 23 16 15>
%<5BY7MASKGEN 6 9 8 28 8 8 8>		;"f"
%<5BY7MASKGEN 0 0 13 19 13 1 14>
%<5BY7MASKGEN 16 16 30 17 17 17 17>
%<5BY7MASKGEN 4 0 4 4 4 4 4>
%<5BY7MASKGEN 2 0 2 2 2 18 12>		;"j"
%<5BY7MASKGEN 16 16 17 18 20 26 17>
%<5BY7MASKGEN 4 4 4 4 4 4 14>
%<5BY7MASKGEN 0 0 27 21 17 17 17>	;"m"
%<5BY7MASKGEN 0 0 22 25 17 17 17>
%<5BY7MASKGEN 0 0 14 17 17 17 14>
%<5BY7MASKGEN 0 0 22 25 22 16 16>
%<5BY7MASKGEN 0 0 12 18 14 2 3>
%<5BY7MASKGEN 0 0 22 25 16 16 16>	;"r"
%<5BY7MASKGEN 0 0 14 16 14 1 14>
%<5BY7MASKGEN 8 8 28 8 8 9 6>
%<5BY7MASKGEN 0 0 17 17 17 19 13>	;"u"
%<5BY7MASKGEN 0 0 17 17 17 10 4>
%<5BY7MASKGEN 0 0 17 17 17 21 10>
%<5BY7MASKGEN 0 0 17 10 4 10 17>
%<5BY7MASKGEN 0 0 17 9 6 4 24>		;"y"
%<5BY7MASKGEN 0 0 31 2 4 8 31>		;"z"
%<5BY7MASKGEN 3 4 4 8 4 4 3>	;"CURLY OPEN BRACKET"
%<5BY7MASKGEN 0 4 4 4 4 4 0>	;"VERTICAL STROKE"
%<5BY7MASKGEN 24 4 4 2 4 4 24>	;"CURLY CLOSE BRACKET"
%<5BY7MASKGEN 0 8 21 2 0 0 0>	;"TILDE (SQUIGGLE)"
%<5BY7MASKGEN 31 31 31 31 31 31 31>	;"RUBOUT"
>>

<ENDPACKAGE>
