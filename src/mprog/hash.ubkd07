<PACKAGE "HASH">

<ENTRY HASH>

<NEWTYPE HASH WORD>

<DEFINE HASH (OB "AUX" (HASHNO 3.14159) OBPRIM) 
   #DECL ((OB OBPRIM) ANY (VALUE) HASH (HASHNO) <PRIMTYPE WORD>)
   <CHTYPE
    <XORB <RAND <RAND <CHTYPE <TYPE-C <TYPE .OB>> FIX>>>
	  <COND (<LOCATIVE? .OB> <HASH <IN .OB>>)
		(<TYPE? .OB RSUBR RSUBR-ENTRY>
					    ;"Avoid possible infinite recursion"
		 <XORB <HASH <1 .OB>> <HASH <2 .OB>> <HASH <3 .OB>>>
					       ;"Use Code, Name, and Decl only")
		(<STRUCTURED? .OB>
		 <MAPF <>
		       <FUNCTION (X) 
			       <SET HASHNO
				    <XORB <ROTATE .HASHNO <CHTYPE .HASHNO FIX>>
					  <HASH .X>>>>
		       .OB>
		 .HASHNO)
		(<TYPE? <SET OBPRIM <CHTYPE .OB <PRIMTYPE .OB>>> ATOM STRING>
		 <HASHER .OBPRIM>)
		(<TYPE? .OBPRIM WORD>
		 <RAND <CHTYPE .OBPRIM FIX>>)
		(<TYPE? .OBPRIM LOCR> <RGLOC-CHTYPE .OBPRIM>)
		(<TYPE? .OBPRIM FRAME>
		 <XORB <HASH <FUNCT .OBPRIM>> <HASH <ARGS .OBPRIM>>>)
		(<TYPE? .OBPRIM ASOC>
		 <XORB <HASH <ITEM .OBPRIM>>
		       <HASH <INDICATOR .OBPRIM>>
		       <HASH <AVALUE .OBPRIM>>>)
		(ELSE <ERROR WRONG-TYPE!-ERRORS .OB HASH>)>>
    HASH>>


<FLOAD "MUDDLE;RAND NBIN">

<FLOAD "MUDDLE;ROTATE NBIN">

<FLOAD "MUDDLE;RGLOCS NBIN">

<FLOAD "MUDDLE;HASHER NBIN">

<ENDPACKAGE>
