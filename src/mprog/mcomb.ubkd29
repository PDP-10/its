
<PACKAGE "MCOMB">

;"MESSAGE COMB"

<ENTRY SAVE-MCOMB MCOMB SHORTPR JUNK-MAIL? MSG-QUERY?>

<USE "M.DAC" "GC">

<SETG MSG-QUERY? <>>	;"Query user before printing whole message?"

<GDECL (MSG-QUERY?) <OR ATOM FALSE>
       (JUNK-MAIL?) APPLICABLE 	;"User's Mail Filter Function">

<DEFINE MCOMB ("EXTRA" H)
	#DECL ((H) HANDLER)
	<COND (<AND <NOT <OPEN "READ" "LIBCOM;STAY DOWN">>
		    <DOPEN>>
	       <SET H <ON "CHAR" ,COMBH 100 0 .INCHAN>>
	       <DO-COMB>
	       <OFF .H> <DCLOSE>
	       "DONE")
	      (ELSE <TERPRI>
	       <PRINC "Mail Data Base not available.">)>>

<DEFINE DO-COMB ("EXTRA" N NXTMSG "ACT" MC)
	#DECL ((N) <OR FALSE FIX> (NXTMSG) FIX (MC) <SPECIAL ACTIVATION>)
	<COND (<SET N <MSG-NUMBER "LIBCOM;NXTMSG >">> <SET NXTMSG .N>)
	      (ELSE <ERROR .N>)>
	<COND (<SET N <MSG-NUMBER "_MCOMB >">>
	       <FLOAD "_MCOMB >">	;"Load user's initialization")
	      (ELSE
	       <SET N <OR <MSG-NUMBER "LIBTMP;MDB <"> .NXTMSG>>
	       <CLOSE <OPEN "PRINT" "_MCOMB" <UNPARSE .N>>>)>
	<RESET .INCHAN>
	<REPEAT ((MSG .N) (OUTCHAN .OUTCHAN) (INCHAN .INCHAN))
		#DECL ((MSG) FIX (OUTCHAN INCHAN) CHANNEL)
		<COND (<L? .MSG .NXTMSG>	;"More Messages left to process?")
		      (<NOT <SET N <MSG-NUMBER "LIBCOM;NXTMSG >">>>
		       <ERROR .N>)
		      (<L? .MSG <SET NXTMSG .N>>	;"New arrivals?")
		      (ELSE <RETURN>)>
		<COND (<SHORTPR .MSG>
		       <PREVENT-GC>
		       <PUT .OUTCHAN 14 0> <PRINC "A--Next?--NA">
		       <RESET .INCHAN>)>
		<SET MSG <+ .MSG 1>>	;"Go on to next Message"
		<RENAME "_MCOMB >" TO "_MCOMB" <UNPARSE .MSG>>>>

<DEFINE MSG-NUMBER (STR "EXTRA" CHN S X)
	#DECL ((STR S) STRING (CHN) <OR FALSE CHANNEL> (VALUE X) <OR FALSE FIX>)
	<PROG ()
	      <COND (<NOT <SET CHN <OPEN "READ" .STR>>>
		     <RETURN <CHTYPE ("NO FILE" .STR) FALSE>>)>
	      <SET S <8 .CHN>>
	      <CLOSE .CHN>
	      <COND (<NOT <TYPE? <SET X <PARSE .S>> FIX>>
		     <RETURN <CHTYPE ("BAD FILE" .STR) FALSE>>)>
	      .X>>

<DEFINE SHORTPR (MSG "AUX" FROM TO S L (OUTCHAN .OUTCHAN) CH H "ACT" SP)
   #DECL ((MSG) FIX (VALUE) <OR FALSE ATOM> (CH) CHARACTER
	  (S) ANY (TO L) <OR FALSE LIST> (FROM) <OR FALSE STRING>
	  (OUTCHAN) CHANNEL (SP) <SPECIAL ACTIVATION>)
     <COND (<NOT <MSLOCK .MSG>>
	    <PUT .OUTCHAN 14 0> <PRINC "CMESSAGE #">
	    <PRIN1 .MSG> <PRINC " CAN'T BE LOCKED.">
	    <RETURN T .SP>)>
     <SET FROM <DRDMSG "" "FROM">>
     <SET TO <DRDMSG "" "TO">>
     <COND (<AND <GASSIGNED? JUNK-MAIL?>
		 <APPLY ,JUNK-MAIL? .MSG .FROM .TO>>
	    <RETURN <> .SP>)>
     <PUT .OUTCHAN 14 0> <PRINC "CMESSAGE #"> <PRIN1 .MSG>
     <COND (<OR ,MSG-QUERY? <==? <SET H <DRDMSG "" "RECEIVED-FROM-HOST">> 70>>
	    <COND (.FROM
		   <PUT .OUTCHAN 14 0>
		   <PRINC "AFrom: ">
		   <PRINC .FROM>)>
	    <COND (.TO
		   <PUT .OUTCHAN 14 0>
		   <PRINC "ATo: ">
		   <PR-ADRL .TO "    ">)>
	    <COND (<SET L <DRDMSG "" "CARBON-COPY-TO">>
		   <PUT .OUTCHAN 14 0>
		   <PRINC "AAlso To: ">
		   <PR-ADRL .L "	 ">)>
	    <COND (<SET S <DRDMSG "" "SUBJECT">>
		   <PUT .OUTCHAN 14 0>
		   <PRINC "ASubject: ">
		   <PRINC .S>)>)
	   (<OR <==? .H 134> <==? .H 198> <==? .H 236>>
	    <COND (.TO
		   <PUT .OUTCHAN 14 0>
		   <PRINC "ATo: ">
		   <PR-ADRL .TO "    ">)>)>
     <COND (<AND ,MSG-QUERY? <OR .FROM .TO>>
	    <PUT .OUTCHAN 14 0>
	    <PRINC "A--Text?--">
	    <RESET .INCHAN>
	    <PREVENT-GC>
	    <SET CH <TYI>>
	    <TERPRI>
	    <COND (<N==? .CH <ASCII 32>> <RETURN <> .SP>)>)>
     <PUT .OUTCHAN 14 0> <PRINC "A">
     <COND (<SET S <DRDMSG "" "TEXT">> <PRINC .S>)
	   (ELSE <PRINC "(UNAVAILABLE)">
	    <COND (,MSG-QUERY? <RETURN <> .SP>)>)>
     T>

<DEFINE PR-ADRL (L "OPTIONAL" PREFIX
		 "EXTRA" (OUTCHAN .OUTCHAN) (LL <13 .OUTCHAN>) U)
 #DECL ((L) LIST (OUTCHAN) CHANNEL (PREFIX U) STRING (LL) FIX)
 <REPEAT ()
  <COND (<EMPTY? .L> <RETURN>)
	(<TYPE? <1 .L> STRING> <SET U <1 .L>>)
	(ELSE <SET U <UNPARSE <1 .L>>>)>
  <COND (<G? <+ <14 .OUTCHAN> <LENGTH .U> 3> .LL>
	  <TERPRI>
	  <COND (<ASSIGNED? PREFIX> <PRINC .PREFIX>)>)>
  <PRINC .U>
  <COND (<NOT <EMPTY? <SET L <REST .L>>>> <PRINC ", ">)>>>

<DEFINE COMBH (CHAR CHAN "EXTRA" X)
	#DECL ((CHAR) CHARACTER (CHAN) CHANNEL (X) ANY)
	<COND (<==? .CHAR <ASCII 19>>	;"^S"
	       <RESET .CHAN>
	       <COND (<AND <ASSIGNED? SP>
			   <TYPE? <SET X .SP> ACTIVATION>
			   <LEGAL? .X>>
		      <RESET ,OUTCHAN>
		      <DISMISS T .X>)>
	       <DISMISS T>)
	      (<==? .CHAR <ASCII 7>>	;"^G"
	       <RESET .CHAN>
	       <COND (<AND <ASSIGNED? MC>
			   <TYPE? <SET X .MC> ACTIVATION>
			   <LEGAL? .X>>
		      <RESET ,OUTCHAN>
		      <DISMISS T .X>)>
	       <DISMISS T>)
	       (<==? .CHAR <ASCII 26>>	;"^Z"
		<RESET .CHAN>
		<RESET ,OUTCHAN>
		<INT-LEVEL 0>
		<ERROR CONTROL-G?!-ERRORS>)>>

<DEFINE SAVE-MCOMB ("OPTIONAL" (FIL "LIBSAV;MCOMB SAVE") (GC? T)
		    "EXTRA" (SN <SNAME>))
	#DECL ((FIL SN) STRING (GC?) <OR FALSE ATOM>)
	<AND .GC? <GC 0 T>>
	<SNAME "">
	<COND (<=? <SAVE .FIL <>> "SAVED"> <SNAME .SN> .FIL)
	      (ELSE
	       <PUT .OUTCHAN 14 0> <PRINC "CMAIL COMB">
	       <DIVERT> <SETG DIVERT-LMT 40000>
	       <COND (<=? <JNAME> "STOP"> <ERRET>)>
	       <MCOMB>
	       <QUIT>)>>

<ENDPACKAGE>
