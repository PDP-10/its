<PACKAGE "QPUT">

<ENTRY	PUTAWAY UNPUTAWAY
	READ-PUTAWAY UNPUTAWAY-WRITE
	MAKE-TEMPLATE FALSE-PRIME>

<NEWTYPE FALSE-PRIME LIST>

	; "PUTAWAY CONVERTS EXTERNAL FORMAT INTO A COMPLETE, NEW STRUCTURE,"
	; "LIKE THE TEMPLATE, WITH A FALSE FOR EVERY NON-SPECIFIED FIELD"
	; "EXTERNAL FORMAT IS AN ASSOCIATIVE STRUCTURE OF ATOMS AND OBJECTS"
	; "WHERE THE ATOMS MUST GVAL TO A FIX"

<DEFINE PUTAWAY (TEMPLAT DATA "OPTIONAL" (ERRORFUNC ,ERROR)
		 "AUX" (RESULT <CHTYPE <APPLY ,<PRIMTYPE .TEMPLAT> !.TEMPLAT>
				       <TYPE .TEMPLAT>>))
    #DECL ((VALUE TEMPLAT DATA RESULT) STRUCTURED
	   (ERRORFUNC) APPLICABLE)

    <REPEAT ()
	<COND (<EMPTY? .DATA> <RETURN .RESULT>)
	      (<AND <TYPE? <1 .DATA> ATOM>
		    <GASSIGNED? <1 .DATA>>
		    <TYPE? ,<1 .DATA> FIX>>
	       <PUT .RESULT ,<1 .DATA> <2 .DATA>>)
	      (.ERRORFUNC
		<APPLY .ERRORFUNC <1 .DATA> <2 .DATA>
			ILLEGAL-FIELD-NAME PUTAWAY>)>
	<SET DATA <REST .DATA 2>> >>


	; "READ-PUTAWAY READS A PUTAWAY FORM OF DATA STRUCTURE ACCORDING TO THE"
	; "GIVEN TEMPLATE, AND CONVERTS IT INTO INTERNAL FORM (SEE PUTAWAY ABOVE)"

<DEFINE READ-PUTAWAY (TEMPLAT CHNL
		      "OPTIONAL" (EF ,ERROR) (OBL .OBLIST) "AUX" D)
    #DECL ((VALUE TEMPLAT) STRUCTURED (CHNL) CHANNEL
	   (EF) APPLICABLE (D) ANY
	   (OBL) <LIST [REST OBLIST]>)

    <PROG ((OBLIST .OBL))
	  #DECL ((OBLIST) <SPECIAL LIST>)
	  <SET D <READ .CHNL <> >>>
    <COND (<OR <=? <PRIMTYPE .D> VECTOR> <=? <PRIMTYPE .D> LIST>>
	   <PUTAWAY .TEMPLAT .D .EF>)> >


	; "UNPUTAWAY-WRITE WRITES THE EXTERNAL FORM OF A DATA BASE, WRITING"
	; "OUT ONLY NON-FALSE FIELDS SPECIFIED BY ITS LIST OF ATOMS"
	; "AN OBJECT OF TYPE 'FALSE-PRIME' WILL BE OUTPUT AS AN EMPTY FALSE"

<DEFINE UNPUTAWAY-WRITE (ATOMS DATA CHNL
			 "OPTIONAL" (EF ,ERROR) (OBL .OBLIST) "AUX" D)
   #DECL ((VALUE) ATOM
	  (ATOMS) <VECTOR [REST ATOM]>
	  (DATA) STRUCTURED
	  (CHNL) CHANNEL
	  (EF) APPLICABLE
	  (OBL) <LIST [REST OBLIST]>
	  (D) <VECTOR [REST ATOM ANY]>)

   <SET D <UNPUTAWAY .ATOMS .DATA .EF>>
   <PRINC !"[ .CHNL>
   <REPEAT ((OBLIST .OBL))
	#DECL ((OBLIST) <SPECIAL LIST>)
	<COND (<EMPTY? .D> <RETURN T>)>
	<PRIN1 <1 .D> .CHNL>
	<PRINC <ASCII 9> .CHNL>
	<PRIN1 <2 .D> .CHNL>
	<TERPRI .CHNL>
	<SET D <REST .D 2>> >
   <PRINC !"] .CHNL>
   <TERPRI .CHNL>
   T>


	; "UNPUTAWAY CONVERTS INTERNAL FORM OF A DATA BASE INTO EXTERNAL"
	; "FORM; THAT IS, AN ASSOCIATIVE OBJECT OF ATOMS (NAMING THE SUB FIELDS)"
	; "AND THEIR RESPECTIVE 'VALUES'.  'FALSE' FIELDS ARE NOT WRITTEN"
	; "OUT, BUT A FIELD OF TYPE 'FALSE-PRIME!-QPUT!-PACKAGE!- ' WILL BE"
	; "CONVETED INTO AN EMPTY FALSE"

<DEFINE UNPUTAWAY (ATOMS DATA "OPTIONAL" (ERRORFUNC ,ERROR)
		   "AUX" (L <LENGTH .DATA>))
   #DECL ((VALUE) <VECTOR [REST ATOM ANY]>
	  (ATOMS) <VECTOR [REST ATOM]> (L) FIX
	  (DATA) STRUCTURED
	  (ERRORFUNC) APPLICABLE)

   <MAPF ,VECTOR
	 #FUNCTION ((A "AUX" DATUM) #DECL((A) ATOM (DATUM) ANY)
		<COND (<AND <GASSIGNED? .A>
			    <TYPE? ,.A FIX>
			    <G? ,.A 0>
			    <L=? ,.A .L>>
		       <COND (<TYPE? <SET DATUM <NTH .DATA ,.A>> FALSE-PRIME>
			      <MAPRET .A %<>>)
			     (.DATUM <MAPRET .A .DATUM>)
			     (<MAPRET>)>)
		      (T
		       <APPLY .ERRORFUNC
			      .A
			      ILLEGAL-FIELD-NAME
			      UNPUTAWAY>
		       <MAPRET>)>
		T  ; "MAKE COMPILER HAPPY")
	 .ATOMS> >


	; "MAKE-TEMPLATE GENERATES A TEMPLATE FOR THE ABOVE FUNCTIONS TO USE"
	; "IT SHOULD BE CALLED WITH A STRUCTURE (USUALLY VECTOR) CONTAINING"
	; "ATOMS (WATCH THE OBLIST) AND THEIR INITIAL VALUES FOR READIN"
	; "(DEFAULT VALUES WILL BE FALSE)"


<DEFINE MAKE-TEMPLATE (ATOMVAL
		       "AUX" (POS 1) (TMPL <IVECTOR </ <LENGTH .ATOMVAL> 2>>))
    #DECL ((TMPL VALUE) VECTOR
	   (ATOMVAL) <STRUCTURED [REST ATOM ANY]>
	   (POS) FIX)

    <REPEAT ()
	<COND (<EMPTY? .ATOMVAL> <RETURN .TMPL>)>
	<SET <1 .ATOMVAL> .POS>
	<COND	(<NOT <GASSIGNED? <1 .ATOMVAL>>>
		 <SETG <1 .ATOMVAL> .POS>
		 <MANIFEST <1 .ATOMVAL>>)
		(<N==? ,<1 .ATOMVAL> .POS>
		 <ERROR TEMPLATE-INDICES-DIFFER <1 .ATOMVAL> MAKE-TEMPLATE>)>
	<PUT .TMPL .POS <2 .ATOMVAL>>
	<SET POS <+ .POS 1>>
	<SET ATOMVAL <REST .ATOMVAL 2>> >>

<ENDPACKAGE>
