
<PACKAGE "FMACRO">

<ENTRY FAST-MACROS>

<USE "HASH">

<NEWTYPE NORMAL-MACRO LIST>

<COND (<NOT <APPLYTYPE NORMAL-MACRO>>	;"Not already setup"
       <APPLYTYPE NORMAL-MACRO MACRO>
       <SETG FAST-MACRO (T)>)>

<GDECL (FAST-MACRO) LIST	;"Association Indicator for Expansions">

<DEFINE FAST-MACROS ("OPTIONAL" (FLAG T))
	<COND (.FLAG
	       <APPLYTYPE MACRO ,FAST-MACROER>)
	      (ELSE
	       <APPLYTYPE MACRO ,APPLY>
	       <SETG FAST-MACRO (T)>)>
	"DONE">

<DEFINE FAST-MACROER (MAC "ARGS" BODY
		      "EXTRA" (HASHB <HASH .BODY>) SAVED EXP
		      "ACT" FMR)
	#DECL ((MAC) MACRO (BODY) LIST (FMR) ACTIVATION
	       (SAVED) <OR FALSE <VECTOR HASH ANY>> (HASHB) HASH)
	<COND (<AND <SET SAVED <GETPROP <COND (<EMPTY? .BODY> .MAC)
					      (.BODY)>
					,FAST-MACRO>>
		    <==? <1 .SAVED> .HASHB>>	;"Old expansion saved ?"
	       <SET EXP <2 .SAVED>>)
	      (ELSE	;"Generate expansion and save it"
	       <SET EXP <EXPAND <CHTYPE (<CHTYPE .MAC NORMAL-MACRO> !.BODY) FORM>>>
	       <PUTPROP <COND (<EMPTY? .BODY> .MAC)
			      (.BODY)>
			,FAST-MACRO [.HASHB .EXP]>)>
	<COND (<==? <FUNCT <COND (<TYPE? ,FAST-MACROER RSUBR RSUBR-ENTRY>
				  <FRAME <FRAME <CHTYPE .FMR FRAME>>>)
				 (ELSE <FRAME <CHTYPE .FMR FRAME>>)>>
		     EXPAND>	;"Macro expansion only called for"
		.EXP)
	      (ELSE		;"Macro evaluation called for"
	       <EVAL .EXP>)>>

<ENDPACKAGE>
