
<PACKAGE "DIR">

<ENTRY GET-DIRECTORY
       PARSE-DIRECTORY
       DESC-AREA
       NAME-AREA
       DESC-PTR
       PACK-NUMBER
       LINK-BIT
       OPEN-WRITE
       GC-MARK-BIT
       DELETE-ON-CLOSE
       DELETE-FROM-UNMOUNTED-PACK
       DELETED
       IGNORED
       DUMP-BIT
       EXTRA-WORDS
       CREATION-TIME
       CREATION-DATE
       REF-DATE
       MONTH
       DAY
       YEAR
       DISK-DATE>

<USE "NSTR">

<SETG DESC-AREA 11>

<SETG NAME-AREA 2>

<SETG DESC-PTR <BITS 13>>

<SETG PACK-NUMBER <BITS 5 13>>

<SETG LINK-BIT #WORD *000001000000*>

<SETG OPEN-WRITE #WORD *000004000000*>

<SETG GC-MARK-BIT #WORD *000010000000*>

<SETG DELETE-ON-CLOSE #WORD *000020000000*>

<SETG DELETE-FROM-UNMOUNTED-PACK #WORD *000040000000*>

<SETG DELETED
      <ANDB ,DELETE-ON-CLOSE ,DELETE-FROM-UNMOUNTED-PACK>>

<SETG IGNORED <ANDB ,DELETED ,OPEN-WRITE>>

<SETG DUMP-BIT #WORD *400000000000*>

<SETG EXTRA-WORDS <BITS 10 24>>

<SETG CREATION-TIME <BITS 18>>

<SETG CREATION-DATE <BITS 16 18>>

<SETG REF-DATE <BITS 16 18>>

<SETG MONTH <BITS 4 23>>

<SETG DAY <BITS 5 18>>

<SETG YEAR <BITS 7 27>>

<SETG SIXBYTES
      (<BITS 6 30>
       <BITS 6 24>
       <BITS 6 18>
       <BITS 6 12>
       <BITS 6 6>
       <BITS 6 0>)>

<MANIFEST DESC-AREA
	  NAME-AREA
	  DESC-PTR
	  PACK-NUMBER
	  LINK-BIT
	  OPEN-WRITE
	  GC-MARK-BIT
	  DELETE-ON-CLOSE
	  DELETE-FROM-UNMOUNTED-PACK
	  DELETED
	  IGNORED
	  DUMP-BIT
	  EXTRA-WORDS
	  CREATION-TIME
	  CREATION-DATE
	  REF-DATE
	  MONTH
	  DAY
	  YEAR
	  SIXBYTES>

<DEFINE FIELD (W B)
	#DECL ((W) <PRIMTYPE WORD> (B) BITS (VALUE) FIX)
	<CHTYPE <GETBITS .W .B> FIX>>

<DEFINE CDATE (W)
	#DECL ((W) <PRIMTYPE WORD> (VALUE) <LIST [3 FIX]>)
	(<FIELD .W ,YEAR> <FIELD .W ,MONTH> <FIELD .W ,DAY>)>

<DEFINE CTIME (W "AUX" H M S)
	#DECL ((W) <PRIMTYPE WORD> (H M S) FIX (VALUE) <LIST [3 FIX]>)
	<SET W </ <FIELD .W ,CREATION-TIME> 2>>
	<SET H </ .W 3600>>
	<SET W <MOD .W 3600>>
	<SET M </ .W 60>>
	<SET S <MOD .W 60>>
	(.H .M .S)>

<DEFINE DISK-DATE ("OPTIONAL" (DT <DTNOW>) "AUX" (D <1 .DT>) (T <2 .DT>))
	#DECL ((VALUE) WORD (DT) LIST (D T) <LIST [3 FIX]>)
	<PUTBITS
	 <PUTBITS <PUTBITS <PUTBITS #WORD 0 ,YEAR <1 .D>> ,MONTH <2 .D>> ,DAY <3 .D>>
	 ,CREATION-TIME
	 <* 2 <+ <3 .T> <* 60 <+ <2 .T> <* 60 <1 .T>>>>>>>>

<DEFINE OTHER-CRUFT (N "OPTIONAL" (L <>) "AUX" (O ()))
	#DECL ((N) <PRIMTYPE WORD> (L) ANY (O) <LIST [REST CHARACTER]>)
	<COND (<BIT-ON? .N ,OPEN-WRITE> <SET O (!\W !.O)>)>
	<COND (<BIT-ON? .N ,DELETE-ON-CLOSE> <SET O (!\C !.O)>)>
	<COND (<AND <NOT .L> <NOT <BIT-ON? .N ,DUMP-BIT>>>
	       <SET O (!\X !.O)>)>
	.O>

<DEFINE BIT-ON? (W BIT)
	#DECL ((W BIT) <PRIMTYPE WORD> (VALUE) ANY)
	<NOT <0? <CHTYPE <ANDB .W .BIT> FIX>>>>

<DEFINE WORDS-LINK (W DIR)
	#DECL ((W) <PRIMTYPE WORD> (DIR) <UVECTOR [1024 FIX]>
	       (VALUE) <OR LIST FIX>)
	<COND (<BIT-ON? .W ,LINK-BIT>
	       <LINK-TO <FIELD .W ,DESC-PTR> .DIR>)
	      (ELSE
	       <COUNT-WORDS <FIELD .W ,DESC-PTR>
			    .DIR
			    <FIELD .W ,EXTRA-WORDS>>)>>

<DEFINE LINK-TO (DESC DIR "AUX" (N <+ 1 <MOD .DESC 6>>))
	#DECL ((DESC) FIX (DIR) <SPECIAL <UVECTOR [REST FIX]>> (N) <SPECIAL
								    FIX>
	       (VALUE) <LIST [3 STRING]>)
	<SET DIR <REST .DIR <+ ,DESC-AREA </ .DESC 6>>>>
	<ILIST 3 '<LINK-PART>>>

<DEFINE LINK-PART ("AUX" W CH (NN 0))
	#DECL ((W) <SPECIAL <PRIMTYPE WORD>> (CH) CHARACTER (NN N) FIX
	       (DIR) <UVECTOR [REST FIX]>)
	<SET W <1 .DIR>>
<MAPF ,STRING
 <FUNCTION ()
  <COND (<PROG ((Q T))
	       #DECL ((Q) <OR ATOM FALSE>)
	       <SET CH <SIX-TO-ASCII <FIELD .W <NTH ,SIXBYTES .N>>>>
	       <COND (<AND .Q <==? .CH !\:>> <DO-ILDB> <SET Q <>> <AGAIN>)
		     (<==? .NN 6> <RETURN <>>)
		     (<AND .Q <==? .CH !\;>> <DO-ILDB> <RETURN <>>)
		     (<==? .CH !\ > <RETURN <>>)
		     (ELSE <DO-ILDB> T)>>
	 <PROG () <SET NN <+ .NN 1>> .CH>)
	(T <MAPSTOP>)>>>>

<DEFINE DO-ILDB ()
	#DECL ((N) FIX (DIR) <UVECTOR [REST FIX]> (W) <PRIMTYPE WORD>)
	<COND (<G? <SET N <+ .N 1>> 6>
	       <SET N <MOD .N 6>>
	       <SET DIR <REST .DIR>>
	       <SET W <1 .DIR>>)>>

<DEFINE SIX-TO-ASCII (S)
	#DECL ((S) FIX (VALUE) CHARACTER)
	<CHTYPE <+ 32 .S> CHARACTER>>

<DEFINE GET-DIRECTORY ("OPTIONAL" (SNM <SNAME>) (BUF <IUVECTOR 1024 0>)
		       "AUX" (INCHAN
			      <OPEN "READB" ".FILE." "(DIR)" "DSK" .SNM>))
	#DECL ((SNM) STRING (BUF) <UVECTOR [1024 FIX]>
	       (INCHAN) <OR CHANNEL FALSE> (VALUE) <OR UVECTOR FALSE>)
	<COND (.INCHAN <READB .BUF .INCHAN> <CLOSE .INCHAN> .BUF)>>

<DEFINE PARSE-DIRECTORY (DIR "AUX" (L </ <- 1024 <2 .DIR>> 5>))
	#DECL ((DIR) <UVECTOR [REST FIX]> (L) FIX
	       (VALUE) <VECTOR [REST VECTOR]>)
	<SET DIR <REST .DIR <- <2 .DIR> 5>>>
	<IVECTOR .L
		 '<PROG (WORDS)
			#DECL ((WORDS) <OR FIX LIST>)
			<SET DIR <REST .DIR 5>>
			[<SIXTOS <1 .DIR>>
			 <SIXTOS <2 .DIR>>
			 <CDATE <4 .DIR>>
			 <CTIME <4 .DIR>>
			 <CDATE <5 .DIR>>
			 <SET WORDS <WORDS-LINK <3 .DIR> <TOP .DIR>>>
			 <FIELD <3 .DIR> ,PACK-NUMBER>
			 <OTHER-CRUFT <3 .DIR> <TYPE? .WORDS LIST>>]>>>

<FLOAD "MBPROG;COUNT">

<ENDPACKAGE>
