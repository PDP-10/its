
<PACKAGE "PDUMP">

<BLOCK (<ROOT>)>

<SET GLUE T>

PGLUE 

TMP 

IMPURE 

<ENDBLOCK>

<FLOAD "CLR;BYTER NBIN">

<MANIFEST R HW INDEX-FIELD>

<SETG R 14>

<SETG HW <BITS 18>>

<SETG INDEX-FIELD <BITS 4 18>>

<ENTRY PDUMP SAVDIR>

<SET TMPOB <MOBLIST TMP 7>>

<GDECL (SAVDIR) STRING (NDIRS) FIX>

<COND (<==? ,MUDDLE 51> <SETG NDIRS 2>)
      (<==? ,MUDDLE 52> <SETG NDIRS 3>)
      (ELSE
       <SETG NDIRS
	     <ERROR ERRET-NUMBER-OF-DIRS-FOR-THIS-VERSION!-ERRORS>>)>

<SETG SAVDIR
      <COND (<N==? ,MUDDLE 49> <STRING "MUD" <UNPARSE ,MUDDLE>>)
	    (49 "MHILIB")>>

<FLOAD "MUDDLE;PDRSUBR BIN">

<DEFINE TYPE-C-OUT (TC "AUX" (THIS <NTH <ALLTYPES> <+ 1 <CHTYPE .TC FIX>>>)) 
	<PRINTSTRING "%<">
	<PRIN1 <TYPE .TC>>
	<PRINTSTRING " ">
	<PRIN1 .THIS>
	<PRINTSTRING " ">
	<PRIN1 <TYPEPRIM .THIS>>
	<PRINC "> ">>  
 
<DEFINE TYPE-W-OUT (TW
		    "AUX" (RH <CHTYPE <GETBITS .TW <BITS 18>> FIX>)
			  (LH <GETBITS .TW <BITS 18 18>>)
			  (THIS <NTH <ALLTYPES> <+ 1 <CHTYPE .LH FIX>>>))
	<PRINTSTRING "%<">
	<PRIN1 <TYPE .TW>>
	<PRINTSTRING " ">
	<PRIN1 .THIS>
	<PRINTSTRING " ">
	<PRIN1 <TYPEPRIM .THIS>>
	<COND (<NOT <0? .RH>> <PRINTSTRING " "> <PRIN1 .RH>)>
	<PRINC "> ">>    

<DEFINE FIXUP-MUNG (GOODS OFFSET
		    "AUX" TMP TYP IT FOO (POS 0)
			  (FIXUPS <CHUTYPE .GOODS WORD>))
   #DECL ((FIXUPS) <<PRIMTYPE UVECTOR> [REST WORD]>
	  (OFFSET TMP FOO EXTRA-COUNT) FIX (GOODS) <PRIMTYPE UVECTOR>
	  (EXTRA-SLOT-POS) LIST (IT FOO) <PRIMTYPE WORD>)
   <SET GOODS <REST .GOODS>>
   <SET FIXUPS <REST .FIXUPS>>
   <REPEAT ()
     <COND (<EMPTY? .FIXUPS> <RETURN .GOODS>)>
     <COND (<G? <SET TMP <CHTYPE <1 .FIXUPS> FIX>> 0>
	    <SET FIXUPS <REST .FIXUPS <+ 1 .TMP>>>
	    <SET TYP <*HLRE <1 .FIXUPS>>>
	    <SET POS ,*HRRE>
	    <REPEAT ()
		    <SET IT <1 .FIXUPS>>
		    <SET FOO <APPLY .POS .IT>>
		    <COND (<0? .FOO>
			   <SET GOODS <SET FIXUPS <REST .FIXUPS>>>
			   <RETURN>)
			  (<L? .FOO 0>			      ;"TYPE-WORD FIXUP"
			   <SET FOO <- .FOO>>
			   <TYPE-WORD-SMASH .COD <- .FOO 1> .EXTRA-COUNT>
			   <PUTREST .EXTRA-SLOT-POS
				    <LIST <CHTYPE <NTH .COD .FOO> TYPE-W>>>)
			  (T				      ;"TYPE-CODE FIXUP"
			   <SET IT <NTH .COD .FOO>>
			   <SET IT
				<PUTBITS <NTH .COD .FOO>
					 <BITS 23>
					 <+ .EXTRA-COUNT 7864320>>>
			   <PUT .COD .FOO .IT>
			   <PUTREST .EXTRA-SLOT-POS
				    <LIST <CHTYPE .TYP TYPE-C>>>)>
		    <SET EXTRA-SLOT-POS <REST .EXTRA-SLOT-POS>>
		    <SET EXTRA-COUNT <+ .EXTRA-COUNT 2>>
		    <COND (<==? .POS ,*HLRE> <SET POS ,*HRRE>)
			  (T <SET FIXUPS <REST .FIXUPS>> <SET POS ,*HLRE>)>>)
	   (<SET FIXUPS <REST .FIXUPS>>
	    <SET POS 0>
	    <REPEAT ()
		    <SET IT <1 .FIXUPS>>
		    <COND (<0? <SET FOO
				    <CHTYPE <GETBITS .IT <BITS 18 .POS>> FIX>>>
			   <SET FIXUPS <REST .FIXUPS>>
			   <RETURN>)>
		    <SET IT <PUTBITS .IT <BITS 18 .POS> <+ .FOO .OFFSET>>>
		    <PUT .FIXUPS 1 .IT>
		    <COND (<0? .POS> <SET FIXUPS <REST .FIXUPS>> <SET POS 18>)
			  (<SET POS 0>)>>)>>>

<DEFINE RSUB-DMP (RSUB NAM BINCHN FIXCHN
		  "AUX" OFF FIXES (EXTRA-SLOTS (1))
			(EXTRA-SLOT-POS .EXTRA-SLOTS)
			(EXTRA-COUNT <+ <* <LENGTH .RSUB> 2> 1>)
			(COD <SUBSTRUC <1 .RSUB>>) GB RNAM)
   #DECL ((RSUB) RSUBR (NAM) STRING
	  (COD) <SPECIAL <<PRIMTYPE UVECTOR> [REST <PRIMTYPE WORD>]>>
	  (EXTRA-COUNT) <SPECIAL <PRIMTYPE WORD>> (OFF) <PRIMTYPE WORD>
	  (GB FIXES) <OR FALSE <UVECTOR [REST <PRIMTYPE WORD>]>>
	  (EXTRA-SLOTS) LIST (EXTRA-SLOT-POS) <SPECIAL LIST>)
   <SET OFF <17 .BINCHN>>
   <SET FIXES
	<SET FIXES
	     <GET .RSUB RSUBR '<ERROR "NO FIXUPS AVAILABLE">>>>
   <COND (<NOT <TYPE? .FIXES UVECTOR>>
	  <ERROR "RSUBRS NOT IN MAGIC FORMAT">)>
   <SET FIXES <FIXUP-MUNG <UVECTOR !.FIXES> .OFF>>
   <PRINTB .FIXES .FIXCHN>
   <PRINTB .COD .BINCHN>
   <PRINC " %<RSUBR!- '[ %<PCODE!- ">
   <PRIN1 .NAM>
   <PRINC " ">
   <PRINC .OFF>
   <PRINC ">">
   <REPEAT ((GOODS <LIST !<REST .RSUB> !<REST .EXTRA-SLOTS>>))
	   <COND (<EMPTY? .GOODS> <RETURN>)>
	   <PRINC " ">
	   <COND (<NOT <TYPE? <1 .GOODS> RSUBR RSUBR-ENTRY>>
		  <PRIN1 <1 .GOODS>>)
		 (<OR <GASSIGNED? <SET RNAM <2 <1 .GOODS>>>>
		      <TYPE? <1 .GOODS> RSUBR-ENTRY>>
		  <PRIN1 <2 <1 .GOODS>>>)
		 (T
		  <PUT <1 .GOODS>
		       2
		       <OR <LOOKUP <SET RNAM <PNAME .RNAM>> .TMPOB>
			   <INSERT .RNAM .TMPOB>>>
		  <RSUB-DMP <1 .GOODS> .NAM .BINCHN .FIXCHN>)>
	   <SET GOODS <REST .GOODS>>>
   <PRINC "]>">
   <COND (<SET GB <GET .RSUB GLUE>>
	  <PROG (XG (COD <1 .RSUB>) IL
		 (LN
		  <- <LENGTH .COD>
		     <CHTYPE <GETBITS <NTH .COD <SET IL <LENGTH .COD>>>
				      <BITS 18>>
			     FIX>
		     <CHTYPE <GETBITS <NTH .COD .IL> <BITS 18 18>> FIX>
		     1>)
		 (NG
		  <BYTER <SET XG <IUVECTOR <+ 1 </ <LENGTH .RSUB> 16>> 0>>>)
		 (OG <BYTER .GB>) WD (CT 0))
		#DECL ((LN CT) FIX (WD) <PRIMTYPE WORD> (OG NG) STRING
		       (XG) <UVECTOR [REST <PRIMTYPE WORD>]> (COD) CODE)
		<REPEAT ()
			<AND <G? <SET CT <+ .CT 1>> .LN> <RETURN>>
			<SET WD <NTH .COD .CT>>
			<AND <NOT <0? <CHTYPE <1 .OG> FIX>>>
			     <==? <CHTYPE <GETBITS .WD ,INDEX-FIELD> FIX> ,R>
			     <PUT .NG
				  <+ </ <CHTYPE <GETBITS .WD ,HW> FIX> 2> 1>
				  <1 .OG>>>
			<SET OG <REST .OG>>>
		<PUT .RSUB PGLUE .XG>>)>>

<DEFINE PDUMP ("TUPLE" NAMS
	       "AUX" RSUB NAM TMP DIR
		     (PNAM
		      <REPEAT ((INAM
				<COND (<TYPE? <SET TMP <1 .NAMS>> STRING>
				       <SET NAMS <REST .NAMS>>
				       .TMP)
				      (<PNAME <1 .NAMS>>)>)
			       (N 0)
			       (NM .INAM)
			       CHN)
			#DECL ((NM INAM) STRING (N) FIX)
			<SET DIR
			 <COND
			  (<==? ,MUDDLE 49> ,SAVDIR)
			  (ELSE
			   <STRING
			    <ASCII
			     <+ 48
				<MOD <- <ASCII <NTH .NM <MIN <LENGTH .NM> 6>>>
					32>
				     ,NDIRS>>>
			    ,SAVDIR>)>>
			<COND (<SET CHN <OPEN-NR "READ" .NM ">" "DSK" .DIR>>
			       <CLOSE .CHN>
			       <SET N <+ .N 1>>
			       <SET NM <STRING <UNPARSE .N> .INAM>>)
			      (T <RETURN .NM>)>>) BINCHN FIXCHN
		     (OBL <COND (<EMPTY? .OBLIST> FULL-OBL) (BLOCK)>)
		     (OOBLIST .OBLIST) GOODS THIS-FORM FIXUPS POS OBLIST)
   #DECL ((PNAM MUDNUM) STRING (POS) FIX (FIXUPS) <UVECTOR <PRIMTYPE WORD>>
	  (THIS-FORM) FORM (OBLIST) <SPECIAL ANY> (GOODS) LIST
	  (BINCHN FIXCHN) <OR FALSE CHANNEL>)
   <PROG ()
     <COND
      (<NOT <SET BINCHN
		 <OPEN "PRINTB"
		       .PNAM
		       <STRING "SAV" <UNPARSE ,MUDDLE>>
		       "DSK"
		       .DIR>>>
       <RETURN .BINCHN>)
      (<NOT <SET FIXCHN <OPEN "PRINTB" .PNAM "FIXUP" "ARC" .DIR>>>
       <CLOSE .BINCHN>
       <RETURN .FIXCHN>)
      (ELSE
       <PRINTB '![0 0!] .FIXCHN>
       <REPEAT (OUTCHAN)
	 #DECL ((OUTCHAN) <SPECIAL <OR FALSE CHANNEL>>)
	 <COND (<EMPTY? .NAMS> <RETURN>)>
	 <SET NAM <1 .NAMS>>
	 <PROG ()
	   <COND
	    (<SET OUTCHAN <OPEN "PRINT" "_PDMP_" ">" "DSK" <SNAME>>>)
	    (ELSE
	     <ERROR OUTPUT-OPEN-FAILED-ERRET-ANYTHING-TO-RETRY-ERRORS
		    PDUMP
		    .OUTCHAN>
	     <AGAIN>)>>
	 <PROG ()
	   <COND (<NOT <ASSIGNED? .NAM>>
		  <RETURN '#FALSE ("OBJECT NOT GROUP NAME")>)>
	   <SET GOODS ..NAM>
	   <SET OBLIST <GET .NAM .OBL '.OOBLIST>>
	   <PRINC "'<PCODE ">
	   <PRIN1 .PNAM>
	   <PRINC ">">
	   <CRLF>
	   <REPEAT (MCR)
		   <COND (<EMPTY? .GOODS> <RETURN>)>
		   <COND (<AND <TYPE? <1 .GOODS> FORM>
			       <SET THIS-FORM <1 .GOODS>>
			       <==? 3 <LENGTH .THIS-FORM>>
			       <OR <AND <SET MCR <TYPE? <3 .THIS-FORM> MACRO>>
					<TYPE? <SET RSUB <1 <3 .THIS-FORM>>>
					       RSUBR>>
				   <TYPE? <SET RSUB <3 .THIS-FORM>> RSUBR>>
			       <COND (<TYPE? <1 .RSUB> CODE>)
				     (ELSE
				      <PRINC "PURE-RSUBR-IN-GROUP " ,OUTCHAN>
				      <PRIN1 <2 .THIS-FORM> ,OUTCHAN>
				      <TERPRI ,OUTCHAN>)>
			       <NOT <GET .RSUB IMPURE>>>
			  <TERPRI>
			  <PRINC "<">
			  <PRIN1 <1 .THIS-FORM>>
			  <PRINC " ">
			  <PRIN1 <2 .THIS-FORM>>
			  <PRINC " ">
			  <COND (.MCR <PRINC "#MACRO (">)>
			  <RSUB-DMP .RSUB .PNAM .BINCHN .FIXCHN>
			  <PRINC <COND (.MCR ")> ") (ELSE "> ")>>
			  <AND <GET .RSUB PGLUE>
			       <PRINT <FORM AND
					    '<ASSIGNED? GLUE>
					    '.GLUE
					    <FORM PUT
						  <FORM GVAL <2 .RSUB>>
						  PGLUE
						  <GET .RSUB PGLUE>>>>>)
			 (T <PRINT <1 .GOODS>>)>
		   <TERPRI>
		   <SET OBLIST <GETPROP .GOODS .OBL '.OBLIST>>
		   <SET GOODS <REST .GOODS>>>>
	 <COND
	  (<RENAME .OUTCHAN <PNAME .NAM> "FBIN">)
	  (ELSE
	   <ERROR
	    RENAME-OF-TEMP-FAILED-ERRET-ANYTHING-TO-CONTINUE-ERRORS
	    PDUMP
	    .NAM>)>
	 <CLOSE .OUTCHAN>
	 <SET NAMS <REST .NAMS>>>
       <SET POS <17 .FIXCHN>>
       <ACCESS .FIXCHN 0>
       <PRINTB <UVECTOR .POS ,MUDDLE> .FIXCHN>
       <CLOSE .BINCHN>
       <CLOSE .FIXCHN>
       "DUMPED")>>>

<ENDPACKAGE>
