
<RPACKAGE "L">

<USE "SDML">

<ENTRY L-RETRIEVE L-LISTE L-LISTP L-PATH L-OBL L-LISTPE
       L-COUNTE L-COUNTP L-FILE L-WHERE>

<DEFINE L-RETRIEVE (PCKNM "OPTIONAL" (FILNM .PCKNM) (LIBNM "LIBMUD;LIBMUD"))
	#DECL ((PCKNM FILNM LIBNM) STRING)
	<L-DOITEM .LIBNM ,L-ITEMS-NAME <> ,SDMGETARC .PCKNM .FILNM>>

<DEFINE L-LISTE ("OPTIONAL" (LIBNM "LIBMUD;LIBMUD") (OUTCHAN .OUTCHAN))
	#DECL ((LIBNM) STRING (OUTCHAN) <SPECIAL CHANNEL>)
	<AND <L-DOITEM .LIBNM ,L-INDEX-NAME <> ,SDMLST> ,NULL>>

<DEFINE L-COUNTE ("OPTIONAL" (LIBNM "LIBMUD;LIBMUD"))
	#DECL ((LIBNM) STRING)
	<L-DOITEM .LIBNM ,L-INDEX-NAME <> ,SDMGDL>>

<DEFINE L-LISTP ("OPTIONAL" (LIBNM "LIBMUD;LIBMUD") (OUTCHAN .OUTCHAN))
	#DECL ((LIBNM) STRING (OUTCHAN) <SPECIAL CHANNEL>)
	<AND <L-DOITEM .LIBNM ,L-ITEMS-NAME <> ,SDMLST> ,NULL>>

<DEFINE L-COUNTP ("OPTIONAL" (LIBNM "LIBMUD;LIBMUD"))
	#DECL ((LIBNM) STRING)
	<L-DOITEM .LIBNM ,L-ITEMS-NAME <> ,SDMGDL>>

<DEFINE L-PATH ()
	<MAPF <>
	      <FUNCTION (X) #DECL ((X) OBLIST) <PRINT <GET .X OBLIST>>>
	      .OBLIST>
	,NULL>

<DEFINE L-OBL (ATM)
	#DECL ((ATM) ATOM)
	<GET <OBLIST? .ATM> OBLIST>>

<DEFINE L-LISTPE (PCKNM
		  "OPTIONAL" (LIBNM "LIBMUD;LIBMUD") (OUTCHAN .OUTCHAN)
		  "AUX" TMP)
	#DECL ((PCKNM LIBNM) STRING (OUTCHAN) <SPECIAL CHANNEL>)
	<AND <SET TMP
		  <L-DOITEM .LIBNM
			    ,L-INDEX-NAME
			    <>
			    ,SDMGET
			    <STRING <ASCII 127> .PCKNM>>>
	     <REPEAT ()
		     <COND (<EMPTY? .TMP> <RETURN ,NULL>)>
		     <TERPRI>
		     <PRINTSTRING <1 .TMP>
				  .OUTCHAN
				  <- <LENGTH <1 .TMP>>
				     <LENGTH <MEMQ !\! <1 .TMP>>>>>
		     <SET TMP <REST .TMP>>>>>

<DEFINE L-FILE (NAM "OPTIONAL" (LIBNM "LIBMUD;LIBMUD"))
	#DECL ((LIBNM NAM) STRING (OUTCHAN) <SPECIAL CHANNEL>)
	<OR <L-DOITEM .LIBNM ,L-ITEMS-NAME <> ,SDMGET .NAM ,SDBIGN>
	    <AND <L-DOITEM .LIBNM ,L-ITEMS-NAME <> ,SDMDSRC .NAM>
		 #FALSE ("PACKAGE IS ARC, NOT VARC")>>>

<DEFINE L-WHERE (NAM "OPTIONAL" (L-LIBRARIES .L-LIBRARIES)
		 "AUX" VAL CHN)
	#DECL ((VALUE) <OR <VECTOR [REST STRING]> FALSE> (CHN) <OR CHANNEL FALSE>
	       (L-LIBRARIES) <LIST [REST STRING]>
	       (NAM) STRING (VAL) ANY)
	<SET VAL
	     <REPEAT ((L .L-LIBRARIES))
		#DECL ((L) <LIST [REST STRING]>)
		<COND (<EMPTY? .L> <RETURN <>>)
		      (<SET VAL <L-DOITEM <1 .L> ,L-ITEMS-NAME <> ,SDMGET .NAM ,SDBIGN>>
		       <RETURN .VAL>)
		      (<L-DOITEM <1 .L> ,L-ITEMS-NAME <> ,SDMDSRC .NAM>
		       <RETURN <CHTYPE ("PACKAGE/DATUM ARCHIVED IN LIBRARY.") FALSE>>)>
		<SET L <REST .L>>>>
	<COND (<AND .VAL <SET CHN <OPEN "READ" .VAL>>>
	       <CLOSE .CHN>
	       [<7 .CHN> <8 .CHN> <9 .CHN> <10 .CHN>])>>

<ENDPACKAGE>
