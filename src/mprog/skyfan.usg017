<PACKAGE "SKY-FANS">
<ENTRY SKY-FANS-MSG M.I.T. C.I.T. STANFORD D.C. C.M.U.>
<USE "CELEST" "DATIME" "TIMFCN">
<USE-DEFER "LSRHIL" "MFD">

<SETG M.I.T.	'((-  71  5 20) (+ 42 21 50) 5 !\E)>	;"10 sec = 1000 ft"
<SETG C.I.T.	'((- 118  7 30) (+ 34  8  0) 8 !\P)>
<SETG STANFORD	'((- 122 10  0) (+ 37 25 50) 8 !\P)>
<SETG D.C.	'((-  77  1  0) (+ 38 54  0) 5 !\E)>
<SETG C.M.U.	'((-  79 56 20) (+ 40 26 30) 5 !\E)>

<DEFINE SKY-FANS-MSG ("OPTIONAL" (TO "SKY-FANS")(SUBJ "upcoming events in sky")
				 (LONG <1 ,M.I.T.>) (LAT <2 ,M.I.T.>)
				 (TZ <3 ,M.I.T.>) (TZL <4 ,M.I.T.>)
				 (BIRTHDAYS? <>) (DTARG <DTNOW>)
				 "AUX" DT)
   #DECL ((TO SUBJ) STRING (LONG LAT) LIST (TZ) FIX (TZL) CHARACTER
	  (BIRTHDAYS?) <OR FALSE ANY> (DTARG) <LIST LIST>
	  (DT) <LIST <LIST FIX FIX FIX>>)
   <SETG LONGITUDE!-ICELEST .LONG> <SETG LATITUDE!-ICELEST .LAT>
   <SETG GMT-OFFSET!-ICELEST <+ .TZ <DAY-SAVE-CROCK!-ICELEST <1 <DTNOW>>>>>
   <SETG ZONSTR <PUT <GETTIMEZONE> 1 .TZL>>
   <SET DT (<1 <DTNORM .DTARG>> ())>				;"zero o'clock"
   <SET DT <DTADD .DT ((<- 5 <DATE-DOW <1 .DT>>>) ())>>		      ;"Friday"
   <REPEAT ((NDAYS 7) H BL
	    (NP <NEXT-PHASE <DTADD .DT '((1) ())>>)	  ;"next phase of moon"
	    (NP2 <2 .NP>)				 ;"day & time of ditto"
	    (NP2 <COND (<AND <DST?> <NOT <DST? .NP2>>> <DTADD .NP2 '(() (-1))>)
		       (<AND <NOT <DST?>> <DST? .NP2>> <DTADD .NP2 '(() (+1))>)
		       (T .NP2)>)	 ;"adjusted for standard/daylight time"
	    (NPD <1 .NP2>)					  ;"day number"
	    (NS <NEXT-SEASON <DTADD .DT '((1) ())>>)	;"next seasonal change"
	    (NS2 <2 .NS>)						;"etc."
	    (NS2 <COND (<AND <DST?> <NOT <DST? .NS2>>> <DTADD .NS2 '(() (-1))>)
		       (<AND <NOT <DST?>> <DST? .NS2>> <DTADD .NS2 '(() (+1))>)
		       (T .NS2)>)
	    (NSD <1 .NS2>)
	    (MSG (			;"skeleton of message as list of lines"
"Key:	<> bracket sunrise/set
	() bracket moonrise/set
" "
    0  1  2  3  4  5  6  7  8  9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24  
" !<ILIST 7 '<STRING
"    |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  
">>))	
	   (MMSG <REST .MSG>) ;"cd..dr" MMSG1 ;"cad..dr"
	   (KEYMSG .MSG)	;"place to add items to 'Key:'"
	   (SRMSG <REST			       ;"digital form of sunrise times"
"    Sunrises:                                                                
" 5>)
	   (SSMSG <REST
"    Sunsets:                                                                 
" 5>)
	   C OUTCHAN)
     #DECL ((MMSG1 SRMSG SSMSG) STRING (H) <OR FALSE STRING>
	    (NP NS) <VECTOR FIX <LIST LIST <LIST FIX FIX>>>
	    (NP2 NS2) <LIST LIST <LIST FIX FIX>>
	    (NDAYS) FIX
	    (MSG MMSG KEYMSG) <LIST [REST STRING]>
	    (BL NPD NSD) LIST
	    (C) <OR FALSE CHANNEL> (OUTCHAN) <SPECIAL CHANNEL>)
      <COND
       (<G=? <SET NDAYS <- .NDAYS 1>> 0>
	<SET MMSG1 <1 <SET MMSG <REST .MMSG>>>>
	<SET SRMSG <REST .SRMSG 9>>
	<SET SSMSG <REST .SSMSG 9>>
	<SET DT <DTADD .DT '((1) ())>>
	<SUBSTRUC <UNPARSE <+ 100 <3 <1 .DT>>>> 1 2 .MMSG1>	;"day of month"
	<COND (<SET H <OR <HOLIDAY? <1 .DT>>
			  <NON-MIT-HOLIDAY? <1 .DT>>>>
	       <SUBSTRUC .H 0 <LENGTH .H> <REST .MMSG1 4>>)>
	<MUNG .MMSG1 "<" <SUNRISE .DT 3E-4> .SRMSG>;"sunrise to nearest second"
	<MUNG .MMSG1 ">" <SUNSET .DT 3E-4>  .SSMSG> ;"sunset to nearest second"
	<MUNG .MMSG1 "(" <MOONRISE .DT>>		;"moonrise"
	<MUNG .MMSG1 ")" <MOONSET  .DT>>		;"moonset"
	<COND (<=? <1 .DT> .NPD>		     ;"change of phase of moon"
	       <SUBSTRUC <NTH '["*0Q" "*1Q" "*2Q" "*3Q"] <+ 1 <1 .NP>>>
			 0 3 <REST .MMSG1
				   <FIX <+ <* 3 <1 <2 .NP2>>>
					   </ <2 <2 .NP2>> 20.0>
					   4.5>>>>
	       <SET KEYMSG <REST <PUTREST .KEYMSG
				    (<NTH '["	*0Q = new moon
"					    "	*1Q = moon at first quarter
"					    "	*2Q = full moon
"					    "	*3Q = moon at third quarter
"]					  <+ 1 <1 .NP>>>
				     !<REST .KEYMSG>)>>>)>
	<COND (<=? <1 .DT> .NSD>			;"change of season"
	       <SUBSTRUC <NTH '["*VE" "*SS" "*AE" "*WS"] <+ 1 <1 .NS>>>
			 0 3 <REST .MMSG1
				   <FIX <+ <* 3 <1 <2 .NS2>>>
					   </ <2 <2 .NS2>> 20.0>
					   4.5>>>>
	       <SET KEYMSG <REST <PUTREST .KEYMSG
				    (<NTH '["	*VE = vernal equinox
"					    "	*SS = summer solstice
"					    "	*AE = autumnal equinox
"					    "	*WS = winter solstice
"]					  <+ 1 <1 .NS>>>
				     !<REST .KEYMSG>)>>>)>
	<COND (<AND .BIRTHDAYS?
		    <NOT <EMPTY? <SET BL <BD? <GET-MFD "DM"> .DT>>>>>
	       <SET H <COND (<LENGTH? .BL 1> <1 .BL>)
			    (<LENGTH? .BL 2> <STRING <1 .BL> " and " <2 .BL>>)
			    (T <STRING <1 .BL>
				       <MAPF ,STRING
					     <FUNCTION (S) #DECL ((S) STRING)
						       <MAPRET ", " .S>>
					     <REST .BL>>>)>>
	       <COND (<TYPE? .H STRING> <SUBSTRUC .H 0 <LENGTH .H> .MMSG1>)>)>)
       (T	;"Now send message."
	<COND (<SET C <OPEN "PRINT" "COMSYS;_SKY >">>
	       <SET OUTCHAN .C>
	       <MAPF <>
		     ,PRINT
		     ("TO"		(.TO)
		      "FROM"		<XUNAME>
		      "ACTION-TO"	(.TO)
		      "SUBJECT"		.SUBJ
		      "TEXT"	       <STRING !.MSG <TOP .SRMSG> <TOP .SSMSG>>
		      "SCHEDULE"	'("SENDING" #FALSE ()))>
	       <RENAME .C "M >">
	       <CLOSE .C>)>
	<RETURN>)>>>

<DEFINE MUNG (LINE S DT "OPTIONAL" (M #FALSE ()) "AUX" DTH DTM DTS)
	#DECL ((LINE S) STRING (DT) <OR FALSE <LIST <LIST FIX FIX FIX> LIST>>
	       (M) <OR FALSE STRING> (DTH DTM DTS) FIX)
 <COND (.DT
	<SET DT <COND (<AND <DST?> <NOT <DST? .DT>>> <DTADD .DT '(() (-1))>)
		      (<AND <NOT <DST?>> <DST? .DT>> <DTADD .DT '(() (+1))>)
		      (T .DT)>>
	<SET DTH <1 <2 .DT>>>
	<SET DTM <2 <2 .DT>>>
	<SET DTS <3 <2 .DT>>>
	<SUBSTRUC .S 0 <LENGTH .S>
		  <REST .LINE <FIX <+ <* 3 .DTH> </ .DTM 20.0> 4.5>>>>
	<COND (.M
	       <SUBSTRUC <UNPARSE <+ 100 .DTH>> 1 2 .M>
	       <PUT .M 3 !\:>
	       <SUBSTRUC <UNPARSE <+ 100 .DTM>> 1 2 <REST .M 3>>
	       <PUT .M 6 !\:>
	       <SUBSTRUC <UNPARSE <+ 100 .DTS>> 1 2 <REST .M 6>>)>)>>

<ENDPACKAGE>
