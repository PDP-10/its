
<PACKAGE "SCR">

<ENTRY TTY
       STREAM
       TERM-MOVE?
       SCR-INIT
       GET-TTY
       TTY-SETUP
       TTY-SCR
       SET-ECHO-MODE
       SET-IMAGE-MODE
       TTY-RESET
       CLEAR-SCREEN
       CLEAR-EOL
       CLEAR-EOS
       FRESH-LINE
       KILL-CHAR
       ERASE-CHAR
       HOME-CURSOR
       BOTTOM-CURSOR
       HOR-POS-CURSOR
       VER-POS-CURSOR
       MOVE-CURSOR
       BACK-CURSOR
       DOWN-CURSOR
       UP-CURSOR
       FORWARD-CURSOR
       SAVE-CURSOR
       RESTORE-CURSOR
       DO-OUTPUT
       DO-INPUT
       DO-BIN
       TCHAN
       TICHAN
       TWIDTH
       THEIGHT
       TCURSX
       TCURSY
       TSCRIPT>

<NEWSTRUC TTY VECTOR
	  TCHAN CHANNEL					      ;"Channel to tty"
	  TICHAN CHANNEL				       ;"Input channel"
	  TSVMOD <UVECTOR [3 WORD]>				 ;"Saved RFMOD"
	  TCMOD <UVECTOR [3 WORD]>		    ;"Current setting of RFMOD"
	  TSCRIPT <OR FALSE <LIST [REST <OR UVECTOR STRING>]>>>

<FLOAD "MPROG;VTSDEF >">
<FLOAD "MBPROG;SCRCRF NBIN">
<FLOAD "MPROG;SCRMAC >">

<DEFINE TERM-MOVE? (TTY)
  <NOT <0? <CHTYPE <ANDB <5 <CNSGET ,CNSUV <TICHAN .TTY>>> ,TOERS> FIX>>>>

<DEFINE NORMAL-OUT (TTY CHRS "OPTIONAL" (LENGTH -1) (TERMB <ASCII 0>))
  #DECL ((TTY) TTY (CHRS) <OR STRING CHARACTER>
	 (LENGTH) FIX (TERMB) CHARACTER)
  <COND (<TSCRIPT .TTY>
	 <ADDSCRIPT .TTY .CHRS>)>
  <COND (<TYPE? .CHRS CHARACTER>
	 <IOT 0 <TCHAN .TTY> .CHRS>)
	(T
	 <SIOT .CHRS 0 <TCHAN .TTY> .LENGTH>)>>

<DEFINE IMAGE-OUT (TTY CHRS
		   "OPTIONAL" (LENGTH -1) (TERMB <ASCII 0>)
		   "AUX" NEWCHRS)
	#DECL ((LENGTH) FIX (TERMB) CHARACTER (TTY) TTY
	       (CHRS) <OR FIX CHARACTER STRING>
	       (NEWCHRS) <OR CHARACTER STRING>)
	<COND
	 (<TSCRIPT .TTY>
	  <ADDSCRIPT .TTY .CHRS>)>
	<COND (<TYPE? .CHRS STRING>
	       <SIOT .CHRS ,TJDIS <TCHAN .TTY> .LENGTH>)
	      (<IOT ,TJDIS <TCHAN .TTY> .CHRS>)>>

<DEFINE ADDSCRIPT (TTY CHRS "AUX" TL)
  #DECL ((TTY) TTY (CHRS) <OR CHARACTER STRING>)
  <COND (<EMPTY? <SET TL <TSCRIPT .TTY>>>
	 <TSCRIPT .TTY (.CHRS)>)
	(<PUTREST <REST .TL <- <LENGTH .TL> 1>> (.CHRS)>)>>


<DEFINE SCR-INIT ("OPTIONAL" (FORCE? <>))
  #DECL ((FORCE?) <OR ATOM FALSE>)
  "DONE">

<DEFINE GET-TTY (CHN ICHN "OPTIONAL" (PROPS 0)
		 "AUX" (TUV <IUVECTOR 3 #WORD 0>))
  #DECL ((ICHN CHN) CHANNEL (TWORD) <UVECTOR [REST WORD]>
	 (PROPS) <PRIMTYPE WORD>)
  <SCR-INIT <>>
  <CHTYPE [.CHN .ICHN <TTY-GET .TUV .ICHN> <UVECTOR !.TUV>
	   <>] TTY>>

;"CHARACTER GROUP MODES FOR USE IN GETSTRACT"

<SETG TTY1 #WORD *020202020202*>

;"DON'T ECHO CTL CHARS"

<SETG TTY2 #WORD *030202020202*>

<DEFINE TTY-SETUP (TTY "AUX" (TUV <TCMOD .TTY>))
  #DECL ((TTY) TTY)
  <PUT .TUV 1 ,TTY1>
  <PUT .TUV 2 ,TTY2>
  <TTY-SET .TUV <TCHAN .TTY>>>

<DEFINE SET-ECHO-MODE (TTY VAL)
  #DECL ((TTY) TTY (VAL) <OR ATOM FALSE>)
  .TTY>

<DEFINE SET-IMAGE-MODE (TTY VAL)
  #DECL ((TTY) TTY (VAL) <OR ATOM FALSE>)
  .TTY>

<DEFINE TTY-SCR (TTY)
  #DECL ((TTY) TTY)
  <TTY-SETUP .TTY>>

<DEFINE TTY-RESET (TTY)
  #DECL ((TTY) TTY)
  <COND (<N=? <TCMOD .TTY> <TSVMOD .TTY>>
	 <TTY-SET <TSVMOD .TTY> <TICHAN .TTY>>
	 <MAPR <>
	   <FUNCTION (X Y)
	     <PUT .X 1 <1 .Y>>>
	   <TCMOD .TTY> <TSVMOD .TTY>>)>
  .TTY>


"SUBTITLE User output routines"

<SETG TEMPSTRING " ">

<GDECL (TEMPSTRING) STRING>

<DEFINE DO-OUTPUT (TTY STR
		   "OPTIONAL" (ECHO? <>)
			      (LEN
			       <COND (<TYPE? .STR CHARACTER> 1)
				     (<LENGTH .STR>)>))
	#DECL ((TTY) TTY (STR) <OR CHARACTER STRING> (ECHO?) <OR ATOM FALSE>
	       (LEN) FIX)
	<COND (<TYPE? .STR CHARACTER>
	       <PUT ,TEMPSTRING 1 .STR>
	       <SET STR ,TEMPSTRING>)>
	<NORMAL-OUT .TTY .STR .LEN>>

<DEFINE DO-INPUT (TTY "OPTIONAL" (ECHO? <>) "AUX" CHR)
  #DECL ((TTY) TTY (ECHO?) <OR ATOM FALSE STRING> (CHR) CHARACTER)
  <SET CHR <IOT 0 <TICHAN .TTY>>>
  <COND (<AND .ECHO?
	      <OR <TYPE? .ECHO? ATOM>
		  <NOT <MEMQ .CHR .ECHO?>>>>
	 <DO-OUTPUT .TTY .CHR T>)>
  .CHR>

<SETG JUNKSTR "   ">
<DEFINE CTRL-P (TTY LET "OPTIONAL" (ARG1 <>) (ARG2 <>) "AUX" (JUNK ,JUNKSTR)
		(CT 2))
  #DECL ((TTY) TTY (LET) CHARACTER (ARG1 ARG2) <OR FALSE FIX> (JUNK) STRING
	 (CT) FIX)
  <PUT .JUNK 2 .LET>
  <COND (.ARG1
	 <PUT .JUNK 3 <ASCII <+ .ARG1 8>>>
	 <COND (.ARG2
		<PUT .JUNK 4 <ASCII <+ .ARG2 8>>>
		<SET CT 4>)
	       (<SET CT 3>)>)>
  <IMAGE-OUT .TTY .JUNK .CT>>

<DEFINE DPYOP (TTY OPR "OPTIONAL" (ARG1 <>) (ARG2 <>) "AUX" OPSP)
  #DECL ((TTY) TTY (OPR) FIX (ARG1 ARG2) <OR FIX FALSE>
	 (OPSP) <OR APPLICABLE STRING FALSE>)
  <CASE ,==? .OPR
    (,VTCLR
     <CTRL-P .TTY !\C>)
    (,VTCEL
     <CTRL-P .TTY !\L>)
    (,VTCEW
     <CTRL-P .TTY !\E>)
    (,VTADV
     <COND (<NOT .ARG1> <SET ARG1 1>)>
     <REPEAT ()
       <COND (<L? <SET ARG1 <- .ARG1 1>> 0> <RETURN>)>
       <CTRL-P .TTY !\A>>)
    (,VTERA
     <CTRL-P .TTY !\K>)
    (,VTBEC
     <COND (<NOT .ARG1> <SET ARG1 1>)>
     <REPEAT ()
       <COND (<L? <SET ARG1 <- .ARG1 1>> 0> <RETURN>)>
       <CTRL-P .TTY !\X>>)
    (,VTHOM
     <CTRL-P .TTY !\P>)
    (,VTHMD
     <CTRL-P .TTY !\Z>)
    (,VTHRZ
     <CTRL-P .TTY !\H .ARG1>)
    (,VTVRT
     <CTRL-P .TTY !\V .ARG1>)
    (,VTMOV
     <CTRL-P .TTY !\V .ARG2>
     <CTRL-P .TTY !\H .ARG1>)
    (,VTBCK
     <COND (<NOT .ARG1> <SET ARG1 1>)>
     <REPEAT ()
       <COND (<L? <SET ARG1 <- .ARG1 1>> 0> <RETURN>)>
       <CTRL-P .TTY !\B>>)
    (,VTDWN
     <COND (<NOT .ARG1><SET ARG1 1>)>
     <REPEAT ()
       <COND (<L? <SET ARG1 <- .ARG1 1>> 0> <RETURN>)>
       <CTRL-P .TTY !\D>>)
    (,VTUP
     <COND (<NOT .ARG1> <SET ARG1 1>)>
     <REPEAT ()
       <COND (<L? <SET ARG1 <- .ARG1 1>> 0> <RETURN>)>
       <CTRL-P .TTY !\U>>)
    (,VTFWD
     <COND (<NOT .ARG1> <SET ARG1 1>)>
     <REPEAT ()
       <COND (<L? <SET ARG1 <- .ARG1 1>> 0> <RETURN>)>
       <CTRL-P .TTY !\F>>)
    (,VTSAV
     <CTRL-P .TTY !\S>)
    (,VTRES
     <CTRL-P .TTY !\R>)>>


<SETG CNSUV <IUVECTOR 5 0>>
<GDECL (CNSUV) <UVECTOR [REST FIX]>>
<DEFINE TWIDTH (TTY "AUX" (CUV ,CNSUV))
  #DECL ((TTY) TTY (CUV) <UVECTOR [REST FIX]>)
  <CNSGET .CUV <TICHAN .TTY>>
  <2 .CUV>>

<DEFINE THEIGHT (TTY "AUX" (CUV ,CNSUV))
  #DECL ((TTY) TTY (CUV) <UVECTOR [REST FIX]>)
  <CNSGET .CUV <TICHAN .TTY>>
  <1 .CUV>>

<DEFINE TCURSX (TTY)
  #DECL ((TTY) TTY)
  <CHTYPE <GETBITS <RCPOS <TCHAN .TTY>> <BITS 18 0>> FIX>>

<DEFINE TCURSY (TTY)
  #DECL ((TTY) TTY)
  <CHTYPE <GETBITS <RCPOS <TCHAN .TTY>> <BITS 18 18>> FIX>>

<ENDPACKAGE>
