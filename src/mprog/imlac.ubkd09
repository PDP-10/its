;"********************************************************************************
  RSUBR to send one character to the IMLAC in image-mode
  ********************************************************************************"

<TITLE IMLAC-OUT>
	<DECLARE (CHARACTER)>

<PSEUDO
<DEFINE OUTPUT (WHERE) <CHTYPE
	('<PUSH	TP* $TFIX>
	<FORM PUSH TP* .WHERE>
	'<MOVE	B* <MQUOTE OUTCHAN>>
	'<PUSHJ	P* IGVAL>
	'<CAME	A* $TCHANNEL>
	'<JRST WRONGT>
	'<PUSH	TP* A>
	'<PUSH	TP* B>
	'<MCALL	2 IMAGE>) SPLICE>>>

	<MOVE	B* (AB) 1>		;"Get character from input"
	<CAILE	B* *37*>		;"Control character ?"
	<JRST	SNDTTY>			;"No, just go output it"
	<CAIE	B* *15*>		;"Allow cr"
	<CAIN	B* *12*>		;" and lf"
	<JRST	SNDTTY>
	<CAIE	B* *11*>		;"Allow tab"
	<CAIN	B* *10*>		;" and backspace"
	<JRST	SNDTTY>
	<CAIN	B* *33*>		;"Allow altmode"
	<JRST	SNDTTY>

CTLSND	<OUTPUT	[!"]>		;"Precede a control character by control B"
	<MOVE	B* (AB) 1>		;"Regobble control character"
SNDTTY	<OUTPUT	B>			;"Send to SCREEN"
	<JRST	FINIS>
;"********************************************************************************
  RSUBR to receive one character from the IMLAC
  ********************************************************************************"

<TITLE IMLAC-IN>
	<DECLARE ("VALUE" CHARACTER)>

<PSEUDO <DEFINE GET-CHAR ()
		'<*IOT B* B>>>

	<PUSHJ	P* INPUT>	;"Go input"
	<MOVSI	A* TCHARACTER>
	<JRST	FINIS>

INPUT	<SKIPN	<IQUOTE 0 XMIT?	;"XMIT flag for imlac"> 1>
			;"In process of XMITing ?"
	<JRST	XCL-ALT>		;"NO, don't gobble new char"
	<GET-CHAR>
	<CAIL	B* *40*>		;"Control character ?"
	<POPJ	P*>			;"NO, so it can't be special, return"
	<JUMPL	B* [<MCALL 0 ERROR>]>
	<CAIN	B* !">		;"Character a control B ?"
	<JRST	INPUT1>			;"YES, quote next char"
	<CAIE	B* *15*>		;"Either CR character,"
	<CAIN	B* *34*>		;" or long-line kludge character"
	<JRST	ACKN>			;"Acknowledge its reception to Imlac"
	<CAIE	B* !">		;"End of page xmit ?"
	<POPJ	P*>			;"NO, therefore char wasn't special, return"
	<SETZM	XMIT? 1>		;"Return to non-XMIT state"
XCL-ALT	<MOVEI	B* *400033*>		;"Equivalent to !$"
	<POPJ	P*>

ACKN	<PUSH	P* B>			;"Save the character in question"
	<PUSH	TP* $TFIX>
	<PUSH	TP* [ 6 ]>		;"Cntrl F character"
	<PUSH	TP* $TATOM>
	<PUSH	TP* <MQUOTE OUTCHAN>>
	<MCALL	1 GVAL>
	<CAME	A* $TCHANNEL>
	<JRST	[<PUSH TP* $TATOM>
		 <PUSH TP* <MQUOTE BAD-ECHO-CHANNEL!-ERRORS>>
		 <JRST CALER1>]>
	<PUSH	TP* A>
	<PUSH	TP* B>
	<MCALL	2 IMAGE>		;"Signal imlac to continue"
	<POP	P* B>			;"Regobble character in question"
	<CAIN	B* *15*>		;"CR character ?"
	<POPJ	P*>			;"YES, return"
	<JRST	INPUT>			;"NO, ignore and try again"

INPUT1	<GET-CHAR>
	<POPJ	P*>


<SUB-ENTRY	IMLAC-COMMAND ("VALUE" <OR FALSE CHARACTER>)>
	<SKIPE	XMIT? 1>		;"No commands when in XMIT"
	<JRST	IC-FALSE>
	<GET-CHAR>
	<MOVSI	A* TCHARACTER>
	<CAIE	B* *34*>		;"Start of XMIT ?"
	<JRST	FINIS>
	<SETOM	XMIT? 1>
IC-FALSE
	<MOVSI	A* TFALSE>
	<SETZ	B*>
	<JRST	FINIS>

<SUB-ENTRY IMLAC-RESET>
IR-LOOP	<SKIPN	XMIT? 1>
	<JRST	IR-RETURN>
	<PUSHJ	P* INPUT>
	<JRST	IR-LOOP>

IR-RETURN
	<DMOVE	A* <PQUOTE T>>
	<JRST FINIS>
