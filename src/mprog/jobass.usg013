<PACKAGE "JOBASS">
<ENTRY ATTY DTTY FILE-LOAD JOB-CREATE JOB-GET-NUM UCLOSE>
<EXTERNAL "ITS" "STR">

<TITLE ATTY>
<DECLARE ("VALUE" <OR CHANNEL <FALSE [REST]>> CHANNEL)>

	<MOVE C* 1(AB)>		; "CHAN"
	<HRLZ C* 1(C)>
	<LSH C* 5>
	<IOR C* [<*ATTY>]>
	<MOVE A* $TFALSE>
	<MOVE B* <MQUOTE ()>>
	<XCT C>
	<JRST FINIS>
	<MOVE A* 0(AB)>
	<MOVE B* 1(AB)>
	<JRST FINIS>

<TITLE DTTY>
<DECLARE ("VALUE" <OR CHANNEL <FALSE [REST]>> CHANNEL)>

	<MOVE C* 1(AB)>		; "CHAN"
	<HRLZ C* 1(C)>
	<LSH C* 5>
	<IOR C* [<*DTTY>]>
	<MOVE A* $TFALSE>
	<MOVE B* <MQUOTE ()>>
	<XCT C>
	<JRST FINIS>
	<MOVE A* 0(AB)>
	<MOVE B* 1(AB)>
	<JRST FINIS>

<TITLE FILE-LOAD>
<DECLARE ("VALUE" <OR WORD <FALSE STRING>> CHANNEL CHANNEL)>

	<MOVE A* 1(AB)>		; "CPTR TO IN CHAN"
	<MOVE A* 1(A)>		; "ITS CHAN IN"
	<MOVE B* 3(AB)>		; "CPTR TO OUT CHAN"
	<MOVE B* 1(B)>		; "ITS CHAN OUT"
	<*CALL PLDBLK>
	<JRST BADPLD>
	<PUSHJ P* READ1>
	<JRST EOFP>
	<MOVE A* $TWORD>
	<JRST FINIS>

EOFP	<MOVE A* $TFALSE>
	<MOVE B* <MQUOTE ("FILE-LOAD: Premature end of file.")>>
	<JRST FINIS>

READ1	<MOVE A* 1(AB)>
	<HRLZ A* 1(A)>
	<LSH A* 5>
	<IOR A* [<*IOT 0>]>
	<HRROI 0 B>
	<XCT A>
	<SKIPL 0>
	<AOS (P)>
	<POPJ P*>

BADPLD	<MOVE A* $TFALSE>
	<MOVE B* <MQUOTE ("FILE-LOAD: .CALL LOAD failed.")>>
	<JRST FINIS>

PLDBLK	<SETZ>
	<#OPCODE *545741440000*>	; "SIXBIT /LOAD/"
	<B>
	<SETZ A>

<TITLE JOB-CREATE>		;" S.W.GALLEY (SG) 2 MAY 74"
<DECLARE ("VALUE" <OR CHANNEL <FALSE STRING>> STRING)>

; "Find an unused ITS channel."
	<MOVE C* [<*STATU A* B>]>	; "Test chan statuses, starting with 1."
CHNLP	<XCT C>
	<ANDI B* *77*>		; "Isolate device code."
	<JUMPE B* CHNFND>	; "Jump if this chan not in use."
	<ADD C* [<(*40*)>]>	; "Move on to next chan,"
	<TLNE C* *740*>
	<JRST CHNLP>		;	"if any."
	<MOVE B* <MQUOTE ("NO CHANNELS AVAILABLE.")>>
	<MOVSI A* TFALSE>	; "No chans unused: quit."
	<JRST FINIS>

; "We found a chan."
CHNFND	<PUSH TP* 0(AB)>	; "Remember input string."
	<PUSH TP* 1(AB)>
	<LDB D* [<(*270400*) C>]>	; "Isolate chan."
	<HRLI D* 8>		; "foreign-job open mode"
	<PUSH P* D>		; "Save it."
	<PUSH TP* $TFIX>	; "Lock out intrpts."
	<PUSH TP* [<#OPCODE *377777777777*>]>
	<MCALL 1 INT-LEVEL>
	<PUSH TP* A>		; "Remember old intrpt level."
	<PUSH TP* B>
	<PUSH TP* 0(AB)>	; "Convert arg to sixbit."
	<PUSH TP* 1(AB)>
	<MCALL 1 STRTOX>
	<MOVE 0 B>		; "Save candidate JNAME in 0."
	<POP P* D>		; "Retrieve mode + chan."

; "Obtain JNAMEs of existing inferiors."
	<PUSH P* [<0>]>		; "Put marker on stack."
	<MOVEI E* 7>		; "counter for inferiors"
INFLP	<MOVSI A* 1>		; "Make intrpt bit in A."
	<LSH A* (E)>
	<*UTRAN A>		; "Get U/JNAME for bit."
	<JRST HERE 2>		; "If there is an inferior,"
	<PUSH P* C>		;	"save JNAME on stack."
	<SOJGE E* INFLP>	; "Go on to next bit."
	<HRRZ E* D>		; "Isolate found chan."
	<MOVSI B* *656362*>	; "Put device USR in B for OPEN."
	<SETZI C*>		; "Put default UNAME in C."

; "Ensure JNAME in 0 is not inferior."
INFLUK	<MOVEI A* (P)>		; "pntr to first inferior JNAME"
INFLU1	<SKIPN (A)>
	<JRST OPFJOB>		; "Jump if done."
	<CAME 0 (A)>		; "JNAMEs equal?"
	<SOJA A* INFLU1>	; "No -- continue."
ADD0	<ADD 0 [<(*100*)>]>	; "Yes - try another JNAME, with no embedded spaces."
	<JRST INFLUK>

; "Ensure JNAME in 0 is not a foreign job."
OPFJOB	<*CALL OPNBLK>		; "Try to open foreign job."
	<JRST NOFJOB>		; "No foreign job: jump."
	<*CALL CLSBLK>		; "A foreign job: close unwanted chan."
	<JFCL>
	<JRST ADD0>		; "Try another JNAME."

NOFJOB	<POP P* A>		; "Reset P-stack."
	<JUMPN A* NOFJOB>

; "Open a Muddle Channel to create a new inferior."
	<PUSH TP* <MQUOTE "PRINTB"> -1>
	<PUSH TP* <MQUOTE "PRINTB">>
	<PUSH TP* $TFIX>	; "Convert my UNAME to string."
	<*SUSET [<(4)A>]>
	<PUSH TP* A>
	<PUSH P* 0>		; "Don't clobber good JNAME!"
	<MCALL 1 SIXTOS>
	<POP P* 0>
	<PUSH TP* A>
	<PUSH TP* B>
	<PUSH TP* $TFIX>	; "Convert good JNAME to string."
	<PUSH TP* 0>
	<MCALL 1 SIXTOS>
	<PUSH TP* A>
	<PUSH TP* B>
	<PUSH TP* <MQUOTE "USR"> -1>
	<PUSH TP* <MQUOTE "USR">>
	<MCALL 4 OPEN>

; "Restore previous intrpt level, fix up channel, and return."
	<PUSH TP* (TP)-1>	; "Put old intrpt level on stack again."
	<PUSH TP* (TP)-1>
	<MOVEM A* (TP)-3>	; "Save Channel on stack."
	<MOVEM B* (TP)-2>
	<MCALL 1 INT-LEVEL>	; "Restore intrpt level."
	<POP TP* B>		; "Channel again"
	<POP TP* A>
	<MOVEI C* E -7>
	<CAMN A* $TCHANNEL>	; "If it's really a Channel,"
	<MOVE C* B>
	<POP TP* 7(C)>		; "put input string in input-name2 slot."
	<POP TP* 6(C)>
	<JRST FINIS>

; "*CALL blocks"
OPNBLK	<SETZ 0>
	<#OPCODE *576045560000*>	; "SIXBIT /OPEN/"
	<D>			; "mode and channel"
	<B>			; "[SIXBIT /USR/]"
	<C>			; "[SIXBIT /<-uname->/]"
	<0>			; "[SIXBIT /<-jname->/]"
	<SETZ C>

CLSBLK	<SETZ 0>
	<#OPCODE *435457634500*>	; "SIXBIT /CLOSE/"
	<SETZ E>		; "channel"

<TITLE JOB-GET-NUM>
<DECLARE ("VALUE" FIX CHANNEL)>

	<PUSH TP* 0(AB)>	; "CHAN"
	<PUSH TP* 1(AB)>
	<PUSH TP* $TWORD>
	<PUSH TP* [<*11*>]>	; ".RINTB"
	<MCALL 2 USET>
	<JFFO B* HERE 1>
	<SUBI C* 17>
	<MOVN B* C>
	<MOVE A* $TFIX>
	<JRST FINIS>

<TITLE UCLOSE>
<DECLARE ("VALUE" CHANNEL CHANNEL)>

	<MOVE A* 1(AB)>		; "CPTR TO CHAN"
	<HRLZ A* 1(A)>		; "ITS CHAN"
	<LSH A* 5>
	<IOR A* [<*UCLOS>]>	; "INSN IN A"
	<XCT A>
	<MOVE A* 0(AB)>		; "RETURN CHANNEL."
	<MOVE B* 1(AB)>
	<JRST FINIS>
<END>

<ENDPACKAGE>
