<TITLE COUNT-WORDS>
	<DECLARE ("VALUE" <OR FIX FALSE> FIX <UVECTOR [REST FIX]> FIX)>
	<MOVE	C* 3(AB)>	; "directory"
	<MOVE	A* 1(AB)>	; "count of desc bytes"
	<IDIVI	A* 6>		; "number of words into desc area"
	<ADDI	A* 11>
	<HRLS	A>
	<ADD	C* A>
	<JUMPGE	C* LOSER>	; "C/ vector pointer to desc word"
	<MOVE	D* C>
	<HRLI	D* *440600*>	; "6bit byte pointer to start of desc"
; "update for remainder of division"
	<JUMPE	B* BLKLUP>
	<ILDB	O* D>
	<SOJG	B* HERE -1>

BLKLUP	<ILDB	O* D>		; "pick up a byte"
	<JUMPE	O* LSTBYT>	; "if 0, end of descriptor"
	<CAILE	O* 12>	; "UDTKMX: if >12, can just add"
	<JRST	ADDTHEM>
	<ADD	B* 0>
	<JRST	BLKLUP>

ADDTHEM	<CAIGE	O* 31>		; "UDTPWH: place holder"
	<AOJA	B* BLKLUP>
	<CAIN	O* 31>
	<JRST	BLKLUP>
	<IBP	D>
	<IBP	D>
	<AOJA	B* BLKLUP>

LSTBYT	<MOVE	A* <TYPE-WORD FIX>>
	<JUMPE	B* FINIS>	; "if zero blocks"
	<SOS	B>
	<IMULI	B* 1024>	; "convert to words"
	<ADD	B* 5 (AB)>	; "extra words"
	<JRST	FINIS>

LOSER	<MOVE	A* <TYPE-WORD FALSE>>
	<SETZ	B*>
	<JRST	FINIS>
