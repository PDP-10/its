
<PACKAGE "CALRDR">

<ENTRY IMLAC?
       COMMAPRINT
       BLTREST  ;"copies tuple into vector -- tuple may contain els from vector"
       LKPR
	  ;"vector for symtab lookup results -- used to be specialled in READER"
       READER-NULL-LINE
       READER-ABORT
       OTTY				  ;"saves tty status during reader call"
       MULTLIST
       READER
       READARGS
       RTOBRK						       ;"ASSEMBLY-CODED"
       LAST-READER-BREAK
       CALRDRINIT
       BFR						        ;"reader buffer"
       LEN						 ;"number of chars used"
       CSACT
       RDRHAND
       RDRHEAD
       CONFIRMS
       COMPLETES
       TERMINS
       NSTERMS
       POSCHAR
       HELPCHAR
       ABORTCHAR
       MULTCHAR
       RUBCHAR
       LINEKILL
       WORDKILL
       BUFFERKILL
       CRETYPE
       DRETYPE
       MASTER
       MULT-CR-MASTER					    ;"string for RTOBRK"
       MASTER-STRING
       QUOTCHAR
       INTTYCHAR
       EXACT-MATCH-CHAR
       PARTIAL-MATCH-CHAR
       NO-MATCH-CHAR
       CALICO-FILE-ATOMS
       SPCCHAR-TEMPLATE
       SPCCHARS
       XSPCCHARS>

<USE "CALSYM" "CALUTL" "STR" "TTY" "BLT" "TIMFCN">

<SETG READER-NULL-LINE #FALSE ("Null line")>

<SETG READER-ABORT #FALSE (#FALSE ())>

<SET LKPR [0 0 "" 0]>

<SET FN-ATOMS
     '[CALICO-DEV CALICO-SNM CALICO-NM1 CALICO-NM2]>

<SET FN-SEPS
     <COND (,TENEX '(":<" !\> !\. !\ )) (ELSE ":;  ")>>

<SET TORDR <IVECTOR 4>>

<SETG SPCCHAR-TEMPLATE
	      [POSCHAR
	       '<DO-POSSYM>
	       ALLCHAR
	       '<WINNER-TAKE-ALL>
	       HELPCHAR
	       '<HELP-MESSAGE>
	       ABORTCHAR
	       '<RETURN ,READER-ABORT .LEAVE-READER>
	       MULTCHAR
	       '<CHECK-FOR-MULT>
	       INTTYCHAR
	       '<INPUT-FROM-TTY>
	       RUBCHAR
	       '<RUBOUT-ONE>
	       WORDKILL
	       '<BACKUPRUB " 	">
	       LINEKILL
	       '<BACKUPRUB "">
	       BUFFERKILL
		'<KILLBUFFER>
	       CRETYPE
	       '<RETYPE-BUFFER <>>
	       DRETYPE
	       '<RETYPE-BUFFER T>
	       QUOTCHAR
	       '<QUOTE-NEXT-CHAR>]>

<TITLE RTOBRK>
	<DECLARE ("VALUE" <OR FALSE CHARACTER>
	ATOM ATOM ATOM
	STRING FIX
	STRING
	"OPTIONAL" STRING STRING STRING STRING STRING)>

<PSEUDO <SET PBIN #OPCODE *104000000073*>>	;"JSYS 73"
	<MOVE	B* <MQUOTE IMLAC?>>	;"check CONSOLE"
	<PUSHJ	P* CILVAL>
	<GETYP	A* A>
	<PUSH	P* [0]>
	<CAIE	A* TFALSE>
	<SETOM	(P)>	;"-1 FOR IMLAC, 0 FOR PRINTING -2(P)"

	<MOVE	B* <MQUOTE TENEX>>	;"check OS"
	<PUSHJ	P* CIGVAL>
	<GETYP	A* A>
	<PUSH	P* [0]>
	<CAIN	A* TFALSE>
	<SETOM	(P)>	;"-1 FOR ITS, 0 FOR TENEX -1(P)"

	<MOVE	B* <MQUOTE READER-SILENCE>>
	<PUSHJ	P* CILVAL>
	<SETZM	B>
	<GETYP	A* A>
	<CAIN	A* TFALSE>
	<SETOM	B>
	<PUSH	P* B>		; "Save silence switch on P stack"
	<MOVE	B* 1(AB)>	; "Get value of input arg"
	<PUSHJ	P* CILVAL>
	<PUSH	TP* A>		; "Save on TP stack"
	<PUSH	TP* B>
	<PUSH	TP* $TCHARACTER>	; "Make a slot on stack"
	<PUSH	TP* [0]>
	<PUSH	TP* $TFIX>	; "Make slot for char count"
	<PUSH	TP* [0]>
	<MOVE	B* <MQUOTE OUTCHAN>>
	<PUSHJ	P* CILVAL>
	<PUSH	TP* A>		; "Save value of OUTCHAN"
	<PUSH	TP* B>

LOOP	<GETYP	A* (TB)>	; "Get type of first arg"
	<CAIN	A* TFALSE>	; "False?"
	<JRST	DOIOT>		; "Yes - go do IOT on TTY"
	<CAIN	A* TSTRING>	; "String?"
	<JRST	DOILDB>		; "Yes - go get char from string"
	<PUSHJ	P* NCH>		; "Must be channel - get next char"
	<MOVEM	A* 3(TB)>	; "Save char"
	<CAIE	A* 3>		; "EOF?"
	<JRST	GOTCHR>		; "No - use this char"
	<JRST	LOOP>		; "Yes - try again with new CURXCHAN"


DOILDB	<HRRZ	B* (TB)>	; "Get string length"
ILDBLP	<JUMPE  B* STGPOP>	; "Quit if string is empty"
	<ILDB	D* 1(TB)>	; "Get first char"
	<SOS	B>		; "Decrease count"
	<CAIN	D* 10>		; "Line feed"
	<JRST	ILDBLP>		; "yes - don't want it"
	<MOVEM	D* 3 (TB)>
	<HRRM	B* (TB)>	; "Restore count"
	<MOVE	E* 1(TB)>	; "Get bptr for bumping out of string"
ILDBLP2	<JUMPE	B* STGPOP2>	; "Pop if string is empty"
	<ILDB	D* E>		; "Get next chr"
	<CAIN	D* 10>		; "Line feed"
	<SOJA	B* ILDBLP2>	; "If everything else is lf - flush"
	<JRST	GOTCHR>

STGPOP	<MCALL	0 XFPOP>
	<MOVEM	A* (TB)>	; "Pop and save new CURXCHAN"
	<MOVEM	B* 1(TB)>
	<JRST	LOOP>		; "Try again"

STGPOP2	<MCALL	0 XFPOP>
	<MOVEM	A* (TB)>	; "Pop and save new CURXCHAN"
	<MOVEM	B* 1(TB)>
	<JRST	GOTCHR>		; "But use last character of string"

DOIOT	<AOSE	INTFLG>
	<JSR	LCKINT>
	<SKIPE	-1(P)>		; "SKIP IF tenex"
	<JRST ITSCHR>
	<PBIN>			;"char to A from primary input file"
	<CAIN	A* 31>		;"tenex uses EOL, not CR"
	<MOVEI	A* 13>		;"convert back to CR"
	<SKIPA>
ITSCHR	<(*40000*) B* A>	;"iot into A"
	<MOVEM	A* 3(TB)>
	<SETZM	INTFLG>

GOTCHR	<MOVE	A* 3(TB)>	; "Check char"
	<CAIN	A* 10>		; "Reject line feeds"
	<JRST	LOOP>
	<MOVEI	E* 1>		; "Initialize loop"
	<PUSH	P* [0]>
	<MOVE	D* AB>
	<ADD	D* [*12000012*]>	; "Point at test strings"
SCLOOP	<HRRZ	A* (D)>		; "Get length of next string"
	<JUMPE	A* ESCL>	; "If empty - ignore"
	<MOVE	B* 1(D)>	; "Get byte pointer"
SCLIL	<ILDB	C* B>		; "Get next character"
	<CAMN	C* 3(TB)>		; "Match?"
	<JRST	MATCH>		; "Yes"
	<SOJG	A* SCLIL>	; "No - keep looking"
ESCL	<ADD	D* [*2000002*]>	; "Move to next string"
	<LSH	E* 1>		; "Update bit position"
	<JUMPL	D* SCLOOP>	; "Keep going if more strings"
	<SKIPN	(P)>		; "In any group?"
	<JRST	ACCEPT>		; "Not a break char - accept it"
	<JRST	GOTBRK>		; "Got a break - go return"

MATCH	<IORM	E* (P)>		; "Got one - set the corresponding bit"
	<JRST	ESCL>

ACCEPT	<POP	P* (P)>		; "Flush P stack"
	<SKIPN	(P)>		; "Silence switch set?"
	<JRST	BMPCHR>		; "Yes - no rubout - no echo"
	<SKIPN	-2(P)>		; "skips if IMLAC"
	<JRST	OUTCHR>		; "don't use ctl-p if printing tty"
	<SKIPE	-1(P)>		; "don't RO on TENEX -- should check IMLAC?"
	<SKIPG	9(AB)>		; "Any extra characters to flush"
	<JRST	OUTCHR>		; "No"
RO	<MOVEI	A* 16>		; "Rubout with ^P and X"
	<MOVE	B* 7(TB)>
	<MOVNI 3>		;"ctl-P,X,+DELETED CHR"
	<ADDM 27 (B)>		;"reduce line pos, so won't CR"
	<PUSHJ	P* W1C>
	<MOVEI	A* 88>
	<MOVE	B* 7(TB)>
	<PUSHJ	P* W1C>
	<SOSLE	9(AB)>
	<JRST	RO>		; "More - keep going"
OUTCHR	<MOVE	A* 3(TB)>	; "Echo the char"
	<MOVE	B* 7(TB)>	; "Get OUTCHAN"
	<PUSHJ	P* W1C>
	<MOVE	A* 3(TB)>	; "Was it a carriage return?"
	<CAIE	A* *15*>
	<JRST	BMPCHR>		; "No - done"
	<MOVEI	A* *12*>	; "Follow up with line feed"
	<MOVE	B* 7(TB)>
	<PUSHJ	P* W1C>

BMPCHR	<AOS	5(TB)>		; "Bump char count"
	<HRRZ	B* 6(AB)>	; "Get length of buffer"
	<SOS	B>		; "Decrease for char to go in now"
	<MOVE	A* 3(TB)>	; "Get the char"
	<IDPB	A* 7(AB)>	; "Put char into buffer"
	<HRRM	B* 6(AB)>	; "And update count"
	<JUMPLE	B* LOSER>	; "If more room in buffer - keep on"
	<CAIE	A* *15*>		
;"didn't break at CR (JFCL INSTEAD OF CAIE TO RETURN TO FORMER METHOD)"
	<JRST	LOOP>		;"continue input"
	<MOVEI	A* *12*>		;"make a LF"
	<MOVEM	A* 3 (TB)>	;"put back in char slot"
	<JRST	BMPCHR>		;"add to BFR -- makes strings win"
LOSER	<MOVE	C* 1(AB)>	; "Must reset input arg"
	<MOVE	A* (TB)>
	<MOVE	B* 1(TB)>
	<PUSHJ	P* CISET>
	<MOVE	C* 5(AB)>	; "Reset length"
	<MOVE	A* 4(TB)>
	<MOVE	B* 5(TB)>
	<PUSHJ	P* CISET>
	<MOVSI	A* TFALSE>	; "Return false"
	<SETZM	B>
	<JRST	FINIS>


GOTBRK	<POP	P* B>		; "Get bit pattern back"
	<MOVE	C* 3(AB)>	; "Set the bit pattern arg"
	<MOVSI	A* TWORD>
	<PUSHJ	P* CISET>
	<MOVE	C* 1(AB)>	; "Now set input arg to what it is now"
	<MOVE	A* (TB)>
	<MOVE	B* 1(TB)>
	<PUSHJ	P* CISET>
	<MOVE	C* 5(AB)>	; "Reset length"
	<MOVE	A* 4(TB)>
	<MOVE	B* 5(TB)>
	<PUSHJ	P* CISET>
	<MOVE	A* 2(TB)>	; "Return the break character"
	<MOVE	B* 3(TB)>
	<JRST	FINIS>


; "Following code taken mostly from interpreter (INEOF)"



NCH	<SUBM	M* (P)>		; "Magic"

NCHLOOP	<MOVE	B* 1(TB)>	; "Get channel"
	<PUSHJ	P* R1C>		; "Get next char"
	<ANDI	A* *177*>		; "Isolate character"
	<CAIN	A* 10>		; "Line feed?"
	<JRST	NCHLOOP>	; "Yes - ignore"
	<CAIE	A* 3>		; "EOF?"
	<JRST	MPOPJ>		;"no --return the char"
	;"Can't see the need for lookahead.  Change is to return and let the next
read catch the EOF char, whereupon <AGAIN .CSACT> is performed to
display prompt if READER-SILENCE is now false."
NCHPOP	<PUSH	P* A>		; "Save character to return"
	<MCALL	0 XFPOP>
	<MOVE	B* <MQUOTE CSACT>>
	<PUSHJ P* CILVAL>	;"get the activation"
	<PUSH	TP* A>
	<PUSH	TP* B>
	<MCALL 1 AGAIN>	;"let the silence be checked"

	<JRST FINIS>	;"should not get here"

	<END>


<DEFINE PBREAK () 
	#DECL ((READER-SILENCE) <OR FALSE ATOM> (BREAK) CHARACTER)
	<COND (<NOT .READER-SILENCE>
	       <COND (<==? .BREAK <ASCII 13>> <TERPRI>)
		     (ELSE <PRINC .BREAK>)>)>>

<DEFINE COMPLETE (STR LEN TO TOLEN) 
	#DECL ((STR TO) STRING (LEN) FIX (OUTCHAN) CHANNEL (TOLEN VALUE) FIX)
	<SET TOLEN <- .TOLEN .LEN>>				  ;"EXTRA CHARS"
	<COND (<G? .TOLEN 0>
	       <SET TO <REST .TO .LEN>>
	       <COND (<NOT .READER-SILENCE> <PRINTSTRING .TO .OUTCHAN .TOLEN>)>
					     ;"copy extra chars into the buffer"
	       <EXTRACT .TO <REST .STR .LEN> .TOLEN>)>
	.TOLEN>

<DEFINE TPROMPT (STR SYN
		 "OPTIONAL" (SYNSW T) (LEVEL T) (INMULT <>)
		 "AUX" X Y LEN)
   #DECL ((STR Y) STRING (LEN) FIX (SYN) VECTOR (X) <OR ATOM FALSE>
	  (SYNSW LEVEL) <OR FALSE ATOM> (VALUE) ATOM (INMULT) <OR LIST FALSE>)
   <AND .INMULT <COMMAPRINT .INMULT>>
   <COND
    (.LEVEL
     <COND (<G? <14 .OUTCHAN> 40> <TERPRI>) (ELSE <PRINC !\ >)>
     <PRINC .STR>
     <COND (.SYNSW
	    <PRINC " (">
	    <REPEAT ()
		    <COND (<EMPTY? .SYN> <RETURN>)>
		    <PRINC <1 .SYN>>
		    <COND (<1? <LENGTH .SYN>> <PRINC ")">) (ELSE <PRINC "|">)>
		    <SET SYN <REST .SYN>>>
	    <PRINC ":  ">)>)
    (ELSE
     <TERPRI>
     <PRINC .STR>
     <TERPRI>
     <COND (.SYNSW
	    <PRINC "(">
	    <SET Y
		 <COND (<1? <LENGTH .SYN>> " is allowed.)")
		       (<MEMBER "MULT" .SYN>
			<PRINC "multiple ">
			" (s) are allowed.)")
		       (" are allowed.)")>>
	    <REPEAT ()
		    <COND (<EMPTY? .SYN> <RETURN>)>
		    <PRINC <COND (<SET X <LOOKUP <1 .SYN> ,FULLSYN>> <1 ,.X>)
				 (<MEMQ <PARSE <1 .SYN>> <ALLTYPES>>
				  <PRINC "Object of type ">
				  <1 .SYN>)
				 (<=? <1 .SYN> "MULT"> "")
							  ;"don't print unknown"
				 (ELSE "Unknown type")>>
		    <COND (<1? <LENGTH .SYN>> <PRINC .Y>)
			  (<=? <1 .SYN> "MULT">)
			  (ELSE <PRINC ", ">)>
		    <SET SYN <REST .SYN>>>
	    <TERPRI>
	    <COND (<==? .MODE 3>
		   <PRINC "Current default is ">
		   <MAPF <>
			 <FUNCTION (A B C) 
				 #DECL ((A) <OR ATOM FALSE STRING>
					(B) <OR STRING CHARACTER> (C) ATOM)
				 <PRINC <COND (<TYPE? .A ATOM> ..A) (.A) (..C)>>
				 <PRINC .B>>		       ;"separator char"
			 <COND (<EMPTY? .TABLE> .FN-ATOMS) (.TABLE)>
			 .FN-SEPS
			 .FN-ATOMS>
		   <COND (<EMPTY? .TABLE> T)
			 (<L? <SET LEN <LENGTH .TABLE>> 4>
			  <MAPF <>
				<FUNCTION (A B) 
					#DECL ((A) ATOM (B) CHARACTER)
					<PRINC ..A>
					<PRINC .B>>
				<REST .FN-ATOMS .LEN>
				<REST .FN-SEPS .LEN>>)>
		   <TERPRI>)
		  (<==? .MODE 5>
		   <PRINC "Current default is ">
		   <COND (<EMPTY? .TABLE>
			  <PDATE <DATE>>)
			 (<PDATE <1 .TABLE>>)>
		   <PRINC ".">
		   <TERPRI>)>		
	    <PRINC ":  ">)>)>
   T>

<DEFINE READER (TABLE P1 P2 SYN
		"OPTIONAL" (SYNSW T) (MULTSW FOO)
			   (BFR
			    <COND (<AND <ASSIGNED? BFR> <TYPE? .BFR STRING>>
				   <COND (<AND <ASSIGNED? LEN> <TYPE? .LEN FIX>>
					  <REST .BFR .LEN>)
					 (ELSE .BFR)>)
				  (ELSE <ISTRING 100>)>)
		"AUX" FOO I TEMP BITP LENS BREAK (XTRCHR 0) (LEN 0) OTTY
		      (WD0 #WORD *000000000000*) (INSTRING <>) (INCHARACTER <>)
		      (HELPSW <>) (XFPOPSW <>) (CAL-PARSE 0)
		      (MODE
		       <COND (<AND <MEMBER "SYM" .SYN> <NOT <EMPTY? .TABLE>>> 0)
			     (<MEMBER "TEXT" .SYN> 1)
			     (<MEMBER "LINE" .SYN> 2)
			     (<MEMBER "FILE" .SYN> 3)
			     (<OR <MEMBER "FORM" .SYN>
				  <MEMBER "VECTOR" .SYN>
				  <MEMBER "LIST" .SYN>
				  <MEMBER "OBJECT" .SYN>>
			      4)
			     (<MEMBER "OBJECTS" .SYN> <SET CAL-PARSE 1> 4)
			     (<MEMBER "DATE" .SYN> 5)
			     (6)>) ST1 ST2 ST3 ST4)
   #DECL ((VALUE VAL) ANY (I) FIX (LEN LENS XTRCHR MODE) <SPECIAL FIX>
	  (XSPCCHARS TEMP) <VECTOR [REST CHARACTER FORM]> (WD0) WORD
	  (BITP) <SPECIAL WORD> (BFR P1) <SPECIAL STRING>
	  (P2) <SPECIAL <OR TUPLE VECTOR STRING>>
	  (LKPR) <VECTOR FIX ANY STRING FIX> (ST1 ST2 ST3 ST4) STRING
	  (TABLE) <SPECIAL <OR SYMTABLE VECTOR>>
	  (SYN) <SPECIAL <VECTOR [REST STRING]>>
	  (INSTRING INCHARACTER XFPOPSW SYNSW MULTSW) <SPECIAL <OR FALSE ATOM>>
	  (HELPSW) <SPECIAL <OR FALSE ATOM CHANNEL>>
	  (CONFIRMS COMPLETES NSTERMS TERMINS SPCCHARS) STRING
	  (MASTER MULTCHAR) CHARACTER (IMLAC?) <OR FIX FALSE>
	  (FOO READER-SILENCE) <OR ATOM FALSE>
	  (CURXCHAN) <OR FALSE CHANNEL STRING>
	  (BREAK) <SPECIAL <OR FALSE CHARACTER>> (CAL-PARSE) <SPECIAL FIX>
	  (OTTY) <SPECIAL <OR UVECTOR WORD>>)
   <COND (<NOT <ASSIGNED? IMLAC?>> <CALRDRINIT>)>
   <OR .MULTSW <SET MULTSW FOO>>
   <SET ST1 <COND (<0? .MODE> ,CONFIRMS) ("")>>
   <SET ST2 <COND (<0? .MODE> ,COMPLETES) ("")>>
   <SET ST4 <COND (<0? .MODE> ,NSTERMS) ("")>>
   <SET ST3
	<COND (<OR <1? .MODE> <==? .MODE 4>> ,MASTER-STRING)
	      (<OR <==? .MODE 2> <==? .MODE 5>> <REST ,MULT-CR-MASTER>) ;"cr and master"
	      (<==? .MODE 3>
	       <COND (<MEMBER "MULT" .SYN> ,MULT-CR-MASTER)
		     (ELSE <REST ,MULT-CR-MASTER>)>)
	      (ELSE ,TERMINS)>>
   <COND (<TYPE? .P2 STRING> <SET P2 [.P2 ""]>)>
   <UNWIND
    <PROG ()
      <COND (<NOT .CURXCHAN> <SET OTTY <TTY-GET>> <CAREFUL-TTY-OFF>)>
					   ;"save tty status and set new status"
      <PROG CSACT ()
	#DECL ((CSACT) <SPECIAL ACTIVATION>)
	<COND (<NOT .READER-SILENCE>
	       <TPROMPT .P1 .SYN .SYNSW>
	       <PRINTSTRING .BFR .OUTCHAN .LEN>)>
	<SET .MULTSW <>>
	<SET VAL
	 <REPEAT LEAVE-READER ()
		 #DECL ((LEAVE-READER) <SPECIAL ACTIVATION>)
				 ;"Read the input stream through the next break"
		 <SET BREAK
		      <COND (.INSTRING
			     <RTOBRK CURXCHAN
				     BITP
				     LENS
				     <REST .BFR .LEN>
				     .XTRCHR
				     ""
				     ""
				     ""
				     ,SPCCHARS
				     ""
				     "\"">)
			    (ELSE
			     <RTOBRK CURXCHAN
				     BITP
				     LENS
				     <REST .BFR .LEN>
				     .XTRCHR
				     .ST1
				     .ST2
				     .ST3
				     ,SPCCHARS
				     .ST4
				     "\"">)>>
		 <SET LAST-READER-BREAK .BREAK>
		 <COND (<G? .LENS 0>
			<SET XTRCHR 0>
			<SET LEN <+ .LEN .LENS>>
			<COND (<TYPE? .HELPSW CHANNEL> <CLOSE .HELPSW>)>
			<SET HELPSW <>>)>
    ;"First check to see if RTOBRK returned because it ran out of buffer space."
		 <COND (<NOT .BREAK> <SET BFR <EXTRACT .BFR <* .LEN 2>>>)
		       (<AND <L? <- <LENGTH .BFR> .LEN> 2>
						        ;"leave breathing-space"
			     <SET BFR <EXTRACT .BFR <* .LEN 2>>>
			     <>>)
		       (<AND <==? .BREAK !\">
			     <NOT <==? .MODE 1>>
			     <NOT <==? .MODE 2>>
					 ;"ignore quotes in text and line modes"
			     <SET BITP #WORD *000000000000*>
			     <SET INSTRING <NOT .INSTRING>>
					  ;"complement the inside-a-string flag"
			     <AND <NOT <0? .LEN>>
				  <OR .INCHARACTER <==? <NTH .BFR .LEN> !\!>>
				  <SET INCHARACTER T>
			      ;"in char, not string (only cleared by RUBOUTCHR)"
				  <SET INSTRING <NOT .INSTRING>>>
			     <>>)
 ;
"Now check to see if user typed a special character. If so execute handler for it."
		       (<AND <N==? <ANDB .BITP 8> #WORD *000000000000*>
			     <SET TEMP <MEMQ .BREAK ,XSPCCHARS>>
			     <EVAL <2 .TEMP>>>)
	 ;"Are we in symbol input mode?  If so, was symbol oriented char typed?"
		       (<AND <0? .MODE>
			     <N==? <ANDB .BITP 7> #WORD *000000000000*>>
			<SYMBOL-ANALYZE>)
		       (<N==? <ANDB .BITP 4> #WORD *000000000000*>
			<RUBOUTCHR .XTRCHR>
			<PBREAK>			      ;"print the break"
			<COND (<L=? .MODE 2> <RETURN <EXTRACT .BFR .LEN>>)
			      (<==? .MODE 5>
			       <RETURN <DATE-FIXUP <EXTRACT .BFR .LEN>>>)
			      (<==? .MODE 3>
			       <RETURN <FSP-FIXUP <EXTRACT .BFR .LEN>>>)
			      (ELSE <RETURN <READER-PARSE .BFR .LEN>>)>)
		       (ELSE
			<PUT .BFR <SET LEN <+ 1 .LEN>> .BREAK>
			<PBREAK>			 ;"print the break")>>>>
      <AND <ASSIGNED? OTTY> <TTY-SET .OTTY>>
      <COND (.XFPOPSW <XFPOP>)>>				 ;"unwind scope"
    <AND <ASSIGNED? OTTY> <TTY-SET .OTTY>>>
   .VAL>

<DEFINE SUBSTRUC* (OBJ RESTNUM TO) 
	#DECL ((OBJ) TUPLE (RESTNUM) FIX (TO) <VECTOR [4 ANY]>)
	<MAPR <>
	      <FUNCTION (FRM INTO) 
		      #DECL ((FRM) TUPLE (INTO) VECTOR)
		      <PUT .INTO
			   1
			   <COND (<TYPE? <1 .FRM> FORM> <EVAL <1 .FRM>>)
				 (ELSE <1 .FRM>)>>>
	      <REST .OBJ .RESTNUM>
	      .TO>>

<DEFINE READARGS (
		  "TUPLE" RDARGS
		  "AUX" SYNTAX TEMP (MULTERM <>) (N 1) (I 1)
			(LEN <LENGTH .RDARGS>) (INMULT <>) SETAT
			(ARGVCT <IVECTOR </ .LEN 4>>) ML (MULTLIST ()))
   #DECL ((RDARGS) TUPLE (N I LEN) FIX (SYNTAX) <VECTOR [REST STRING]>
	  (ARGVCT) VECTOR (VALUT) <OR VECTOR FALSE> (TEMP) ANY
	  (MULTERM) <SPECIAL <OR FALSE ATOM>> (MULTLIST) <SPECIAL LIST>
	  (INMULT) <OR FALSE ATOM> (TORDR) <VECTOR [4 ANY]>
	  (SETAT) <OR ATOM FALSE>)
   <REPEAT ()
	   <AND <L? .N 1> <RETURN ,READER-ABORT>>	  ;"control-R to excess"
	   <AND <G? .N .LEN>
		<RETURN <COND (<==? <SET I <- .I 1>>
				    <SET TEMP <LENGTH .ARGVCT>>>
			       .ARGVCT)
			      (ELSE
			       <BLTREST <REST .ARGVCT <- .TEMP .I>>
					!.ARGVCT>)>>>
	   <COND (.INMULT T)
		 (<TYPE? <NTH .RDARGS .N> ATOM>
		  <SET SETAT <NTH .RDARGS .N>>
		  <SET N <+ .N 1>>)
		 (ELSE <SET SETAT <>>)>
	   <SUBSTRUC* .RDARGS <- .N 1> .TORDR>
	   <COND
	    (<SET TEMP
		  <COND (<NOT <1 .TORDR>> <CHTYPE (FLUSHED !<1 .TORDR>) FALSE>)
			(<READER <1 .TORDR>
				 <COND (.INMULT "and ") (ELSE <2 .TORDR>)>
				 <3 .TORDR>
				 <SET SYNTAX <4 .TORDR>>
				 <NOT .INMULT>
				 MULTERM>)>>
	     <COND (.INMULT
		    <COND (<TYPE? .TEMP SPLICE>
			   <SET ML
				<REST <PUTREST .ML .TEMP> <- <LENGTH .ML> 1>>>)
			  (<SET ML <REST <PUTREST .ML (.TEMP)>>>)>)
		   (<MEMBER "MULT" .SYNTAX>
		    <SET INMULT T>
		    <COND (<TYPE? .TEMP SPLICE>
			   <SET MULTLIST <CHTYPE .TEMP LIST>>
			   <SET ML <REST .MULTLIST <- <LENGTH .MULTLIST> 1>>>)
			  (<SET MULTLIST <SET ML (.TEMP)>>)>)>
	     <COND (.MULTERM <SET INMULT T>)
		   (.INMULT
		    <SET TEMP .MULTLIST>
		    <SET MULTLIST ()>
		    <SET INMULT <>>)>
	     <COND (<NOT .INMULT>
		    <PUT .ARGVCT .I .TEMP>
		    <AND .SETAT <SET .SETAT .TEMP>>
		    <SET I <+ .I 1>>
		    <SET N <+ .N 4>>)>)
	    (<1 .TEMP>				 ;"gets here if null line, etc."
	     <COND (<==? <1 .TEMP> FLUSHED>
		    <SET TEMP
			 <COND (<1? <LENGTH .TEMP>> #FALSE ("Aborted"))
			       (<2 .TEMP>)>>)
		   (.INMULT <SET TEMP .MULTLIST> <SET INMULT <>>)
		   (<MEMBER "MULT" .SYNTAX> <SET TEMP ()>)>
	     <PUT .ARGVCT .I .TEMP>
	     <AND .SETAT <SET .SETAT .TEMP>>
	     <SET I <+ .I 1>>
	     <SET N <+ .N 4>>)
	    (.INMULT			        ;"gets here if abort char typed"
	     <COND (<NOT <LENGTH? .MULTLIST 1>>	   ;"if length > 1, can putrest"
		    <PUTREST <SET ML <REST .MULTLIST <- <LENGTH .MULTLIST> 2>>>
			     ()>
		    <COND (<NOT .READER-SILENCE> <COMMAPRINT .MULTLIST>)>)
		   (ELSE <SET INMULT <>> <SET MULTLIST ()>)>)
	    (ELSE
	     <TERPRI>
	     <AND <G? .N 1>
		  <TYPE? <NTH .RDARGS <- .N 1>> ATOM>
		  <SET N <- .N 1>>>		       ;"back up over atom hack"
	     <PROG ()
		   <SET I <- .I 1>>
		   <SET N <- .N 4>>
		   <AND <L? .N 1> <RETURN>>	   ;"let abort happen in REPEAT"
		   <SET TEMP <NTH .RDARGS .N>>
		   <AND <TYPE? .TEMP FORM> <SET TEMP <EVAL .TEMP>>>
								 ;"see if FALSE"
		   <COND (<AND <G? .N 1> <TYPE? <NTH .RDARGS <- .N 1>> ATOM>>
			  <SET SETAT <NTH .RDARGS <SET N <- .N 1>>>>)
			 (ELSE <SET SETAT <>>)>
		   <OR .TEMP <AGAIN>>>	        ;"back up beyond last FLUSH")>>>

<DEFINE CALRDRINIT () 
	#DECL ((IMLAC?) <OR 'T FALSE> (SYN) <VECTOR [REST STRING]>
	       (POSCHAR HELPCHAR ABORTCHAR MULTCHAR RUBCHAR LINEKILL WORDKILL
		BUFFERKILL CRETYPE DRETYPE MASTER QUOTCHAR STRCHAR) CHARACTER
	       (COMPLETES CONFIRMS NSTERMS TERMINS SPCCHARS) CHARACTER
	       (XSPCCHARS) <VECTOR [REST CHARACTER FORM]> (OUTCHAN) CHANNEL
	       (XTRCHR MODE LEN) FIX (BFR P2 P1) STRING
	       (SYNSW INSTRING) <OR ATOM FALSE> (VALUE MULTSW) ATOM
	       (WD0 BITP) WORD (CURXCHAN) <OR CHANNEL STRING FALSE>
	       (BREAK) CHARACTER (TABLE) SYMTABLE)
	<SET IMLAC? <==? <MOD <11 ,INCHAN> 128> 2>>
	<SET READER-SILENCE <>>
	<SETG FULLSYN <MOBLIST SYNOB>>
	<MAPF <>
	      <FUNCTION (A) 
		      #DECL ((A) <LIST STRING VECTOR>)
		      <SETG <OR <LOOKUP <1 .A> ,FULLSYN>
				<INSERT <1 .A> ,FULLSYN>>
			    <2 .A>>>
	      '[("FIX" ["integer" FIX])
		("FLOAT" ["floating point number" FLOAT])
		("ATOM" ["MUDDLE atom name" ATOM])
		("CHARACTER" ["character preceded by !\"" CHARACTER])
		("LIST" ["list of objects" LIST])
		("VECTOR" ["vector of objects" VECTOR])
		("FORM" ["MUDDLE form" FORM])
		("OBJECT" ["any EVALable object" ANY])
		("OBJECTS" ["any number of EVALable objects" LIST])
		("ANY" ["any EVALable object" ANY])
		("STRING" ["string of characters" STRING])
		("FILE" ["file specification" STRING])
		("DATE" ["date specification" STRING])
	        ("LINE" ["line of text" STRING])
		("TEXT" ["text terminated by master break" STRING])
		("SYM" ["symbol" SYMBOL])]>
	<SETG EXACT-MATCH-CHAR !\!>
	<SETG PARTIAL-MATCH-CHAR
	      <COND (,TENEX <ASCII 7>) (ELSE !\&)>>
	<SETG NO-MATCH-CHAR !\?>
	<SETG POSCHAR <ASCII 6>>
	<SETG HELPCHAR !\?>
	<SETG ABORTCHAR <ASCII 18>>
	<SETG MULTCHAR !\,>
	<SETG RUBCHAR <ASCII <COND (,TENEX 1) (ELSE 127)>>>
	<SETG LINEKILL <ASCII 25>>
	<SETG WORDKILL <ASCII 23>>
	<SETG BUFFERKILL <ASCII 0>>
	<SETG CRETYPE <ASCII 12>>
	<SETG DRETYPE <ASCII 4>>
	<SETG MASTER <ASCII 27>>
	<SETG ALLCHAR <COND (,TENEX <ASCII 21>)
			    (ELSE <ASCII 1>)>>
	<SETG QUOTCHAR <ASCII 17>>
	<SETG INTTYCHAR <ASCII 20>>
	<SETG MASTER-STRING
	      <REST <SETG MULT-CR-MASTER <STRING !\, <ASCII 13> <ASCII 27>>>
		    2>>
	<SETG COMPLETES " ">
	<SETG CONFIRMS "">
	<SETG NSTERMS "">
	<SETG TERMINS "î	,">
	<SETG SPCCHARS
	    <MAPF ,STRING
		<FUNCTION (A)
		#DECL ((A) <OR ATOM FORM>)
		<COND (<TYPE? .A ATOM>
			,.A)
		      (ELSE <MAPRET>)>> ,SPCCHAR-TEMPLATE>>
	<SETG XSPCCHARS
	    <MAPF ,VECTOR
		<FUNCTION (A)
		#DECL ((A) <OR ATOM FORM>)
		<COND (<TYPE? .A ATOM>
			,.A)
		      (ELSE .A)>> ,SPCCHAR-TEMPLATE>>
	T>

<DEFINE KILLBUFFER ()
	<SET XTRCHR <SET LEN  0>>
	<TPROMPT .P1 .SYN .SYNSW>>

<DEFINE DATE-FIXUP (INP "EXTRA" INPARSE)
    #DECL ((INP) STRING (INPARSE) <OR FALSE <LIST [3 FIX]>> (XTRCHR LEN) FIX)
    <COND (<EMPTY? .TABLE>
	   <SET INPARSE <DATE-PARSE .INP <DATE>>>)
	  (<SET INPARSE <DATE-PARSE .INP <1 .TABLE>>>)>
    <COND (.INPARSE)
	  (T <TERPRI>
	   <PRINC "Date unrecognizable. Please retype.">
	   <TERPRI>
	   <SET XTRCHR <SET LEN 0>>
	   <AGAIN .CSACT>)>>

<DEFINE FSP-FIXUP (INP "AUX" FSPV (OS ,TENEX)) 
	#DECL ((INP VALUE) STRING (FSPV) VECTOR (OS) <OR ATOM FALSE>)
	<SET FSPV <FSP-PARSE .INP>>
	<COND (<EMPTY? .TABLE>		    ;"symbol-table can contain defaults"
	       <MAPF <>
		     <FUNCTION (A B) 
			     #DECL ((A) ATOM (B) <OR LOSE STRING>)
			     <COND (<TYPE? .B STRING> <SET .A .B>)>>
		     (CALICO-DEV CALICO-SNM CALICO-NM1 CALICO-NM2)
		     .FSPV>
	       <OR <TYPE? <1 .FSPV> STRING>		     ;"user gave device"
		   <AND <TYPE? <2 .FSPV> STRING> <SET CALICO-DEV "DSK">>>
						       ;"else if sname, dev=dsk"
	       <STRING .CALICO-DEV
		       <COND (.OS ":<") (ELSE !\:)>
		       .CALICO-SNM
		       <COND (.OS !\>) (ELSE !\;)>
		       .CALICO-NM1
		       <COND (.OS !\.) (ELSE !\ )>
		       .CALICO-NM2>)
	      (ELSE		  ;"defaults in TABLE, USE CALICO-FOO otherwise"
	       <MAPR <>
		     <FUNCTION (A B) 
			     #DECL ((A) VECTOR (B) <OR FALSE STRING>)
			     <COND (<NOT <TYPE? <1 .A> STRING>>
				    <PUT .A 1 <1 .B>>)>>
		     .FSPV
		     .TABLE>
	       <MAPF ,STRING
		     <FUNCTION (A B C) 
			     #DECL ((A) <OR STRING LOSE FALSE> (B) ATOM
				    (C) <OR STRING CHARACTER>)
			     <COND (<TYPE? .A STRING> <MAPRET .A .C> T)
				   (ELSE <MAPRET ..B .C> T)>>
		     .FSPV
		     .FN-ATOMS
		     .FN-SEPS>)>>

<DEFINE RUBOUTCHR ("OPTIONAL" (ARGN 1)
		   "AUX" N (SPCPRT "	î") (SOSCH <REST .OUTCHAN 13>))
	#DECL ((ARGN) <OR CHARACTER FIX> (VALUE) ATOM (SPCPRT) STRING
	       (MODE N) FIX (IMLAC?) <OR FIX FALSE> (SOSCH) <VECTOR FIX>)
	<COND (<NOT .READER-SILENCE>
	       <COND (<TYPE? .ARGN CHARACTER>
		      <AND <==? .ARGN !\">
			   <NOT <==? .MODE 1>>
			   <NOT <==? .MODE 2>>
			   <COND (.INCHARACTER
				  <AND <==? <NTH .BFR <- .LEN 1>> !\!>
				       <SET INCHARACTER <>>>)
				 (ELSE <SET INSTRING <NOT .INSTRING>>)>>
						    ;"rubbed out a double-quote"
		      <COND (<NOT .IMLAC?>
			     <COND (,TENEX <PRINC !\\\>)>
			     <PRINC .ARGN>)
			    (<AND <L? <SET N <ASCII .ARGN>> 32>
				  <NOT <MEMBER .ARGN .SPCPRT>>>
			     <SET N 2>)
			    (ELSE <SET N 1>)>)
		     (ELSE <SET N .ARGN>)>
	       <COND (.IMLAC?
		      <REPEAT ()
			      <COND (<L? <SET N <- .N 1>> 0> <RETURN T>)>
			      <PRINC "X">
			      <PUT .SOSCH 1 <- <NTH .SOSCH 1> 3>>
		        ;"account for ^P,X and rubbed-out char in .OUTCHAN">)>)>
	T>

<DEFINE BACKUPRUB ("OPTIONAL" (STPRS "") "AUX" (NBREAKFLAG <>))
    #DECL ((VALUE) ATOM
	   (BFR STPRS P1) STRING
	   (SYN) <VECTOR [REST STRING]>
	   (SYNSW NBREAKFLAG) <OR ATOM FALSE>
	   (XTRCHR LEN) FIX)
    <COND (<0? .LEN> <TPROMPT .P1 .SYN .SYNSW>)
	  (ELSE <RUBOUTCHR .XTRCHR>
	        <SET XTRCHR 0>
		<REPEAT ()
	    <COND (<MEMBER <NTH .BFR .LEN> .STPRS> 
		   <COND (.NBREAKFLAG <RETURN T>)>)
		  (<SET NBREAKFLAG T>)>	;"REACHED NON-STOPPER"
	    <RUBOUTCHR <NTH .BFR .LEN>>	;"RUB OUT NON-STPR, TRAILING STPRS"
	    <COND (<0? <SET LEN <- .LEN 1>>> <RETURN T>)>
	>
	    <SET XTRCHR 0>)>
    T>

<DEFINE SYMBOL-ANALYZE ("AUX" I (WD0 #WORD *000000000000*)) 
	#DECL ((MODE LEN XTRCHR I) FIX (BITP WD0) WORD (TABLE) SYMTABLE
	       (LKPR) <VECTOR FIX ANY STRING FIX> (BFR) STRING (VALUE) ATOM)
	<APPLY ,<1 .TABLE> 1 .BFR .LEN <2 .TABLE> .LKPR>
	<SET I
	     <COND (<N==? <ANDB .BITP 1> .WD0>
		    <COND (<==? <1 .LKPR> 3> 0)
			  (<AND <0? <1 .LKPR>> <N==? <ANDB .BITP 16> .WD0>> 1)
			  (<AND <N==? <1 .LKPR> 0> <N==? <ANDB .BITP 2> .WD0>>
			   2)
			  (3)>)
		   (<N==? <ANDB .BITP 2> .WD0>
		    <COND (<0? <1 .LKPR>>
			   <COND (<N==? <ANDB .BITP 16> .WD0> 1) (3)>)
			  (<AND <N==? <1 .LKPR> 2> <N==? <ANDB .BITP 4> .WD0>>
			   0)
			  (2)>)
		   (<=? <1 .LKPR> 3> 0)
		   (1)>>				 ;"I=0 => return symbol"
	<COND (<0? .I>
	       <COMPLETE .BFR .LEN <3 .LKPR> <4 .LKPR>>
	       <RUBOUTCHR .XTRCHR>
	       <PBREAK>					      ;"print the break"
	       <RETURN <APPLY ,<1 .TABLE> 2 <2 .LKPR> <2 .TABLE>>
		       .LEAVE-READER>)
	      (<1? .I>
	       <RUBOUTCHR .XTRCHR>
	       <PBREAK>					      ;"print the break"
	       <RETURN <READER-PARSE .BFR .LEN> .LEAVE-READER>)
	      (<==? .I 2>
	       <COND (<AND <NOT .READER-SILENCE> <==? .LEN <LENGTH <3 .LKPR>>>>
		      <PRINC ,EXACT-MATCH-CHAR>
		      <SET XTRCHR <+ .XTRCHR 1>>)
		     (ELSE
		      <COMPLETE .BFR .LEN <3 .LKPR> <4 .LKPR>>
		      <SET LEN <4 .LKPR>>
		      <COND (<AND <NOT .READER-SILENCE>
				  <L? .LEN <LENGTH <3 .LKPR>>>>
			     <PRINC ,PARTIAL-MATCH-CHAR>
			     <SET XTRCHR <+ .XTRCHR 1>>)>)>)
	      (<AND <NOT .READER-SILENCE> <==? .I 3>>
	       <PRINC ,NO-MATCH-CHAR>
	       <SET XTRCHR <+ .XTRCHR 1>>)>
	T>

<DEFINE READER-PARSE (BFR ALEN "AUX" TEMP FS (OKSYN .SYN) TYPE-NAME) 
   #DECL ((BFR) STRING (LEN ALEN XTRCHR) FIX (TEMP) ANY
	  (FS) <OR ATOM FALSE <VECTOR [REST STRING ATOM]>> (TYPE-NAME) ATOM
	  (OKSYN SYN) <VECTOR [REST STRING]> (VALUE) ANY)
   <REPEAT ()
     <COND
      (<0? .ALEN> <RETURN ,READER-NULL-LINE>)
      (<OR <==? <1 .BFR> !\ > <==? <1 .BFR> !\	>>
       <SET BFR <REST .BFR>>
       <SET ALEN <- .ALEN 1>>)
      (ELSE
       <PUT .BFR <+ .ALEN 1> !\!>
       <PUT .BFR <+ .ALEN 2> !\>
       <SET TEMP <LPARSE .BFR>>
       <COND (<0? .CAL-PARSE>
	      <AND <EMPTY? .TEMP> <RETURN ,READER-NULL-LINE>>
	      <SET TEMP <1 .TEMP>>)>
       <RETURN <REPEAT ()
		       <COND (<EMPTY? .OKSYN>
			      <TERPRI>
			      <PRINC 
"Type of input unacceptable.  Please retype.">
			      <TERPRI>
			      <SET XTRCHR <SET LEN 0>>
			      <AGAIN .CSACT>)
			     (<AND <SET FS <LOOKUP <1 .OKSYN> ,FULLSYN>>
				   <SET FS ,.FS>
				   <OR <==? <TYPE .TEMP> <2 .FS>>
				       <==? ANY <2 .FS>>
				       <AND <==? FLOAT <2 .FS>>
					    <TYPE? .TEMP FIX>
					    <SET TEMP <FLOAT .TEMP>>>>>
			      <RETURN .TEMP>)
			     (<AND <SET TYPE-NAME <PARSE <1 .OKSYN>>>
				   <==? <TYPE .TEMP> .TYPE-NAME>>
			      <RETURN .TEMP>)>
		       <SET OKSYN <REST .OKSYN>>>>)>>>

<DEFINE HELP-MESSAGE ("AUX" BFR L) 
   #DECL ((BFR) STRING (L LEN) FIX (BITP) WORD (VALUE) <OR ATOM FALSE>
	  (HELPSW) <OR FALSE ATOM CHANNEL> (P2) <VECTOR STRING STRING>
	  (SYN) <VECTOR [REST STRING]>)
   <COND (.READER-SILENCE T)
	 (<NOT <0? .LEN>>
	  <SET BITP #WORD *000000000000*>
	  <RUBOUTCHR .XTRCHR>
	  <SET XTRCHR 0>
	  <>)
	 (<AND .HELPSW
	       <NOT <EMPTY? <2 .P2>>>
	       <OR <TYPE? .HELPSW CHANNEL>
		   <SET HELPSW <OPEN "READ" <2 .P2>>>>>
	  <SET BFR <ISTRING 50>>
	  <REPEAT ()
		  <COND (<0? <SET L
				  <READSTRING .BFR
					      .HELPSW
					      <STRING <ASCII 12> <ASCII 3>>>>>
			 <RETURN T>)>
		  <PRINTSTRING .BFR .OUTCHAN .L>
		  <COND (<L? .L 50> <RETURN T>)>>
	  <COND (<NOT <READCHR .HELPSW <>>>
		 <CLOSE .HELPSW>
		 <TERPRI>
		 <PRINC "End of help message.">
		 <TERPRI>
		 <SET HELPSW <>>)>
	  T					       ;"don't print the break")
	 (ELSE <TPROMPT <1 .P2> .SYN T <>> <SET HELPSW T>)>>

<DEFINE WINNER-TAKE-ALL () 
	#DECL ((MODE XTRCHR LEN) FIX (TABLE) <OR VECTOR SYMTABLE> (BFR) STRING
	       (SYN) VECTOR (SYNSW) <OR ATOM FALSE>)
	<COND (<AND <0? .MODE>
		    <MEMBER "MULT" .SYN>
		    <SET WINLIST <APPLY ,<1 .TABLE> 6 .BFR .LEN <2 .TABLE>>>
		    <NOT <EMPTY? .WINLIST>>>
	       <CHECK-FOR-MULT>
	       <BACKUPRUB>
	       <COMMAPRINT .WINLIST <>>
	       <RETURN <CHTYPE .WINLIST SPLICE> .LEAVE-READER>)>>

<DEFINE DO-POSSYM () 
	#DECL ((MODE XTRCHR LEN) FIX (TABLE) <OR VECTOR SYMTABLE>
	       (BFR P1) STRING (SYN) VECTOR (SYNSW) <OR ATOM FALSE>
	       (VALUE) ATOM)
	<COND (.READER-SILENCE)
	      (<0? .MODE>
	       <APPLY ,<1 .TABLE>
		      3
		      .BFR
		      .LEN
		      <2 .TABLE>
		      "
The following are possible:
"
		      "No symbol begins like that.">
	       <TERPRI>
	       <TPROMPT .P1 .SYN .SYNSW>
	       <PRINTSTRING .BFR .OUTCHAN .LEN>
	       <SET XTRCHR 0>)
	      (ELSE
	       <TERPRI>
	       <PRINC "Symbol input not currently available.">
	       <TERPRI>)>
	T>

<DEFINE CHECK-FOR-MULT () 
	#DECL ((SYN) <VECTOR [REST STRING]> (MULTSW) ATOM (VALUE) FALSE)
	<COND (<MEMBER "MULT" .SYN> <SET .MULTSW T> <>)>>

<DEFINE RUBOUT-ONE () 
	#DECL ((LEN XTRCHR) FIX (P1 BFR) STRING (SYN) VECTOR
	       (SYNSW) <OR ATOM FALSE> (VALUE) ATOM)
	<COND (<0? .LEN> <SET XTRCHR 0> <TPROMPT .P1 .SYN .SYNSW>)
	      (ELSE
	       <RUBOUTCHR .XTRCHR>
	       <RUBOUTCHR <NTH .BFR .LEN>>
	       <SET XTRCHR 0>
	       <SET LEN <- .LEN 1>>)>
	T>

<DEFINE RETYPE-BUFFER (PRE) 
	#DECL ((PRE SYNSW) <OR ATOM FALSE> (P1 BFR) STRING (SYN) VECTOR
	       (LEN XTRCHR) FIX (VALUE) ATOM)
	<COND (<NOT .READER-SILENCE>
	       <COND (.PRE <TERPRI>)
		     (,TENEX <PRINC <ASCII 12>>)		    ;"form-feed"
		     (ELSE <PRINC "C">)>		     ;"ITS clear-screen"
	       <TPROMPT .P1
			.SYN
			.SYNSW
			T
			<AND <ASSIGNED? MULTLIST> .MULTLIST>>
	       <PRINTSTRING .BFR .OUTCHAN .LEN>
	       <SET XTRCHR 0>)>
	T>

<DEFINE QUOTE-NEXT-CHAR ("AUX" (XBFR <ISTRING 1 !\ >) I J) 
	#DECL ((XBFR) STRING (I BITP) WORD (J XTRCHR) FIX (BREAK) CHARACTER
	       (VALUE) ATOM)
	<RTOBRK CURXCHAN I J .XBFR .XTRCHR "">
	<PUT .BFR <SET LEN <+ .LEN 1>> <1 .XBFR>>
	<SET XTRCHR 0>
	T>

<DEFINE INPUT-FROM-TTY () 
	#DECL ((P1 BFR) STRING (SYN) VECTOR
	       (SYNSW READER-SILENCE XFPOPSW) <OR ATOM FALSE> (LEN) FIX
	       (VALUE) ATOM)
	<XFPUSH <>>
	<TPROMPT .P1 .SYN .SYNSW>
	<PRINTSTRING .BFR .OUTCHAN .LEN>
	<SET XFPOPSW T>
	T>

<SET BREAKS '" 	î! ">

<DEFINE COMMAPRINT (STRUC "OPTIONAL" (TERP!-INITIAL T)) 
	#DECL ((STRUC) STRUCTURED)
	<AND .TERP!-INITIAL <TERPRI>>
	<MAPF <>
	      <FUNCTION (X) 
		      <COND (<TYPE? .X SYMBOL> <PRINC <1 .X>>) (<PRIN1 .X>)>
		      <PRINC ", ">>
	      .STRUC>>

<DEFINE BLTREST (TOVEC "TUPLE" FROMTUP) 
	#DECL ((TOVEC VALUE) <OR TUPLE <PRIMTYPE VECTOR>> (FROMTUP) TUPLE)
	<BLT .FROMTUP .TOVEC>>

<ENDPACKAGE>
