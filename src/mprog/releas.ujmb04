<TITLE	RELEASE>				; "TELLS MUDDLE TO RECYCLE THIS OBJECT"
<DECLARE ("VALUE" ATOM <OR <PRIMTYPE LIST>
			   <PRIMTYPE VECTOR>
			   <PRIMTYPE UVECTOR>
			   <PRIMTYPE STRING>
			   <PRIMTYPE BYTES>>)>

	<DPUSH	TP*	(AB)>
	<PUSHJ	P*	RL>
	<JRST	FINIS>

	<INTERNAL-ENTRY RL 1>
	<SUBM	M*	(P)>
	<HLRZ	A*	-1	(TP)>		; "GET STORAGE ALLOCATION TYPE"
	<PUSHJ	P*	SAT>
	<CAIN	A*	2>			; "IS IT A LIST?"
	<JRST	PUTLST>
	<CAIE	A*	16>			; "IS IT A STRING OR BYTES?"
	<CAIN	A*	12>
	<JRST	PUTSTR>
	<HLRE	A*	(TP)>			; "NEGATIVE NUMBER OF WORDS"
	<HRRZ	B*	(TP)>
	<SUBI	B*	-1	(A)>		; "SECOND DOPE WORD"
PUTVEC	<HRRZ	C*	RCLV>			; "FRONT OF CURRENT RECYCLE CHAIN"
	<HRRM	C*	(B)>			; "CONS VECTOR ONTO IT"
	<HRRZM	B*	RCLV>			; "REPLACE CHAIN POINTER"
	<HLRZ	A*	(B)>			; "GET NUMBER OF WORDS"
	<SUBI	A*	2>			; "SUBTRACT DOPE WORDS"
	<JUMPE	A*	DONE>			; "EMPTY?"
	<SUBI	B*	2>			; "LAST NON-DOPE WORD"
ZERO	<SETZM	(B)>				; "CLEAR VECTOR"
	<SOJ	B*>
	<SOJN	A*	ZERO>
DONE	<SUB	TP*	[<(2) 2>]>
	<MOVSI	A*	<TYPE-CODE ATOM>>
	<MOVE	B*	<MQUOTE T>>
	<JRST	MPOPJ>

PUTSTR	<MOVE	B*	(TP)>
	<LDB	B*	[<(*300600*) B>]>	; "GET BYTE SIZE"
	<MOVEI	A*	36>
	<IDIV	A*	B>			; "BYTES PER WORD"
	<HRRZ	B*	-1	(TP)>		; "NUMBER OF BYTES"
	<ADDI	B*	-1	(A)>
	<IDIV	B*	A>			; "NUMBER OF WORDS"
	<MOVE	C*	(TP)>			; "GET BYTE POINTER"
	<IBP	C>				; "INCREMENT IT TO POINT TO FIRST WORD"
	<ADDI	B*	1	(C)>		; "ADD # WORDS +1 TO FIND 2ND DOPE WORD"
	<JRST	PUTVEC>

PUTLST	<HRRZ	A*	(TP)>
	<JUMPE	A*	DONE>
	<MOVE	B*	A>
NXTCEL	<HRRZ	C*	(B)>			; "GET POINTER TO FOLLOWING CELL"
	<HLRZ	D*	(B)>			; "GET TYPE-CODE OF NEXT CELL"
	<CAIN	D*	TDEFER>			; "IF A DEFER, PICKUP STRING POINTER"
	<JRST	GOTDFR>
NXTCL1	<JUMPE	C*	LSTCEL>			; "LAST CELL?"
	<MOVE	B*	C>
	<JRST	NXTCEL>
GOTDFR	<HRRZ	D*	1	(B)>		; "GET POINTER TO STRING POINTER"
	<HRRM	D*	(B)>			; "MUNG DEFER TO POINT TO SP"
	<HRRM	C*	(D)>			; "MUNG SP TO POINT TO NEXT CELL"
	<MOVE	B*	C>
	<SKIPE	C>
	<JRST	NXTCEL>
	<MOVE	B*	D>
LSTCEL	<MOVE	C*	RCL>			; "FRONT OF CURRENT RECYCLE LIST"
	<HRRM	C*	(B)>			; "CONS LIST ONTO IT"
	<MOVEM	A*	RCL>			; "REPLACE LIST POINTER"
	<JRST	DONE>
