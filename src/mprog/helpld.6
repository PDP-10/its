
<BLOCK (<ROOT>)>

<NEWTYPE INTERNAL-RSUBR WORD>

<NEWTYPE INTERNAL-RSUBR-TABLE UVECTOR>

<ENDBLOCK>

<FLOAD "BTB;LOC NBIN">

<FLOAD "BTB;SUBLOD NBIN">

<DEFINE INTERNAL-RSUBR (NAME NUMARGS "OPTIONAL" (FLG <LOOKUP "COMPILE" <ROOT>>)
					"AUX" RSB NN
					(RNM <CHTYPE <ANDB .NUMARGS *777777*> FIX>)) 
	#DECL ((NAME) ATOM (NUMARGS) FIX)
	<COND (.FLG <BUILD-INTERNAL-RSUBR <RGLOC .NAME T> .NUMARGS>)
	      (<AND <GASSIGNED? .NAME>
		    <TYPE? <SET RSB ,.NAME> RSUBR RSUBR-ENTRY>>
	       <COND (<MEMBER "TUPLE" <3 .RSB>>
		      <SET NN 1>)
		     (ELSE
		      <SET NN <LENGTH <MEMBER "OPTIONAL" <3 .RSB>>>>
		      <COND (<0? .NN><SET NN 1>)>)>
	       <COND (<TYPE? .RSB RSUBR> <RSUBR-HACK .RSB .RNM 0 .NAME .NN>)
		     (<RSUBR-HACK <1 .RSB> .RNM <ENTRY-LOC .RSB> .NAME .NN>)>)
	      (<ERROR BAD-CALL-TO-INTERNAL-RSUBR!-RSUBRS>)>>

<DEFINE RSUBR-HACK (RSUBR ARGNUM STARTLOC NAME NN "AUX" OBJ) 
   #DECL ((RSUBR) RSUBR (ARGNUM NN STARTLOC) FIX)
   <COND
    (<AND <==? <LENGTH .RSUBR> 4>
	  <TYPE? <SET OBJ <4 .RSUBR>> INTERNAL-RSUBR-TABLE>
	  <==? <UTYPE .OBJ> FIX>>
     <SET STARTLOC <+ .STARTLOC <LOC <1 .RSUBR>>>>
     <REPEAT ()
       <COND (<L=? .STARTLOC
		   <CHTYPE
		    <GETBITS <MOVIT <CHTYPE <GETBITS <1 .OBJ> <BITS 18>> FIX>>
			     <BITS 18>>
		    FIX>>
	      <RETURN>)>
       <COND (<EMPTY? <SET OBJ <REST .OBJ>>>
	      <ERROR BAD-CALL-TO-INTERNAL-RSUBR!-ERRORS>)>>
     <REPEAT (WRD)
       <COND
	(<==? <CHTYPE <GETBITS <SET WRD <1 .OBJ>> <BITS 18 18>> FIX> .ARGNUM>
	 <RETURN <BUILD-INTERNAL-RSUBR
		  <RGLOC .NAME> <CHTYPE <GETBITS .WRD <BITS 18>> FIX>>>)>
       <COND (<OR <0? <SET NN <- .NN 1>>>
		  <EMPTY? <SET OBJ <REST .OBJ>>>>
	      <ERROR BAD-CALL-TO-INTERNAL-RSUBR!-ERRORS>)>>)
    (<ERROR BAD-CALL-TO-INTERNAL-RSUBR!-ERRORS>)>>

<DEFINE IRSUBR-PRINT (ITM "AUX" VAL ITEM) 
	#DECL ((ITEM ITM) <PRIMTYPE WORD>)
	<SET VAL <GET-LOCATION .ITM>>
	<COND (<OR <L? <SET ITEM <CHTYPE <GETBITS .ITM <BITS 18>> FIX>> 200>
		   <AND <==? .ITEM *777777*> <SET ITEM -1>>>
	       <PRINC "%<INTERNAL-RSUBR ">
	       <PRIN1 <SPEC-GET-LOC .ITM>>
	       <PRINC " ">
	       <PRIN1 .ITEM>
	       <PRINC ">">)
	      (<TYPE? .VAL RSUBR>
	       <PRINT-INTERNAL-RSUBR <2 .VAL> .ITEM .VAL 0>)
	      (<TYPE? .VAL RSUBR-ENTRY>
	       <PRINT-INTERNAL-RSUBR <2 .VAL>
				     .ITEM
				     <1 .VAL>
				     <ENTRY-LOC .VAL>>)
	      (<ERROR CANT-PRINT-INTERNAL-RSUBR!-ERRORS>)>>

<DEFINE PRINT-INTERNAL-RSUBR (NAME ADDR RSUBR STARTLOC "AUX" OBJ) 
   #DECL ((NAME) ATOM (ADDR) FIX (RSUBR) RSUBR)
   <COND (<AND <==? <LENGTH .RSUBR> 4>
	       <TYPE? <SET OBJ <4 .RSUBR>> INTERNAL-RSUBR-TABLE>
	       <==? <UTYPE .OBJ> FIX>>
	  <REPEAT ()
		  <COND (<L=? .STARTLOC
			      <CHTYPE <GETBITS <1 .OBJ> <BITS 18>> FIX>>
			 <RETURN>)>
		  <COND (<EMPTY? <SET OBJ <REST .OBJ>>>
			 <ERROR BAD-CALL-TO-INTERNAL-RSUBR!-ERRORS>)>>
	  <REPEAT (WRD)
		  <COND (<==? <CHTYPE <GETBITS <SET WRD <1 .OBJ>> <BITS 18>>
				      FIX>
			      .ADDR>
			 <PRINC "%<INTERNAL-RSUBR ">
			 <PRIN1 .NAME>
			 <PRINC !" >
			 <PRIN1 <CHTYPE <GETBITS .WRD <BITS 18 18>> FIX>>
			 <PRINC !">>
			 <RETURN>)>
		  <COND (<EMPTY? <SET OBJ <REST .OBJ>>>
			 <ERROR CANT-PRINT-INTERNAL-RSUBR!-ERRORS>)>>)
	 (<ERROR CANT-PRINT-INTERNAL-RSUBR!-ERRORS>)>>

<PRINTTYPE INTERNAL-RSUBR ,IRSUBR-PRINT>
