;"RSUBR FOR MAPPING PAGES"

<TITLE CORE-BLOCK>		;"MAP A PAGE"
	<DECLARE ("VALUE" <OR FIX FALSE> <PRIMTYPE WORD> FIX FIX FIX FIX)>
	<*CALL	CORE>
	<JRST	FAIL>
	<MOVSI	A* TFIX>
	<MOVE	B* (AB) 5>	;"CORE BLOCK NUMBER"
	<LSH	B* 10>		;"CHANGE TO ADDRESS"
	<JRST	FINIS>

FAIL	<MOVE	C* <TYPE-WORD FIX>>
	<MOVEI	E* 0>
	<PUSHJ	P* CICONS>
	<MOVSI	A* <TYPE-CODE FALSE>>
	<JRST	FINIS>

CORE	<SETZ>
	<SIXBIT "CORBLK">
	<(AB) 1>		;"MODE"
	<(AB) 3>		;"INTO"
	<(AB) 5>
	<(AB) 7>		;"FROM"
	<(AB) 9>
	<SETZB D>		; "ERROR"

;"RSUBR TO FIND FREE CORE PAGES"

<TITLE	PAGE-FIND>
	<DECLARE ("VALUE" <OR FIX FALSE> "OPTIONAL" FIX)>
	<MOVEI	A* 1>		;"DEFAULT NUMBER OF PAGES TO FIND"
	<SKIPGE	AB>		;"ARG SUPPLIED ?"
	<MOVE	A* (AB) 1>	;"YES, GET IT"
	<JUMPLE	A* WRONGT>
	<PUSHJ	P* PGFIND>
	<JUMPL	B* [<MOVSI A* TFALSE> <SETZ B*> <JRST FINIS>]>
	<MOVSI	A* TFIX>
	<JRST	FINIS>

;"RSUBR TO GIVE BACK CORE PAGES"

<TITLE	PAGE-GIVE>
	<DECLARE ("VALUE" FIX FIX "OPTIONAL" FIX)>
	<MOVEI	A* 1>		;"DEFAULT NUMBER OF PAGES TO GIVE"
	<CAMGE	AB* [<(-2)>]>	;"SECOND ARG SUPPLIED ?"
	<MOVE	A* (AB) 3>	;"YES, GET IT"
	<JUMPLE	A* WRONGT>
	<MOVE	B* (AB) 1>	;"GET NUMBER OF FIRST BLOCK"
	<JUMPL	B* WRONGT>
	<MOVEI	O* (B)>
	<ADDI	O* (A)>
	<CAIGE	B* 256>		;"VALID FIRST BLOCK NUMBER ?"
	<CAILE	O* 256>		;"VALID LAST+1 BLOCK NUMBER ?"
	<JRST	WRONGT>		;"NO, LOSE"
	<PUSHJ	P* PGGIVE>
	<MOVSI	A* TFIX>
	<JRST	FINIS>
