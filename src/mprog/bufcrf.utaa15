<TITLE IOT>
	<DECLARE ("VALUE" CHARACTER "OPTIONAL" <PRIMTYPE WORD> CHANNEL CHARACTER)>
	<HLRE	A* AB>
	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<ASH	A* -1>
	<ADDI	A* TABEND>
	<PUSHJ	P* @ (A)>
	<JRST	FINIS>
	<IOT3>
	<IOT2>
	<IOT1>
TABEND	<IOT0>

<INTERNAL-ENTRY IOT0 0>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* [0]>
<INTERNAL-ENTRY IOT1 1>
	<MOVE	A* <MQUOTE <RGLOC INCHAN>>>
	<ADD	A* GLOTOP 1>
	<DPUSH	TP* (A)>
<INTERNAL-ENTRY IOT2 2>
	<PUSH	TP* <TYPE-WORD CHARACTER>>
	<PUSH	TP* [0]>
<INTERNAL-ENTRY IOT3 3>
	<SUBM	M* (P)>
	<MOVE	A* -2(TP)>		; "CHANNEL"
	<MOVE	A* 1(A)>
	<AOSN	INTFLG>
	 <JSR	LCKINT>			; "ENABLE INTERRUPTS"
	<*CALL	[<SETZ>
		 <SIXBIT "IOT">
		 <MOVS	-4(TP)>		; "CONTROL BITS"
		 <MOVE	A>		; "CHANNEL"
		 <SETZ	(TP)>]>		; "LOCATION OF TRANSFER"
	 <*LOSE	*1400*>
	<SETZM	INTFLG>			; "DISABLE INTERRUPTS"
	<POP	TP* B>
	<POP	TP* A>
	<SUB	TP* [<(4) 4>]>
	<JRST	MPOPJ>
<END>

<TITLE SIOT>
	<DECLARE ("VALUE" STRING STRING "OPTIONAL" <PRIMTYPE WORD> CHANNEL FIX)>
	<HLRE	A* AB>
	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<ASH	A* -1>
	<ADDI	A* TABEND>
	<PUSHJ	P* @ 1(A)>
	<JRST	FINIS>
	<SIOT4>
	<SIOT3>
	<SIOT2>
TABEND	<SIOT1>

<INTERNAL-ENTRY SIOT1 1>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* [0]>
<INTERNAL-ENTRY SIOT2 2>
	<MOVE	A* <MQUOTE <RGLOC OUTCHAN>>>
	<ADD	A* GLOTOP 1>
	<DPUSH	TP* (A)>
<INTERNAL-ENTRY SIOT3 3>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* [-1]>
<INTERNAL-ENTRY SIOT4 4>
	<SUBM	M* (P)>
	<MOVE	A* -2(TP)>		; "CHANNEL"
	<MOVE	A* 1(A)>		; "CHANNEL #"
	<SKIPGE	C* (TP)>		; "CHARS TO PRINT"
	 <HRRZ	C* -7(TP)>		; "LENGTH, IF # IS <0"
	<MOVEM	C* (TP)>		; "SAVE IT AWAY"
HACK	<MOVE	B* -6(TP)>		; "BPTR"
	<MOVEI	D* INTFROB>
	<HRLI	D* (<PUSHJ P*>)>
	<MOVEM	D* ONINT>		; "SET UP INTERRUPT RECOVERY"
	<AOSN	INTFLG>
	 <JRST	LCKINT>			; "ENABLE INTERRUPTS"
	<*CALL	[<SETZ>
		 <SIXBIT "SIOT">
		 <MOVS	-4(TP)>		; "CONTROL BITS"
		 <MOVE	A>		; "CHANNEL"
		 <MOVE	B>		; "BPTR"
		 <SETZ	C>]>		; "LENGTH"
	 <*LOSE	*1400*>
	<SETZM	INTFLG>			; "DISABLE INTERRUPTS"
	<SETZM	ONINT>
	<MOVE	A* -7(TP)>
	<MOVE	B* -6(TP)>
	<SUB	TP* [<(8) 8>]>
	<JRST	MPOPJ>

; "PUSHJ'ED TO BY INSN IN ONINT"
INTFROB	<PUSH	P* A>
	<PUSH	P* B>
	<MOVE	B* (TP)>
	<MOVEM	C* (TP)>		; "NUMBER OF CHARACTERS TO PRINT"
	<SUB	B* C>			; "NUMBER OF CHARACTERS PRINTED"
	<HRRZ	A* -7(TP)>		; "STRING LENGTH"
	<SUBI	A* (B)>			; "REST OFF PRINTED CHARS"
	<HRRM	A* -7(TP)>		; "SAVE AWAY LENGTH"
	<POP	P* B>
	<MOVEM	B* -6(TP)>		; "AND BPTR"
	<JUMPLE	A* INTOUT>
	<MOVEI	A* HACK>
	<HRRM	A* LCKINT>		; "SET THIS BACK UP WHEN WE GET OUT OF INT"
INTOUT	<SETZM	ONINT>
	<POP	P* A>
	<POPJ	P*>
<END>

<TITLE RCPOS>
	<DECLARE ("VALUE" FIX "OPTIONAL" CHANNEL)>
	<JUMPGE	AB* NOARG>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* IRCPOS1>
	<JRST	FINIS>
NOARG	<PUSHJ	P* IRCPOS0>
	<JRST	FINIS>

<INTERNAL-ENTRY IRCPOS0 0>
	<MOVE	A* <MQUOTE <RGLOC INCHAN>>>
	<ADD	A* GLOTOP 1>
	<DPUSH	TP* (A)>
<INTERNAL-ENTRY IRCPOS1 1>
	<SUBM	M* (P)>
	<MOVE	A* (TP)>
	<*CALL	[<SETZ>
		 <SIXBIT "RCPOS">
		 <MOVE	1(A)>
		 <SETZM	B>]>
	 <*LOSE	*1400*>
	<MOVSI	A* <TYPE-CODE FIX>>
	<SUB	TP* [<(2) 2>]>
	<JRST	MPOPJ>
<END>
î
<TITLE TTY-GET>
	<DECLARE ("VALUE" <UVECTOR [REST <PRIMTYPE WORD>]> UVECTOR "OPTIONAL" CHANNEL)>
	<HLRE	A* AB>
	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<ASH	A* -1>
	<ADDI	A* TABEND>
	<PUSHJ	P* @ 1(A)>
	<JRST	FINIS>
	<TTYGET2>
TABEND	<TTYGET1>

<INTERNAL-ENTRY TTYGET1 1>
	<MOVE	A* <MQUOTE <RGLOC INCHAN>>>
	<ADD	A* GLOTOP 1>
	<DPUSH	TP* (A)>
<INTERNAL-ENTRY TTYGET2 2>
	<SUBM	M* (P)>
	<MOVE	A* (TP)>
	<MOVE	A* 1(A)>		; "CHANNEL NUMBER"
	<MOVEI	B* 0>
	<MOVE	C* -2(TP)>		; "UVECTOR OF OUTPUT"
	<MOVE	D* [<SIXBIT "TTYGET">]>
	<PUSHJ	P* DOCALL>
	<MOVE	A* -3(TP)>
	<MOVE	B* -2(TP)>
	<SUB	TP* [<(4) 4>]>
	<JRST	MPOPJ>

<SUB-ENTRY TTY-SET ("VALUE" UVECTOR UVECTOR "OPTIONAL" CHANNEL)>
	<HLRE	A* AB>
	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<ASH	A* -1>
	<ADDI	A* TABEND1>
	<PUSHJ	P* @ 1(A)>
	<JRST	FINIS>
	<TTYSET2>
TABEND1	<TTYSET1>

<INTERNAL-ENTRY TTYSET1 1>
	<MOVE	A* <MQUOTE <RGLOC INCHAN>>>
	<ADD	A* GLOTOP 1>
	<DPUSH	TP* (A)>
<INTERNAL-ENTRY TTYSET2 2>
	<SUBM	M* (P)>
	<MOVE	A* (TP)>
	<MOVE	A* 1(A)>		; "CHANNEL #"
	<MOVE	B* -2(TP)>		; "INPUTS"
	<MOVEI	C* 0>			; "NO OUTPUT"
	<MOVE	D* [<SIXBIT "TTYSET">]>
	<PUSHJ	P* DOCALL>
	<MOVE	A* -3(TP)>
	<MOVE	B* -2(TP)>
	<SUB	TP* [<(4) 4>]>
	<JRST	MPOPJ>

; "DO A SYSTEM CALL.  TAKES CHANNEL # IN A, INPUT UVECTOR IN B, OUTPUT
UVECTOR IN C, CALL NAME IN D."
DOCALL	<SUBM	M* (P)>
	<MOVEI	O* 0>
	<PUSH	P* [<SETZ>]>		; "START CALL BLOCK"
	<MOVEI	E* (P)>			; "ADDRESS OF BLOCK"
	<PUSH	P* D>			; "CALL NAME"
	<HRLI	A* (<MOVEI>)>		; "MAKE CHANNEL BE IMMEDIATE"
	<PUSH	P* A>			; "CHANNEL ARG"
	<ADDI	O* 3>			; "NUMBER THINGS SO FAR"
	<MOVEI	A* B>
	<HRLI	A* (<MOVE>)>
	<PUSHJ	P* FROBUV>		; "PUSH IT"
	<MOVEI	A* C>
	<HRLI	A* (<MOVEM>)>
	<PUSHJ	P* FROBUV>
	<PUSH	P* [<SETZB A>]>		; "RANDOM"
	<ADDI	O* 1>			; "NUMBER OF THINGS PUSHED"
	<*CALL	(E)>			; "DO THE CALL"
	 <*LOSE	*1400*>
	<HRLS	O>
	<SUB	P* O>			; "FLUSH THE CALL BLOCK AND SUCH"
	<JRST	MPOPJ>

; "LOCN OF UV IN RH A, CODE IN LH"
FROBUV	<SUBM	M* (P)>
	<HLRE	F* (A)>			; "GET LENGTH"
	<JUMPGE	F* MPOPJ>		; "FLUSH IF 0"
	<POP	P* D>			; "RETURN ADDRESS"
	<PUSH	P* A>
	<HRLZS	(P)>
	<HLLZS	A>
	<IOR	A* (P)>
	<SUB	P* [<(1) 1>]>		; "BUILD <MOVE (n)>"
PUSHLP	<PUSH	P* A>
	<ADDI	A* 1>			; "MAKE IT BE <MOVE N(n)>"
	<ADDI	O* 1>			; "NUMBER PUSHED"
	<AOJL	F* PUSHLP>
	<SUBM	M* D>
	<JRST	@ D>
<END>