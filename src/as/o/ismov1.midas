;
; ISMOV1
;
RELOCATABLE
TITLE ISMOV1
.INSRT C;NC INSERT
.INSRT C;NM INSERT

CENTRY ISMOV1,[BB,CC,P1]

DD==3
P2==2
K==5

	MOVEI	DD,ZDIRLI"		; DIRECTION LIST
NXTDIR:	MOVE	D,(DD)			; NEXT DIRECTION
	ADDI	DD,1			; REST OF LIST
	JUMPE	D,LOSE			; IF NO MORE DIRECTIONS
	MOVE	P2,P1			; INITIAL POSITION
	SETZ	K,			; COUNTER
NXTPOS:	ADD	P2,D			; MOVE 1 STEP
	SKIPG	ZSQVAL"(P2)		; STILL ON THE BOARD?
	 GO	NXTDIR			; NO
	MOVE	A,BB			; THE BOARD
	ADDI	A,(P2)			; ADDRESS OF SQUARE AT P2
	MOVE	A,(A)			; CONTENTS OF SQUARE AT P2
	JUMPE	A,NXTDIR		; IT'S EMPTY
	CAME	A,CC			; IS IT OUR COLOR?
	 AOJA	K,NXTPOS		; NO, COUNT TOKEN AND GO TO NEXT POSITION
	JUMPE	K,NXTDIR		; DID NOT PASS OVER ANY OPPONENT
	MOVEI	A,1			; WIN!
RET:	RETURN
LOSE:	MOVEI	A,0
	GO	RET
END
