title taraka system start up dragon
version==.fnam2
.mllit==1

a=1
b=2
c=3

diric==4
dskic==5
usrch==6

.insrt syseng;fsdefs >


taraka:
ifn 0,[	.call [	setz
		sixbit/sstatu/
		movem a
		setzm b ]	;b:=SYSDBG
	 .value
	jumpge b,tarak0
	movei a,60.
	.sleep a,
	jrst taraka
]
tarak0:	.call [	setz
		sixbit /login/
		[sixbit /taraka/]
		setzi 0]
	 jfcl
	.suset [.ssname,,[sixbit /channa/]]
	.open diric,[.bii,,'dsk
		    sixbit /.file./
		    sixbit /(dir)/]
	 jsr die
	.iot diric,[-2000,,ufd]
	.close diric,
	move a,ufd+udnamp	;get ptr to name area
	addi a,ufd
dr1:	cail a,ufd+2000
	 jsr die
	move b,unfn1(a)
	camn b,[sixbit /rakash/]
	 .call [setz
		sixbit /open/
		[.uii,,dskic]
		[sixbit /dsk/]
		[sixbit /rakash/]
		unfn2(a)
		setz [sixbit /channa/]]
	  jrst nxt
	.call [	setz
		sixbit /open/
		[1,,usrch]
		[sixbit /usr/]
		1000,,0
		setz unfn2(a)]
	 jrst nxt
	.reset usrch,
	.call [	setz
		sixbit /load/
		1000,,usrch
		setzi dskic]
	 jrst nxc
	MOVE B,UNFN2(A)
	.USET USRCH,[.SSNAME,,B]
	.iot dskic,b
	hrrzs b
	.uset usrch,[.supc,,b]
	.CALL [ SETZ
		'DISOWN
		MOVEI USRCH
		405000,,5 ]	;NO APRC.4.9, .USTP=0
	 .VALUE	;HMM
	caia
nxc:	 .uclose usrch,
nxt:	.close dskic,
	.close usrch,
	addi a,lunblk
	jrst dr1

die:	0
	.logout
	.value

ufd:	block 2000

end taraka
