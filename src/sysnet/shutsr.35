	TITLE SHUTDOWN SERVER

;This Chaosnet node is allowed to shut down.
IFNDEF ALLOW,ALLOW==177001

A==1
P==17

CHI=0
CHO=1

.INSRT SYSTEM;CHSDEF

GO:	.CLOSE 1,

	.SUSET [.SJNAME,,['SHUTDN]]

	;Open Chaosnet channel and listen for SHUTDN connections.
	.CALL [SETZ ? 'CHAOSO ? MOVEI CHI ? MOVEI CHO ? SETZI 5]
	 .LOSE %LSSYS
	.CALL [SETZ ? 'PKTIOT ? MOVEI CHO ? SETZI LSN]
	 .LOSE %LSSYS

	;Wait for an RFC.
	MOVEI A,60.*60.
	.CALL [SETZ ? 'NETBLK ? MOVEI CHI ? MOVEI %CSLSN ? A ? SETZM A]
	 .LOSE %LSSYS
	CAIE A,%CSRFC
	 .LOGOUT 1,

	;Receive the RFC packet and send an ANS response.
	.CALL [SETZ ? 'PKTIOT ? MOVEI CHI ? SETZI PACKET]
	 .LOSE %LSSYS
	.CALL [SETZ ? 'PKTIOT ? MOVEI CHO ? SETZI ANS]
	 .LOSE %LSSYS

	;Check the source address.
	LDB A,[$CPKSA PACKET]
	CAME A,[ALLOW]
	 .LOGOUT 1,

	;Patch ITS to remove the lower bound on shutdown time.
	MOVE A,[SQUOZE 0,ASHUTD]
	.EVAL A,
	 .LOSE %LSSYS
	ADDI A,3
	HRLI A,[CAIA]
	.SETLOC A,
	MOVEI A,30.
	.SLEEP A,

	;Shut down quickly.
	MOVEI A,30.
	.SHUTDN A,
	 .LOSE %LSSYS
	.LOGOUT 1,

LSN:	.BYTE 8 ? %COLSN ? 0 ? 0 ? 8 ? .BYTE
LOC LSN+%CPKDT
	.BYTE 8 ? "S ? "H ? "U ? "T ? "D ? "O ? "W ? "N ? .BYTE
ANS:	.BYTE 8 ? %COANS ? .BYTE
PACKET:	BLOCK %CPMXW

END GO
