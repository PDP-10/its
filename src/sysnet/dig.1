; -*- Mode: Midas -*-

title DIG in the DNS for info

X=0		;Super temporary register.
A=1		;General 
B=2		; purpose 
C=3		; utility 
D=4
E=5
F=6		;Flag register.
T=7		;Temporary.
TT=10		;Temporary, T+1.
oc=:12
u1=:13
u2=:14
u3=:15
u4=:16

P=17		;Stack pointer.

tyoc==1
out"$$OERR==1			;OUT: include error code
errchn=2			;define error channel
DQCH==14			;for resolv

ascbp==440700

.insrt ksc;macros >
$$DQCH==1
$$DQRN==1
$$DQDQ==0			;don't use DQ:, use DOMAIN:
.insrt sysnet;resolv >
.insrt syseng;t20mac >
.insrt klh;out

DEFINE ZAP LOC,LEN
SETZM LOC
MOVE T,[LOC,,LOC+1]
BLT T,LOC+LEN-1
TERMIN

typebk:	setz
	sixbit /SIOT/
	c ? t ? setz tt

PDLSIZ==120			; Stack size
PDL:	BLOCK PDLSIZ		; Push down stack
jcl:	block 100
name:	block 100

;; required by OUT
autpsy:	0
	sos t,autpsy
	hrlz t,t
	hrri t,%lsfil
	syscal lose,[t ? autpsy]
	 trn
	.value
	.logout 1,

;; Get JCL
getjcl:	zap jcl,100
	zap name,100
	pushae p,[t,tt,a]
	move tt,[ascbp,,jcl]
	move t,[ascbp,,[asciz "DOMAIN:NQUERY;"]]
	do.
	 ildb a,t
	 skipn a
	  exit.
	 idpb a,tt
	 loop.
	enddo.
	.break 12,[..rjcl,,name]
	skipn name
	 jrst gotjcl
	move t,[ascbp,,name]
	do.
	 ildb a,t
	 cain a,^M
	  setz a,		;zap CR
	 idpb a,tt
	 skipe a
	  loop.
	enddo.
	aos -3(p)
gotjcl::
	popae p,[a,tt,t]
	ret

go:	MOVE P,[-pdlsiz,,pdl]
	.open tyoc,[.uao,,'TTY]
	 .lose %lsfil
	out(tyoc,open(UC$IOT))
	out(,ch(tyoc))
	call getjcl
	 jrst usage
	out(,("Looking for "),tz(name),eol)
	move a,[ascbp,,jcl]
	syscal sopen,[[.uai,,dqch] ? a ? %clerr,,e]	;open domain
	ifnsk.
;;	 .lose %lsfil
	;; Interpret error codes specially
	 setz tt,
	 cain e,%ensfl
	  movei tt,[asciz "Name error"]
	 cain e,%ensjb
	  movei tt,[asciz "Resource not found"]
	 cain e,%enrdv
	  movei tt,[asciz "Server not available"]
	 cain e,%enadv
	  movei tt,[asciz "Local database problem"]
	 cain e,%ebdfn
	  movei tt,[asciz "Format error"]
	 cain e,17		;%ENADR
	  movei tt,[asciz "Unknown class"]
	 cain e,%ensdr
	  movei tt,[asciz "Unknown type"]
	 cain e,%enapk
	  movei tt,[asciz "Authoritative data unavailable"]
	 skipe tt
	 ifnsk.
	   hrli tt,ascbp
	   out(,("Error from DNS: "),tpz(tt),eol)
	   caie e,%ebdfn
	   ifskp.
usage::	     out(,("Usage: :DIG class;type;name"),eol,("  e.g. :DIG in;a;ftp.its.os.org"),eol)
	   endif.
	  .logout 1,
	 else.
	   out(,("Error: "),err,eol)
	  .value
	 endif.
	endif.
rdrr::	syscall iot,[%climm,,dqch ? b ]	;read a char
	 .lose %lsfil
	skipge b		;EOF: -1,,^C
	 .logout 1,
	.iot tyoc,b		;echo
	jrst rdrr
	

;; to create a fresh database, :print domain:xyzzy;..new. (dat)

end go