<DEFINE AP-TO-FIL (FILE "AUX" (STR <ISTRING 10>) OUTCHAN SC FL) 
	#DECL ((FILE) STRING (STR) STRING (OUTCHAN) <OR FALSE CHANNEL>
	       (SC FL) FIX (VALUE) <OR FALSE CHANNEL>)
	<COND (<SET OUTCHAN <OPEN "READB" .FILE>>
	       <ACCESS .OUTCHAN <SET FL <MAX 0 <- <FILE-LENGTH .OUTCHAN> 2>>>>
	       <SET SC
		    <COND (<0? <FILE-LENGTH .OUTCHAN>> 0)
			  (T <READSTRING .STR .OUTCHAN>)>>
	       <CLOSE .OUTCHAN>
	       <SET OUTCHAN <OPEN "PRINTO" .FILE>>
	       <ACCESS .OUTCHAN .FL>
	       <PRINTSTRING .STR .OUTCHAN .SC>
	       .OUTCHAN)
	      (<OPEN "PRINTB" .FILE>)>>

<DEFINE MALE (ME FILE "AUX" (CT 0) OUTCHAN) 
	#DECL ((ME) STRING (FILE) STRING (CT) FIX
	       (OUTCHAN) <OR FALSE CHANNEL> (VALUE) CHANNEL)
	<PROG ()
	  <COND (<SET OUTCHAN <AP-TO-FIL .FILE>>
	         <TERPRI>
	         <PRINC "FROM ">
	         <PRINC .ME>
	         <PDT <DAT> !\/ .OUTCHAN>
	         <PDT <RTIM> !\: .OUTCHAN>
	         <TERPRI>
	         .OUTCHAN)
	        (<G? <SET CT <+ .CT 1>> 5> <ERROR .OUTCHAN MALE>)
		(T
		 <SLEEP 7>
		 <AGAIN>)>>>

<DEFINE PDT (LST SEP OUTCHAN) 
	#DECL ((LST) <LIST [REST FIX]> (SEP) CHARACTER (OUTCHAN) CHANNEL)
	<PRINC "  ">
	<REPEAT ()
		<PRINC <1 .LST>>
		<COND (<EMPTY? <SET LST <REST .LST>>> <RETURN>)>
		<PRINC .SEP>>>


<TITLE IPC-ENABLE>	;"Allow user evaluation of SETZ inquiries"
	<DECLARE ("VALUE" FIX)>
	<PUSHJ	P* IENABLE>
	<JRST	FINIS>

<INTERNAL-ENTRY IENABLE 0>
	<SUBM	M* (P)>
	<MOVNI	A* 1>
	<*SUSET	[<(*400021*) A>]>
	<MOVSI	A* <TYPE-CODE FIX>>
	<MOVEI	B* 0>
	<JRST	MPOPJ>

<TITLE GET-NAME-TWO>	;"Get second name of link"
	<DECLARE ("VALUE" <OR FALSE STRING> CHANNEL)>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* GETT1>
	<JRST	FINIS>

<INTERNAL-ENTRY GETT1 1>
	<SUBM	M* (P)>
	<MOVE	B* (TP)>
	<DPUSH	TP* 4(B)>	; "First file name"
	<MCALL	1 STSB>		; "-->sixbit"
	<PUSH	P* B>
	<MOVE	B* (TP)>
	<DPUSH	TP* 6(B)>	; "Second file name"
	<MCALL	1 STSB>
	<PUSH	P* B>
	<MOVE	B* (TP)>
	<DPUSH	TP* 8(B)>	; "Device name"
	<MCALL	1 STSB>
	<PUSH	P* B>
	<MOVE	B* (TP)>
	<DPUSH	TP* 10(B)>	; "Directory"
	<MCALL	1 STSB>
	<PUSH	P* B>
	<MOVE	B* (TP)>
	<MOVE	A* 1(B)>	; "Channel number"
	<LSH	A* *27*>
	<IOR	A* [<*IOPUS>]>
	<XCT	A>		; "Do iopush"
	<*CALL	[<SETZ>
		 <SIXBIT "OPEN">
		 <MOVSI	*20*>	; "Don't chase links"
		 <1(B)>		; "Channel number"
		 <-1(P)>
		 <-3(P)>
		 <-2(P)>
		 <SETZM (P)>]>
	 <JRST	LOSOUT>
	<*CALL	[<SETZ>
		 <SIXBIT "RFNAME">
		 <1(B)>
		 <MOVEM>
		 <MOVEM>
		 <SETZM C>]>
	 <*VALUE>
	<MOVE	B* [<(*440600*) C>]>
	<MOVEI	A* 0>
STRLOP	<ILDB	O* B>
	<JUMPE	O* MAKSTR>
	<ADDI	O* *40*>
	<ADDI	A* 1>
	<PUSH	TP* <TYPE-WORD CHARACTER>>
	<PUSH	TP* O>
	<CAIGE	A* 6>
	 <JRST	STRLOP>
MAKSTR	<PUSHJ	P* CISTNG>	;"Cons string"
OUT	<MOVE	C* (TP)>
	<MOVE	C* (C)>
	<LSH	C* *27*>
	<IOR	C* [<*IOPOP>]>
	<XCT	C>		;"Do iopop"
	<SUB	TP* [<(2) 2>]>
	<SUB	P* [<(4) 4>]>
	<JRST	MPOPJ>
LOSOUT	<MOVSI	A* <TYPE-CODE FALSE>>
	<MOVEI	B* 0>
	<JRST	OUT>


<TITLE STSB>		;"String to Sixbit word Converter"
	<DECLARE ("VALUE" WORD STRING)>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* STSB1>
	<JRST	FINIS>

<INTERNAL-ENTRY STSB1 1>
	<SUBM	M* (P)>
	<POP	TP* D>
	<POP	TP* O>
	<HRRZ	O* O>
	<CAILE	O* 6>
	<MOVEI	O* 6>
	<MOVSI	A* <TYPE-CODE WORD>>
	<MOVEI	B* 0>
	<MOVE	C* [<(*440600*) B>]>
STLOOP	<JUMPE	O* MPOPJ>
	<ILDB	E* D>
	<SUBI	E* *40*>
	<CAIL	E* *100*>
	<SUBI	E* *40*>
	<IDPB	E* C>
	<SOJA	O* STLOOP>
<END>

<TITLE SDS>		;"Set Demon's Schedule"
	<DECLARE ("VALUE" <OR ATOM FALSE> <OR STRING FIX> FIX FIX FIX)>
	<DPUSH	TP* (AB)>
	<DPUSH	TP* (AB) 2>
	<DPUSH	TP* (AB) 4>
	<DPUSH	TP* (AB) 6>
	<PUSHJ	P* SDS4>
	<JRST	FINIS>

<INTERNAL-ENTRY SDS4 4>
	<SUBM	M* (P)>
	<GETYP	A* (TP) -7>		;"First Arg: DEMON Identity"
	<CAIE	A* <TYPE-CODE STRING>>
	<JRST	SDSFIX>
	<PUSH	TP* (TP) -7>		;"First Arg: DEMON Name"
	<PUSH	TP* (TP) -7>
	<MCALL	1 STSB>
	<MOVE	C* (TP) -4>		;"Second Arg: Sched Increment (minutes)"
	<ASH	C* -1>			;"To (Two Minute)s from Minutes"
	<MOVE	D* (TP) -2>		;"Third Arg: Pending Requests"
	<HRL	D* C>
	<MOVE	E* (TP)>		;"Fourth Arg: Time (minutes) to next Sched"
	<JUMPE	E* DO-SDS>
	<TRNN	E* 1>			;"Be Conservative"
	<SOS	E>
	<ASH	E* -1>			;"To (Two Minute)s from Minutes"
	<JRST	DO-SDS>

SDSFIX	<MOVE	B* (TP) -6>		;"First Arg"
	<MOVE	D* (TP) -2>		;"Third Arg"
	<HRL	D* (TP) -4>		;"Second Arg"
	<MOVE	E* (TP)>		;"Fourth Arg"
DO-SDS	<SUB	TP* [<8 (8)>]>
	<*CALL	STDMST>
	<JRST	NO-SDS>
	<MOVSI	A* <TYPE-CODE ATOM>>
	<MOVE	B* <MQUOTE T>>
	<JRST	MPOPJ>
NO-SDS	<MOVE	A* <TYPE-WORD FALSE>>
	<SETZ	B*>
	<JRST	MPOPJ>
STDMST	<SETZ>
	<SIXBIT	"STDMST">
	<B>
	<D>
	<SETZ	E>
<END>

<TITLE RDS>		;"Read Demon's Schedule"
	<DECLARE ("VALUE" <OR <VECTOR [4 FIX]> FALSE> <OR STRING FIX>)>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* RDS1>
	<JRST	FINIS>

<INTERNAL-ENTRY RDS1 1>
	<SUBM	M* (P)>
	<GETYP	A* (TP) -1>
	<CAIE	A* <TYPE-CODE STRING>>
	<JRST	RDSFIX>
	<MCALL	1 STSB>
	<JRST	DO-RDS>
RDSFIX	<MOVE	B* (TP)>
	<SUB	TP* [<2 (2)>]>
DO-RDS	<*CALL	RDDMST>
	<JRST	NO-DEM>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* A>			;"DEMON's User Index"
	<HLRZ	O* C>			;"Sched Increment in (two minute)s"
	<ASH	O* A>			;"To minutes"
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* O>
	<HRRZ	O* C>			;"Pending Requests"
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* O>
	<PUSH	TP* <TYPE-WORD FIX>>
	<ASH	D* A>			;"Time to Next Sched"
	<PUSH	TP* D>
	<MOVEI	A* 4>
	<PUSHJ	P* CIVEC>
	<JRST	MPOPJ>
NO-DEM	<DMOVE	A* <PQUOTE #FALSE ("Demon not in the system table")>>
	<JRST	MPOPJ>
RDDMST	<SETZ>
	<SIXBIT "RDDMST">
	<B>
	<(*2000*) A>
	<(*2000*) C>
	<SETZM	D>
<END>

<TITLE RTIM>		;"Real Time:  (Hour Minute Second)"
	<DECLARE ("VALUE" <LIST FIX FIX FIX>)>
	<PUSHJ	P* RTIM0>
	<JRST	FINIS>

<INTERNAL-ENTRY RTIM0 0>
	<SUBM	M* (P)>
	<(*42000*) B* *45*>		;".RTIME"
	<MOVEI	C* 3>
RTLOOP	<SETZ	A*>
	<LSHC	A* 6>
	<MOVEI	D* (A) -16>
	<IMULI	D* *12*>
	<SETZ	A*>
	<LSHC	A* 6>
	<ADDI	D* (A) -16>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* D>
	<SOJG	C* RTLOOP>
	<MOVEI	A* 3>
	<PUSHJ	P* IILIST>
	<JRST	MPOPJ>
<END>

<TITLE JONES>		;"DOW - Day Of Week (0 = Sunday, 1 = Monday, etc.)"
	<DECLARE ("VALUE" FIX)>
	<PUSHJ	P* JONES0>
	<JRST	FINIS>

<INTERNAL-ENTRY JONES0 0>
	<SUBM	M* (P)>
	<(*42000*) *101*>		;".RLPDT"
	<LDB	B* [<(*320300*) A>]>
	<MOVSI	A* <TYPE-CODE FIX>>
	<JRST	MPOPJ>
<END>

<TITLE DAT>		;"Date:  (Year Month Day)"
	<DECLARE ("VALUE" <LIST FIX FIX FIX>)>
	<PUSHJ	P* DAT0>
	<JRST	FINIS>

<INTERNAL-ENTRY DAT0 0>
	<SUBM	M* (P)>
	<(*42000*) B* *46*>		;".RDATE"
	<MOVEI	C* 3>
DALOOP	<SETZ	A*>
	<LSHC	A* 6>
	<MOVEI	D* (A) -16>
	<IMULI	D* 10>
	<SETZ	A*>
	<LSHC	A* 6>
	<ADDI	D* (A) -16>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* D>
	<SOJG	C* DALOOP>
	<MOVEI	A* 3>
	<PUSHJ	P* IILIST>
	<JRST	MPOPJ>
<END>

<TITLE SWAPS>		;"# swaps for this job"
	<DECLARE ("VALUE" FIX)>
	<PUSHJ	P* ISWAPS>
	<JRST	FINIS>
	<INTERNAL-ENTRY ISWAPS 0>
	<SUBM	M* (P)>
	<MOVE	A* [<SQUOZE "L" 0>]>
	<*EVAL	A*>
	 <JRST	LOSS>
	<MOVE	B* [<SQUOZE "USIPRQ" 0>]>
	<*EVAL	B*>
	 <JRST	LOSS>
	<*SUSET	[<(*23*) C>]>	; ".RUIND"
	<IMULI	A* (C)>
	<ADDI	A* (B)>
	<HRLI	A* (A)>
	<HRRI	A* B>
	<*GETLO	A*>
	<MOVSI	A* <TYPE-CODE FIX>>
	<JRST	MPOPJ>
LOSS	<MOVSI	A* <TYPE-CODE FIX>>
	<MOVEI	B* 0>
	<JRST	MPOPJ>

<TITLE LGN>		;"Login"
	<DECLARE ("VALUE" <OR ATOM FALSE> STRING)>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* LGN1>
	<JRST	FINIS>

<INTERNAL-ENTRY LGN1 1>
	<SUBM	M* (P)>
	<MCALL	1 STSB>
	<MOVEI	C* 0>
	<*CALL	CLOGIN>
	<JRST	NO-LGN>
	<MOVSI	A* <TYPE-CODE ATOM>>
	<MOVE	B* <MQUOTE T>>
	<JRST	MPOPJ>

NO-LGN	<MOVSI	A* <TYPE-CODE FALSE>>
	<SETZ	B*>
	<JRST	MPOPJ>

CLOGIN	<SETZ>
	<SIXBIT "LOGIN">
	<B>
	<SETZ	C>
<END>


<TITLE ELOGOUT>		;"Emergency Logout"
	<DECLARE ("VALUE" ANY)>
	<PUSHJ	P* ELOG0>
	<JRST	FINIS>

<INTERNAL-ENTRY ELOG0 0>
	<SUBM	M* (P)>
	<(*42000*) *33*>		;".LOGOUT"
	<MOVSI	A* <TYPE-CODE FALSE>>
	<MOVEI	B* 0>
	<JRST	MPOPJ>
<END>
