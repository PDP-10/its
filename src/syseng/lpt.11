
TITLE PILPT
RELOCA
DEFINE COND A,B
.LIFS A
B
.ELDC
LOC $.
TERMIN
.LIBRA PILPT,LPTBRK
LPTS:	MOVEI A",40
PILPT":	PUSH P",A
	CAIN A,13
	JRST LPTVTB	;VERTICAL TAB
	CAIN A,11
	JRST LPTTAB	;HORIZONTAL TAB
	CAIE A,12
	CAIN A,14
	JRST LPC
	CAIGE A,140
	JRST LPT2
	CAILE A,173
	CAIN A,177
	JRST LPTRET
	MOVEI A,"$	;ALT MODE
LPT2:	SETOM PCL
	CAIE A,15
	AOSA LINEPOS
	CLEARM LINEPOS
LPC1:	CAIN A,12
	AOS PAGEPOS
	CAIN A,14
	CLEARM PAGEPOS
	CAIL A," 
	JRST LPC3
	CAIL A,^I
	CAILE A,^M
	AOSA LINEPOS
	JRST LPC3
	MOVEI A,"^
	PUSH P,[.+3]
	PUSH P,-1(P)
	JRST LPC3
	TRO A,100
LPC3:	SUBI A,40
	SKIPG LPTCC
	JRST .-1
	IDPB A,LPTIP
	HRRZ A,LPTIP
	CAIN A,LPTBE-1
	MOVEI A,LPTBO
	HRRM A,LPTIP
	SOS LPTCC
	CONSO LPT,7
	CONO LPT,LPTCHN
LPTRET:	POP P,A
	POPJ P,


LPTWAT":	MOVE A,LPTCC
	CAIE A,<LPTBE-LPTBO>*5-5оо	JRST .-2
	POPJ P,

LPTTAB:	PUSHJ P,LPTS
	LDB A,[300,,LINEPOS]
	JUMPN A,.-2
	JRST LPTRET

LPTVTB:	PUSH P,LINEPOS
	SETZM LINEPOS
LPTVT2:	MOVEI A,12
	PUSHJ P,PILPT
	LDB A,[300,,PAGEPOS]
	JUMPN A,LPTVT2
	POP P,LINEPOS
	MOVEI A,12
	PUSHJ P,PILPT
	JRST LPTRET

LPC:	PUSH P,LINEPOS
	MOVEI A,15
	AOSN PCL
	SKIPE LINEPOS
	PUSHJ P,PILPT
	SOSGE (P)
	JRST LPC2
	PUSHJ P,LPTS
	JRST .-3

LPC2:	POP P,A
	MOVE A,(P)
	JRST LPC1

LPTCC:	<LPTBE-LPTBO>*5-5
LINEPOS":	0
PAGEPOS":	0
PCL:	0
LPTIP:	700,,LPTBO-1
LPTOP:	700,,LPTBO-1


LPTCN:	CAIN A,<15-40>&177
	JRST LPTCR
	CAIN A,<12-40>&177
	MOVEI A,400
	CAIN A,<14-40>&177
	MOVEI A,401
	JRST LPTCN1

LPTCR:	CONO LPT,10+LPTCHN
	JRST LPTCR1

LOC $.	;AVOID STINK BUG

COND LPTBRK",[
LPTBRK":	
LPENT:	CONSZ LPT,7
	CONSZ LPT,10
	JRST LPTRTN"
]
COND -LPTBRK",[
ZZ==.
LOC 40+2*LPTCHN"
JSR LPTBRK
LOC ZZ
LPTBRK":	0
LPENT:	CONSZ LPT,7
	CONSZ LPT,10
	JRST 12,@LPTBRK
]
	PUSH P,A
	MOVEI A,<LPTBE-LPTBO>*5-5
	CAMG A,LPTCC
	JRST LPTSTP
	ILDB A,LPTOP
	TRNE A,100
	JRST LPTCN
LPTCN1:	DATAO LPT,A
LPTCR1:	HRRZ A,LPTOP
	CAIN A,LPTBE-1
	MOVEI A,LPTBO
	HRRM A,LPTOP
	AOSA LPTCC
LPTSTP:	CONO LPT,0
	POP P,A
	JRST LPENT

COND -LPTBSZ",[
LPTBSZ"=40
]
LPTBO:	BLOCK LPTBSZ"
LPTBE:
END
