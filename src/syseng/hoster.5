!PUT THIS MACRO IN Q-REGISTER M, IT READS "HOSTS TXT" AND WRITES "HOSTS PRETTY"!
ERHOSTS TXT EI Y

!MAKE HEADER FOR ALL PAGES!
FXA
I;THIS FILE IS EITHER "SYSENG;HOSTS PRETTY" OR "SYSENG;HOSTS INSERT".
;IT WAS PRODUCED USING A TECO MACRO FOUND ON "SYSENG;HOSTER >".  THE
;FILE PROCESSED BY THE TECO MACRO TO PRODUCE THIS FILE IS MAINTAINED
;AT THE NETWORK INFORMATION CENTER (OFFICE-1), "<NETINFO>HOSTS.TXT".

;NAME		DEC	OCT	STATUS
 ^0,.FXA

ZJ -S= L .,ZK J HXZ

!PUT ALL HOST INFO ON ONE LINE!

<.-Z; :L I	 2D .U1 S= Q1,.K L K> !PUT IT ALL ON ONE LINE!
0J <S,;-DI	>                      !REPLACE COMMA'S WITH TABS!
[..E 10U..E W J               !PUSH CURRENT OUTPUT RADIX!
<
S	; .U1 \U2 Q1,.K 3,Q2\          !PAD HOST NUMBERS WITH LEADING SPACES!
8U..E W I	 3,Q2\ 10U..E W :L   !INSERT OCTAL HOST #!
>
]..E                            !POP OUTPUT RADIX!

HXB

!LINE UP THE COLUMNS ALL PRETTY!
J<.-Z;.U1S	.-Q1-9"LI	'L>
J GA PW HK GB

!SORT BY HOST NUMBER!
JS	$S	$RL

HXC

!LINE UP THE COLUMNS ALL PRETTY!
J<.-Z;.U1S	.-Q1-9"LI	'L>

J GA 15.I12.I PWEFHOSTS PRETTY HK GB J
<S	;-DI,>
J<.-Z;IHOST	L>
J GA EI PWEFHOSTS INSERT HK 0UA 0UB 0UC 