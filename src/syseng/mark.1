TITLE MARK TAPE

;Reconstructed in 2020 by lightly editing SYSTEM GEN to match @ MARK
;binary.

MACLOD=37726

A=1
D=2
B=3
C=4
P=5


BEG:	CONO 635550
TRY:	CONO UTC,203410
	CONSZ UTS,2000
	JRST .-1
	CONSZ UTS,4
	JRST TRY
	CONO UTC,221410
	CONO DC,3410
	MOVEI B,2700.
	MOVE A,[707707707707]
END1:	BLAT
	SOJG B,END1
	MOVEI C,1102
BLK:	MOVE A,[(70707)70770]
	BLAT
	MOVE A,[(77070)7000]
	BLAT
	HRLI A,7000
	BLAT
	HRRI A,-1000
	BLAT
	MOVEI B,175
	HRLI A,-1000
	BLAT
	SOJG B,.-1
	HRRI A,-701
	BLAT
	HRLI A,-701
	BLAT
	HRRI A,707007
	BLAT
	MOVE A,[(700707)70707]
	BLAT
	SOJG C,BLK
	MOVE A,[70070070070]
	MOVEI B,2700.
END2:	BLAT
	SOJG B,END2

TRY2:	CONO UTC,202510
	CONSZ UTS,2000
	JRST .-1
	CONSZ UTS,4
	JRST TRY2
	CONO UTC,233510
	CONO DC,3410
	MOVEI B,1101
	MOVEI C,1
LUP:	MOVEI A,0
	CAIN C,1
	MOVE A,B
	CAIE C,2
	CAIN C,203
	MOVE A,234
	CAIN C,3
	MOVEI A,77
	CAIN C,204
	JRST COMP
WR:	BLAT
	SOJG C,LUP
	MOVEI C,204
	SOJGE B,LUP
	CONSZ UTS,40
	JRST .-1

	CONO UTC,3000
	CONSZ UTS,2000
	JRST .-1
	JRST TRY

WA:	CONSZ UTS,6
	JRST DING
	CONSO DC,1000
	JRST WA
	DATAO DC,A
	JRST (P)
DING:	DATAO TTY,BELL
	CONO UTC,1000
	JRST 4,BEG
BLAT=JSP P,WA
COMP:	MOVE D,B
	ROT D,-3
	REPEAT 3,[ROTC A,3
	ROT D,-6
]	ROTC A,27.
BELL:	SETCAI A,7
	JRST WR
	END BEG
