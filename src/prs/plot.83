TITLE PLOT 83  700611  1908

RELOCA

A=1
B=2
C=3
D=4
E=5
F=6
G=7
H=10
P=11
I=12
J=13
K=14
L"=15
M=16
N=17


VI==40
IV==20
RT==1	;+X
LT==2	;-X
UP==10	;-Y
DN==4	;+Y

MAXNP"==10.

DEFINE FIX U/V
	MULI U,400
	TSC U,U
	ASH U+1,-243(U)
TERMIN

DEFINE FLOAT U/V
	PUSH P,U+1
	IDIVI U,400000
	SKIPE U
	TLC U,254000
	TLC U+1,233000
	FAD U,U+1
	POP P,U+1
TERMIN

DEFINE DBP7 X
	ADD X,[7_30.]
	SKIPGE X
	ADD X,[-<43_30.>-1]
TERMIN

;DESTIN X IN A, Y IN M, FIG PTR IN E

PLOTGO:	HLRE B,(E)
	ASH B,11.
	MUL B,FGSCAL
	ADD A,B
	HRRE B,(E)
	ASH B,11.
	MUL B,FGSCAL
	AOS E
	ADD M,B
PLOTG0:	MOVEM A,PLX'	;HERE IF NO FIG PTR
	MOVEM M,PLY'
	ADD A,PLINC
	ADD M,PLINC
	MUL A,PLSCAL	
	MUL M,PLSCAL
PLOTG8:	SKIPGE PLOWS"
	JRST PLAOG
PLAOG9:	SUB A,PLXPOS'
	SUB M,PLYPOS'
	JUMPN A,PLOTG9
	JUMPE M,CPOPJ
PLOTG9:	PUSHJ P,PLINV
	MOVEI C,RT+DN
	JUMPGE A,PLOTG1
	XORI C,RT+LT
	MOVMS A
PLOTG1:	JUMPGE M,PLOTG2
	XORI C,UP+DN
	MOVMS M
PLOTG2:	CAMG A,M
	JRST PLOTG3
	EXCH A,M
	SKIPA B,[UP+DN]
PLOTG3:	MOVEI B,RT+LT
	SUB M,A
	PUSHJ P,PLINE9
	MOVE A,M
	TDZ C,B
	JRST PLINE9
PLINE:	ADD A,PLINC
	MUL A,PLSCAL
PLINE9:	SKIPN PLOWS"
	POPJ P,
	SKIPG PLOWS"
	JRST PLAOL
	MOVEM C,PLINEC'
PLINE0:	SETOM PLINED'
PLINE1:	JUMPE A,CPOPJ
	PUSHJ P,PLOINT
	EXCH C,PLINEC
	SOSN PLINED
	JRST PDLN1
	SOJA A,PLINE1

PLINV:	SKIPA C,[IV]
PLVIS:	MOVEI C,VI
	CAMN C,PLINVS'
	POPJ P,
	MOVEM C,PLINVS
	SKIPN PLOWS"
	POPJ P,
	SKIPG PLOWS
	JRST PLAOV
PLOINT:	IDPB C,D
	TRNE C,RT
	AOS PLXPOS
	TRNE C,LT
	SOS PLXPOS
	TRNE C,UP
	SOS PLYPOS
	TRNE C,DN
	AOS PLYPOS
	TLNE D,760000
CPOPJ:	POPJ P,
	SKIPN PLOWS
	JRST PLBLT
	PUSH P,A
	MOVE A,PLOWD'
	PUSHJ P,.UWR"
	  JRST TUFLUK"
	POP P,A
PLBLT:	SETZM PLOWD
	MOVE D,[440700,,PLOWD]
	POPJ P,


PDLINE:	ADD A,PLINC
	MUL A,PLSCAL
	SKIPN PLOWS
	POPJ P,
	SKIPG PLOWS
	JRST PLAOD
	MOVEM C,PDLNC'
	MOVEM C,PLINEC'
	MOVEI B,1
	MOVEM B,PDLNB'
PDLN1:	MOVEI B,10.
	MOVEM B,PLINED
	MOVEI C,VI+IV
	XORB C,PDLNB
	PUSHJ P,PLOINT
	MOVE C,PDLNC
	SOJA A,PLINE1

PLOTSS:	MOVE A,PLOWS
	MOVE A,PLOTXT(A)+1
	MOVEM A,MAXH'
	MOVE A,PLOWS
	MOVE A,@PLOTST(A)+1
	MOVE A,SCRSIZ"
	ASH A,19.
	MOVEM A,PLSCAL
	MOVSI B,200000
	IDIV B,A
	ADDI B,1
	MOVEM B,PLINC
	MOVSI A,100000	;1/4
	MOVEM A,FGSCAL
	POPJ P,

PLSCAL:	(140)
FGSCAL:	(100000)
PLINC:	1253

PLOTST:	ARDSIZ"
	SCRSIZ"
	SCRSIZ"

PLOTXT:	2200.
	2100.
	2100.

PLAOV:	PUSH P,B
	MOVEI B,1
	CAIN C,VI
	MOVEI B,0
	DPB B,[240100,,.PLOTW]
	POP P,B
	POPJ P,

PLAOG:	CAML A,PLAOGL
	JRST PLAOG9
	MOVEM A,PLXPOS
	MOVEM M,PLYPOS
	MOVNS M
	ADD A,PLAOGA
	ADD M,PLAOGM
	SETZB B,C
	SKIPGE A
	MOVEI B,1
	SKIPGE M
	MOVEI C,1
PLAOG1:	MOVMS A
	MOVMS M
	DPB B,[260100,,%PLOTW]
	DPB C,[100100,,%PLOTW]
	LDB B,[50500,,A]
	LDB C,[50500,,M]
PLAOG2:	DPB A,[270500,,%PLOTW]
	DPB B,[170500,,%PLOTW]
	DPB M,[110500,,%PLOTW]
	DPB C,[10500,,%PLOTW]
	MOVEI A,%PLOTW
PLAOG3:	PUSHJ P,TOACS"
	  JFCL
	POPJ P,

PLAOGA:	-540.
PLAOGM:	+707.
PLAOGL:	2000+540.
PLAOD:	SETZM PLAODV'
	SKIPA
PLAOL:	SETOM PLAODV
	JUMPE A,CPOPJ
	PUSH P,I
	PUSH P,J
	MOVEI I,1
	AOS J,PLAODV
	DPB J,[60100,,.PLOTW]	;DOTTED
	MOVEI J,0
PLAOL1:	TRNE C,RT
	DPB J,[260100,,.PLOTW]	;X DIR
	TRNE C,LT
	DPB I,[260100,,.PLOTW]
	TRNE C,UP
	DPB J,[100100,,.PLOTW]	;Y DIR
	TRNE C,DN
	DPB I,[100100,,.PLOTW]
PLAOL2:	TRNE C,RT
	ADDM A,PLXPOS
	TRNE C,DN
	ADDM A,PLYPOS
	MOVNS A
	TRNE C,LT
	ADDM A,PLXPOS
	TRNE C,UP
	ADDM A,PLYPOS
	MOVNS A
PLAOL3:	SETZM PLAOLA'
	CAIGE A,2000
	JRST PLAOL9
	SUBI A,1777
	MOVEM A,PLAOLA
	MOVEI A,1777
PLAOL9:	LDB I,[50500,,A]
	TRNE C,RT+LT
	DPB I,[170500,,.PLOTW]
	TRNE C,RT+LT
	DPB A,[270500,,.PLOTW]
	TRNN C,RT+LT
	DPB J,[170500,,.PLOTW]
	TRNN C,RT+LT
	DPB J,[270500,,.PLOTW]
	TRNE C,UP+DN
PLAOL4:	DPB I,[10500,,.PLOTW]
	TRNE C,UP+DN
	DPB A,[110500,,.PLOTW]
	TRNN C,UP+DN
	DPB J,[10500,,.PLOTW]
	TRNN C,UP+DN
	DPB J,[110500,,.PLOTW]
	MOVEI A,.PLOTW
	PUSHJ P,TOACS"
	  JFCL
	SKIPE A,PLAOLA
	JRST PLAOL3
	POP P,J
	POP P,I
	POPJ P,

PLFIG:	PUSH P,E
	HRLI E,441400
PLFIG0:	ILDB B,E
	LDB A,[700,,B]
	JUMPE A,PLFIG1
	TRNE B,200
	PUSHJ P,PLINV
	TRZN B,200
	PUSHJ P,PLVIS
	LDB C,[100400,,B]
	ASH A,9+2
	MOVE M,A
	XCT PLXI(C)
	XCT PLYI(C)
	MUL A,FGSCAL
	MUL M,FGSCAL
	ADDB A,PLX'
	ADDB M,PLY'
	PUSHJ P,PJ
	JRST PLFIG0

PLFIG1:	TRNE B,200
	JRST PLFIG2
	MOVE C,(P)
	ILDB A,E
	SUB C,A
	PUSH P,E
	MOVE E,C
	PUSHJ P,PLFIG
	POP P,E
	JRST PLFIG0

PLFIG2:	MOVE A,1(E)
	ASH A,2
	MUL A,FGSCAL
	MOVE B,2(E)
	ASH B,2
	MUL B,FGSCAL
	POP P,C
	POPJ P,

PLXI:	MOVEI A,0
	JFCL
	JFCL
	JFCL
	MOVNS A
	MOVNS A
	MOVEI A,0
	MOVNS A

	ASH A,-1
	JFCL
	ASH A,-1
	JFCL
	MUL A,[(600000)]
	MOVNS A
	MUL A,[(600000)]
	MOVNS A

PLYI:	MOVNS M
	MOVNS M
	JFCL
	MOVEI M,0
	MOVNS M
	MOVEI M,0
	JFCL
	JFCL

	MOVNS M
	MUL M,[(600000)]
	JFCL
	ASH M,-1
	MOVNS M
	MUL M,[(600000)]
	JFCL
	ASH M,-1

PLGTS:	SKIPA M,STAFFN(G)
PLGTP:	MOVEI M,(G)
	MOVE M,STAFFL(M)
	ADD M,PLGTSO
	POPJ P,

PLGTSO:	660000
PLGTSP:	120000	;FOR MEASURE #S

;ONE END (0) IN A,M; OTHER (1) IN B,N

PFS:	PUSH P,B	;(1)
	PUSH P,N
	ADD A,PLINC
	ADD M,PLINC
	MUL A,PLSCAL
	MUL M,PLSCAL
	EXCH M,(P)	;(0)
	EXCH A,-1(P)
	ADD A,PLINC
	ADD M,PLINC
	MUL A,PLSCAL
	MUL M,PLSCAL
	PUSH P,A	;(1)
	PUSH P,M
	SUB A,PLXPOS
	SUB M,PLYPOS
	MOVM B,A
	MOVMS M
	CAMGE B,M
	MOVE B,M	;(1)
	MOVE M,-2(P)
	MOVE A,-3(P)
	SUB A,PLXPOS
	SUB M,PLYPOS
	MOVMS A
	MOVMS M
	CAMGE A,M
	MOVE A,M	;(0)
	CAML A,B
	JRST PFS1
	MOVE A,(P)
	EXCH A,-2(P)
	MOVEM A,(P)
	MOVE A,-1(P)
	EXCH A,-3(P)
	MOVEM A,-1(P)

PFS1:	POP P,M	;NEARER END
	POP P,A
	SUBM M,(P)
	SUBM A,-1(P)
	PUSHJ P,PLOTG8
	SETZM PJF'
	POP P,M
	POP P,A
PJ0:	SKIPGE PLOWS"
	JRST PJA
	MOVEI B,RT
	MOVEI C,DN
	SKIPL A
	MOVEI B,LT
	SKIPL M
	MOVEI C,UP
	MOVMS A
	MOVMS M
	CAMGE A,M
	EXCH B,C
	MOVEM B,PFSD1'
	MOVEM C,PFSD2'
	CAML A,M
	EXCH A,M
	JUMPE M,CPOPJ
	ASH A,17.
	IDIV A,M
	MOVEI B,0
	SKIPL PJF'
	PUSHJ P,PLVIS
PFS2:	ADD B,A
	MOVE C,PFSD1
	CAIL B,200000
	IOR C,PFSD2
	CAIL B,200000
	SUBI B,400000
	PUSHJ P,PLOINT
	SOJG M,PFS2
	POPJ P,
	
PJ:	ADD A,PLINC
	ADD M,PLINC
	MUL A,PLSCAL
	MUL M,PLSCAL
	SUB A,PLXPOS
	SUB M,PLYPOS
	MOVNS A
	MOVNS M
	SETOM PJF
	JRST PJ0

PJA:	MOVEI B,1
	MOVEI C,0
	DPB C,[60100,,.PLOTW]
	SKIPL A
	DPB B,[260100,,.PLOTW]
	SKIPGE A
	DPB C,[260100,,.PLOTW]
	SKIPL M
PJA01:	DPB C,[100100,,.PLOTW]
	SKIPGE M
	DPB B,[100100,,.PLOTW]
	MOVNS A
	MOVNS M
	ADDM A,PLXPOS
	ADDM M,PLYPOS
	MOVMS A
	MOVMS M
PJA02:	SKIPL PJF
	PUSHJ P,PLVIS
PJA08:	SETZM PJA0A'
	SETZM PJA0M'
	CAIGE A,2000
	JRST PJA03
	SUBI A,1777
	MOVEM A,PJA0A
	MOVEI A,1777
PJA03:	CAIGE M,2000
	JRST PJA04
	SUBI M,1777
	MOVEM M,PJA0M
	MOVEI M,17
PJA04:	LDB B,[50500,,A]
	LDB C,[50500,,M]
	DPB B,[170500,,.PLOTW]
	DPB A,[270500,,.PLOTW]
	DPB C,[10500,,.PLOTW]
	DPB M,[110500,,.PLOTW]
	MOVEI A,.PLOTW
	PUSHJ P,TOACS"
	  JFCL
	MOVE M,PJA0M
	SKIPN A,PJA0A
	SKIPE M
	JRST PJA08
	POPJ P,

.PLOT":	HRRM J,PLOT1V
	AOS PLOT1V
	MOVEM J,PLOX'
	MOVEM L,PLOTIP'
	MOVEM M,PLOTX0'
	SETZM PLOTX1'
	MOVEM N,PLOTX2'
	MOVE A,FREP"
	MOVEM A,PLFREP'
	MOVEI J,0
	PUSHJ P,PLBLT
	SETZM PLINVS'
	SKIPG PLOWS
	JRST .PLOT1
	MOVEI A,(SIXBIT /PLT/)
	PUSHJ P,FILEST"
	  JRST SSSB"
	PUSHJ P,OPNWR"
	  JRST PLONOP
.PLOT1:	SKIPL PLOWS
	JRST .PLOT2
	MOVEI A,4
	PUSHJ P,TYOOPN"
	MOVEI A,.PLOTT
	PUSHJ P,TOACS"
	  JFCL
	MOVE A,[172010,,040200]
	MOVEM A,.PLOTW
	MOVE A,[166014,,040200]
	MOVEM A,%PLOTW
.PLOT2:	SETZM PLXPOS
	SETZM PLYPOS
	PUSHJ P,PLOTSS
	MOVSI K,-MAXNP
	SETZM TEXP(K)
	SETZM TEXT(K)
	AOBJN K,.-2

	MOVSI K,-1
	MOVE B,K
PLOT1:	ADD K,[1200000]	;STAFF+SPACE
	ADD B,[1200000]
PLOT1V:	HRRZ L,(J)	;ADDR IS BEG OF HEADER TABLE
	SOJL L,PLOT2
	MOVE B,K
	ADD L,PLOX
	MOVEM L,MEAS(J)
	ADDI L,1
PLOT12:	SKIPN M,(L)
	AOJA J,PLOT1
	LDB N,[311300,,M]
	CAIE N,4
	AOJA L,PLOT12
	ADDI K,600000	;FOR TEXT
	HRRZM L,TEXP(J)
	HRLI M,440700
	MOVEM M,TEXT(J)
	AOJA J,PLOT1

.PLOTT:	.BYTE 7
	014
	0
	.BYTE

.PLOTW:	0
	0
%PLOTW:	0
	0

PLOT2:	MOVEM J,PLNLI'
	CAILE J,MAXNP
	JRST PLONOR
	MOVEM K,PLOT1H'
	MOVEM B,PLOT1B'
	MOVSI A,-MAXNP
PLOT2A:	HLLOS PLWSNO(A)
	SETZM PELEG(A)
	SETZM PLCKS(A)
	SETZM PLFKS(A)
	SETZM PLCTS(A)
	SETZM PLFTS(A)
	SETZM TEMPF(A)
	SETZM PLLASN(A)
	SETZM PLSLN(A)
	SETZM PLWT(A)
	SETOM UNDERL(A)
	SETOM PLTRIH(A)
	SETOM POSHH(A)
	MOVE C,STAFFN(A)
	MOVEM A,STAFPN(C)
	AOBJN A,PLOT2A
	MOVEI B,0
	MOVSI A,-MAXNP
PLOT2B:	MOVE C,STAFPN(A)
	MOVEM B,STAFFL(C)
	ADD B,[1200000]
	SKIPE TEXP(C)
	ADDI B,600000
	AOBJN A,PLOT2B
	MUL K,PLSCAL
	CAMG K,MAXH
	JRST PLOTI
	SKIPA B,[440700,,[ASCII ./DOESN'T FIT VERTICALLY\.]]
PLONOP:	MOVE B,[440700,,[ASCII ./PLOTTER NOT AVAILABLE\.]]
	JRST DEATH"

PLONOR:	MOVE B,[440700,,[ASCII ./TOO MANY VOICES\.]]
	JRST DEATH"

PLOTI:	SETZB A,PLOTIW'
	SETZM PLOTSJ'
	SETZM PLOTIL'
	SETZM PLOTIQ'
	SETZM PLARAQ'
PLOTI0:	MOVE J,PLOTIP
	MOVEI B,660000
	JUMPE J,PLOST
	MOVSI B,200000	;1/2
	MOVEM B,FGSCAL
	HRLI J,700
PLOTI1:	MOVEI M,40000
PLOTI2:	ILDB K,J
	CAIN K,40
	JRST PLOTI3
	CAIN K,"\
	JRST PLOST0
	CAIN K,""
	SETCMB B,PLOTIQ
	CAIN K,""
	MOVE K,PLOTIR(B)
	CAIE K,136	;UMLAUT
	JRST PLOTI4
	MOVE A,PLOTSJ
	MOVE B,PLOTIL
	CAIE B,141	;L.C. A
	CAIN B,157	;L.C. O
	ADDI M,54000
	CAIN B,165	;L.C. U
	ADDI M,54000
PLOTI4:	MOVEM K,PLOTIL
	MOVE B,PLOTIW
	MOVEM B,PLOTSJ
	MOVE E,PLOTCT"(K)
	JUMPE E,PLOTI2
	PUSHJ P,PLOTGO
	PUSHJ P,PLFIG
	ADDB A,PLOTIW'
	JRST PLOTI1

PLOTI3:	MOVE A,EMWID"
	ADDB A,PLOTIW
	JRST PLOTI1

	200
PLOTIR:	""

PLOST0:	PUSHJ P,PLOTSS
	MOVE B,[1320000]
PLOST:	MOVEM B,PLGTSO
	SUBI B,540000
	MOVEM B,PLGTSP
	SKIPN PLNLI
	JRST PLCLO
	SETZM PLOTIU'
	SETZM PLOTIV'
	SKIPN PLOTX0
	TDZA A,A
	MOVEI A,100000
	MOVEM A,PLOTIW'
	MOVE M,PLGTSO
	PUSHJ P,PLOTG0
	PUSHJ P,PLVIS
	MOVEI C,DN
	MOVE A,PLOT1B
	SUB A,[(1)]
	PUSHJ P,PLINE
	MOVE G,PLNLI
PLOST1:	HRLI A,PLWTZ
	HRR A,PLWTA(G)-1
	MOVEI B,52(A)
	BLT A,(B)
	SOJG G,PLOST1
	JRST PLOPP

;FOR EACH "INSTANT"

PLOOP9:	MOVE G,PLNLI
	MOVE B,PLOOPD
	HRLOI A,377777
PLOOP1:	CAML B,MSTM(G)-1
	JRST PLUP2
	CAMLE A,MSTM(G)-1
	MOVE A,MSTM(G)-1
PLUP2:	CAML B,RSTM(G)-1
	JRST PLOOP5
	CAMLE A,RSTM(G)-1
	MOVE A,RSTM(G)-1
PLOOP5:	SOJG G,PLOOP1
	CAMG A,[-100]
	JRST PLONM
	MOVEM A,PLOOPB'
	SKIPL PLCLS
	MOVEM A,PLOOPD
	SKIPL PLCLS
	JRST PLOP20
	EXCH A,PLOOPA'
	MOVEM A,PLOOPC'
	MOVE A,PLOTIS'
	MOVEM A,PLOTWW'
	JRST PLOOP2

PLOP20:	SETOM PLWLI'
	SETOM PLWLS'
	MOVE A,PLOOPB
PLOP21:	CAME A,MSTM(G)
	CAMN A,RSTM(G)
	JRST PLOP22
PLOP24:	ADDI G,1
	CAMGE G,PLNLI
	JRST PLOP21
	MOVEI G,0
	SETCMB B,PLWLI
	JUMPE B,PLOP21
	JRST PLOOP2

PLOP22:	SKIPL PLWLI
	JRST PLOP23
	MOVE B,PLW(G)
	CAMLE B,PLWLS
	MOVEM B,PLWLS
	JRST PLOP24

PLOP23:	MOVE B,PLWLS
	MOVEM B,PLW(G)
	JRST PLOP24

;FOR EACH VOICE

PLOOP2:	MOVE A,MSTM(G)
	CAMN A,PLOOPB
	JRST PLOOP6
	SKIPL A,RSTM(G)
	CAME A,PLOOPB
	JRST PLOP4A
	SKIPGE PLCLS
	JRST PLOOP4
PLOC:	SETOM PLRIND'
	MOVE C,RESTW(G)
	PUSHJ P,PLOB
	  JRST PLOC1
	MOVEM C,PLREST'
	MOVEM A,RSTM(G)
	PUSHJ P,GCDD"
	CAIG I,100
	CAILE J,37
	JRST 4,.+1
	DPB I,[310600,,RESTW(G)]
	DPB J,[370500,,RESTW(G)]
	JRST PLOC2

PLOC1:	LDB I,[310600,,RESTW(G)]
	LDB J,[370500,,RESTW(G)]
	SETOM RSTM(G)
	SETZM PLREST'
	SKIPN I
	MOVEI I,100
PLOC2:	MOVEM I,PLUS1D'
	SETZM PLUS1Y'
	MOVEI A,1
	MOVEM A,PLUS1X'
	MOVEM A,PLUS1U'
	JRST PLOOP7

PLOP4A:	SKIPGE MSTM(G)
	SKIPL RSTM(G)
	AOJA G,PLOOP3
PLOOP4:	SKIPN K,PLICI(G)
	AOJA G,PLOOP3
	JRST PLOOP8

PLOB:	MOVEM C,PLOBAR'
	PUSH P,D
	SETZB M,PLOBR'
	LDB C,[60700,,PLCTS(G)]	;NUM
	MOVEM C,PLOBJ'
	MOVEM C,PLOBK'
	CAIGE C,2
	JRST PDPOPJ
	LDB D,[600,,PLCTS(G)]	;DEN
	SKIPN D
	MOVEI D,100
	MOVEM D,PLOBI'
	LDB A,[370500,,PLOBAR]	;HOW LONG NUM
	LDB B,[210100,,PLOBAR]
	DPB B,[50100,,A]
	MOVEM A,PLOB0R'
	LDB B,[310600,,PLOBAR]	;HOW LONG DEN
	SKIPN B
	MOVEI B,100
	MOVEM B,PLOBQ'
PLOB0:	LDB J,[60700,,PLOBAR]	;WHERE NUM
	MOVEM J,PLOBA'
	LDB I,[600,,PLOBAR]	;WHERE DEN
	SKIPN I
	MOVEI I,100
	MOVEM I,PLOBB'
	PUSHJ P,PLOD
	  JRST PDPOPJ
	MOVE A,PLOB0R
	MOVE B,PLOBQ
	MOVE J,PLOBA
	MOVE I,PLOBB
	IMUL A,I
	IMUL J,B
	IMUL I,B
	ADD J,A
	MOVEM I,PLOBE'	;END DEN
	MOVEM J,PLOBF'	;END NUM

PLOB1:	SOSG J,PLOBJ
	JRST PLOB2
	MOVE I,PLOBI	;BEAT LOC => J/I
	MOVE A,J
	ASH A,14.
	IDIV A,I
	MOVEM A,PLOBY'
	MOVE B,PLOBA
	MOVE A,PLOBB	;BEG. OF NOTE => B/A
	IMUL A,J
	IMUL B,I
	SUB A,B
PLOB3:	JUMPLE A,PLOB2	;JUMP IF BEAT NOT AFTER BEG. OF NOTE
	MOVEM A,PLOBSA'
	MOVE A,PLOBB
	IMUL A,I
	MOVEM A,PLOBSB'
	MOVE A,PLOBE
	MOVE B,PLOBF	;END OF NOTE => B/A
	IMUL A,J
	IMUL B,I
	SUB A,B
PLOB4:	JUMPGE A,PLOB1	;JUMP IF BEAT IS NOT BEFORE END OF NOTE
	MOVE C,PLOBAR
	MOVEM C,PLOBR'	;REMAINING NOTE
	PUSH P,I
	PUSHJ P,GCDD"
	DPB J,[60700,,PLOBR]	;WHERE NUM
	DPB I,[600,,PLOBR]	;WHERE DEN
	POP P,I
	IMUL I,PLOBE
	MOVN J,A
	PUSHJ P,GCDD"
	CAIG I,100
	CAILE J,77
	JRST PLOBN
PLOBN1:	DPB J,[370500,,PLOBR]	;HOW LONG NUM
	DPB I,[310600,,PLOBR]	;HOW LONG DEN
	LDB A,[50100,,J]
	DPB A,[210100,,PLOBR]
	MOVE A,PLOBY
	MOVEM A,PLOBZ'
	MOVE C,PLOBK
	MOVE D,PLOBI
	MOVE J,PLOBA
	MOVE I,PLOBB
	MOVE A,PLOBSA
	MOVE B,PLOBSB
	PUSHJ P,PLOD
	  JRST PLOB2
	AOJA M,PLOB1

PLOB2:	SKIPN C,PLOBR
	JRST PDPOPJ
	MOVE J,PLOBSA
	MOVE I,PLOBSB
	MOVE A,PLOBZ
	AOS (P)-1
PDPOPJ:	POP P,D
	POPJ P,

;C/D TIME SIG;  A/B NOTE LENGTH;  J/I NOTE BEGINNING

PLOD:	TRNE C,1	;EVEN MEAS
	JRST PLOB5A
	IMUL C,B
	IMUL D,A
	ASH D,1
	CAME C,D	;NOTE = HALF MEAS
	JRST PLOB6
	JUMPE J,CPOPJ	;AT BEGINNING
	MOVE C,A
	MOVE D,B
	IMUL C,I
	IMUL D,J
	CAMN C,D
	POPJ P,	;AT MIDPOINT
	JRST PLOB5

PLOB6:	MOVE C,PLOBJ
	MOVEM C,PLOBC'
	IDIVI C,3
	JUMPN D,PLOB5
	MOVE C,A	;COMPOUND MEASURE
	MOVE D,B
	IMUL C,PLOBI
	ASH D,1
	CAME C,D	;NOTE = TWO BEATS
	JRST PLOB5
PLOB6A:	MOVNI C,3
	ADDB C,PLOBC
	JUMPL C,PLOB5
	IMUL C,I
	MOVE D,PLOBI
	IMUL D,J
	CAMN D,C
	POPJ P,	;AT BEGINNING OF 3-GROUP
	JRST PLOB6A

PLOB5:	MOVE C,PLOBI
	CAIN C,4
	CAMGE C,PLOBJ	;2/4, 3/4 OR 4/4
	JRST PLOB5A
	MOVE C,A
	MOVE D,B
	IMULI C,10
	IMULI D,3
	CAME C,D	;DOTTED QUARTER
	JRST PLOB5A
	SKIPN C,J
	POPJ P,	;AT BEGINNING
	ASH C,1
	CAMN C,I
	POPJ P,	;AT 1/2
PLOB5A:	AOS (P)
	POPJ P,

PLOOP6:	SETZM PLRIND'
	SETZM PLICI(G)
	SKIPGE PLCLS
	JRST PLOP2
	SETZM PLUS1Y'
	MOVEI A,1
	MOVEM A,PLUS1X'
	MOVEM A,PLUS1U'
	LDB I,[310600,,NOTEW(G)]
	SKIPN I
	MOVEI I,100
	MOVEM I,PLUS1D'
	SETZM PLUSFG'
PLUS20:	LDB I,[310600,,NOTEW(G)]
	LDB J,[370500,,NOTEW(G)]
PLOOP7:	SKIPN I
	MOVEI I,100
	PUSHJ P,GCDD"
	SETZM PL3ND'
	MOVE A,I
	MOVE B,I
	IDIVI B,3
	JUMPN C,PLUP1
	MOVE A,B
	ASH A,1
PLUP1:	MOVEI B,-1(A)
	AND B,A
	JUMPN B,PLOUNN	;DENOM NOT 2^N OR 3*2^N
	ASH A,-1
	SKIPE A
	AOJA B,.-2
PLUP5:	TRZN J,1
	JRST PLUS1	;NUMER NOT 1, 3, 7, ...
PLUP7:	JUMPE J,PLUP6
	AOS PL3ND'
	ASH J,-1
	SOJA B,PLUP5
PLUP6:	MOVEM B,PL3NQ'
	MOVEM C,PLTRII'
	JUMPN C,PLOP1
	JRST PLOP2

PLUS1:	LDB A,[60700,,PLCTS(G)]	;BEAT NUM
	MOVEM A,PLUS1O'
	SOJLE A,PLOUNN	;NO TIME SIG.
	LDB A,[600,,PLCTS(G)]	;BEAT DENOM
	MOVEM A,PLUS1M'
	MOVEM A,PLUS2M'
	SKIPE E,PLRIND
	MOVEI E,1
	SETOM PLUS1N'
	LDB I,[600,,@PLUST1(E)]	;WHERE DEN
	SKIPN I
	MOVEI I,100
	MOVEM I,PLUS1I'
	LDB J,[60700,,@PLUST1(E)]	;WHERE NUM
	MOVEM J,PLUS1J'
	LDB N,[370500,,@PLUST1(E)]	;HOW LONG NUM
	LDB M,[310600,,@PLUST1(E)]	;HOW LONG DEN
	SKIPN M
	MOVEI M,100
	IMUL J,M
	IMUL N,I
	IMUL I,M
	ADD J,N
	PUSHJ P,GCDD"
	MOVEM I,PLUS1K'
	MOVEM J,PLUS1L'	; L / K  IS END OF NOTE
	MOVE I,PLUS1I
	MOVE J,PLUS1J
PLUS10:	AOS N,PLUS1N	;BEAT #
	CAML N,PLUS1O
	JRST PLUS11
	MOVE M,PLUS1M	;BEAT DENOM
	IMUL N,I
	IMUL M,J
	CAMG N,M
	JRST PLUS10
PLUS11:	SOS N,PLUS1N	;(A)
	ADDI N,1
	MOVEM N,PLUS1B'	;(B)
	MOVE M,PLUS1M
	MOVE I,PLUS1K
	MOVE J,PLUS1L
	IMUL N,I
	IMUL M,J
	CAMGE N,M
	JRST PLUS21	;BREAK AT B/M

PLUS12:	MOVE A,PLUS1D
	IDIVI A,3
	MOVEI A,2
	SKIPN B
	MOVEI A,3
	MOVEM A,PLUS1F'	;(F)
	MOVE M,PLUS1D
	IDIV M,A
	MOVEM M,PLUS1D	;(D)
	IMULM A,PLUS1U
PLUS13:	MOVE N,PLUS1N
	MOVE M,PLUS1M
	MOVEI J,1
	MOVE I,PLUS2M
	IMUL I,PLUS1U
	IMUL J,M
	IMUL N,I
	IMUL I,M
	ADD J,N
	PUSHJ P,GCDD"
	MOVEM I,PLUS1Q'
	MOVEM J,PLUS1R'	; R / Q  IS CUT POINT
	MOVE M,PLUS1K
	MOVE N,PLUS1L
	IMUL N,I
	IMUL M,J
	CAMG N,M
	JRST PLUS17	;DIV IS RIGHT OF NOTE
PLUS15:	MOVE M,PLUS1I
	MOVE N,PLUS1J
	IMUL M,J
	IMUL N,I
	CAMG M,N
	JRST PLUS16	;DIV IS LEFT OF NOTE
PLUS18:	SKIPA B,PLUS1R
PLUS21:	SKIPA B,PLUS1B
	SKIPA A,PLUS1Q
	MOVE A,PLUS1M
	MOVEM B,PLUS1H'
	MOVEM A,PLUS1G'	; H / G  IS WHERE SECOND PART BEGINS

PLUS22:	MOVE I,PLUS1I
	MOVE J,PLUS1J
	IMUL B,I
	IMUL J,A
	IMUL I,A
	SUBM B,J
	PUSHJ P,GCDD"
	CAIG J,37
	CAILE I,100
	JRST PLOUNN
	DPB J,[370500,,@PLUST1(E)]	;HOW LONG NUM
	DPB I,[310600,,@PLUST1(E)]	;HOW LONG DEN
	LDB A,[150400,,@PLUST1(E)]	;ARTIC
	MOVEM A,PLUS1Z'
	MOVEI A,1
	DPB A,[200100,,@PLUST1(E)]	;ARTIC _ 3
PLUS23:	MOVE N,PLUS1L
	MOVE M,PLUS1K
	MOVE A,PLUS1G
	MOVE B,PLUS1H
	IMUL N,A
	IMUL B,M
	IMUL M,A
	SUB N,B
	MOVE I,PLUS1X
	MOVE J,PLUS1Y
	IMUL J,M
	IMUL N,I
	IMUL I,M
	ADD J,N
	PUSHJ P,GCDD"
	MOVEM J,PLUS1Y
	MOVEM I,PLUS1X
	SKIPN E
	SETOM PLUSFG
	JUMPE E,PLUS20
	LDB I,[310600,,RESTW(G)]
	LDB J,[370500,,RESTW(G)]
	JRST PLOOP7

PLUS16:	MOVE A,PLUS1Q
	MOVE B,PLUS1R
	MOVEM A,PLUS1M
	MOVEM B,PLUS1N
PLUS17:	JRST PLUS12

PLUST1:	NOTEW(G)
	RESTW(G)

PLOP1:	SKIPN A,TEMPF(G)	;DRAW TRIPLET ARC
	JRST PLOP2
	PUSHJ P,PLGTS
	SUBI M,220000
	SKIPGE N,PLTRIH(G)
	JRST PLOP1A
	CAMGE N,M
	MOVE M,N
PLOP1A:	PUSHJ P,POSHU
	SUBI A,24000
	PUSHJ P,PLOTG0
	PUSHJ P,PLVIS
	MOVEI C,UP
	MOVNI A,40000
	ADDM A,PLY
	MOVEI A,40000
	PUSHJ P,PLINE
	MOVE A,PLTREN(G)
	ADDI A,50000
	SUB A,TEMPF(G)
	ASH A,-1
	SUBI A,40000
	ADDM A,PLX
	MOVEM A,PLOP1L'
	MOVEI C,RT
	PUSHJ P,PLINE
	MOVEI E,PFTRIP"
	PUSHJ P,PLFIG
	PUSHJ P,PLVIS
	MOVE A,PLOP1L
	MOVEI C,RT
	PUSHJ P,PLINE
	MOVEI C,DN
	MOVEI A,40000
	PUSHJ P,PLINE
	SETZM TEMPF(G)
	SETOM PLTRIH(G)

PLOP2:	SKIPE PLRIND
	JRST PLLRR
PLOPA1:	LDB K,[220700,,NOTEW(G)]	;PITCH
PLOOP8:	SETZM SMPLSW'
SMSCP1:	MOVSI L,-52
PLOP2A:	CAMLE K,@PLWTB(G)
	AOBJN L,PLOP2A
	TLZ L,777777
	MOVEI C,1000
	CAMN K,@PLWTB(G)	;SKIP IF ACCIDENTAL NEEDED
	JRST PLOP3
PLOPA2:	MOVEI M,-1(K)
	IDIVI M,14
	MOVE A,SMSCT(N)	;UPPER NEIGHBOR
	MOVEI M,-3(K)
	IDIVI M,14
	MOVE B,SMSCT(N)	;LOWER NEIGHBOR
	CAMN B,A
	JRST PLOPA6
	ASH A,2
	CAML B,A
	JRST PLOP2F	;TAKE UPPER
	ASH B,4
	CAML A,B
	SOJA L,PLOP2F	;TAKE LOWER
PLOPA6:	PUSHJ P,GPNM
PLOPA3:	JUMPE A,PLOPA4
	LDB B,[220700,,A]
	CAMN B,@PLWTB(G)
	SOJA L,PLOP2F	;DON'T STEAL UPPER HOME
	SUBI L,1
	CAMN B,@PLWTB(G)
	AOJA L,PLOP2F	;DON'T STEAL LOWER HOME
	PUSHJ P,GPNM0
	AOJA L,PLOPA3

PLOPA4:	MOVE C,K
	SUB C,PLWTZ(L)
	MOVMS C
	MOVE B,K
	SUB B,PLWTZ(L)-1
	MOVMS B
	CAMGE B,C
	SOJA L,PLOP2F	;AVOID --
	CAMLE B,C
	JRST PLOP2F	;AVOID ++
PLOPA5:	CAMLE K,PLLASN(G)
	SOS L

PLOP2F:	SKIPE SMPLSW
	POPJ P,
	SKIPN PLICI(G)
	MOVEM K,@PLWTB(G)	;(L)
	MOVE B,K
	SUB B,PLWTZ(L)
	HRRZ A,PLSLN(G)
	TRNE A,10
	CAME K,PLLASN(G)
	MOVE C,B
PLOP3:	SKIPE SMPLSW
	POPJ P,
	MOVEM C,PL3AC'	;ACCIDENTAL NUMBER
	SETZM PLCLAW'
	MOVE I,CCBM(G)
	SETOM CCBM(G)
	JUMPGE I,PLOP3D
	HRRE A,PLWSNO(G)
	JUMPL A,PLOP3A	;IF CLEF NEEDED INITIALLY
	CAML L,PLSSLT(A)
	CAMLE L,PLSSGT(A)
	JRST PLOP3A	;IF NEW CLEF NEEDED
PLOP3W:	SKIPL N,PLCKS(G)
	CAME N,PLFKS(G)
	JRST PLOP3E	;IF KEY SIG NEEDED

PLOP3X:	SKIPL A,PLCTS(G)
	CAME A,PLFTS(G)
	JRST PLOP3M	;IF TIME SIG NEEDED
PLOP3Z:	SKIPE PLCLAW
	JRST PLOPA1	;RECOMPUTE PL3AC IF NEW KEY
	MOVE A,PLICI(G)
	SETZM PLICI(G)
	JUMPN A,PLOP4
	HRRE A,PLWSNO(G)
	PUSHJ P,PLGTS
	MOVE B,PLSSOT(A)
	SUB B,L
	ASH B,14.	;*40000
	ADD M,B
	MOVEM M,PL3ML'
	MOVEM L,PL3LL'
	SUB L,PLSSBL(A)
	MOVEM L,PL3LM'
	MOVE E,PL3AC
	CAIN E,1000
	JRST PL3ZA
PL3ZC:	MOVE B,PLOTBR'
	ADD B,STSMSP'
	CAMLE B,PLW(G)
	MOVEM B,PLW(G)
	MOVE B,PLOOPA
	JUMPE B,PL3ZB
	SUB B,PLOOPC
	ASH B,9
	MUL B,STBVC
	ADD B,PLOTWW
	SUB B,PACWDH"(E)	;ACCID WIDTH+40000
	CAMLE B,PLW(G)
	MOVEM B,PLW(G)
PL3ZB:	MOVE E,PLACCT"(E)
	MOVE A,PLW(G)
	PUSHJ P,PLDOIT	;PLOT ACCIDENTAL
	MOVEI A,40000
	ADDB A,PLW(G)
PL3ZA:	SKIPGE PLCLS
	JRST PLOP4
	MOVE A,PLW(G)
	MOVEM K,PL3ZAK'

PLLRR:	MOVE B,PLOTBR'
	ADD B,STSMSP'
	CAMLE B,PLW(G)
	MOVEM B,PLW(G)
	MOVE B,PLOOPA
	JUMPE B,PLLRR1
	SUB B,PLOOPC
	ASH B,9
	MUL B,STBVC
	ADD B,PLOTWW
	CAMLE B,PLW(G)
	MOVEM B,PLW(G)
PLLRR1:	MOVE A,PLW(G)
	MOVEM A,PLOTIS'
	SKIPE PLRIND
	JRST PLOP2R
	SKIPE PLTRII
	JRST PLLRR2
	SKIPN TEMPF(G)
	MOVEM A,TEMPF(G)
PLLRR2:	MOVE L,PL3LM
	JUMPL L,PLLLB
	SUBI L,13
	JUMPGE L,PLLLB
PLLLR:	MOVE M,PL3ML
	MOVE C,PL3NQ
	MOVE B,PL3LM
	MOVE E,PUNOTE"(C)

PLLLRR:	SUBI B,5
	JUMPN B,PLLLR1
	MOVE B,PLSLN(G)
	TRNE B,10
	JRST PLLLR1
	PUSHJ P,GPN
	  JRST PLLLR1
PLLLR2:	LDB A,[220700,,A]	;PITCH
	SUB A,K
	JUMPN A,PLLLR0
	PUSHJ P,GSN
	  JRST PLLLR1
	JRST PLLLR2
PLLLR0:	MOVE B,A
PLLLR1:	MOVEM B,PLSTUP'	;<0 STEM UP
	SKIPL B
	MOVE E,PDNOTE"(C)
PLRLR:	MOVE A,PLOTIS
	PUSHJ P,PLDOIT	;PLOT NOTE OR REST
	MOVEM B,PLRLRB'	;HEIGHT
	MOVE M,PL3ML
	SUBI M,100000
	SKIPGE PLSTUP
	SUB M,B
	PUSHJ P,PTIBU
	MOVE M,PL3ML
	ADDI M,100000
	SKIPL PLSTUP
	ADD M,B
	PUSHJ P,POSHD
	SETZM PLARAF'
	MOVE A,PLOTIS
	SETZM PLOP9F'
	MOVEM A,PLOTSI'
	MOVEM A,PLOTSJ'
	SKIPE PLRIND
	PUSHJ P,PLARA7
	SETZM PLOTIL
	SKIPE TEXT(G)
	SKIPE PLRIND
	JRST PLARA

;HERE FOR TEXT

PLARA0:	CAMGE A,PLWT(G)
	MOVE A,PLWT(G)
	MOVEM A,PLOTSI
	MOVEM A,PLOTSJ
	MOVE B,PLUSGF(G)
	SETZM PLUSGF(G)
	PUSHJ P,PLGTS
	ADD M,[1000000]
	JUMPN B,PLARA6
	ILDB C,TEXT(G)
	CAIG C,15
	CAIGE C,11
	CAIN C,40
	JRST PLARA3
	CAIN C,"\
	JRST PLARA1
	CAIN C,""
	SETCMB B,PLARAQ
	CAIN C,""
	MOVE C,PLOTIR(B)
	CAIE C,"=
	CAIN C,137	;UNDERLINE
	JRST PLARA6
	CAIE C,136	;UMLAUT
	JRST PLARA5
	MOVE A,PLOTSJ
	MOVE B,PLOTIL
	CAIE B,141	;L.C. A
	CAIN B,157	;L.C. O
	ADDI M,54000
	CAIN B,165	;L.C. U
	ADDI M,54000
PLARA5:	MOVEM C,PLOTIL
	MOVE B,PLOTSI
	MOVEM B,PLOTSJ
	PUSHJ P,PLARA9
PLARA4:	MOVE E,PLOTCT"(C)
	JUMPE E,PLARA0
	PUSHJ P,PLOTGO
	PUSHJ P,PLFIG
	ADDB A,PLOTSI
	LDB C,TEXT(G)
	CAIN C,"-
	JRST PLARAD
	SETOM PLARAF
	JRST PLARA0

PLARA6:	SKIPL PLARAF
	JRST PLARAU
	MOVE B,PLUSFG
	MOVEM B,PLUSGF(G)
PLARAC:	MOVEM A,UNDERL(G)
PLARAE:	MOVE B,TEXT(G)
	DBP7 B
	MOVEM B,TEXT(G)
	JRST PLARA

PLARAD:	MOVE C,TEXT(G)
	ILDB B,C
	CAIE B,"-
	SETOM PLOP9F
PLARAU:	SKIPE PLUSFG'
	JRST PLARAE
	JRST PLARA

PLARA7:	SUBI A,40000
PLARA9:	SKIPGE B,UNDERL(G)
	POPJ P,
	ADDI B,40000
	PUSH P,A
	PUSH P,M
	PUSH P,C
	SUBI A,330000
	CAMGE A,B
	MOVE A,B
	ADDI A,170000
	PUSHJ P,PLGTS
	ADD M,[1154000]
	MOVE N,M
	PUSHJ P,PFS
	POP P,C
	POP P,M
	POP P,A
	SETOM UNDERL(G)
	POPJ P,

PLARA1:	SETZM TEXT(G)
PLARA2:	AOS E,TEXP(G)
	SKIPN I,(E)
	JRST PLARA8
	LDB C,[311300,,I]
	CAIE C,4
	JRST PLARA2
	ADD I,PLOX
	HRLI I,440700
	MOVEM I,TEXT(G)
	JRST PLARA0

PLARA8:	PUSHJ P,PLARA9
	JRST PLAR

PLARA3:	SKIPN PLARAF
	JRST PLARA0
	SKIPE PLUSFG
	JRST PLARAC
	MOVE A,ENWID"
	ADDM A,PLOTSI
	SETOM PLOP9F

PLARA:
PLAR:	SETZM PLARSA'
	SKIPN A,PLRIND
	LDB A,[150400,,NOTEW(G)]	;ARTIC
	JUMPL A,PLARZ	;REST
	SETOM PLSTVH'
	MOVEM A,PLARSA
	TRNE A,10
	JRST PLARL	;LEGATO
	JRST PLARZ


PLPA:	AOS E,PLFREP
	CAIL E,FREEND"
	JRST TUFLUK"
	MOVEM A,-1(E)
	POPJ P,

PLAR1:	MOVE B,PL3LM	;HERE FOR STACCATO
	PUSHJ P,PLGTS
	SUBI M,60000
	SKIPGE PLSTUP
	ADDI M,540000
	SUBI B,2
	JUMPL B,PLAR2
	SUBI B,6
	JUMPL B,PLAR3
PLAR2:	ASH B,14.	; * 40000
	SUB M,B
PLAR3:	MOVEI A,31000
	ADD A,PLOTIS
	SKIPGE B,PLSTVH
	JRST PLAR4
	SKIPL PLSTUP
	SKIPG PLARZQ
	JRST PLAR5
	SUBI B,100000
	CAMLE M,B
	MOVE M,B
PLAR5:	SKIPGE PLSTUP
	SKIPL PLARZQ
	JRST PLAR4
	ADDI B,100000
	CAMGE M,B
	MOVE M,BîPLAR4:	MOVEI E,PLDOT"
	PUSHJ P,PLOTGO
	PUSHJ P,PLFIG
	JRST PLARZZ

PLARL:	SETOB K,PELEG(G)	;HERE FOR LEGATO
PLARLE:	LDB A,[220700,,NOTEW(G)]	;PITCH
	CAME A,PLLASN(G)
	JRST PLARE1
	SETOM PLARZW'
	PUSH P,K
	PUSHJ P,PZSL
	POP P,K
PLARE1:	MOVEI A,(G)
	SKIPGE PLSTUP
	TRO A,400000
	LDB B,[220700,,NOTEW(G)]
	DPB B,[220700,,A]
	PUSHJ P,PLPA	;STUP+PITCH,,LEGATO+PART #
	MOVEI A,70000
	ADD A,PLOTIS
	PUSHJ P,PLPA	;HORIZ CENTER
	MOVE M,PL3ML
	SKIPL PLSTUP
	JRST PLARL1
PLARL0:	MOVEI N,100000
	ADD N,M
	SUB M,PLRLRB
	SUBI M,100000
	JRST PLARL2
PLARL1:	MOVE N,PLRLRB
	ADD N,M
	ADDI N,100000
	SUBI M,100000
PLARL2:	MOVE A,M
	PUSHJ P,PLPA	;ABOVE
	MOVE A,N
	PUSHJ P,PLPA	;BELOW
	LDB A,[220700,,NOTEW(G)]
	CAME A,PLLASN(G)
	JRST PLARE2
	MOVEM K,PLARZW
	MOVEI K,0
PLARE2:	JUMPE K,PLARZR
	JRST PLARZY

PLARZ:	AOSE K,PELEG(G)
	JRST PLARZY
	SETZM PLARZW
	SKIPL PLRIND
	JRST PLARLE
PLARZR:	PUSHJ P,PZSL
PLARZY:	SKIPE PLRIND
	JRST PLARZZ
	MOVE A,PLARSA
	CAIE A,3
	CAIN A,4
	JRST PLAR1	;STACCATO
	JRST PLARZZ



PZSL:	PUSHJ P,PLGTS
	ADDI M,200000
	MOVEM M,PLARZC'	;CENTER OF STAFF
	HRLOI J,377777
	MOVEM J,PLAZP3'
	SETOB J,PLAZP2'
	SETOB B,PLAZP7'
	SETOB M,PLARZL'
	SKIPA I,FREP"
PLARZ8:	ADDI I,4
	CAML I,PLFREP
	JRST PLARZ9
	MOVE A,(I)
	TLZ A,177
	TRZ A,400000
	CAIE A,(G)
	JRST PLARZ8
	MOVE M,B
	MOVEM I,PLARZL
	LDB B,[220700,,(I)]
	CAIN J,1
	MOVEM B,PLAZP2
	MOVE A,3(I)
	CAIN J,1
	MOVEM A,PLAZP3
	AOJA J,PLARZ8
PLARZ9:	MOVEM M,PLAZP8'
	HRLOI M,377777	;TOP
	MOVNI N,1	;BOTTOM
	SETZM PLARZU'
	SETZM PLARZD'
	SETOB K,PLG0X'
	SETZM PLG01'
	SETZM PLG02'
	SETOM PLARZQ'	;SGN D2M/DA2

	SKIPA I,FREP"
PLARZ1:	ADDI I,4
	CAML I,PLFREP
	JRST PLARZ2
	MOVE A,(I)
	TLZ A,177
	TRNE A,400000
	MOVEI C,PLARZU
	TRZN A,400000
	MOVEI C,PLARZD
	CAIE A,(G)
	JRST PLARZ1
PLARZ6:	AOS (C)
	MOVEM I,PLARZI'
	MOVE B,(I)
	MOVEM B,PLGXB'
	MOVE B,2(I)
	CAIE C,PLARZD
	CAME I,PLARZL
	JRST PLARZ0
	LDB A,[220700,,(I)]
	CAMGE B,PLAZP7
	CAML A,PLAZP8
	JRST PLARZ0
	MOVE B,3(I)
	SUBI B,200000
	MOVEM B,2(I)
PLARZ0:	MOVEM B,PLAZP7
	CAMGE B,M
	MOVEM B,M
	SKIPL PLG02
	MOVNM B,PLG02
	MOVNM B,PLGP2'
	MOVE B,3(I)
	CAIE C,PLARZU
	SKIPL PLG0X
	JRST PLARZ7
	LDB A,[220700,,(I)]
	CAMLE B,PLAZP3
	CAMG A,PLAZP2
	JRST PLARZ7
	MOVE B,2(I)
	ADDI B,200000
	MOVEM B,3(I)
PLARZ7:	CAMLE B,N
	MOVEM B,N
	SKIPL PLG01
	MOVNM B,PLG01
	MOVNM B,PLGP1'
	MOVE B,1(I)
	SKIPGE PLG0X
	MOVEM B,PLG0X
	MOVEM B,PLGPX'
	AOJA K,PLARZ1

PLARZ2:	JUMPL K,CPOPJ
	MOVE I,PLARZI
	MOVE A,PLG0X
	ADD A,PLGPX
	ASH A,-1
	MOVEM A,1(I)
	MOVE A,PLG02
	ADD A,PLGP2
	ASH A,-1
	ADDI A,100000
	MOVNM A,2(I)
	MOVE A,PLG01
	ADD A,PLGP1
	ASH A,-1
	SUBI A,100000
	MOVNM A,3(I)
	SKIPG PLARZU	;IF SOME STEMS UP
	JRST PLARZ3	;ABOVE
	SKIPG PLARZD	;IF SOME STEMS DOWN
	JRST PLARZ4	;BELOW
	SUB M,PLARZC
	SUB N,PLARZC
	MOVMS M
	MOVMS N
	CAMGE M,N
PLARZ3:	MOVNS PLARZQ
PLARZ4:	MOVE E,PLARZQ
	MOVE A,@PLGPT(E)	;Y.P
	MOVEM A,PLSTVH'
	MOVE A,PLFMR
	MOVEM A,PLFM
	MOVE A,PLGPX
	SUB A,PLG0X
	MOVEM A,PLGL'
	FLOAT A
	MOVEM A,PLFL'
	FSC A,-1
	MOVEM A,PLFL%2'
	FMPR A,A
	MOVEM A,PLFL24'
	MOVE A,PLFL
	FDVR A,PLFM
	MOVEM A,PLFL%M'
	MOVE A,@PLGPT(E)	;Y.P
	SUB A,@PLG0T(E)	;Y.0
	FLOAT A
	FDVR A,PLFL
	MOVEM A,PLFY%L'
	MOVSI A,377777
	MOVEM A,PLFC2'
	SETOM PLGAF'
	SETOM PLGAW'

	SKIPA I,FREP
PLGA:	ADDI I,4
PLGA1:	CAML I,PLFREP
	JRST PLGB
	MOVE A,(I)
	TLZ A,177
	TRZ A,400000
	CAIE A,(G)
	JRST PLGA
	AOSN PLGAW
	JRST PLGA6
PLGA2:	AOS PLGAF
	MOVE A,1(I)	;X.I
	SUB A,PLG0X	;X.0
	MOVE C,A	;X.I - X.0
	ASH A,1
	SUB A,PLGL
	FLOAT A
	FSC A,-1
	MOVE B,A	;X'.I
	MOVN A,@PLGDT(E)	;Y.I
PLGA3:	SUB A,@PLG0T(E)	;Y.0
	FLOAT C
	FMPR C,PLFY%L
	FLOAT A
PLGA4:	FSBRB C,A	;-Y'.I
	MOVNM C,PLFYP'
	FMPR C,PLFL%M	;-Y'.I * L / M
	IMUL C,PLARZQ
	FADR C,PLFL24	;L^2/4 -Y'.I*L/M
	FMPR B,B	;X'.I^2
	FSBR C,B	;L^2 - X'.I^2 - Y.I*L/M
	FMPR A,A	;Y'.I^2
	MOVE E,A
	FADR A,[1.0]	;EPSILON
	FDVR C,A	;C.I^2
PLGA5:	CAML C,PLFC2
	JRST PLGA6
	MOVEM C,PLFC2
	FMPR E,PLFNC2
	FADR E,B	;X'.I^2 + C^2*Y'.I^2
	MOVE C,PLFL24
	FSBRM C,E
	MOVE C,PLFYP
	IMUL C,PLARZQ
	FMPR C,PLFL
	FDVR C,E
	MOVEM C,PLGBMM'
	JUMPG C,PLGA6
	MOVE C,PLFL24
	FSBR C,B
	FDVR C,A
	MOVEM C,PLGBCC'

PLGA6:	HRLI J,4(I)
	HRRI J,(I)
	MOVNI K,4
	ADDB K,PLFREP
	BLT J,-1(K)
	MOVE E,PLARZQ
	JRST PLGA1


PLGB:	SKIPGE PLGAF
	JRST PLGBR	;IF ONLY ONE POINT
	MOVE A,PLFC2
	CAML A,[0.01]
	JRST PLGB0
PLGB9:	MOVE A,PLFNC2
	MOVEM A,PLFC2
	SKIPLE A,PLGBMM
	JRST PLGB8
	MOVE A,PLGBCC
	MOVEM A,PLFC2
	MOVE A,[1.0^10]
PLGB8:	MOVEM A,PLFM
PLGB0:	MOVE A,PLFL24
	FDVR A,PLFC2
	MOVE B,PLFC2
	FMPR B,PLFM
	MOVEM B,PLFMC2'
	FMPR B,B
PLGB2:	MOVE C,PLFL24
	FDVR C,B
	FAD A,C
	PUSHJ P,SQRT"
	MOVEM A,PLFR'
	MOVE A,PLFL
	FDVR A,PLFMC2
	FSC A,-1
	IMUL A,PLARZQ
	MOVEM A,PLF%'
	MOVE A,PLFC2
	PUSHJ P,SQRT"
	MOVEM A,PLFC'
	FMPR A,PLFM
	PUSHJ P,ATAN"
	MOVE B,A
	FSBR A,[1.5707963268]
	IMUL A,PLARZQ
	FADR A,[3.1415926535]
	FSC B,1
	FDVR B,PLFN
	IMUL B,PLARZQ
	MOVEM B,PLFDTH'
	MOVEI K,0

PLGB1:	MOVEM A,PLFT0'
	PUSHJ P,COS"
	FMPR A,PLFR
	FMPR A,PLFC
	FADR A,PLFL%2
	MOVE I,A
	FIX A
PLGB3:	ADD B,PLG0X	;X.0
	MOVEM B,PLGX(K)
	MOVE A,PLFT0
	PUSHJ P,SIN"
	FMPR A,PLFR
	FSBR A,PLF%
	FMPR I,PLFY%L
	FIX I
	FIX A
PLGB4:	ADD B,@PLG0T(E)
	ADD B,J
	MOVNM B,PLGY(K)
	MOVE A,PLFT0
	FSBR A,PLFDTH
	CAME K,PLGN
	AOJA K,PLGB1

PLGC:	MOVEI L,0
PLGC0:	CAML L,SLURDN"
	JRST PLGBR	;ENOUGH LINES DRAWN
	MOVE K,PLGCT1(L)
PLGC1:	CAMN K,PLGCT2(L)
	AOJA L,PLGC0
	MOVE A,PLGX(K)
	MOVE B,PLGX(K)+1
	MOVE M,PLGY(K)
	MOVE N,PLGY(K)+1
	MOVE C,PLG0NT(L)
	IMUL C,PLARZQ
	CAME K,L
	ADD M,C
	CAME K,PLGCNT+1(L)
	ADD N,C
	PUSHJ P,PTIBU
	PUSHJ P,POSHD
	EXCH M,N
	PUSHJ P,PTIBU
	PUSHJ P,POSHD
	EXCH M,N
	PUSHJ P,PFS
	TRNN L,1
	SOJA K,PLGC1
	AOJA K,PLGC1

PLGBR:	SKIPL PLARZW
	POPJ P,
	MOVE A,PLGXB
	PUSHJ P,PLPA
	MOVE A,PLGPX
	PUSHJ P,PLPA
	MOVN A,PLGP2
	PUSHJ P,PLPA
	MOVN A,PLGP1
	PUSHJ P,PLPA
	POPJ P,

PLGCT1:	8
	1
	6
	3

PLGCT2:	-1
	8
	1
	6

PLGCNT:	9
	8
	7
	6
	5

PLG0NT:	0
	-6000
	6000
	-14000

	3(I)
PLGDT:	0
	2(I)

	PLG01
PLG0T:	0
	PLG02

	PLGP1
PLGPT:	0
	PLGP2

PLGX:	BLOCK 10.
PLGY:	BLOCK 10.
PLFMR:	1.0
PLFM:	1.0
PLFN:	9.0
PLGN:	9

PLFNC2:	1.0

PLARZZ:	MOVE I,PL3ZAK
	MOVEM I,PLLASN(G)
	MOVE I,PLSTUP
	HRRZ I,PLARSA
	MOVEM I,PLSLN(G)
	MOVE I,PL3ND	;# DOTS
	JUMPE I,PLOP9
PLOP3Y:	MOVE A,PLW(G)
	SKIPE PLRIND
	JRST PLOP3R
	MOVE M,PL3ML
	HRRE B,PLWSNO(G)
	MOVE L,PL3LL
	XOR L,PLSSXT(B)
	TRNE L,1
	SUBI M,30000	;ON A LINE
PLOP3S:	MOVEI E,PLDOT"
	PUSHJ P,PLDOIT
	SOJG I,PLOP3Y
PLOP9:	MOVE A,PLOTSI
	SKIPL PLOP9F
	JRST PLOP9A
	CAMLE A,PLW(G)
	MOVEM A,PLW(G)
PLOP9A:	MOVEM A,PLWT(G)
	MOVE A,PLW(G)
	SKIPN PLTRII
	MOVEM A,PLTREN(G)
	SKIPN A,PLUS1Y
	JRST PLOP4B
	SKIPE E,PLRIND
	MOVEI E,1
PLUS3:	MOVE A,PLUS1H
	MOVE B,PLUS1G
	CAIG A,177
	CAILE B,100
	JRST PLOUNN
	DPB A,[60700,,@PLUS3U(E)]	;WHERE NUM
	DPB B,[600,,@PLUS3U(E)]	;WHERE DEN
	ASH A,14.
	IDIV A,B
	MOVEM A,@PLUS3T(E)
	MOVE A,PLUS1Y
	MOVE B,PLUS1X
PLUS33:	JUMPE E,PLUS31
	SKIPN PLREST
	JRST PLUS31

PLUS34:	LDB I,[310600,,PLREST]
	SKIPN I
	MOVEI I,100
	LDB J,[370500,,PLREST]
	SETZM PLREST
	IMUL A,I
	IMUL J,B
	IMUL I,B
	ADD J,A
	PUSHJ P,GCDD"
	MOVE A,J
	MOVE B,I
PLUS31:	CAIG A,37
	CAILE B,100
	JRST PLOUNN
	DPB A,[370500,,@PLUS3U(E)]	;HOW LONG NUM
	DPB B,[310600,,@PLUS3U(E)]	;HOW LONG DEN
	MOVE A,PLUS1Z
	DPB A,[150400,,@PLUS3U(E)]	;ARTIC
PLOP4B:	SKIPE A,PLREST
	MOVEM A,RESTW(G)
PLOP4:	AOJA G,PLOOP3


PLUS3T:	MSTM(G)
	RSTM(G)

PLUS3U:	NOTEW(G)
	RESTW(G)

PLOP3R:	PUSHJ P,PLGTS
	ADDI M,140000
	JRST PLOP3S

PLOP2R:	PUSHJ P,PLGTS
	MOVEI N,140000
	ADD N,M
	MOVEM N,PL3ML
	MOVE C,PL3NQ
	MOVE E,PREST"(C)
	JRST PLRLR

PLOOP3:	CAMGE G,PLNLI
	JRST PLOOP2

;ALL PARTS DONE FOR THIS INSTANT; UPDATE X POS

	MOVN A,PLNLI
	HRLZS A
	MOVE B,A
PLOP5:	CAMGE B,PLW(A)
	MOVE B,PLW(A)
	AOBJN A,PLOP5
	MOVEM B,PLOTIW
	SETCMB A,PLCLS
	JUMPGE A,PLOOP9

;ADVANCE TIME OF ALL VOICES

	MOVEI G,0
PLOP8T:	SKIPL A,MSTM(G)
	JRST PLOP8U
	SKIPL RSTM(G)
	JRST PLOP8
	JRST PLOP7Q

PLOP8U:	CAMLE A,PLOOPA
	JRST PLOP8
	LDB I,[310600,,NOTEW(G)]
	LDB J,[370500,,NOTEW(G)]	;OLD HOW LONG NUM
	SKIPN I
	MOVEI I,100
	MOVE A,I
	MOVE B,J
	LDB I,[600,,NOTEW(G)]	;OLD WHERE DEN
	LDB J,[60700,,NOTEW(G)]	;OLD WHERE NUM
	SKIPN I
	MOVEI I,100
	IMUL B,I
	IMUL I,A
	IMUL J,A
	ADD J,B
	PUSHJ P,GCDD
	MOVE M,I
	MOVE N,J
PLOP6:	AOS B,LOCA(G)
PLOP6A:	MOVE A,(B)
	TLNE A,177
	TLNN A,377600
	JUMPL A,PLOP6	;TEMPO OR STOP
	JUMPE A,PLOP7E	;END OF MEAS
	MOVEM A,NOTEW(G)
	LDB I,[600,,A]
	LDB J,[60700,,A]
	SKIPN I
	MOVEI I,100
	MOVE A,I
	MOVE B,J
	IMUL B,M
	IMUL A,N
	CAMGE A,B
	PUSHJ P,PLOIR
	MOVE A,NOTEW(G)
	PUSHJ P,PLMSTM
	MOVEM A,MSTM(G)
PLOP8:	ADDI G,1
	CAMGE G,PLNLI
	JRST PLOP8T
	JRST PLOOP9

PLOP7E:	SETOM MSTM(G)
	LDB I,[600,,PLCTS(G)]
	LDB J,[60700,,PLCTS(G)]
	JUMPE J,PLOP7Q
	SKIPN I
	MOVEI I,100
	MOVE A,I
	MOVE B,J
	IMUL B,M
	IMUL A,N
	CAMGE A,B
	PUSHJ P,PLOIR
PLOP7Q:	MOVE N,PLNLI
PLOP7F:	SKIPL MSTM(N)-1
	JRST PLOP8
	SKIPL RSTM(N)-1
	JRST PLOP8
	SOJG N,PLOP7F
	JRST PLEMD



PLMSTM:	LDB B,[600,,A]
	SKIPN B
	MOVEI B,100
	LDB A,[60700,,A]
	ASH A,14.
	IDIV A,B
	POPJ P,



PLDOIT:	PUSHJ P,PLOTGO
	PUSHJ P,PLFIG
	ADDB A,PLW(G)
	POPJ P,



PLLLB:	MOVE M,PL3ML	;LEDGER LINES
	SKIPGE L
	SUBI M,40000
	TRNE L,1
	ADDI M,40000
	ASH L,-1
	SUBI A,23000
	PUSHJ P,PLOTG0
	MOVE E,PLLLBT"(L)
	PUSHJ P,PLFIG
	JRST PLLLR

PLOIR:	MOVE A,N
	ASH A,14.
	IDIV A,M
	MOVEM A,RSTM(G)
	DPB N,[60700,,RESTW(G)]	;WHERE NUM
	DPB M,[600,,RESTW(G)]	;WHERE DEN
	IMUL N,I
	IMUL I,M
	IMUL J,M
	SUB J,N
	PUSHJ P,GCDD
	CAIG I,100
	CAILE J,37
	JRST PLOINR
PLUP9:	DPB I,[310600,,RESTW(G)]	;HOW LONG DEN
	DPB J,[370500,,RESTW(G)]	;HOW LONG NUM
	POPJ P,



PLEMD:	SKIPGE PLSLVC(G)
	JRST PLEMD0
	LDB C,[600,,PLCTS(G)]
	LDB B,[60700,,PLCTS(G)]
	SKIPN C
	MOVEI C,100
	JUMPE B,PLEMD0
	ASH B,14.
	IDIV B,C
	SUB B,PLOOPA
	ASH B,9
	MUL B,STBVC
	ADD B,PLOTIS
	SUB B,STSMSP
	CAMLE B,PLOTIW
	MOVEM B,PLOTIW
PLEMD0:	MOVE A,PLOTIW
	MOVEM A,PLSTED'
	AOS B,PLOTX1
	CAMN B,PLOTX2
	ADDI A,100000
	MOVEM A,PLOTIW
	MOVEI G,0

PLEMD1:	MOVE E,STAFPN(G)
	SKIPL PLSLVC(E)
	JRST PLEMD9
	PUSHJ P,PLGTP
	MOVE A,PLSTED
	ADD A,PLOTIV
	ASH A,-1
	SUBI A,44000
	MOVEI E,RWHOLE"
	PUSHJ P,PLOTGO
	PUSHJ P,PLFIG
PLEMD9:	PUSHJ P,PLGTP
	MOVEM M,PLEMDM'
	MOVEI J,5
PLEMD2:	MOVE N,M
	MOVE A,PLOTIW
	MOVE B,PLOTIU
	PUSHJ P,PFS	;STAFF LINE
	MOVEI M,100000
	ADDB M,PLEMDM
	SOJG J,PLEMD2
	ADDI G,1
	CAMGE G,PLNLI
	JRST PLEMD1
	SUBI G,1
PLEMD3:	PUSHJ P,PLGTP
	MOVE N,M
	ADDI N,400000
	MOVE A,PLSTED
	MOVEM A,PLOTIU
	MOVEM A,PLOTIV
	MOVE B,A
	PUSHJ P,PFS	;BAR LINE
	SOJGE G,PLEMD3

PLOPP:	SETOM PLOOPA'
	SETOM PLOOPD'
	SETZM PLOOPC'
	MOVE A,PLOTIW
	MOVEM A,PLOTBR'
	SETOM PLCLS'
	MOVE G,PLNLI
PLOPP1:	HRL A,PLWTA(G)-1
	HRR A,PLWTB(G)-1
	MOVEI B,52(A)
	BLT A,(B)
	SOJG G,PLOPP1
PLOPP2:	MOVE N,PLNLI
	MOVNI M,1
PLOP7H:	AOS B,MEAS(N)-1
	SKIPN B,(B)
	JRST PLOP7S	;END OF VOICE
	LDB A,[311300,,B]
	CAIL A,3
	JRST PLOP7H	;UNKNOWN OR TEXT FLAG
	JUMPN A,PLOP7I
	ADD B,PLOX
	HRRM B,LOCA(N)-1
	JRST PLOP7K

PLOP7I:	TLZ B,777600
	MOVEM B,@PLOP7T(A)-1
	JRST PLOP7H

PLOP7T:	PLCTS(N)-1
	PLCKS(N)-1

PLO7A:	MOVE N,PLNLI
PLO7A9:	SKIPN TEXT(N)-1
	JRST PLO7A0
	SKIPA M,LOCA(N)-1
PLO7A1:	AOS M,LOCA(N)-1
	MOVE I,(M)
	TLNE I,177
	TLNN I,377600
	JUMPL I,PLO7A1	;NOT A NOTE
	JUMPE I,PLO7A0	;END OF MEAS.
	SETZM PLARAF'
	MOVE A,TEXT(N)-1

PLO7A2:	SOS PLARAF
PLO7A5:	ILDB C,A
	CAIN C,"\
	JRST PLO7A6
	CAIE C,"=
	CAIN C,137	;U.L.
	JRST PLO7A3
	MOVEM A,TEXT(N)-1
	CAIN C,"-
	JRST PLO7A1
	CAIG C,15
	CAIGE C,11
	CAIN C,40
	AOSN PLARAF
	JRST PLO7A2
	JRST PLO7A1

PLO7A3:	AOSN PLARAF
	MOVEM A,TEXT(N)-1
	JRST PLO7A1

PLO7A6:	SETZM TEXT(N)-1
PLO7A7:	AOS E,TEXP(N)-1
	SKIPN I,(E)
	JRST PLO7A0
	LDB C,[311300,,I]
	CAIE C,4
	JRST PLO7A7
	ADD I,PLOX
	HRLI I,440700
	MOVEM I,TEXT(N)-1
	JRST PLO7A5

PLO7A0:	SOJG N,PLO7A9
	JRST PLOPP2

PLOP7J:	AOS B,LOCA(N)-1
PLOP7K:	MOVE A,(B)
	TLNE A,177
	TLNN A,377600
	JUMPL A,PLOP7J	;TEMPO OR STOP
	JUMPE A,PLOP70	;SILENT MEASURE
	MOVEM A,NOTEW(N)-1
	PUSHJ P,PLMSTM
	MOVEM A,MSTM(N)-1
	SETOM RSTM(N)-1
	SETZM PLSLVC(N)-1
	JUMPE A,PLOP7M
	SETZM RSTM(N)-1
	LDB I,[600,,NOTEW(N)-1]
	SKIPN I
	MOVEI I,100
	LDB J,[60700,,NOTEW(N)-1]
	PUSHJ P,GCDD
	MOVEI M,1
	CAIG I,100
	CAILE J,77
	JRST PLOUNR
PLUP8:	DPB I,[310600,,M]
	DPB J,[370500,,M]
	LDB A,[50100,,J]
	DPB A,[210100,,M]
	MOVEM M,RESTW(N)-1
PLOP7M:	MOVEI M,0
PLOP7L:	SOJG N,PLOP7H
	JUMPL M,PLONM
	MOVE A,PLOTX1
	CAMGE A,PLOTX0
	AOS PLOTX1
	CAMGE A,PLOTX0
	JRST PLO7A
	JUMPE A,SMSC
	CAME A,PLOTX2
	JRST SMSC
	JRST PLCLO


PLOUNN:	JRST 4,PLUP7
PLOUNR:	JRST 4,PLUP8
PLOINR:	JRST 4,PLUP9
PLOBN:	JRST 4,PLOBN1

SMSC1:	SETZM SMSCNQ'
	MOVE A,MEAS(G)
	MOVEM A,SMSCMG'
	MOVE A,PLOTIW
	MOVEM A,PLW(G)
	MOVE F,LOCA(G)
	MOVEI J,7
SMSC2:	MOVE A,(F)
	TLNE A,177
	TLNN A,377600
	JUMPL A,SMSC9
	JUMPE A,SMSC8A
	LDB B,[220700,,A]	;PITCH
	SKIPN PLICI(G)
	MOVEM B,PLICI(G)
	SKIPE SMSCNQ
	AOJA F,SMSC2
	SUBI B,2
	JUMPLE B,SMSC2A
	IDIVI B,14
	AOS SMSCT(C)
SMSC2A:	LDB B,[370500,,A]
	LDB C,[210100,,A]
	DPB C,[50100,,B]
	LDB C,[310600,,A]
	SKIPN C
	MOVEI C,100
	ASH B,7
	IDIV B,C
	MOVNI A,1
SMSC3:	ASH B,-1
	SKIPE B
	AOJA A,SMSC3
	SKIPGE A
	MOVEI A,0
	CAILE A,7
	MOVEI A,7
	CAML A,I
	AOJA F,SMSC2
	CAMN A,J
	MOVE I,A
	CAMGE A,J
	MOVE J,A
SMSC9:	AOJA F,SMSC2

SMSC8A:	SKIPE PLICI(G)
	JRST SMSCA
	SETOM SMSCNQ
SMSC8B:	AOS F,SMSCMG
	MOVE F,(F)
	JUMPE F,SMSC8
	LDB A,[311300,,F]
	JUMPN A,SMSC8B
	ADD F,PLOX
	JRST SMSC2

SMSCA:	SETOM CCBM(G)
	SETOM SMPLSW
	HRRE A,PLWSNO(G)
	JUMPGE A,SMSCA1
	MOVE K,PLICI(G)
	PUSHJ P,SMSCP1	; K TO L
	PUSHJ P,SMSCP0	; BEST CLEF TO A
SMSCA1:	MOVEM A,SMSCAA'
	MOVE F,SMSCMG
	MOVE F,(F)
	ADD F,PLOX
	MOVEM F,SMSCAX'
SMSCA2:	MOVE B,(F)
	TLNE B,177
	TLNN B,377600
	JUMPL B,SMSCA9
	JUMPE B,SMSC8	;NO CLEF CHANGE
	LDB K,[220700,,B]
	PUSHJ P,SMSCP1
	MOVE A,SMSCAA
	PUSHJ P,SMSCP5
SMSCA9:	AOJA F,SMSCA2

SMSCA5:	MOVEM F,SMSCAF'
	PUSHJ P,SMSCP0
	MOVEM A,SMSCAB'
	MOVEM L,SMSCAL'
	MOVE F,SMSCAX
SMSCA6:	MOVE B,(F)
	TLNE B,177
	TLNN B,377600
	JUMPL B,SMSCA8
	LDB K,[220700,,B]
	PUSHJ P,SMSCP1
	MOVE A,SMSCAB
	PUSHJ P,SMSCP5
SMSCA8:	AOJA F,SMSCA7
	JRST SMSC8	;CAN'T CHANGE AT BEG.

SMSCA7:	CAMGE F,SMSCAF
	JRST SMSCA6
	MOVEM A,CCBM(G)
	MOVE L,SMSCAL
	MOVEM L,PLICI(G)
	JRST SMSC8

SMSCP0:	SKIPGE A,PRECLE"(G)
	JRST SMSC01
	PUSHJ P,SMSCP5
	POPJ P,
SMSC01:	MOVEI A,0
	CAML L,PLSSBS(A)
	AOJA A,.-1
	POPJ P,

SMSCP5:	CAML L,PLSSLT(A)
	CAMLE L,PLSSGT(A)
	AOS (P)
	POPJ P,

SMSC:	MOVE G,PLNLI
	SETZM SMSCT
	MOVE A,[SMSCT,,SMSCT+1]
	BLT A,PLICI+MAXNP-1
	MOVEI I,10
SMSC8:	SOJGE G,SMSC1
	CAIL I,10
	MOVE I,J
	MOVE J,PLNLI
	SKIPG TEXT(J)-1
	SOJG J,.-1
	SKIPN M,STBARM
	MOVE M,STSCBT(I)
	MOVE K,STSCBV(I)
	JUMPLE J,SMSC8M
	IMULI K,5
	ASH K,-2
	IMULI M,5
	ASH M,-2
SMSC8M:	MOVE B,K
	MUL B,SCRSPA"
	ADD B,K
	MOVEM B,STBVC'
	MOVE B,M
	MUL B,SCRSPA"
	ADD B,M
	MOVEM B,STSMSP'
	JRST PLSMD

STSCBV:	(202000)
	(101000)
	(050500)
	(031300)
	(017700)
	(011740)
	(006160)
	(003704)

STSCBT:	101000
	101000
	121000
	145400
	177000
	237000
	307000
	370400

STBARM:	0	;IF NONZERO IS CONSTANT DELTA X FOR 1ST NOTE IN MEAS.

SMSCT:	BLOCK 14	;PITCH USE
PLICI:	BLOCK MAXNP	;PITCH OF 1ST NOTE

PLSMD:	MOVE I,PLOTX1
	CAMN I,PLOTX0
	JRST PLSMD4
	ADDI I,1
	IDIV I,SCRINT"
	JUMPN J,PLSMD9
	MOVE I,PLOTX1
PLSMD4:	ADDI I,1
	MOVE A,PLOTBR
	ADDI A,160000
	MOVEM A,PLDIGA
	MOVE M,PLGTSP
	MOVEM M,PLDIGM
	MOVSI B,240000
	MULM B,FGSCAL
	PUSHJ P,PLOP3P
	PUSHJ P,PLOTSS
PLSMD5:	MOVE A,PLDIGA
	ADDI A,35000
	MOVEM A,PLSMD1'
	MOVE B,PLOTBR
	ADDI B,100000
	MOVEM B,PLSMD0'
	MOVE M,PLGTSO
	SUBI M,360000
	MOVE N,M
	PUSHJ P,PFS
PLSMD6:	MOVEI C,UP
	MOVEI A,220000
	PUSHJ P,PLINE
	MOVE A,PLSMD1
	MOVE B,PLSMD0
	MOVE M,PLGTSO
	SUBI M,600000
	MOVE N,M
	PUSHJ P,PFS
PLSMD7:	MOVEI C,DN
	MOVEI A,220000
	PUSHJ P,PLINE
PLSMD9:	JRST PLOOP9

PLOP7S:	SETOM MSTM(N)-1
	SOS MEAS(N)-1
	JRST PLOP7L

PLOP70:	SETOM PLSLVC(N)-1
	SETOM PLSLVS(N)-1
	SETOM MSTM(N)-1
	SETOM RSTM(N)-1
	JRST PLOP7M

PLONM:	MOVEI G,0
PLONM1:	PUSHJ P,PLGTS
	MOVE A,PLOTIW
	MOVEI E,PLHBAR"
	MOVE B,PLOTX1
	CAME B,PLOTX2
	MOVEI E,PLHBWS"
	PUSHJ P,PLOTGO
	PUSHJ P,PLFIG
	ADDI G,1
	CAMGE G,PLNLI
	JRST PLONM1

PLCLO:	SKIPG PLOWS"
	JRST .PLOTR"
	HRLZI A,1
	ADD A,PLOTIW
	MOVEI M,0
	PUSHJ P,PLOTG0
	MOVE A,PLOWD
	PUSHJ P,.UWR"
	  JRST TUFLUK
PLCLOS:	MOVEI C,1
	MOVSI A,(SIXBIT /@/)
	PUSHJ P,.FILEB"
	  JRST TUFLUK
	JRST .PLOTR"


PLWSNO:	BLOCK MAXNP	;CLEF IN USE (RH); IGNORE PART (LH)

PLSSLT:	0	;LEAST SPACE TO USE STAFF
	7
	23
	32
	7
	13
	15
	21

PLSSGT:	20	;GREATEST SPACE TO USE STAFF
	27
	45	;ANOTHER LINE FOR VIOLINS
	52
	27
	33
	35
	41

PLSSBS:	6	;UPPER CLEF PREFERRED AT
	25
	44

PLSSOT:	14	;OFFSET FROM TOP OF STAFF
	23
	37
	46
	23
	27
	31
	35

PLSSBL:	3	;L OF SPACE BELOW STAFF
	12
	26
	35
	12
	16
	20
	24

PLSSXT:	1
	0
	0
	1
	0
	0
	0
	0

GPN:	MOVE A,LOCA(G)
	MOVEM A,GPLOC'
	MOVE A,MEAS(G)
	MOVEM A,GPMES'
GSN:	AOSA A,GPLOC
GPN3:	HRRZM A,GPLOC
	MOVE A,(A)
	TLNE A,177
	TLNN A,377600
	JUMPL A,GSN
	JUMPE A,GPN2
	AOS (P)
	POPJ P,

GPN2:	AOS A,GPMES
	SKIPN A,(A)
	POPJ P,
	TLNE A,777600
	JRST GPN2
	ADD A,PLOX
	JRST GPN3

GPNM:	MOVE A,LOCA(G)
	MOVEM A,GPLOC
GPNM0:	AOS A,GPLOC
	MOVE A,(A)
	TLNE A,177
	TLNN A,377600
	JUMPL A,GPNM0
	POPJ P,

PTIBU:	SKIPN TEMPF(G)	;IF IN TRIPLET
	JRST POSHU
	PUSH P,M
	SKIPL PLTRIH(G)
	CAMGE M,PLTRIH(G)
	MOVEM M,PLTRIH(G)
	ADDI M,100000
	PUSHJ P,POSHU
	POP P,M
	POPJ P,


POSHD:	TDZA C,C
POSHU:	MOVEI C,1
	PUSH P,B
	HRRZ B,PLWSNO(G)
	CAME B,POSHDT(C)
	JRST POSHUX
	XCT POSHUU(C)
	XCT POSHUT(C)
	MOVEM M,POSHH(G)
POSHUX:	POP P,B
	POPJ P,

POSHDT:	0
	3

POSHUT:	CAMLE M,POSHH(G)
	CAMGE M,POSHH(G)

POSHUU:	JFCL
	SKIPL POSHH(G)

PLOP3A:	SKIPL I,PRECLE"(G)
	JRST PL3A1
PL3A0:	MOVEI I,0
	CAML L,PLSSBS(I)
	AOJA I,.-1
PLOP3D:	HRRE N,PLWSNO(G)
	JUMPL N,PLOP3B
	SKIPN PLSSXT(N)
	JRST PLOP3B	;NOT ENDING 8VE SHIFT
	MOVE A,PLWSW(G)
	PUSHJ P,PLGTS
	JUMPE N,PLOSH1
	SUBI M,160000
	MOVEI E,PL8VU"
	SKIPGE POSHH(G)
	JRST PLOP3C
	CAMLE M,POSHH(G)
	MOVE M,POSHH(G)
	SUBI M,40000
PLOP3C:	PUSH P,N
	PUSHJ P,PLOTGO
	PUSHJ P,PLFIG
	MOVE A,PLOTIW
	SUB A,PLWSW(G)
	MOVEI C,RT
	PUSHJ P,PDLINE
	MOVEI A,100000
	MOVEI C,DN
	SKIPN (P)
	MOVEI C,UP
	PUSHJ P,PDLINE
	POP P,N
PLOP3B:	SETOM POSHH(G)
	MOVE A,PLOTIW
	SKIPE PLSSXT(I)
	MOVEM A,PLWSW(G)	;WHERE SHIFT WAS BEGUN
	XOR N,I
	CAIN N,1
	JRST PLOP3W	;JUST 8VE SHIFT, NO NEW CLEF
	MOVEI A,100000
	ADDB A,PLW(G)
	PUSHJ P,PLGTS
	MOVE E,PLCLEF"(I)
	PUSHJ P,PLDOIT
	HRRM I,PLWSNO(G)
	SKIPL N,PLCKS(G)
	CAME N,PLFKS(G)
	JRST PLOP3Q
	JRST PLOP3X

PLOSH1:	ADDI M,560000
	MOVEI E,PL8VD"
	CAMGE M,POSHH(G)
	MOVE M,POSHH(G)
	ADDI M,40000
	JRST PLOP3C

PL3A1:	CAML L,PLSSLT(I)
	CAMLE L,PLSSGT(I)
	JRST PL3A0
	JRST PLOP3D

PLOP3E:	MOVE N,PLFKS(G)
	JUMPL N,PLOP3Q
	MOVEI A,100000
	ADDM A,PLW(G)
	MOVEI F,0
PLOP3L:	SETOM PLCLAW
	MOVEM N,PL3FKS'
PLOPL1:	HRLI A,PLWTZ
	HRR A,PLWTA(G)
	MOVEI B,52(A)
	BLT A,(B)
	SETZM PL3ANP'
	MOVSI J,-14.
PLOP3F:	LDB A,PLFNP(J)
	JUMPE A,PLOP3G
	CAML J,[-7,,]
	MOVMS F
	CAML J,[-7,,]
	JRST PLOP3H	;ONLY SHARPS LEFT
	CAIGE A,3
	JRST PLOP3G

PLOP3H:	MOVE E,PLACCT"(F)
	SETOM PL3ANP'
	MOVEI A,0
	DPB A,PLFNP(J)
	MOVE A,PLW(G)
	MOVE N,PLFNO(J)
	MOVE B,PLWSNO(G)
	ADD N,PLAOFS(B)
	XCT PLTLMN(F)
	ADDI N,340000
	PUSHJ P,PLGTS
	ADD M,N
	PUSHJ P,PLDOIT
	MOVE L,PLOPAB(J)
PLOP3J:	ADDM F,@PLWTA(G)
	ADDI L,7
	CAIGE L,53
	JRST PLOP3J
PLOP3G:	AOBJN J,PLOP3F
PLOPG1:	HRL A,PLWTA(G)
	HRR A,PLWTB(G)
	MOVEI B,52(A)
	BLT A,(B)
	MOVEI A,110000	;SPACE AFTER KEY SIG.
	SKIPE PL3ANP
	ADDB A,PLW(G)
	JUMPN F,PLOP3X
PLOP3Q:	MOVNI F,1
	MOVE N,PLCKS(G)
	MOVEM N,PLFKS(G)
	JRST PLOP3L


PLOP3P:	IDIVI I,10.
	JUMPE I,PLOP3N
	PUSH P,J
	PUSHJ P,PLOP3P
	POP P,J
PLOP3N:	MOVE E,PLBN"(J)
	MOVE M,PLDIGM
	PUSHJ P,PLOTGO
	PUSHJ P,PLFIG
	ADDB A,PLDIGA
	POPJ P,


PLAOFS:	100000
	100000
	0
	0
	100000
	-40000
	40000
	-100000

	CAMG N,[-40000]
PLTLMN:	CAMG N,[-40000]
	PUSHJ P,PLTLM1

PLTLM1:	CAMLE N,[-40000]
	JRST PPOPJ
	MOVE M,PLWSNO(G)
	CAIL M,2
	CAILE M,3
	POPJ P,
	CAMLE N,[-100000]
PPOPJ:	AOS (P)
	POPJ P,


PLOP3M:	MOVE B,PLOTBR
	ADDI B,60000
	CAMLE B,PLW(G)
	MOVEM B,PLW(G)
	LDB I,[600,,A]	;DEN
	LDB J,[60700,,A]	;NUM
	SKIPN I
	MOVEI I,100
	MOVEI A,0
	CAIL I,10.
	ADDI A,44000
	CAIL I,20.
	ADDI A,16000
	CAIL J,100.
	SUBI A,44000
	CAIL J,10.
	SUBI A,44000
	CAIL J,20.
	SUBI A,16000
	MOVNM A,PL3MAO'
	SKIPGE A
	MOVEI A,0
	ADD A,PLW(G)
	PUSHJ P,PLGTS
	MOVEM A,PLDIGA'
	MOVEM M,PLDIGM'
	PUSH P,I
	MOVE I,J
	PUSHJ P,PLOP3P
	POP P,I
	MOVEI A,200000
	ADDM A,PLDIGM
	SKIPGE A,PL3MAO'
	MOVEI A,0
	ADD A,PLW(G)
	MOVE B,PLDIGA
	MOVEM A,PLDIGA
	MOVEM B,PLW(G)
	PUSHJ P,PLOP3P
	CAMLE A,PLW(G)
	MOVEM A,PLW(G)
	MOVEI A,60000
	ADDB A,PLW(G)
	MOVEM A,PLOTIV
	MOVE A,PLCTS(G)
	MOVEM A,PLFTS(G)
	JRST PLOP3Z


PLFNP:	000300,,PL3FKS
	140300,,PL3FKS
	030300,,PL3FKS
	170300,,PL3FKS
	060300,,PL3FKS
	220300,,PL3FKS
	110300,,PL3FKS

	110300,,PL3FKS
	220300,,PL3FKS
	060300,,PL3FKS
	170300,,PL3FKS
	030300,,PL3FKS
	140300,,PL3FKS
	000300,,PL3FKS

PLOPAB:	6
	2
	5
	1
	4
	0
	3

	3
	0
	4
	1
	5
	2
	6

PLFNO:	200000
	 40000
	240000
	100000
	300000
	140000
	340000

	0
	140000
	-40000
	100000
	240000
	 40000
	200000

PLWTZ:	ZZZ==2
	REPEAT 6,[IRPC X,,[2212221]
		ZZZ
		ZZZ==ZZZ+X
		TERMIN
		]
	ZZZ

DEFINE PLMAC1 P,Q
	L,,P!Q
TERMIN

DEFINE PLMAC2 R,S
R!S:	BLOCK 53
TERMIN

PLWTA:	REPEAT MAXNP,PLMAC1 PLWA,\.RPCNT
PLWTB:	REPEAT MAXNP,PLMAC1 PLWB,\.RPCNT
	REPEAT MAXNP,PLMAC2 PLWA,\.RPCNT
	REPEAT MAXNP,PLMAC2 PLWB,\.RPCNT

PLLASN:	BLOCK MAXNP	;PREV PITCH
PLW:	BLOCK MAXNP	;HORIZ LOCN
PLWT:	BLOCK MAXNP	;HORIZ LOCN OF TEXT (SOMETIMES)
RSTM:	BLOCK MAXNP	;TIME WHEN REST OCCURS
PLSLVC:	BLOCK MAXNP	;-1 IF SILENT MEASURE
PLSLVS:	BLOCK MAXNP	;-1 AT START OF SILENT MEASURE
RDUR:	BLOCK MAXNP	;LENGTH OF REST
MSTM:	BLOCK MAXNP	;TIME WHEN NOTE OCCURS
LOCA:	BLOCK MAXNP	;POINTER TO NOTES
MEAS:	BLOCK MAXNP	;POINTER TO MEASURES
TEMPF:	BLOCK MAXNP	;BEGINNING X OF TRIPLET
PLLEG:	BLOCK MAXNP	;DURATION OF NOTE FOR SPACING
PLTREN:	BLOCK MAXNP	;FINAL X OF TRIPLET
NOTEW:	BLOCK MAXNP	;CURRENT NOTE
RESTW:	BLOCK MAXNP	;CURRENT REST
STAFFN:	REPEAT MAXNP,.RPCNT	;PHYS STAFF #
STAFPN:	BLOCK MAXNP	;LOG STAFF #
STAFFL:	BLOCK MAXNP	;M-COORD OF STAFF TOP
PELEG:	BLOCK MAXNP	;SLUR INDICATOR
TEXP:	BLOCK MAXNP	;POINTER TO CURRENT TEXT FLAG
TEXT:	BLOCK MAXNP	;POINTER (ADVANCED) TO TEXT
UNDERL:	BLOCK MAXNP	;WHERE UNDERLINE BEGINS
CCBM:	BLOCK MAXNP	;CLEF TO BE PUT AT BEG. OF MEAS.
PLSLN:	BLOCK MAXNP	;PLSTUP,,ARTIC OF PREV NOTE
PLUSGF:	BLOCK MAXNP	;NON-0 IF SYL TO BE SPLIT
PRECLE":	BLOCK MAXNP	;PREFERRED CLEF
PLFTS:	BLOCK MAXNP
PLCTS:	BLOCK MAXNP
PLFKS:	BLOCK MAXNP
PLCKS:	BLOCK MAXNP
PLWSW:	BLOCK MAXNP	;WHERE 8VE SHIFT BEGAN
PLTRIH:	BLOCK MAXNP	;HEIGHT TO CLEAR FOR TRIPLET BRACKET
POSHH:	BLOCK MAXNP	;HEIGHT TO CLEAR FOR OCTAVE SHIFT


END
ð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒð0`Áƒ