<PACKAGE "UNLINK">

<ENTRY UNLINK PURE? UNPURIFY>

<FLOAD "MUDBUG;UNPURE NBIN">

<DEFINE UNLINK UNLINK (ATML
		"OPTIONAL" (HAIR? <>)
		"AUX" (CASOC <ASSOCIATIONS>) TVAL TOBL (Y <LOOKUP "TMP" <ROOT>>)
		(T <AND .Y <GET .Y OBLIST>>) TLIST)
   #DECL ((HAIR?) <OR ATOM FALSE> (ATML) <OR FALSE ATOM <LIST [REST ATOM]>>
	  (CASOC) <OR ASOC FALSE> (TVAL) ANY (TOBL) <OR ATOM OBLIST>
	  (Y) <OR ATOM FALSE> (T) <OR FALSE OBLIST> (TLIST) LIST (UNLINK) ACTIVATION)
   <COND (<TYPE? .ATML ATOM>
	  <COND (<ASSIGNED? .ATML>
		 <SET TLIST ..ATML>
		 <SET ATML
		   <MAPF ,LIST
		     <FUNCTION (X)
		       <COND (<AND <TYPE? .X FORM>
				   <G=? <LENGTH .X> 3>
				   <OR <==? <1 .X> SETG>
				       <==? <1 .X> DEFINE>>
				   <TYPE? <2 .X> ATOM>>
			      <MAPRET <2 .X>>)
			     (<MAPRET>)>>
		     .TLIST>>)
		(<RETURN #FALSE ("NOT A GROUP") .UNLINK>)>)>
   <REPEAT ()
     <COND
      (<AND <==? <INDICATOR .CASOC> OBLIST>
	    <TYPE? <SET TOBL <AVALUE .CASOC>> OBLIST>>
       <MAPF <>
	<FUNCTION (X) 
	   #DECL ((X) <LIST [REST <OR ATOM LINK>]>)
	   <MAPF <>
	    <FUNCTION (X "AUX" PURE?) 
	       #DECL ((PURE?) <OR ATOM FALSE> (X) <OR ATOM LINK>)
	       <COND
		(<AND <TYPE? .X ATOM>
		      <GASSIGNED? .X>
		      <TYPE? <SET TVAL ,.X> RSUBR>
		      <OR <NOT <SET PURE? <PURE? .TVAL>>> .HAIR?>>
				  ;"Win if rsubr, either not pure or hairy mode"
		 <MAPR <>
		       <FUNCTION (X "AUX" (FOO <1 .X>) FOOAT) 
			       #DECL ((FOOAT) ATOM)
			       <COND (<AND <TYPE? .FOO RSUBR RSUBR-ENTRY>
					   <SET FOOAT <2 .FOO>>
					   <OR <NOT .T>
					       <N==? <OBLIST? .FOOAT> .T>>
					   <OR <NOT .ATML> <MEMQ .FOOAT .ATML>>>
				      <COND (.PURE?
				   ;"Unpurify if in hairy mode and haven't yet."
					     <UNPURIFY .TVAL>
					     <SET PURE? <>>)>
				      <PUT .X 1 .FOOAT>)>>
		       .TVAL>)>>
	    .X>>
	.TOBL>)>
     <COND (<NOT <SET CASOC <NEXT .CASOC>>> <RETURN T>)>>>

<ENDPACKAGE>