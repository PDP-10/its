<SETG SAVSTR <REST "      " 6>>

<TITLE \ \ F!- >
	<DECLARE ("TUPLE" ANY)>
	<HRRZ	A* -7(TB)>	; "Pointer to called function"
	<PUSH	TP* 2(A)>
	<PUSH	TP* 3(A)>	; "Push the atom"
	<PUSH	TP* 2(A)>
	<PUSH	TP* 3(A)>
	<MOVE	B* (TP)>
	<PUSHJ	P* CIGVAL>
	<MOVE	B* 1(B)>
	<CAME	B* R>		; "Is he SETGed to us?"
	 <JRST	[<SUB	TP* [<(4) 4>]>
		 <JRST	CONTIN>]> ; "Nope.  Must already be loaded"
	<MCALL	1 GUNASSIGN>	; "GUNASSIGN it"
	<MCALL	1 L-GASSIGNED?>	; "Try to load it"
	<GETYP	O* A>
	<CAIN	O* <TYPE-CODE FALSE>>
	 <JRST	LOSS>		; "Obscure!"
; "Is this or is this not a complete obscene disgusting lossage?"
	<HRRZ	A* -7(TB)>
	<MOVE	B* 3(A)>
	<PUSHJ	P* CIGVAL>	; "Get new gval"
CONTIN	<GETYP	O* A>
	<MOVEI	C* 0>
	<CAIE	O* <TYPE-CODE RSUBR-ENTRY>>
	 <JRST	DIRECT>
	<HRRZ	C* 2(B)>	; "Pick up offset"
	<MOVE	B* 1(B)>	; "Point to RSUBR"
DIRECT	<MOVE	D* 1(B)>	; "Pointer to code"
	<GETYP	O* (B)>		; "Get type of code"
	<CAIE	O* <TYPE-CODE PCODE>>	; "FBIN"
	 <JRST	NRMSET>
	<MOVE	A* 1(B)>	; "Get offset into PURVEC"
	<MOVEI	D* (A)>
	<HLRS	A>
	<ADD	A* PURVEC 1>	; "Point into PURVEC"
	<HLLM	TB* 2(A)>	; "For LRU-age"
	<SKIPE	O* 1(A)>	; "Skip if not loaded"
	 <JRST	[<ADDI	O* (D)>	; "Point to real start of code"
		 <MOVE	D* O>
		 <JRST	NRMSET>]>	; "Finish setup"
	<PUSH	TP* <TYPE-WORD RSUBR>>
	<PUSH	TP* B>		; "Save the rsubr"
	<PUSH	P* C>		; "Save offset from RSUBR-ENTRY"
	<PUSH	P* D>		; "Save offset into FBIN"
	<PUSH	TP* <TYPE-WORD UVECTOR>>
	<PUSH	TP* A>		; "Save PURVEC"
	<SKIPN	A* (A)>		; "Pick up sixbit name"
	 <ERRUUO* <MQUOTE BAD-PURECODE-NAME>>
	<MOVE	C* <MQUOTE <RGLOC SAVSTR>>>
	<ADD	C* GLOTOP 1>
	<MOVE	C* 1(C)>	; "Byte pointer"
	<MOVEI	O* 0>
	<MOVEI	B* 0>
	<LSHC	A* -6>
	<JUMPE	B* HERE -1>
STRLOP	<LSH	B* -30>
	<ADDI	B* *40*>
	<DPB	B* C>
	<ADDI	O* 1>
	<MOVEI	B* 0>
	<ADD	C* [<(*70000*) 0>]>
	<TLNE	C* *400000*>
	 <ADD	C* [<(*347777*) *777777*>]>
	<LSHC	A* -6>
	<JUMPN	B* STRLOP>
	<HRLI	O* <TYPE-CODE STRING>>
	<PUSH	TP* O>
	<PUSH	TP* C>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* (P)>
	<SETOM	NOSHUF>
	<MCALL	2 PCODE>
	<SETZM	NOSHUF>
	<POP	TP* A>
	<SUB	TP* [<(1) 1>]>	; "Get PURVEC back"
	<POP	TP* B>		; "Get RSUBR back"
	<SUB	TP* [<(1) 1>]>
	<MOVE	D* 1(A)>
	<ADD	D* (P)>
	<SUB	P* [<(1) 1>]>
	<POP	P* C>		; "Offset for RSUBR-ENTRY"
NRMSET	<MOVE	R* B>		; "Set up M and R"
	<MOVE	M* D>
	<ADD	C* M>
	<HRRM	B* -7(TB)>	; "Clobber the frame"
	<JRST	(C)>		; "And away we go"
LOSS	<HRRZ	A* -7(TB)>
	<DPUSH	TP* <PQUOTE LOAD-FAILED>>
	<DPUSH	TP* 2(A)>
	<DPUSH	TP* <PQUOTE \ \ F!->>
	<MCALL	3 ERROR>
	<JRST	FINIS>