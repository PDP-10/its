<TITLE ZERO>
	<DECLARE ("VALUE" DATA-BASE DATA-BASE FIX FIX)>
	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<DPUSH	TP* 4(AB)>
	<PUSHJ	P* IZERO>
	<JRST	FINIS>

<INTERNAL-ENTRY IZERO 3>
	<SUBM	M* (P)>
	<PUSH	TP* -5(TP)>
	<PUSH	TP* -5(TP)>
	<PUSH	TP* -5(TP)>
	<PUSH	TP* -5(TP)>
	<MCALL	2 DB-ACCESS>	; "Returns uv"
	<SETZM	(B)>
	<HRL	D* B>
	<HRRI	D* 1(B)>
	<ADD	B* (TP)>
	<BLT	D* -1(B)>
	<SUB	TP* [<(4) 4>]>
	<POP	TP* B>
	<POP	TP* A>
	<JRST	MPOPJ>

<SUB-ENTRY STR-BLT ("VALUE" UVECTOR UVECTOR STRING FIX FIX)>
	<DPUSH	TP* (AB)>	; "Uvector to put it in"
	<DPUSH	TP* 2(AB)>	; "String to move"
	<DPUSH	TP* 4(AB)>	; "Offset of first word"
	<DPUSH	TP* 6(AB)>	; "Place to put length"
	<PUSHJ	P* ISTRBL>
	<JRST	FINIS>

<INTERNAL-ENTRY ISTRBL 4>
	<SUBM	M* (P)>
	<MOVE	B* -6(TP)>	; "Pick up uvector"
	<MOVE	C* -4(TP)>	; "String"
	<HRRZ	A* -5(TP)>	; "Length"
	<ADD	B* -2(TP)>
	<HRLI	B* *440700*>
STRBLL	<ILDB	O* C>
	<IDPB	O* B>
	<SOJG	A* STRBLL>
	<MOVE	B* -6(TP)>
	<ADD	B* (TP)>
	<HRRZ	A* -5(TP)>
	<DPB	A* [<%<CHTYPE ,LENGET OPCODE!-OP!-PACKAGE> (B)>]>
	<SUB	TP* [<(6) 6>]>
	<POP	TP* B>
	<POP	TP* A>
	<JRST	MPOPJ>

<SUB-ENTRY TDSKDATE ("VALUE" FIX)>
	<PUSHJ	P* ITDSKDA>
	<JRST	FINIS>

<INTERNAL-ENTRY ITDSKDA 0>
	<SUBM	M* (P)>
	<HRROI	B* -1>		; "-1 TO SAY CURRENT TIME"
	<MOVEI	D* 0>		; "NOTHING FANCY"
	<ODCNV>			; "B HAS YEAR,,MONTH; C DAY,,; D ,,TIME"
	<TLZ	D* -1>		; "CLEAN OUT LH OF D"
	<ASH	D* 1>		; "TIME IN HALF-SECONDS"
	<HLRZS	C>		; "GET DAY OF MONTH -1"
	<ADDI	C* 1>		; "DO THE RIGHT THING"
	<DPB	C* [<(*220500*) D>]>	; "STUFF DAY INTO D"
	<IDIV	B* [(1)]>	; "SPLIT B IN HALF"
	<ADDI	C* 1>		; "GET REAL MONTH"
	<DPB	C* [<(*270400*) D>]>	; "STUFF IN MONTH"
	<IDIVI	B* 100>		; "GET YEAR OF CENTURY IN C"
	<DPB	C* [<(*330700*) D>]>	; "STUFF IN YEAR"
	<MOVE	B* D>
	<MOVSI	A* <TYPE-CODE FIX>>
	<JRST	MPOPJ>

<SUB-ENTRY IDSKDATE ("VALUE" FIX)>
	<PUSHJ	P* IIDSKDA>
	<JRST	FINIS>

<INTERNAL-ENTRY IIDSKDA 0>
	<SUBM	M* (P)>
	<*CALL	[<SETZ>
		 <SIXBIT "RQDATE">
		 <SETZM	B>]>
	 <*LOSE	*1000*>
	<MOVSI	A* <TYPE-CODE FIX>>
	<JRST	MPOPJ>

<SUB-ENTRY MYIOT ("VALUE" FIX UVECTOR CHANNEL "OPTIONAL" FIX)>
	<HLRE	A* AB>
PLOOP	<DPUSH	TP* (AB)>
	<ADD	AB* [<(2) 2>]>
	<JUMPL	AB* PLOOP>
	<ASH	A* -1>
	<ADDI	A* TABEND>
	<PUSHJ	P* @ 2 (A)>
	<JRST	FINIS>
	<IMYIOT3>
TABEND	<IMYIOT2>

<INTERNAL-ENTRY IMYIOT2 2>
	<HLRE	A* -2(TP)>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* A>
<INTERNAL-ENTRY IMYIOT3 3>
	<SUBM	M* (P)>
	<SKIPLE	(TP)>
	 <MOVNS	(TP)>
	<MOVE	A* <MQUOTE <RGLOC MUDDLE>>>
	<ADD	A* GLOTOP 1>
	<MOVE	A* 1(A)>
	<CAIL	A* 100>
	 <JRST	TMYIOT>
	<MOVE	B* -4(TP)>
	<MOVE	A* -2(TP)>
	<HRL	B* (TP)>
	<*CALL	[<SETZ>
		 <SIXBIT "IOT">
		 <MOVE	1(A)>
		 <SETZ	B>]>
	 <*LOSE	*1400*>
	<HLRES	B>
	<SUB	B* (TP)>
MYIOTO	<MOVSI	A* <TYPE-CODE FIX>>
	<SUB	TP* [<(6) 6>]>
	<JRST	MPOPJ>
TMYIOT	<MOVE	D* -2(TP)>
	<MOVE	A* 1(D)>
	<HRR	B* -4(TP)>
	<HRLI	B* *444400*>
	<MOVE	C* (TP)>		; "<0, SO IGNORES D"
	<MOVE	D* 3(D)>
	<ILDB	D* D>
	<MOVE	E* [<SIN-JSYS>]>
	<CAIE	D* !\R>
	 <MOVE	E* [<SOUT>]>
	<XCT	E>
	<SUB	C* (TP)>
	<MOVE	B* C>
	<JRST	MYIOTO>

<SUB-ENTRY LOCR-TO-ATOM ("VALUE" ATOM LOCR)>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* ILOCR>
	<JRST	FINIS>

<INTERNAL-ENTRY ILOCR 1>
	<SUBM	M* (P)>
	<MOVE	C* (TP)>
	<ADD	C* GLOTOP 1>
	<MOVE	B* -1(C)>
	<MOVSI	A* <TYPE-CODE ATOM>>
	<SUB	TP* [<(2) 2>]>
	<JRST	MPOPJ>
