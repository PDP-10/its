TITLE JEDGAR

;This is not the original source code.  It's created from disassembly
;of the file MOON; TS JEDGAR, dated 1976-10-17.  The assembled binary
;is identical.

;The original binary shows signs of binary patching.  This source file
;recreates the patched binary without any intermediary steps.

A=1
B=2
C=3
D=4
T=5
TT=6
P=7
U=10
N=11

CH=16
CH2=17

SYMS:	SQUOZE 0,USRHI
USRHI:	0
	SQUOZE 0,LUBLK
LUBLK:	0
	SQUOZE 0,FLSINS
FLSINS:	(U)
	SQUOZE 0,UNAME
UNAME:	(U)
	SQUOZE 0,EPDL
EPDL:	(U)
	SQUOZE 0,SUPPRO
SUPPRO:	(U)
	SQUOZE 0,TTYTBL
TTYTBL:	(U)
	SQUOZE 0,UHANG
UHANG:	0
	SQUOZE 0,TOOP
TOOP:	(N)
	SQUOZE 0,UMAPS
UMAPS:	(U)
	SQUOZE 0,UPGMP
UPGMP:	(U)
	SQUOZE 0,USTP
USTP:	(U)
SYMP:	SYMS-.,,SYMS

BADGE:	0

PDL:	-40,,.
	BLOCK 40

JEDGAR:	MOVE P,PDL
	MOVEI T,-1
	TDNE T,USRHI
	JRST RESTRT
	MOVE T,SYMP
EVAL:	MOVE TT,(T)
	.EVAL TT,
	 JFCL
	HRRM TT,1(T)
	AOBJN T,.+1
	AOBJN T,EVAL

RESTRT:	.OPEN CH,['#TTY]
	 .VALUE
	MOVEI T,CH
	.SUSET [.RIOC+CH,,T]
	LDB N,[220600,,T]
	MOVE T,ASKPTR
	.IOT CH,T
	.CLOSE CH,

	MOVEI T,401001
ABSCOR:	MOVSI TT,1000
	IOR TT,T
	.CBLK TT,
	 JFCL
	ADDI T,1001
	TRNN T,400
	 JRST ABSCOR

	.OPEN CH,['TTY]
	 .VALUE
	.IOT CH,T
	SETZM BADGE
	CAIE T,"y
	 CAIN T,BADGE
	  SETOM BADGE
	.CLOSE CH,

	.VALUE [ASCIZ /:PROCED
/]

LOOP:	SETZM U
NXTUSR:	CAMG U,@USRHI
	 JRST LOOKU
	MOVEI T,60.
	.SLEEP T,
	JRST LOOP

LOOKU:	SKIPE @USTP
	 JRST NXUSR
	MOVEI TT,@UHANG
	HRLI TT,(PUSHJ 15,)
	CAME TT,@FLSINS
	 JRST NXUSR
	HRRZ A,@EPDL
	MOVE B,A
	LSH B,-10.
	ROT B,-1
	SKIPE @UMAPS
	 JRST NXUSR
	MOVEI TT,@UPGMP
	ADD TT,B
	SKIPGE B
	 SKIPA B,(TT)
	  MOVS B,(TT)
	TRNN B,600000
	 JRST NXUSR
	ANDI B,777
	LSH B,6
	ANDI A,1777
	IOR A,B
	CAIN A,@TOOP
	 JRST CAUGHT
NXUSR:	ADDI U,@LUBLK
	JRST NXTUSR

CLIOPN:	SIXBIT /  'CLI/
	SIXBIT /UNAME /
	SIXBIT /HACTRN/

CLIMES:	ASCII / You are the subject of an illegal wiretap by
convicted Watergate conspirator "/
CLINAM:	BLOCK 2
CLIPTR:	CLIMES-.,,CLIMES
CLIMS2:	ASCII /
The miscreant has been executed. /
CLIPT2:	CLIMS2-.,,CLIMS2
ASKMES:	ASCII /Am I licensed to kill?   /
ASKPTR:	ASKMES-.,,ASKMES

CAUGHT:	MOVE TT,@UNAME
	MOVE B,[440700,,CLINAM]
CT1:	SETZM T
	LSHC T,6
	ADDI T,40
	IDPB T,B
	JUMPN TT,CT1
	MOVEI T,""
CT2:	IDPB T,B
	MOVEI T," 
	CAME B,[010700,,CLINAM+1]
	 JRST CT2
	.SUSET [.RUNAME,,CLIOPN+1]
	.OPEN CH,CLIOPN
	 .VALUE
	MOVE T,CLIPTR
	.IOT CH,T
	SKIPE BADGE
	 JRST JUSTIC
DEPUTY:	.CLOSE CH,
	.BREAK 16,60000

JUSTIC:	PUSHJ P,SPCTTY
WHO1:	MOVE B,@SUPPRO
	CAME B,[-1]
	 JRST [HRR U,B ? JRST WHO1]
	HRRZ D,U
	IDIVI D,@LUBLK
	JRST PATCH
	JFCL
PRETT:	MOVEI T,90.
	.SLEEP T,
	MOVEI C,'T00
	JUMPL A,JUST00
	LSHC A,-3
	LSH A,3
	LSHC A,3
	IOR C,A 
	HRLI C,3
	.OPEN CH2,C
	 JRST JUST00
	MOVE T,MRLPTR
	.IOT CH2,T
	.CLOSE CH2,
JUST00:	MOVE T,CLIPT2
	.IOT CH,T
	.CLOSE CH,
	JRST LOOP

MRLMES:	ASCII /Crime does not pay
/
MRLPTR:	MRLMES-.,,MRLMES

SPCTTY:	MOVE A,@TTYTBL
	JUMPGE A,SPCGOT
SPCUP:	MOVE B,@SUPPRO
	CAME B,[-1]
	 JRST [HRR U,B ? JRST SPCUP]
SPCDWN:	MOVE A,@TTYTBL
	JUMPGE A,SPCGOT
	HRR U,A
	TLNN A,200000
	 JRST SPCDWN
	MOVSI A,-1
SPCGOT:	POPJ P,

CONSTANTS

	0
PATCH:	MOVE T,@UNAME
	.WSNAME T,
	JFCL
	JFCL
	JRST 503
	.CLOSE
	JRST DEPUTY
	0

UDIRF:	SIXBIT /   DSK/
	SIXBIT /.FILE./
	SIXBIT /(DIR) /

.=503
	IORI D,400000
	.CALL 514
	 JFCL
	JRST PRETT
.=514
	SETZ
	SIXBIT /DETACH/
	SETZ D

END JEDGAR
