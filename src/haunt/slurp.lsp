
;;;		SLURP Version 7.0
;;;
;;; The SLURP package provides a comfortable top-level I/O
;;; environment for CMU MacLisp users.  SLURP loads and saves
;;; user programs, keeping track of the contents of each file
;;; it has loaded.  SLURP's file syntax is more reasonable than
;;; the standard MacLisp syntax.  TOPS-20 file syntax is now
;;; supported too.
;;;
;;;	David S. Touretzky
;;;	Leonard N Zubkoff
;;;	Carnegie-Mellon University
;;;	September, 1980

(COMMENT --CONTENTS-- SLURP QSLURP LSLURP VSLURP SLURP1 SLURP2
         SLURPPARSE SLURPPARSEMCL DEVP PPNSPEC CMUDEC CMUDEC1 CMUDEC2
         SEDIT UNSLURP VUNSLURP SLURPSET TYPE CHANGES SAVEWORK NEWFILE
         DROPFILE COPYFN SLURP-DEFUN MARK-CHANGED FNS-IN-LIST
         LISTPRINT DEFSLURP AUTOLOAD AUTOSLURP UNIONQ INTERQ DIFFQ
         DSKIN DSKIN1 DSKIN1ERR DSKINLINETEST DSKINSET DSKINSET1
         DSKOUT DSKOUT1 DSKOUT2 DSKOUT3 DSKPRINT FILEATOM CHANGEDATOM
         PROPATOM PROPATOMVAL SHORTSPEC DEVIFY BROKIFY TAB READSEMI
         SEMICOLON FULLSEMI BRIEFSEMI)

(DECLARE (MACROS T)
         (FIXSW T)
         (*LEXPR MARK-CHANGED))

(DECLARE (SPECIAL ^W ^Q ^R INFILE OUTFILES *NOPOINT AUTOLOAD))

(DECLARE (SPECIAL PRINLEVEL PRINLENGTH LINEL))

(DECLARE (SPECIAL SLURPCOMMENTS SLURP1-COMMENTS SLURPFORMFEEDS
          SLURP1-FORMFEEDS SLURPPROPS SLURP-VERBOSITY SEMICOLON
          DSKOUT-LENGTH LASTFILE SLURPEDFILES FASLOAD-MESSAGES
          SLURP-INPUT INSIDE-SLURP LAST-SLURP-OUTPUT-FILE))

(DECLARE (SPECIAL LASTWORD CHANGEDLIST))

(DECLARE (SPECIAL G1OPEN-DEL G1CLOSE-DEL G1LEVEL G1FORMAT-LIST G1FCODE
          G1BCODE G1PRINC-ATOMS))

(DECLARE (*EXPR G1FORMAT-LIST G1ENTER-OBJ EDITE))

(DECLARE (*LEXPR LISTPRINT TAB))

(PROG NIL
  (SETQ LASTFILE NIL)
  (SETQ SLURPCOMMENTS T)
  (SETQ SLURPFORMFEEDS T)
  (SETQ SLURPPROPS NIL)
  (SETQ SLURP-VERBOSITY NIL)
  (SETQ SEMICOLON 'FULLSEMI)
  (SETQ DSKOUT-LENGTH 72.)
  (SETQ FASLOAD-MESSAGES NIL)
  (SETQ INSIDE-SLURP NIL)
  (SETQ LAST-SLURP-OUTPUT-FILE NIL)
  (OR (BOUNDP 'SLURPEDFILES) (SETQ SLURPEDFILES NIL))
  (OR (BOUNDP 'CHANGEDLIST) (SETQ CHANGEDLIST NIL)))

(DEFUN SLURP FEXPR (XFILE)
  ((LAMBDA (SLURP1-COMMENTS SLURP1-FORMFEEDS) (SLURP1 XFILE))
   SLURPCOMMENTS SLURPFORMFEEDS)) 

(DEFUN QSLURP FEXPR (XFILE)
  (PROG (RESULT)
    ((LAMBDA (^W ^R)
       (PROG (FILE SLURP1-COMMENTS SLURP1-FORMFEEDS LASTFILE
              SLURPEDFILES CHANGEDLIST)
         (SETQ RESULT (SLURP1 XFILE))
         (SETQ FILE (SLURPPARSEMCL XFILE))
         (AND FILE
              (MAKUNBOUND (FILEATOM FILE))
              (MAKUNBOUND (CHANGEDATOM FILE)))
         (COND (RESULT
                (OR (ZEROP (CHARPOS T)) (TERPRI MSGFILES))
                (PRINC (CONS 'SLURPED (CDR RESULT)) MSGFILES)
                (TERPRI MSGFILES)))))
     NIL NIL)
    (RETURN RESULT))) 

(DEFUN LSLURP FEXPR (XFILE)
  (PROG (RESULT)
    ((LAMBDA (^W ^R)
       (PROG (FILE SLURP1-COMMENTS SLURP1-FORMFEEDS LASTFILE
              SLURPEDFILES CHANGEDLIST)
         (SETQ RESULT (SLURP1 XFILE))
         (SETQ FILE (SLURPPARSEMCL XFILE))
         (AND FILE
              (MAKUNBOUND (FILEATOM FILE))
              (MAKUNBOUND (CHANGEDATOM FILE)))))
     NIL NIL)
    (RETURN RESULT))) 

(DEFUN VSLURP FEXPR (XFILE)
  ((LAMBDA (SLURP-VERBOSITY SLURP1-COMMENTS SLURP1-FORMFEEDS)
     (SLURP1 XFILE))
   T SLURPCOMMENTS SLURPFORMFEEDS)) 

(DEFUN SLURP1 (XFILE)
  ((LAMBDA (INSIDE-SLURP DEFAULTF)
     (PROG (FILE EXT RESULT MBKF)
           (SETQ FILE (SLURPPARSE (OR XFILE LASTFILE)))
           (COND ((NULL FILE)
                  (PROG (^W ^R)
                    (PRINC '|Invalid file specification - |
                           MSGFILES)
                    (PRINC (OR XFILE LASTFILE) MSGFILES)
                    (TERPRI MSGFILES))
                  (RETURN NIL)))
           (SETQ EXT (CADDR FILE))
           (AND EXT (GO DOIT))
           (DO ((EXTS (CASEQ (STATUS OPSYSTEM-TYPE)
                             (CMU '(FAS NFS OFS MCL MBK))
                             (T '(FASL NFASL OFASL LSP LBK)))
                      (CDR EXTS)))
             ((OR (AND (SETQ EXT (CAR EXTS))
                       (PROBEF (LIST (CAR FILE) (CADR FILE) EXT)))
                  (NULL EXTS))))
           (COND ((NULL EXT) (SETQ EXT '-ANY-))
                 ((MEMQ EXT '(MBK LBK))
                  (PROG (^W ^R)
                    (OR (ZEROP (CHARPOS T)) (TERPRI MSGFILES))
                    (PRINC '|Found only backup file... will treat as source.|
                                       
                           MSGFILES)
                    (TERPRI MSGFILES))
                  (SETQ MBKF EXT)))
      DOIT (SETQ FILE (LIST (CAR FILE) (CADR FILE) EXT))
           (COND ((EQ EXT '-ANY-) (SETQ RESULT 'FILE-NOT-FOUND))
                 (T (SETQ RESULT (SLURP2 FILE))))
           (OR (ZEROP (CHARPOS T))
               (TERPRI MSGFILES)
               (TERPRI MSGFILES))
           (COND ((ATOM RESULT)
                  (PROG (^W ^R)
                    (PRINC (CASEQ RESULT
                                  (FILE-NOT-FOUND '|File not found - |
                                                  )
                                  (OPEN-ERROR '
                                   |Error in reading file - |)
                                  (LINE-NUMBERS '|Can't read files with line numbers - |
                                                  )
                                  (T '|Unknown error - |))
                           MSGFILES)
                    (PRINC (SHORTSPEC FILE) MSGFILES)
                    (TERPRI MSGFILES))
                  (GO DONE)))
           (SETQ LASTFILE (SHORTSPEC FILE))
           (AND MBKF
                (RPLACA (LAST LASTFILE)
                        (CADR (ASSQ (CAR (LAST LASTFILE))
                                    '((MBK MCL) (LBK LSP)))))
                (SET (FILEATOM (LIST (CAR FILE)
                                     (CADR FILE)
                                     (CAR (LAST LASTFILE))))
                     (EVAL (FILEATOM FILE))))
           (AND SLURP-VERBOSITY
                (NOT (ZEROP (CHARPOS T)))
                (TERPRI MSGFILES))
      DONE (RETURN (COND ((NOT (ATOM RESULT)) RESULT)))))
   T DEFAULTF)) 

(DEFUN SLURP2 (FILE)
  (PROG (RESULT FASL)
    (OR (PROBEF FILE) (RETURN 'FILE-NOT-FOUND))
    (SETQ RESULT
          (COND ((SETQ FASL
                       (MEMQ (CADDR FILE)
                             '(FAS NFS OFS FASL NFASL OFASL)))
                 ((LAMBDA (^W) (APPLY 'FASLOAD FILE))
                  (NOT FASLOAD-MESSAGES))
                 FILE)
                ((SETQ FASL (FASLP FILE))
                 ((LAMBDA (^W) (APPLY 'FASLOAD FILE))
                  (NOT FASLOAD-MESSAGES))
                 FILE)
                (T (DSKIN FILE))))
    (RETURN (COND ((ATOM RESULT) RESULT)
                  (T (COND ((NOT FASL)
                            (OR (MEMBER (SHORTSPEC FILE)
                                        SLURPEDFILES)
                                (PUSH (SHORTSPEC FILE)
                                      SLURPEDFILES))
                            (PUTPROP (FILEATOM FILE) T 'FILEATOM)
                            (SETQ CHANGEDLIST
                                  (DIFFQ CHANGEDLIST
                                         (FNS-IN-LIST (EVAL (FILEATOM
                               FILE)))))))
                     (SHORTSPEC FILE)))))) 

(DEFUN SLURPPARSE (X)
  (PROG (DEV DIR NAME EXT)
    (OR X (RETURN NIL))
    (AND (ATOM X) (SETQ X (LIST X)))
    (SETQ DEV
          (CASEQ (STATUS FILESYSTEM-TYPE) (DEC20 'PS) (T 'DSK))
          DIR (STATUS UDIR)
          EXT 'NIL)
    (AND (DEVP (CAR X)) (SETQ DEV (DEVP (POP X))))
    (AND (PPNSPEC (CAR X)) (SETQ DIR (PPNSPEC (CAR X))) (POP X))
    (POP X NAME)
    (AND X (POP X EXT))
    (OR (AND (SYMBOLP DIR) (SYMBOLP NAME) (SYMBOLP EXT) (NULL X))
        (RETURN NIL))
    (OR (EQ (STATUS FILESYSTEM-TYPE) 'DEC20)
        (< (FLATC NAME) 7.)
        (SETQ NAME
              ((LAMBDA (NAME)
                 (RPLACD (NTHCDR 5. NAME) NIL)
                 (READLIST NAME))
               (EXPLODEC NAME))))
    (OR (EQ (STATUS FILESYSTEM-TYPE) 'DEC20)
        (< (FLATC EXT) 3.)
        (SETQ EXT
              ((LAMBDA (EXT)
                 (RPLACD (NTHCDR 2. EXT) NIL)
                 (READLIST EXT))
               (EXPLODEC EXT))))
    (RETURN (LIST (LIST DEV DIR) NAME EXT)))) 

(DEFUN SLURPPARSEMCL (X)
  (SETQ X (SLURPPARSE X))
  (COND ((OR (NULL X) (CADDR X)) X)
        (T (LIST (CAR X)
                 (CADR X)
                 (CASEQ (STATUS OPSYSTEM-TYPE) (CMU 'MCL) (T 'LSP)))))) 

(DEFUN DEVP (X)
  (AND (NOT (GET X 'PPNSPEC))
       (EQ (GETCHAR X (FLATC X)) ':)
       (INTERN (IMPLODE (NREVERSE (CDR (NREVERSE (EXPLODEC X)))))))) 

(DEFUN PPNSPEC (X)
  (COND ((NULL X) NIL)
        ((AND (EQ (TYPEP X) 'LIST)
              (FIXP (CAR X))
              (FIXP (CADR X))
              (NULL (CDDR X)))
         X)
        ((PPNSPEC (GET X 'PPNSPEC)))
        ((AND (EQ (STATUS FILESYSTEM-TYPE) 'DEC20)
              (ATOM X)
              (EQ (CAR (SETQ X (EXPLODEC X))) '<)
              (EQ (CAR (LAST X)) '>))
         (INTERN (IMPLODE (NREVERSE (CDR (NREVERSE (CDR X)))))))
        ((AND (EQ (STATUS OPSYSTEM-TYPE) 'CMU) (CMUDEC X)) X))) 

(DEFUN CMUDEC (PPN)
  (PROG (P0 P1 P2 P3 PN0 PN1 PN2 PN3)
    (OR (= (LENGTH (SETQ PPN (EXPLODEC PPN))) 8.) (RETURN NIL))
    (SETQ P0 (CMUDEC1 (CAR PPN))
          P1 (CMUDEC2 (NTH 1. PPN))
          P2 (CMUDEC2 (NTH 2. PPN))
          P3 (CMUDEC2 (NTH 3. PPN))
          PN0 (CMUDEC1 (NTH 4. PPN))
          PN1 (CMUDEC1 (NTH 5. PPN))
          PN2 (CMUDEC2 (NTH 6. PPN))
          PN3
          (OR (CMUDEC2 (NTH 7. PPN))
              (AND (CMUDEC1 (NTH 7. PPN))
                   (+ (CMUDEC1 (NTH 7. PPN)) 10.))))
    (RETURN (AND P0
                 P1
                 P2
                 P3
                 PN0
                 PN1
                 PN2
                 PN3
                 (LIST (+ (* P0 1000.)
                          (* P1 100.)
                          (* P2 10.)
                          P3
                          9.)
                       (+ (* PN0 9360.)
                          (* PN1 360.)
                          (* PN2 36.)
                          PN3)))))) 

(DEFUN CMUDEC1 (X)
  (SETQ X (GETCHARN X 1.))
  (OR (AND (< 64. X) (> 91. X) (- X 65.))
      (AND (< 96. X) (> 123. X) (- X 97.)))) 

(DEFUN CMUDEC2 (X)
  (SETQ X (GETCHARN X 1.))
  (AND (< 47. X) (> 58. X) (- X 48.))) 

(DEFUN SEDIT FEXPR (FILE)
  (PROG (FA CA OLDFNS NEWFNS PRINLEVEL PRINLENGTH SEMICOLON)
    (SETQ SEMICOLON 'BRIEFSEMI
          PRINLEVEL 2.
          PRINLENGTH 6.)
    (COND ((AND (NULL FILE) LASTFILE)
           (SETQ FILE LASTFILE)
           (TERPRI MSGFILES)
           (PRINC '|= | MSGFILES)
           (PRINC FILE MSGFILES)))
    (SETQ FILE (SLURPPARSEMCL FILE))
    (COND ((NULL FILE)
           (OR (ZEROP (CHARPOS T)) (TERPRI MSGFILES))
           (PRINC '|Invalid file specification - | MSGFILES)
           (PRINC (SHORTSPEC FILE) MSGFILES)
           (TERPRI MSGFILES)
           (RETURN NIL)))
    (SETQ FA (FILEATOM FILE)
          CA (CHANGEDATOM FILE))
    (COND ((NOT (AND (BOUNDP FA) (GET FA 'FILEATOM)))
           (OR (ZEROP (CHARPOS T)) (TERPRI MSGFILES))
           (PRINC '|No functions associated with | MSGFILES)
           (PRINC (SHORTSPEC FILE) MSGFILES)
           (TERPRI MSGFILES)
           (RETURN NIL)))
    (OR (SYMEVAL FA) (SET FA (LIST NIL)))
    (SETQ LASTFILE (SHORTSPEC FILE))
    (SETQ OLDFNS (FNS-IN-LIST (SYMEVAL FA)))
    (EDITE (SYMEVAL FA) NIL FA)
    (SETQ NEWFNS (FNS-IN-LIST (SYMEVAL FA)))
    (SETQ CHANGEDLIST (DIFFQ CHANGEDLIST (DIFFQ NEWFNS OLDFNS)))
    (SET CA
         (DIFFQ (NCONC (DIFFQ NEWFNS OLDFNS) (SYMEVAL CA))
                (DIFFQ OLDFNS NEWFNS)))
    (MARK-CHANGED (DIFFQ OLDFNS NEWFNS) T)
    (RETURN (SHORTSPEC FILE)))) 

(DEFUN UNSLURP FEXPR (XFILE)
  (PROG (*NOPOINT PRINLEVEL PRINLENGTH FA FILE RESULT)
    (AND (NULL XFILE) LASTFILE (SETQ XFILE (CDR LASTFILE)))
    (SETQ FILE (SLURPPARSEMCL XFILE))
    (COND ((NULL FILE)
           (PRINC '|Invalid file specification - | MSGFILES)
           (PRINC XFILE MSGFILES)
           (TERPRI MSGFILES)
           (RETURN NIL)))
    (SETQ FA (FILEATOM FILE))
    (COND ((NOT (AND (BOUNDP FA) (EVAL FA)))
           (PRINC '|No functions associated with | MSGFILES)
           (PRINC (SHORTSPEC FILE) MSGFILES)
           (TERPRI MSGFILES)
           (RETURN NIL)))
    (SETQ LASTFILE (SHORTSPEC FILE))
    (SETQ RESULT (DSKOUT FILE))
    (COND ((ATOM RESULT)
           (OR (ZEROP (CHARPOS T))
               (TERPRI MSGFILES)
               (TERPRI MSGFILES))
           (PRINC '|Error encountered while unslurping file - |
                  MSGFILES)
           (PRINC (SHORTSPEC FILE) MSGFILES)
           (TERPRI MSGFILES)))
    (AND SLURP-VERBOSITY
         (NOT (ZEROP (CHARPOS T)))
         (TERPRI MSGFILES))
    (RETURN (COND ((NOT (ATOM RESULT))
                   (SET (CHANGEDATOM FILE) NIL)
                   (SHORTSPEC FILE)))))) 

(DEFUN VUNSLURP FEXPR (X)
  (PROG (SLURP-VERBOSITY)
    (SETQ SLURP-VERBOSITY T)
    (RETURN (APPLY 'UNSLURP X)))) 

(DEFUN SLURPSET FEXPR (XFILE)
  ((LAMBDA (INSIDE-SLURP DEFAULTF)
     (PROG (FILE FNS RESULT SLURP-VERBOSITY SLURP1-COMMENTS
            SLURP1-FORMFEEDS)
       (SETQ SLURP-VERBOSITY T
             SLURP1-COMMENTS SLURPCOMMENTS
             SLURP1-FORMFEEDS SLURPFORMFEEDS)
       (SETQ FNS (INTERQ (CDR XFILE) (CDR XFILE)))
       (SETQ FILE (SLURPPARSEMCL (OR (CAR XFILE) LASTFILE)))
       (COND ((NULL FILE)
              (PRINC '|Invalid file specification - | MSGFILES)
              (PRINC (CAR XFILE) MSGFILES)
              (TERPRI MSGFILES)
              (RETURN NIL)))
       (SETQ RESULT (DSKINSET FILE FNS))
       (OR (ZEROP (CHARPOS T)) (TERPRI MSGFILES))
       (COND ((EQ RESULT 'FILE-NOT-FOUND)
              (PRINC '|File not found - | MSGFILES)
              (PRINC (SHORTSPEC FILE) MSGFILES)
              (TERPRI MSGFILES))
             ((EQ RESULT 'OPEN-ERROR)
              (PRINC '|Error reading file - | MSGFILES)
              (PRINC (SHORTSPEC FILE) MSGFILES)
              (TERPRI MSGFILES))
             ((EQ RESULT 'LINE-NUMBERS)
              (PRINC '|Can't read files with line numbers - |
                     MSGFILES)
              (PRINC (SHORTSPEC FILE) MSGFILES)
              (TERPRI MSGFILES))
             ((EQUAL FNS (INTERQ FNS RESULT)))
             (T (SETQ FNS
                      (MAPCAN '(LAMBDA (XFILE)
                                 (AND (NOT (MEMQ XFILE RESULT))
                                      (NCONS XFILE)))
                              FNS))
                (PRINC '|Not found:  | MSGFILES)
                (PRINC FNS MSGFILES)
                (TERPRI MSGFILES)))
       (SETQ LASTFILE (SHORTSPEC FILE))
       (RETURN (COND ((NOT (ATOM RESULT)) (SHORTSPEC FILE))))))
   T DEFAULTF)) 

(DEFUN TYPE FEXPR (XFILE)
  (PROG (FILE FORM FCHAN)
        (COND ((NULL (SETQ FILE (SLURPPARSEMCL (OR XFILE LASTFILE))))
               (PRINC '|Invalid file specification - | MSGFILES)
               (PRINC (OR XFILE LASTFILE) MSGFILES)
               (TERPRI MSGFILES)
               (RETURN NIL))
              ((AND (PROBEF FILE)
                    (ERRSET (SETQ FCHAN (OPEN FILE 'IN))))
               (SETQ LASTFILE (SHORTSPEC FILE)))
              (T (PRINC '|Open error on | MSGFILES)
                 (PRINC (SHORTSPEC FILE) MSGFILES)
                 (TERPRI MSGFILES)
                 (RETURN NIL)))
   LOOP (TERPRI MSGFILES)
        (COND ((EQ (SETQ FORM (READLINE FCHAN '*EOF*)) '*EOF*)
               (CLOSE FCHAN)
               (PRINC '|--- End of File ---| MSGFILES)
               (TERPRI MSGFILES)
               (RETURN (SHORTSPEC FILE))))
        (PRINC FORM MSGFILES)
        (GO LOOP))) 

(DEFUN CHANGES NIL
  (PROG (CHANGED)
    (MAPC '(LAMBDA (FILE)
             (COND ((SETQ CHANGED (EVAL (CHANGEDATOM FILE)))
                    (TERPRI MSGFILES)
                    (PRINC FILE MSGFILES)
                    (PRINC '| ==> | MSGFILES)
                    (LISTPRINT CHANGED MSGFILES)
                    (TERPRI MSGFILES))))
          SLURPEDFILES)
    (COND ((SETQ CHANGED (FNS-IN-LIST CHANGEDLIST))
           (TERPRI MSGFILES)
           (PRINC '|Unassigned Functions ==> | MSGFILES)
           (LISTPRINT CHANGED MSGFILES)
           (TERPRI MSGFILES))))) 

(DEFUN SAVEWORK NIL
  (PROG (CHANGED)
    (MAPC '(LAMBDA (FILE)
             (COND ((EVAL (CHANGEDATOM FILE))
                    (TERPRI MSGFILES)
                    (PRINC (APPLY 'UNSLURP FILE) MSGFILES)
                    (TERPRI MSGFILES))))
          SLURPEDFILES)
    (COND ((SETQ CHANGED (FNS-IN-LIST CHANGEDLIST))
           (TERPRI MSGFILES)
           (PRINC '|Unassigned Functions ==> | MSGFILES)
           (LISTPRINT CHANGED MSGFILES)
           (TERPRI MSGFILES))))) 

(DEFUN NEWFILE FEXPR (XFILE)
  (PROG (FA FILE)
    (SETQ FILE (SLURPPARSEMCL XFILE))
    (COND ((NULL FILE)
           (OR (ZEROP (CHARPOS T)) (TERPRI MSGFILES))
           (PRINC '|Invalid file specification - | MSGFILES)
           (PRINC XFILE MSGFILES)
           (TERPRI MSGFILES)
           (RETURN NIL)))
    (SETQ FA (FILEATOM FILE))
    (COND ((BOUNDP FA)
           (OR (ZEROP (CHARPOS T)) (TERPRI MSGFILES))
           (PRINC '|File Atom already exists - | MSGFILES)
           (PRINC (SHORTSPEC FILE) MSGFILES)
           (TERPRI MSGFILES)
           (RETURN NIL)))
    (SET FA (LIST NIL))
    (SET (CHANGEDATOM FILE) NIL)
    (SET (PROPATOM FILE) NIL)
    (PUTPROP FA T 'FILEATOM)
    (PUSH (SHORTSPEC FILE) SLURPEDFILES)
    (APPLY 'SEDIT XFILE)
    (AND (NULL (CAR (SYMEVAL FA))) (SET FA (CDR (SYMEVAL FA))))
    (RETURN (SHORTSPEC FILE)))) 

(DEFUN DROPFILE FEXPR (XFILE)
  (PROG (FA FILE)
    (SETQ FILE (SLURPPARSEMCL XFILE))
    (COND ((NULL FILE)
           (OR (ZEROP (CHARPOS T)) (TERPRI MSGFILES))
           (PRINC '|Invalid file specification - | MSGFILES)
           (PRINC XFILE MSGFILES)
           (TERPRI MSGFILES)
           (RETURN NIL)))
    (SETQ FA (FILEATOM FILE))
    (SETQ FILE (SHORTSPEC FILE))
    (COND ((NOT (BOUNDP FA))
           (OR (ZEROP (CHARPOS T)) (TERPRI MSGFILES))
           (PRINC '|File Atom does not exist - | MSGFILES)
           (PRINC FILE MSGFILES)
           (TERPRI MSGFILES)
           (RETURN NIL)))
    (SET (CHANGEDATOM FILE) NIL)
    (RETURN FILE))) 

(DEFUN COPYFN FEXPR (FNS)
  (PROG (TEMP SOURCEFN TARGETFN)
    (SETQ TARGETFN (CAR FNS)
          SOURCEFN (CADR FNS))
    (RETURN (AND (SETQ TEMP
                       (GETL SOURCEFN
                             '(EXPR FEXPR SUBR FSUBR LSUBR MACRO)))
                 (PROG2 (MAPCAR '(LAMBDA (PROP)
                                   (REMPROP TARGETFN PROP))
                                '(EXPR FEXPR
                                       SUBR
                                       FSUBR
                                       LSUBR
                                       MACRO))
                        T)
                 (PUTPROP TARGETFN
                          (SUBST NIL NIL (CADR TEMP))
                          (CAR TEMP))
                 (MARK-CHANGED TARGETFN)
                 (SETQ LASTWORD TARGETFN)
                 TARGETFN)))) 

(DEFUN SLURP-DEFUN FEXPR (FNDEF)
  (PROG (FNNAME PROP DEFINED)
    (COND ((ATOM (CAR FNDEF))
           (COND ((MEMQ (CADR FNDEF) '(EXPR FEXPR MACRO))
                  (SETQ FNNAME (CAR FNDEF))
                  (SETQ PROP (CADR FNDEF)))
                 (T (SETQ FNNAME (CAR FNDEF)) (SETQ PROP 'EXPR))))
          (T (SETQ FNNAME (CAAR FNDEF)) (SETQ PROP (CADR FNDEF))))
    (AND (MEMQ PROP '(EXPR FEXPR MACRO))
         (SETQ DEFINED (GETL FNNAME '(EXPR FEXPR MACRO EXPR-HASH)))
         (MAPCAR '(LAMBDA (PROP) (REMPROP FNNAME PROP))
                 '(EXPR FEXPR MACRO EXPR-HASH)))
    (SUBRCALL NIL (GET 'DEFUN 'SYSTEM-DEFUN) FNDEF)
    (COND ((OR (NULL INSIDE-SLURP) DEFINED)
           (MARK-CHANGED FNNAME)
           (SETQ LASTWORD FNNAME)))
    (RETURN FNNAME))) 

(DEFUN MARK-CHANGED NARGS
  (PROG (FNS DONT-TOUCH-FILEATOMS)
    (SETQ FNS (ARG 1.))
    (AND (> NARGS 1.) (SETQ DONT-TOUCH-FILEATOMS (ARG 2.)))
    (MAPCAR '(LAMBDA (FN)
               (OR (NOT (SYMBOLP FN))
                   (MEMQ FN '(T NIL))
                   (MAPCAN '(LAMBDA (FILE)
                              (COND ((MEMQ FN
                                           (SYMEVAL (CHANGEDATOM FILE))
                                           )
                                     (LIST T))
                                    ((MEMQ FN
                                           (CONS (FILEATOM FILE)
                                                 (SYMEVAL (FILEATOM
                             FILE))))
                                     (OR DONT-TOUCH-FILEATOMS
                                         (SET (CHANGEDATOM FILE)
                                              (CONS FN
                                                    (SYMEVAL (
                                   CHANGEDATOM FILE)))))
                                     (LIST T))))
                           SLURPEDFILES)
                   (MEMQ FN CHANGEDLIST)
                   (PUSH FN CHANGEDLIST)))
            (COND ((ATOM FNS) (LIST FNS))
                  (T FNS)))
    (RETURN T))) 

(DEFUN FNS-IN-LIST (CHANGEDLIST)
  (MAPCAN '(LAMBDA (FN)
             (AND (GETL FN '(EXPR FEXPR MACRO FILEATOM)) (LIST FN)))
          CHANGEDLIST)) 

(DEFUN LISTPRINT NARGS
  (PROG (XLIST CPOS LINL FILES)
    (SETQ XLIST (ARG 1.))
    (SETQ FILES
          (COND ((> NARGS 1.)
                 (COND ((ATOM (ARG 2.)) (LIST (ARG 2.)))
                       (T (ARG 2.))))
                (T MSGFILES)))
    (PRINC '|(| FILES)
    (SETQ CPOS (CHARPOS (CAR FILES))
          LINL (LINEL (CAR FILES)))
    (DO ((TLIST XLIST (CDR TLIST)) (FIRST T NIL))
      ((NULL TLIST))
      (COND ((< (1+ (FLATC (CAR TLIST)))
                (1- (- LINL (CHARPOS T))))
             (OR FIRST (PRINC '| | FILES))
             (PRINC (CAR TLIST) FILES))
            (T (TERPRI FILES)
               (TAB CPOS FILES)
               (PRINC (CAR TLIST) FILES))))
    (PRINC '|)| FILES)
    (RETURN T))) 

(DEFUN DEFSLURP FEXPR (XFILE)
  (PROG (FNLIST FILE)
    (SETQ FNLIST
          (COND ((ATOM (CAR XFILE)) (NCONS (CAR XFILE)))
                (T (CAR XFILE))))
    (SETQ FILE (CDR XFILE))
    (AND (NOT (SLURPPARSE FILE))
         ((LAMBDA (^W)
            (TERPRI MSGFILES)
            (PRINC '|Invalid DEFSLURP file specification - |
                   MSGFILES)
            (PRINC (CDR XFILE) MSGFILES)
            (BREAK DEFSLURP))
          NIL))
    (RETURN (MAPC '(LAMBDA (A) (PUTPROP A FILE 'AUTOLOAD)) FNLIST)))) 

(SETQ AUTOLOAD 'AUTOSLURP)

(DEFUN AUTOSLURP (XFILE)
  (PROG (LASTFILE FILE DEFAULTF)
    (SETQ FILE (CDR XFILE))
    (AND (NOT (ATOM (CAR FILE)))
         (NOT (FIXP (CAAR FILE)))
         (SETQ FILE
               (CONS (COND ((MEMQ (CAAR FILE) '(LISP LSP))
                            (COND ((EQ (STATUS FILESYSTEM-TYPE)
                                       'DEC20)
                                   '<MACLISP>)
                                  (T 'LISP:)))
                           (T (CADAR FILE)))
                     (CDR FILE))))
    (APPLY 'QSLURP FILE)
    (COND ((NOT (GETL (CAR XFILE)
                      '(EXPR FEXPR MACRO SUBR FSUBR LSUBR)))
           (TERPRI MSGFILES)
           (PRINC '/; MSGFILES)
           (PRINC (CAR XFILE) MSGFILES)
           (PRINC '| from file | MSGFILES)
           (PRINC (CDR XFILE) MSGFILES)
           (PRINC '| undefined after autoslurp| MSGFILES)
           (BREAK AUTOSLURP))))) 

(DEFUN UNIONQ (X Y)
  (APPEND X
          (MAPCAN '(LAMBDA (Y1)
                     (COND ((MEMQ Y1 X) NIL)
                           (T (LIST Y1))))
                  Y))) 

(DEFUN INTERQ (X Y)
  (MAPCAN '(LAMBDA (X1) (AND (MEMQ X1 Y) (LIST X1))) X)) 

(DEFUN DIFFQ (X Y)
  (MAPCAN '(LAMBDA (X1) (AND (NOT (MEMQ X1 Y)) (LIST X1))) X)) 

(DEFUN DSKIN (FILE)
  (PROG (OLDSEMI OLDFORM RESULT ECHOFILES)
    (SETQ OLDSEMI (STATUS MACRO 59.))
    (SETQ OLDFORM (STATUS MACRO 12.))
    (AND (DSKINLINETEST FILE) (RETURN 'LINE-NUMBERS))
    (SETQ RESULT
          ((LAMBDA (READTABLE)
             (SETQ READTABLE (*ARRAY NIL 'READTABLE NIL))
             (COND (SLURP1-FORMFEEDS
                    (SETSYNTAX 12.
                               'MACRO
                               '(LAMBDA NIL (LIST 'CONTROL-L))))
                   (T (SSTATUS SYNTAX 12. (STATUS SYNTAX 13.))))
             (AND SLURP1-COMMENTS
                  (SETSYNTAX '/; 'MACRO 'READSEMI))
             (LIST (DSKIN1 FILE) READTABLE))
           READTABLE))
    (SETQ READTABLE (CADR RESULT))
    (SETSYNTAX 59. (OR (CADR OLDSEMI) 'MACRO) (CAR OLDSEMI))
    (SETSYNTAX 12. 'MACRO (CAR OLDFORM))
    (RETURN (CAR RESULT)))) 

(DEFUN DSKIN1 (FILE)
  (PROG (FORM FORMV ATOMLIST FILEPROPS SLURP-INPUT INFILE ^Q)
        (AND (ATOM (ERRSET (SETQ SLURP-INPUT (OPEN FILE 'IN))))
             (RETURN 'OPEN-ERROR))
        (SETQ INFILE SLURP-INPUT)
        (SETQ ^Q T)
        (SETQ FILEPROPS SLURPPROPS)
   LOOP (COND ((EQ (CAR (SETQ FORM
                              (ERRSET (READ SLURP-INPUT '*EOF*))))
                   '*EOF*)
               (CLOSE SLURP-INPUT)
               (SET (FILEATOM FILE) (REVERSE ATOMLIST))
               (SET (CHANGEDATOM FILE) NIL)
               (OR (BOUNDP (PROPATOM FILE))
                   (SET (PROPATOM FILE) NIL))
               (RETURN FILE))
              ((ATOM FORM)
               (CLOSE SLURP-INPUT)
               (RETURN 'OPEN-ERROR))
              (T (SETQ FORM (CAR FORM))))
        (COND ((ATOM FORM))
              ((OR (EQ (CAR FORM) 'DEFUN)
                   (AND (EQ (CAR FORM) 'SETQ) (NULL (CDDDR FORM)))
                   (AND (EQ (CAR FORM) 'DEFPROP)
                        (MEMQ (CADDDR FORM) FILEPROPS)))
               (OR (MEMQ (CADR FORM) ATOMLIST)
                   (PUSH (CADR FORM) ATOMLIST)))
              ((EQ (CAR FORM) 'SEMICOLON)
               (COND ((AND (EQ (TYPEP (CAR ATOMLIST)) 'LIST)
                           (EQ (CAAR ATOMLIST) 'SEMICOLON))
                      (NCONC (CAR ATOMLIST) (CDR FORM)))
                     (T (PUSH FORM ATOMLIST))))
              ((AND (EQ (CAR FORM) 'COMMENT)
                    (COND ((EQ (CADR FORM) '--CONTENTS--))
                          ((EQ (CADR FORM) '--PROPS--)
                           (SET (PROPATOM FILE) (CDDR FORM))
                           (SETQ FILEPROPS
                                 (APPEND FILEPROPS (CDDR FORM))))
                          (T NIL))))
              (T (PUSH FORM ATOMLIST)))
        (COND ((ATOM FORM))
              ((EQ (CAR FORM) 'SEMICOLON))
              ((EQ (CAR FORM) 'CONTROL-L) (DSKPRINT FORM))
              (T (SETQ FORMV (ERRSET (EVAL FORM)))
                 (AND (ATOM FORMV) (DSKIN1ERR FORM))
                 (AND (EQ (CAR FORM) 'SETQ)
                      (NULL (CDDDR FORM))
                      (SETQ FORMV (NCONS (CADR FORM))))
                 (DSKPRINT (CAR FORMV))))
        (GO LOOP))) 

(DEFUN DSKIN1ERR (FORM)
  (PROG NIL
    (TERPRI MSGFILES)
    (PRINC '|FORM = | MSGFILES)
    (PRIN1 FORM MSGFILES)
    (TERPRI MSGFILES)
    (BREAK |eval error during slurp; type $P to proceed|))) 

(DEFUN DSKINLINETEST (FILE)
  (PROG (FILEOB R)
    (OR (PROBEF FILE) (RETURN 'FILE-NOT-FOUND))
    (AND (ATOM (ERRSET (SETQ FILEOB (OPEN FILE 'FIXNUM))))
         (RETURN 'OPEN-ERROR))
    (ERRSET (SETQ R (AND (ODDP (IN FILEOB)) 'LINE-NUMBERS)))
    (CLOSE FILEOB)
    (RETURN R))) 

(DEFUN DSKINSET (FILE FNS)
  ((LAMBDA (READTABLE)
     (SETQ READTABLE (*ARRAY NIL 'READTABLE NIL))
     (AND SLURP1-COMMENTS (SETSYNTAX '/; 'MACRO 'READSEMI))
     (COND ((DSKINLINETEST FILE))
           (T (DSKINSET1 FILE FNS))))
   READTABLE)) 

(DEFUN DSKINSET1 (FILE FNS)
  (PROG (GFNS FORM SLURP-INPUT INFILE ^Q ECHOFILES)
        (OR (PROBEF FILE) (RETURN 'FILE-NOT-FOUND))
        (AND (ATOM (ERRSET (SETQ SLURP-INPUT (OPEN FILE 'IN))))
             (RETURN 'OPEN-ERROR))
        (SETQ INFILE SLURP-INPUT)
        (SETQ ^Q T)
   LOOP (COND ((ATOM (SETQ FORM (ERRSET (READ SLURP-INPUT '*EOF*))))
               (CLOSE SLURP-INPUT)
               (RETURN 'OPEN-ERROR))
              ((EQ (SETQ FORM (CAR FORM)) '*EOF*)
               (CLOSE SLURP-INPUT)
               (RETURN GFNS)))
        (OR (AND (EQ (TYPEP FORM) 'LIST)
                 (MEMQ (CAR FORM) '(DEFUN DEFPROP SETQ))
                 (MEMQ (CADR FORM) FNS))
            (GO LOOP))
        (AND (ATOM (ERRSET (EVAL FORM))) (DSKIN1ERR FORM))
        (SETQ GFNS (CONS (CADR FORM) GFNS))
        (DSKPRINT (CADR FORM))
        (GO LOOP))) 

(DEFUN DSKOUT (FILE)
  (PROG (FNS)
    (AND (GETL 'TRACE '(SUBR LSUBR FSUBR EXPR FEXPR))
         (SETQ FNS (TRACE))
         (APPLY 'UNTRACE FNS))
    (RETURN (DSKOUT1 FILE)))) 

(DEFUN DSKOUT1 (FILE)
  (PROG (OUTPUT NWFILE BKFILE CONTENTS)
    (SETQ NWFILE (LIST (CAR FILE) (CADR FILE) 'M00))
    (SETQ BKFILE
          (LIST (CAR FILE)
                (CADR FILE)
                (COND ((EQ (STATUS OPSYSTEM-TYPE) 'CMU) 'MBK)
                      (T 'LBK))))
    (AND LAST-SLURP-OUTPUT-FILE (CLOSE LAST-SLURP-OUTPUT-FILE))
    (AND (ATOM (ERRSET (SETQ OUTPUT (OPEN NWFILE 'OUT))))
         (RETURN 'OPEN-ERROR))
    (LINEL OUTPUT DSKOUT-LENGTH)
    (SETQ LAST-SLURP-OUTPUT-FILE OUTPUT)
    (SETQ CONTENTS (EVAL (FILEATOM FILE)))
    (SETQ CONTENTS (DSKOUT2 CONTENTS FILE))
    (DSKOUT3 CONTENTS FILE OUTPUT)
    (TERPRI OUTPUT)
    (CLOSE OUTPUT)
    (COND ((PROBEF FILE)
           (AND (PROBEF BKFILE) (DELETEF BKFILE))
           (RENAMEF FILE BKFILE)))
    (RENAMEF NWFILE FILE)
    (RETURN FILE))) 

(DEFUN DSKOUT2 (CONTENTS FILE)
  (PROG (CON2)
   LOOP (COND ((AND CONTENTS
                    (EQ (TYPEP (CAR CONTENTS)) 'LIST)
                    (MEMQ (CAAR CONTENTS) '(COMMENT SEMICOLON))
                    (COND ((NOT (EQ (CADAR CONTENTS) '--CONTENTS--)))
                          (T (SETQ CONTENTS (CDR CONTENTS)) NIL)))
               (SETQ CON2 (APPEND CON2 (NCONS (CAR CONTENTS))))
               (SETQ CONTENTS (CDR CONTENTS))
               (GO LOOP)))
        (RETURN (APPEND CON2
                        (NCONS (LIST* 'COMMENT
                                      '--CONTENTS--
                                      (MAPCAN '(LAMBDA (X)
                                                 (AND X
               (ATOM X)
               (NCONS X)))
                                              CONTENTS)))
                        (AND (PROPATOMVAL FILE)
                             (NCONS (LIST* 'COMMENT
                                           '--PROPS--
                                           (PROPATOMVAL FILE))))
                        CONTENTS)))) 

(DEFUN DSKOUT3 (CONTENTS FILE OUTPUT)
  (PROG (FILEPROPS VAL)
    (SETQ FILEPROPS (UNIONQ SLURPPROPS (PROPATOMVAL FILE)))
    (MAPC '(LAMBDA (FORM)
             (COND ((NULL FORM))
                   ((SYMBOLP FORM)
                    (AND (GETL FORM '(EXPR FEXPR MACRO))
                         ((LAMBDA (^R ^W OUTFILES)
                            (APPLY 'GRINDEF (LIST FORM))
                            (TERPRI OUTPUT))
                          T T (LIST OUTPUT)))
                    (COND ((BOUNDP FORM)
                           (TERPRI OUTPUT)
                           (SETQ VAL (SYMEVAL FORM))
                           (FUNCALL (OR PRIN1 'PRIN1)
                                    `(SETQ ,FORM
                                           ,(COND ((OR (EQ VAL T)
                (EQ VAL NIL)
                (NUMBERP VAL))
                                                   VAL)
                                                  (T (LIST 'QUOTE
                    VAL))))
                                    OUTPUT)
                           (TERPRI OUTPUT)))
                    (MAPC '(LAMBDA (PROP)
                             (COND ((GET FORM PROP)
                                    (TERPRI OUTPUT)
                                    (FUNCALL (OR PRIN1 'PRIN1)
                                             `(DEFPROP ,FORM
                ,(GET FORM PROP)
                ,PROP)
                                             OUTPUT)
                                    (TERPRI OUTPUT))))
                          FILEPROPS))
                   ((AND (EQ (TYPEP FORM) 'LIST)
                         (EQ (CAR FORM) 'SEMICOLON))
                    (TERPRI OUTPUT)
                    (MAPC '(LAMBDA (COM)
                             (PRINC '/; OUTPUT)
                             (PRINC COM OUTPUT)
                             (TERPRI OUTPUT))
                          (CDR FORM)))
                   ((EQ (CAR FORM) 'CONTROL-L)
                    (TERPRI OUTPUT)
                    (PRINC (ASCII 12.) OUTPUT))
                   (T (TERPRI OUTPUT)
                      (FUNCALL (OR PRIN1 'PRIN1) FORM OUTPUT)
                      (TERPRI OUTPUT)))
             (DSKPRINT FORM))
          CONTENTS))) 

(DEFUN DSKPRINT (X)
  (PROG (COLPOS LINELEN FX PRIN1)
    (OR SLURP-VERBOSITY (RETURN NIL))
    (SETQ COLPOS
          ((LAMBDA (X)
             (COND ((ZEROP X) X)
                   (T (+ X 16. (MINUS (REMAINDER X 16.))))))
           (CHARPOS T)))
    (SETQ LINELEN (LINEL T))
    (COND ((EQUAL X '(CONTROL-L))
           (OR (ZEROP COLPOS) (TERPRI MSGFILES))
           (TERPRI MSGFILES))
          ((NOT (EQ (TYPEP X) 'LIST))
           (COND ((AND (EQ (TYPEP X) 'HUNK8)
                       (STATUS FEATURE NETL))
                  (SETQ X (CXR 7. X)))
                 ((MEMQ X '(T NIL COMMENT DECLARE)) (RETURN NIL))
                 ((OR (ATOM X) (NUMBERP X)))
                 (T (SETQ X (TYPEP X))))
           (COND ((LESSP (FLATSIZE X) (- LINELEN COLPOS))
                  (OR (ZEROP COLPOS) (PRINC (ASCII 9.) MSGFILES))
                  (OR (ZEROP (REMAINDER (CHARPOS T) 16.))
                      (PRINC (ASCII 9.) MSGFILES))
                  (PRIN1 X MSGFILES))
                 (T (TERPRI MSGFILES) (PRIN1 X MSGFILES))))
          ((MEMQ (CAR X)
                 '(DECLARE COMMENT
                           SEMICOLON))
           (RETURN NIL))
          ((LESSP (LENGTH (SETQ FX (EXPLODE X))) LINELEN)
           (OR (ZEROP COLPOS) (TERPRI MSGFILES))
           (PRIN1 X MSGFILES)
           (TERPRI MSGFILES))
          (T (OR (ZEROP COLPOS) (TERPRI MSGFILES))
             (RPLACD (NTHCDR (- LINELEN 5.) FX) NIL)
             (MAPC 'PRINC FX)
             (PRINC '|...| MSGFILES))))) 

(DEFUN FILEATOM (FILE)
  (AND (ATOM (CAR FILE)) (SETQ FILE (SLURPPARSE FILE)))
  (IMPLODE (NCONC (EXPLODEC (CADR FILE))
                  (LIST '-)
                  (EXPLODEC (CADDR FILE))
                  (LIST '- 'F 'N 'S)))) 

(DEFUN CHANGEDATOM (FILE)
  (AND (ATOM (CAR FILE)) (SETQ FILE (SLURPPARSE FILE)))
  (IMPLODE (NCONC (EXPLODEC (CADR FILE))
                  (LIST '-)
                  (EXPLODEC (CADDR FILE))
                  (LIST '- 'C 'H 'G)))) 

(DEFUN PROPATOM (FILE)
  (AND (ATOM (CAR FILE)) (SETQ FILE (SLURPPARSE FILE)))
  (IMPLODE (NCONC (EXPLODEC (CADR FILE))
                  (LIST '-)
                  (EXPLODEC (CADDR FILE))
                  (LIST '- 'P 'R 'O 'P 'S)))) 

(DEFUN PROPATOMVAL (FILE)
  (AND (BOUNDP (SETQ FILE (PROPATOM FILE))) (SYMEVAL FILE))) 

(DEFUN SHORTSPEC (X)
  (PROG (Y)
    (AND (ATOM (CAR X)) (RETURN X))
    (SETQ Y (CDR X))
    (AND (CADAR X)
         (PUSH (COND ((EQ (STATUS FILESYSTEM-TYPE) 'DEC20)
                      (BROKIFY (CADAR X)))
                     (T (CADAR X)))
               Y))
    (RETURN (COND ((EQ (CAAR X)
                       (CASEQ (STATUS FILESYSTEM-TYPE)
                              (DEC20 'PS)
                              (T 'DSK)))
                   Y)
                  (T (CONS (DEVIFY (CAAR X)) Y)))))) 

(DEFUN DEVIFY (X) (INTERN (IMPLODE (APPEND (EXPLODEC X) '(:))))) 

(DEFUN BROKIFY (X)
  (INTERN (IMPLODE (APPEND '(<) (EXPLODEC X) '(>))))) 

(DEFUN TAB NARGS
  (PROG (N CHAN P)
        (SETQ N (ARG 1.))
        (SETQ CHAN
              (COND ((> NARGS 1.)
                     (COND ((ATOM (ARG 2.)) (LIST (ARG 2.)))
                           (T (ARG 2.))))
                    (T MSGFILES)))
        (SETQ P (CHARPOS (CAR CHAN)))
        (AND (> P N) (SETQ P 0.) (TERPRI CHAN))
   LOOP (COND ((= P N) (RETURN NIL))
              ((NOT (< N (+ P (- 8. (\ P 8.)))))
               (PRINC (ASCII 9.) CHAN)
               (SETQ P (+ P (- 8. (\ P 8.)))))
              (T (PRINC '| | CHAN) (SETQ P (1+ P))))
        (GO LOOP))) 

(DEFUN READSEMI NIL (LIST 'SEMICOLON (READLINE SLURP-INPUT))) 

(DEFUN SEMICOLON MACRO (X) NIL) 

(SETQ SEMICOLON 'FULLSEMI)

(DEFPROP SEMICOLON
         (LAMBDA (TEMP ITEM) (FUNCALL SEMICOLON TEMP ITEM))
         GGRIND-FN)

(DEFUN FULLSEMI (TEMP ITEM)
  (PROG (G1PRINC-ATOMS)
    (SETQ G1PRINC-ATOMS T)
    (SETQ G1OPEN-DEL '/;
          G1CLOSE-DEL '||
          G1LEVEL (1+ G1LEVEL))
    (COND (G1FORMAT-LIST (FUNCALL G1FORMAT-LIST TEMP (CDR ITEM)))
          ((G1FORMAT-LIST TEMP (CDR ITEM))))
    (SETQ G1LEVEL (1- G1LEVEL))
    (PROGN (SETQ G1FCODE 'ALWAYS
                 G1BCODE 0.)
           (G1ENTER-OBJ '|| 'PRINC NIL)))) 


(DEFUN BRIEFSEMI (TEMP ITEM)
  TEMP
  (G1ENTER-OBJ NIL NIL 'START)
  (PROGN (SETQ G1FCODE 'NORMAL
               G1BCODE 0.)
         (G1ENTER-OBJ '|(| 'PRINC NIL))
  (PROGN (SETQ G1FCODE 'NEVER
               G1BCODE 0.)
         (G1ENTER-OBJ (CADR ITEM) 'PRIN1 NIL))
  (PROGN (SETQ G1FCODE 'NEVER
               G1BCODE 1.)
         (G1ENTER-OBJ '|)| 'PRINC 'END))) 

(PROG (TEMP1 TEMP2 TEMP3)
  (SETQ TEMP1 (NCONS '(NEVER)))
  (RPLACD TEMP1 TEMP1)
  (SETQ TEMP2 (NCONS (CONS 'ALWAYS (CONS 1. TEMP1))))
  (RPLACD TEMP2 TEMP2)
  (SETQ TEMP1 (CONS 'NEVER (CONS 1. TEMP1)))
  (SETQ TEMP3 (CONS NIL (CONS '(NEVER) (CONS TEMP1 TEMP2))))
  (PUTPROP 'DECLARE TEMP3 'GGRIND-TEMPLATE))

(PROG (TEMP1)
  (SETQ TEMP1 (NCONS '(NEVER)))
  (RPLACD TEMP1 TEMP1)
  (SETQ TEMP1 (CONS NIL TEMP1))
  (PUTPROP 'COMMENT TEMP1 'GGRIND-TEMPLATE))

(DEFPROP |`-expander/||
         (LAMBDA (TEMP ITEM)
           (SETQ G1OPEN-DEL '/`)
           (SETQ G1CLOSE-DEL '||)
           (SETQ G1LEVEL (1+ G1LEVEL))
           (CONTROL-L)
           (FUNCALL (OR G1FORMAT-LIST 'G1FORMAT-LIST)
                    TEMP
                    (NCONS (CDR ITEM))))
         GGRIND-FN)

(DEFPROP |`,/||
         (LAMBDA (TEMP ITEM)
           (SETQ G1OPEN-DEL '/,)
           (SETQ G1CLOSE-DEL '||)
           (SETQ G1LEVEL (1+ G1LEVEL))
           (FUNCALL (OR G1FORMAT-LIST 'G1FORMAT-LIST)
                    TEMP
                    (NCONS (CDR ITEM))))
         GGRIND-FN)

(DEFPROP |`,@/||
         (LAMBDA (TEMP ITEM)
           (SETQ G1OPEN-DEL '/,@)
           (SETQ G1CLOSE-DEL '||)
           (SETQ G1LEVEL (1+ G1LEVEL))
           (FUNCALL (OR G1FORMAT-LIST 'G1FORMAT-LIST)
                    TEMP
                    (NCONS (CDR ITEM))))
         GGRIND-FN)

(DEFPROP MACROEXPANDED
         (LAMBDA (TEMP ITEM)
           (G1FORMAT-DISPATCH NIL (CADDDR ITEM)))
         GGRIND-FN)

(PROG (CHANGEDLIST)
  (OR (GET 'DEFUN 'SYSTEM-DEFUN)
      (PUTPROP 'DEFUN (GET 'DEFUN 'FSUBR) 'SYSTEM-DEFUN))
  (COPYFN DEFUN SLURP-DEFUN))

   