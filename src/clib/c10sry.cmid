;
;	C10SRY - CHANGE .VCALL'S TO PUSHJ'S
;
;	This file is ITS dependent.
;	This routine assumes a superior DDT.
;

TITLE SORRY
.INSRT NC
.INSRT NM

.GLOBAL SEG2LO,SEG2HI,SEG3LO,SEG3HI

IENTRY SORRY

	.VALUE	[ASCIZ/:UNPURE
P/]

;	MOVE	A,SEG2LO
;	TRZ	A,1777
;	MOVE	B,SEG2HI
;	SUBI	B,(A)
;	LSH	A,-10.
;	ADDI	B,1777
;	LSH	B,-10.
;	MOVN	B,B
;	HRL	A,B
;	.CALL	I$IMPR
;	.VALUE

	SKIPA	A,SEG2LO	; POINTER TO BEGINNING OF CODE AREA
S1:	 ADDI	A,1		; NEXT WORD
	CAML	A,SEG2HI	; AT END OF CODE AREA?
	 GO	DONE
	HLRZ	B,(A)		; INSTRUCTION
	TRZ	B,000777	; ISOLATE OPCODE
	CAIE	B,(.VCALL)	; IS IT A .CCALL?
	 GO	S1		; NO, GO ON
	HLRZ	B,(A)		; THE INSTRUCTION AGAIN
	TRZ	B,777740	; FLUSH OPCODE AND ACCUMULATOR
	IORI	B,(PUSHJ P,)	; MAKE IT A PUSHJ
	HRLM	B,(A)		; STORE IT
	GO	S1

DONE:	MOVE	A,SEG2LO
	TRZ	A,1777
	MOVE	B,SEG3HI
	SUBI	B,(A)
	LSH	A,-10.
	ADDI	B,1777
	LSH	B,-10.
	MOVN	B,B
	HRL	A,B
	.CALL	I$PURE
	.VALUE

	.VALUE	[ASCIZ/:PDUMP /]
	.BREAK	16,0

.PDATA
I$PURE:	SETZ ? 'CORBLK ? 1000,,%CBNDR ? 1000,,%JSELF ? 400000,,A
;I$IMPR:	SETZ ? 'CORBLK ? 1000,,%CBRED+%CBWRT ? 1000,,%JSELF ? 400000,,A
END
