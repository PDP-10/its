;
;	CSAVE - Routine to prepare for saving a program file
;
;	This file is ITS dependent.
;	This file contains the size of the shared library file.
;

TITLE CSAVE
.INSRT NC
.INSRT NM

.GLOBAL	TYICHN,TYOCHN,PDLBOT,PDLTOP

CENTRY	CSAVE

	SETOM	TYICHN
	SETOM	TYOCHN
	MOVE	A,[-10.,,246.]
	.CALL	[SETZ ? 'CORBLK ? MOVEI 0 ? MOVEI %JSELF ? SETZ A]
		; DELETE SHARED LIBRARY PAGES
	CROAK	CORBLK FAILED

	; DELETE STACK PAGES

	MOVE	A,PDLBOT
	ADDI	A,1777
	TRZ	A,1777		; MOVE TO NEXT PAGE BOUNDARY
	MOVE	B,PDLTOP	; LAST WORD OF STACK
	ADDI	B,1
	TRZ	B,1777		; MOVE TO PREVIOUS PAGE BOUNDARY
	LSH	A,-10.
	LSH	B,-10.		; GET PAGE NUMBERS
	SUB	B,A		; NUMBER OF PAGES
	MOVN	B,B		; NEGATIVE NUMBER OF PAGES
	HRL	A,B		; MAKE COUNTING POINTER

	.CALL	[SETZ ? 'CORBLK ? MOVEI 0 ? MOVEI %JSELF ? SETZ A]
	CROAK	CORBLK FAILED

	.VALUE	[ASCIZ/:PDUMP /]
	RETURN

END
