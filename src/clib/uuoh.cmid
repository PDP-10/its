;
;	UUOH - C UUO Handler
;
;	This file is PDP-10 dependent, system-independent.
;

TITLE UUOH
.INSRT NC
.INSRT NM

;
;	UUO DISPATCH TABLE
;

.PDATA
IENTRY	UUOTAB
	ILLUUO
	UCCALL		;.CCALL	- FIRST TIME CALL
	UVCALL		;.VCALL - CALL OF VARIABLE PROC
	UACALL		;.ACALL - CALL NEEDING ADDITIONAL ARGS
	UXCALL		;.XCALL - CALL WITH EXTRA ARGS
REPEAT UUOTAB+40-.,[ILLUUO?]

;
;	BASIC UUO DISPATCHER
;

.IDATA
MDATA	UUOH
	0
	GO	UUO$HANDLER
MDATA	SMASH
	-1
.UDATA
MDATA	USAVEA
	BLOCK 1
MDATA	USAVEB
	BLOCK 2
MDATA	USAVEC
	BLOCK 3
MDATA	USAVED
	BLOCK 4
.CODE

IENTRY	UUO$HANDLER

	MOVEM	D,USAVED
	LDB	D,[330500,,40]	; GET UUO CODE
	GO	@UUOTAB(D)	; DISPATCH BASED ON THE UUO

URETA:	MOVE	A,USAVEA
URETB:	MOVE	B,USAVEB
URETC:	MOVE	C,USAVEC
URETD:	MOVE	D,USAVED
	GO	@UUOH

;
;	ILLEGAL UUO HANDLER
;

IENTRY	ILLUUO

	CROAK ILLEGAL UUO
	GO	URETD

;
;	.CCALL HANDLER
;

IENTRY	UCCALL

	MOVEM	B,USAVEB	; MUST NOT CHANGE ANY REGS
	MOVEM	C,USAVEC	; AS CALL MIGHT BE THRU A REG
	HRRZ	C,40		; THE CALLED PROCEDURE
	JUMPE	C,UCBAD		; NO SUCH PROCEDURE
	HLRZ	0,-1(C)		; THE NUMBER OF FORMAL ARGS
	CAIL	0,20		; REASONABLE NUMBER?
	 GO	UCBAD		; NO, NOT A PROCEDURE
	SKIPN	SMASH		; SHOULD I SMASH THE CALL
	 GO	UC$GO		; NO, LEAVE IT
	SOS	D,UUOH		; ADDRESS OF THE CALL
	MOVE	B,(D)		; THE .CCALL INSTRUCTION
	TLZ	B,777740	; ZERO OUT ALL BUT ADDRESS PART
	CAIE	B,(C)		; IS IT A CONSTANT CALL?
	 GO	UCV		; NO, CHANGE IT INTO A .VCALL
	LDB	B,[270400,,40]	; THE NUMBER OF ACTUAL ARGS
	HLRZ	0,-1(C)		; THE NUMBER OF FORMAL ARGS
	SUB	B,0		; THE NUMBER OF EXTRA ACTUALS
	JUMPL	B,UCA		; TOO FEW ACTUALS
	JUMPG	B,UCX		; TOO MANY ACTUALS
	MOVEI	B,(PUSHJ P,)
	HRLM	B,(D)		; SMASH .CCALL TO PUSHJ
	GO	URETB		; RE-EXECUTE CALL

UCA:	MOVN	B,B		; THE NUMBER OF EXTRA ARGS NEEDED
	LSH	B,5		; SHIFT INTO ACCUMULATOR POSITION
	IORI	B,(.ACALL)
	HRLM	B,(D)		; SMASH .CCALL TO .ACALL
	GO	URETB		; RE-EXECUTE CALL
UCX:	LSH	B,5		; SHIFT INTO ACCUMULATOR POSITION
	IORI	B,(.XCALL)
	HRLM	B,(D)		; SMASH .CCALL TO .ACALL
	GO	URETB		; RE-EXECUTE CALL
UCV:	MOVE	B,(D)		; THE ORIGINAL CALL
	TLZ	B,777000	; MASK OUT OPCODE
	TLO	B,(.VCALL)	; MAKE IT A .VCALL
	MOVEM	B,(D)		; SMASH CALL
	GO	URETB		; RE-EXECUTE CALL

IENTRY	UCBAD
	LDB	B,[270400,,0]	; THE NUMBER OF ACTUAL ARGS
UVBAD:	MOVEM	B,USAVEA	; SAVE NUMBER OF ACTUAL ARGS
	SETZ	A,		; SET DEFAULT RETURN VALUE
	MOVE	D,UUOH
	SUBI	D,1		; LET USER LOOK AT CALL
	CROAK CALL OF UNDEFINED PROCEDURE
	SUB	P,USAVEA	; POP OFF ARGS
	GO	URETB		; RETURN TO CALLER

IENTRY	UVCALL

	HRRZ	C,40		; THE CALLED PROCEDURE
	JUMPE	C,UVBAD		; NO SUCH PROCEDURE
	HLRZ	0,-1(C)		; THE NUMBER OF FORMAL ARGS
	CAIL	0,20		; REASONABLE NUMBER?
	 GO	UVBAD		; NO, NOT A PROCEDURE
UC$GO:	LDB	B,[270400,,40]	; THE NUMBER OF ACTUAL ARGS
	SUB	0,B		; NUMBER OF ARGS NOT GIVEN
	JUMPL	0,UVHACK	; TOO MANY ARGS GIVEN
UVLOOP:	SOJL	0,UVDOIT	; FOR EACH ARG NEEDED
	PUSH	P,[0]		; PUSH ZERO ARG
	GO	UVLOOP		; LOOP

UVHACK:				; TOO MANY ARGS GIVEN
	ADD P,0			; POP OFF EXTRA ARGS
UVDOIT:	PUSH	P,UUOH		; PUSH RETURN ADDRESS
	GO	(C)		; EXECUTE PROCEDURE

IENTRY	UACALL

	LDB	B,[270400,,40]	; THE NUMBER OF EXTRA ARGS NEEDED
	HRRZ	C,40		; THE CALLED PROCEDURE
UALOOP:	SOJL	B,UVDOIT	; FOR EACH ARG NEEDED
	PUSH	P,[0]		; PUSH ZERO ARG
	GO	UALOOP		; LOOP

IENTRY	UXCALL

	LDB	B,[270400,,40]	; THE NUMBER OF EXTRA ARGS
	HRRZ	C,40		; THE CALLED PROCEDURE
	SUBI	P,(B)		; POP OFF EXTRA ARGS
	PUSH	P,UUOH		; PUSH RETURN ADDRESS
	GO	(C)		; EXECUTE PROCEDURE

IENTRY EUUOH
END
