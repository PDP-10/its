title babel server
%colsn==12 ? %coopn==2 ? %cslsn==1 ? %csrfc==2

	loc 42
	-tsintl,,tsint

	loc 100
tsint:	17
	%piioc+%pilos ? 0 ? -1 ? -1 ? die	;handle ioc errors and LOSE's
tsintl==.-tsint

lsnpkt:	.byte 16. ? %colsn_8 ? 5 ? .byte
	0 ? 0 ? 0
	.byte 8 ? irpc ch,,[BABEL] ? "ch ? termin ? .byte
opnpkt:	.byte 16. ? %coopn_8 ? 0 ? .byte
pdl:	block 4
go:	.close 1,
	move 17,[-4,,pdl]
	.suset [.roption,,1]
	tlo 1,optint
	.suset [.soption,,1]
	.suset [.smask,,[%piioc+%pilos]]
	.call [ setz ? 'chaoso ? movei 0 ? movei 1 ? setzi 2]
	  jrst die
	.call [ setz ? 'pktiot ? movei 1 ? setzi lsnpkt]
	  jrst die
	movei 1,30.*60.*2	;two minutes
	.call [ setz ? 'netblk ? movei 1 ? movei %cslsn ? move 1 ? setzm 1]
	  jrst die
	caie 1,%csrfc
	  jrst die
	.call [ setz ? 'pktiot ? movei 1 ? setzi opnpkt]
	  jrst die
loop:	move 1,[440700,,[asciz ~ !"#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_`abcdefghijklmnopqrstuvwxyz{|}~]]
	movei 2,176-40
	.call [setz ? sixbit /siot/ ? movei 1 ? move 1 ? setz 2]
	  jrst die
	jrst loop
die:	.logout 1,
	.break 16,160000
	.value
end go

;;; local modes:
;;; mode:midas
;;; auto fill mode:
;;; fill column:70
;;; compile command: :midas mc:device;chaos babel_1 î
;;; end:
