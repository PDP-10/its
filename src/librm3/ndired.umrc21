
<PACKAGE "NDIRED">

<USE "DATIME" "TTY" "ITS" "DIR">

<ENTRY NDIRED ND>

<DEFINE NDIRED ("EXTRA" (FIRST T) PD OTTY HAHA)
    #DECL ((FIRST) <SPECIAL <OR 'T FALSE>> (PD) <VECTOR [REST VECTOR]>
	   (HAHA) <UVECTOR [3 FIX]>)
    <SET OTTY <TTY-GET>>
    <SET HAHA ![*232323232323* *232023202323* 0]>
    <PUT .HAHA 3 <CHTYPE <ORB <3 .OTTY> *000002000000*> FIX>>
    <SETG IH <GET ,INCHAN INTERRUPT>>
    <SET ERINT <ON "ERROR" <FUNCTION (X "TUPLE" Y) <EVENT ,IH> <TTY-SET .OTTY>> 1000 0>>
    <OFF ,IH>
    <TTY-SET .HAHA>
    <SET PD <PARSE-DIRECTORY <GET-DIRECTORY <UNAME>>>>
    <INMSG>
    <TERPRI>
    <COND (<TYPE? <6 <1 .PD>> FIX> <FILE-PRINT <1 .PD> <>>)
	  (T <LINK-PRINT <1 .PD> <>>)>
    <TERPRI>
    <DELETE?>
    <TERPRI>
    <DIRPR .PD>
    <OUTMSG>
    <TTY-SET .OTTY>
    <PROG () <EVENT ,IH> <OFF .ERINT> ,NULL>>
	       

<DEFINE PSLEEP (STR TIM "EXTRA" (STIME <+ .TIM <* .03 <MOD <RANDOM <TIME>> 10>>>))
    #DECL ((STR) STRING (TIM STIME) FLOAT)
    <MAPF <> <FUNCTION (X) <PRINC .X> <SLEEP .STIME>> .STR>>

<DEFINE INMSG ()
    <IMAGE 7>
    <TERPRI>
    <PRINC "MESSAGE FROM AV ">
    <TERPRI>
    <SLEEP 1>
    <PRINC <ASCII 26>>
    <SLEEP .7>
    <PRINC "
745522)   XCT 35(2)  ">
    <SLEEP .4>
    <PSLEEP "NDIRED" .07>
    <PRINC !">
    <SLEEP .5>
    <PRINC !"!>    
    <SLEEP .6>
    <PRINC "
NDIRED.146">
    <SLEEP .3>
    <PRINC "This is new dired.  No warranty is expressed or implied.
Type ? for command list, ? <cmd> for each command.
" >
    <SLEEP .3>>

<DEFINE DELETE? ()
    <SLEEP .5>
    <PSLEEP "@D * :" .2>
    <SLEEP .3>
    <PRINC "X">
    <SLEEP .3>
    <PRINC "*">
    <SLEEP .18>
    <PRINC "
Delete? (Y or N): ">
    <SLEEP .4>
    <PRINC "Yes.">
    <SLEEP .2>
    <PRINC <STRING "
	DSK:" <UNAME> " -- DELETE all remaining files? (Y or N): ">>
    <SLEEP 1>
    <PRINC "Yes.">
    <SLEEP .5>>

<DEFINE OUTMSG ("AUX" (TTYNUM <CHTYPE <GETBITS <USET <> "TTY"> <BITS 18>> FIX>))
   <PRINC "
@">
   <SLEEP .7>
   <PSLEEP "QUIT" .1>
   <TERPRI>
   <SLEEP .6>
   <PRINC <STRING ":KILL
 " <JNAME> "$J
*">>
   <SLEEP 1>
   <PSLEEP "$$0U" .2>
   <SLEEP .3>
   <PRINC <STRING <ASCII 16> "C">>
   <SET T <RTIME>>
   <TERPRI>
   <PRINC "DM ITS 982 CONSOLE ">
   <PUT .OUTCHAN 18 8>
   <PRINC .TTYNUM>
   <PUT .OUTCHAN 18 10>
   <PRINC " FREE ">
   <AND <L? <1 .T> 10> <PRIN1 0>>
   <PRIN1 <1 .T>>
   <PRINC !":>
   <AND <L? <2 .T> 10> <PRIN1 0>>
   <PRIN1 <2 .T>>
   <PRINC !":>
   <AND <L? <3 .T> 10> <PRIN1 0>>
   <PRIN1 <3 .T>>
   ,NULL>
   
<DEFINE FILE-PRINT (ENT FUDGE)
  #DECL ((ENT) <VECTOR [2 STRING] [3 <LIST [REST FIX]>] [2 FIX] LIST>
	 (FUDGE) <OR ATOM FALSE>)
  <COND (.FUDGE <PRINC "+  ">)
	(T <PRINC "   ">)>
  <PRINC <7 .ENT>>
  <PRINC "  ">
  <PRINC <STRING <1 .ENT> !"  <2 .ENT> !" >>
  <PRINC <COND (<0? <6 .ENT>> 0)(T <+ 1 </ <6 .ENT> 1024>>)>>
  <PRINC " (">
  <PRINC <MOD <6 .ENT> 1024>>
  <PRINC <STRING ") " <COND (<EMPTY? <8 .ENT>> !" )(T !"!)>>>
  <DATE-PRINT <3 .ENT>>
  <PRINC !" >
  <TIME-PRINT <4 .ENT>>
  <PRINC " (">
  <DATE-PRINT <5 .ENT>>
  <PRINC ")">>

<DEFINE LINK-PRINT (ENT FUDGE "AUX" (LNAME <6 .ENT>))
  #DECL ((ENT) <VECTOR [2 STRING] [3 <LIST [REST FIX]>] <LIST [REST STRING]> FIX LIST>
	 (FUDGE) <OR ATOM FALSE> (LNAME) <LIST [REST STRING]>)
  <COND (.FUDGE <PRINC "+  L   ">)
	(T <PRINC "   L   ">)>
  <PRINC <STRING <1 .ENT> !"  <2 .ENT> !" >>
  <PRINC <STRING <1 .LNAME> !"  <2 .LNAME> !"  <3 .LNAME>>>>

<DEFINE DATE-PRINT (DATE)
  #DECL ((DATE) <LIST [3 FIX]>)
  <PRINC <2 .DATE>>
  <PRINC !"/>
  <PRINC <3 .DATE>>
  <PRINC !"/>
  <PRINC <1 .DATE>>>

<DEFINE TIME-PRINT (TIME)
  #DECL ((TIME) <LIST [3 FIX]>)
  <TP <1 .TIME>>
  <PRINC !":>
  <TP <2 .TIME>>
  <PRINC !":>
  <TP <3 .TIME>>>

<DEFINE TP (ST) #DECL ((ST) FIX)
  <COND (<L? .ST 10><PRINC 0>)>
  <PRINC .ST>>

<DEFINE DIRPR (PD "AUX" (FUDGE T)) 
  #DECL ((PD) <VECTOR [REST VECTOR]> (FUDGE) <OR ATOM FALSE>)
	<MAPF <>
	      <FUNCTION (ENT) 
		      <COND (<TYPE? <6 .ENT> FIX> <FILE-PRINT .ENT .FUDGE>)
			    (T <LINK-PRINT .ENT .FUDGE>)>
		      <TERPRI>
		      <SET FUDGE <>>>
	      .PD>>

<ENDPACKAGE>