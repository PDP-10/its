

<REMOVE TENEX>
<PACKAGE "SMCELLS">
<ENTRY MCELL  DISPCOM MIGS MCELLS>
<MOBLIST DISPCOM>
<BLOCK (<GET DISPCOM OBLIST><ROOT>)>
COMSWICTH
<ENDBLOCK>
<MOBLIST MIGS>
<MOBLIST MCELLS 1>
<BLOCK (<GET MIGS OBLIST><ROOT>)>
TENEX TRMTYP PHYCEN
<ENDBLOCK>
<NEWTYPE MCELL
	 LIST
	 '<LIST LIST
		LIST
		[2 <OR ATOM FALSE>]
		[REST <LIST FIX [REST <OR FIX FLOAT FORM ATOM>]>]>>


<ENDPACKAGE>


<PACKAGE "MIGSPK">
<ENTRY MIGS: MIGSINIT WHO DIR MIGSHELP FROB>
<SET REDEFINE T>
<USE "SMCELLS">
<BLOCK (!.OBLIST <GET MIGS OBLIST>)>
<SET TENEX <>>
<SET MIGS-INIT-COUNT 0>

<DEFINE MIGS: () ,NULL>

<DEFINE MIGSINIT ("ACT" MPKACT)	;" ... tmp bug fix ... "	
	<COND (<0? .MIGS-INIT-COUNT> "OK")
		  (T <PRINC "CHANGING TERMINAL TYPE AFTER MIGS HAS STARTED">
		  	 <TERPRI> <PRINC "DOSN'T WORK YET"> <TERPRI>
		  	 <RETURN ,NULL .MPKACT> ) >
	<COND (<0? .MIGS-INIT-COUNT> "OK")
	      ("ELSE" <L-UNUSE "MIGSMN"> <L-UNUSE "DISPLA">
		      <SET A ,TRMTYP!-MIGS >
		      <COND (<=? .A !\T> <L-UNUSE "MTPRIM">)
			    (<=? .A !\A> <L-UNUSE "MAPRIM">)
			    (<=? .A !\G> <L-UNUSE "MGPRIM">)
			    (<=? .A !\P> <L-UNUSE "MTPRIM">)
			    (<=? .A !\V> <L-UNUSE "MVPRIM">) >)>
	<SET MIGS-INIT-COUNT <+ 1 .MIGS-INIT-COUNT>>
	<RESET .INCHAN>
	<SET REDEFINE T>
	<GC-MON <>>
	<PRINC 
"
Muddle Interactive Graphics System
Enter terminal code or 'H' (help):">
	<PRINC <REPEAT (A)
		       <SET A <TYI .INCHAN>>
		       <SETG TRMTYP!-MIGS .A>
		       <TERPRI>
		       <COND (<==? .A !\T>
			      <SETG PHYCEN!-MIGS (512 512)>
			      <USE "MTPRIM">
			      <RETURN " TEKTRONIX PRIMITIVES LOADED">)
			     (<==? .A !\A>
			      <SETG PHYCEN!-MIGS (0 0)>
			      <USE "MAPRIM">
			      <RETURN " ARDS PRIMITIVES LOADED">)
			     (<==? .A !\I>
			      <SETG PHYCEN!-MIGS (0 0)>
			      <USE "MAPRIM">
			      <RETURN " IMLAC PRIMITIVES LOADED">)
			     (<==? .A !\G>
			      <USE "MGPRIM">
			      <RETURN " GT42 PRIMITIVES LOADED">)
			     (<==? .A !\P>
			      <USE "HPTEK">
			      <EVAL <PARSE "<HPTON>">>
			      <SETG TRMTYP!-MIGS <SET A !\T>>
			      <USE "MTPRIM">
			      <RETURN "HP-TEK MODE. TEK PRIMITIVES LOADED">)
			     (<==? .A !\V> 
			      <USE "MVPRIM">
			      <RETURN "VT125 PRIMITIVES LOADED">)
			     (<==? .A !\H>
			      <PRINC "ENTER YOUR TERMINAL TYPE THEN TYPE ">
			      <PRINC "<MIGSHELP>$ FOR HELP. $ IS <-ESC->">
			      <TERPRI>
			      <PRINC "TERMINALS ARE : A FOR ARDS & IMLACS">
			      <PRINC ", G FOR GT42'S, T FOR TEKTRONIX">
			      <TERPRI>
			      <PRINC "P IS FOR HP-2648'S ">
			      <TERPRI>
			      <PRINC "V IS FOR VT125s">
			      <TERPRI>
			      <PRINC "IDENTIFY YOUR TERMINAL :">)
			     (ELSE <PRINC "INVALID ENTRY! TRY AGAIN :">)>>>
	<USE "MIGSMN">
	<TERPRI>
       <USE "DISPLA">
       <TERPRI>
	<PRINC "
MIGS LOADED
">
	<SET REDEFINE <>>
	,NULL>

<DEFINE MIGSHELP ()
	<USE "MGHELP">
	<APPLY ,<PARSE "HELP1!-MGHELP!-PACKAGE!- "> 
	       .<PARSE "HLIST!-MGHELP!-PACKAGE!- "> 
	       .<PARSE "INFOT!-MGHELP!-PACKAGE!- ">>
	<L-UNUSE "MGHELP">>

<DEFINE FROB() <PRINC "DOING"><TERPRI><FLOAD "<MDLLIB>TEST.GRAPHS"><TERPRI> "DONE">

<DEFINE WHO()
	<FILECOPY <OPEN "READ" "TTY:.FILE. (DIR)"> .OUTCHAN>
	<TERPRI>
	,NULL>
<DEFINE DIR ("OPTIONAL" DIR0 "AUX" (D <SNAME>))
	<COND (<ASSIGNED? DIR0> <SET D <PNAME .DIR0>>)>
	<FILECOPY <OPEN "READ" <STRING .D ";.FILE. (DIR)">>
		  .OUTCHAN>
<TERPRI>
,NULL>

<ENDBLOCK>
<ENDPACKAGE>
<COND (<GASSIGNED? TRMTYP!-MIGS!-SMCELLS!-PACKAGE> "GOOD")
      ("ELSE" <MIGSINIT!-MIGSPK!-PACKAGE>)>
<USE "MIGSPK">
<USE "SMCELLS">
<USE "MIGSMN">
<USE "DISPLA">
<BLOCK (<GET MIGS!-SMCELLS!-PACKAGE OBLIST> <ROOT>)>
<COND (<=? ,TRMTYP !\T> <USE "MTPRIM">)
      (<=? ,TRMTYP !\A> <USE "MAPRIM">)
      (<=? ,TRMTYP !\G> <USE "MGPRIM">)
      (<=? ,TRMTYP !\P> <USE "MTPRIM">)
      (<=? ,TRMTYP !\V> <USE "MVPRIM">
       <SETCLIP!-DISPLA!-PACKAGE!- 0 760 0 470>) >
<ENDBLOCK>

<PACKAGE "ERROR-HANDLER">
<ENTRY ERROR-CHECKER>

<DEFINE ERROR-CHECKER (
		      "TUPLE" ETUP
		      "AUX" (ETYP <2 .ETUP>)
			    (EFUN
			     <COND (<G? <LENGTH .ETUP> 2> <3 .ETUP>) (T T)>))
	<COND (<AND <==? .EFUN EVAL>
		    <==? .ETYP NON-APPLICABLE-TYPE!-ERRORS>>
	       <INT-LEVEL 0>
	       <FORM-ERROR .ETUP>)
	      (T <DISMISS T>)>>

<DEFINE FORM-ERROR (ETUP) 
	<ERRET <EVAL <1 <1 <ARGS <FRAME <1 .ETUP>>>>>>
	       <FRAME <1 .ETUP>>>>

<ON "ERROR" ,ERROR-CHECKER 1>

<ENDPACKAGE>
