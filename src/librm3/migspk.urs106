



<PACKAGE "MIGSPK">
<ENTRY MIGS: MIGSINIT WHO DIR MIGSHELP FROB>
<SET REDEFINE T>
<USE "SMCELLS">
<BLOCK (!.OBLIST <GET MIGS OBLIST>)>
<SET TENEX <>>
<SET MIGS-INIT-COUNT 0>

<DEFINE MIGS: ("OPTIONAL" STR) 
	#DECL((STR) CHARACTER)
	<COND (<ASSIGNED? STR> <SET THINGS!-MIGS .STR>)
	      (T <UNASSIGN THINGS!-MIGS>)>
	,NULL>

<DEFINE MIGSINIT ()
	<COND (<0? .MIGS-INIT-COUNT> "OK")
	      ("ELSE" <L-UNUSE "MIGSMN"> <L-UNUSE "DISPLA">
		      <SET A ,TRMTYP!-MIGS >
		      <COND (<=? .A !"T> <L-UNUSE "MTPRIM">)
			    (<=? .A !"A> <L-UNUSE "MAPRIM">)
			    (<=? .A !"G> <L-UNUSE "MGPRIM">)>)>
	<SET MIGS-INIT-COUNT <+ 1 .MIGS-INIT-COUNT>>
	<RESET .INCHAN>
	<SET REDEFINE T>
	<GC-MON <>>
	<PRINC 
"
Muddle Interactive Graphics System
Enter terminal code or 'H' (help):">
	<PRINC <REPEAT (A)
		       <COND (<ASSIGNED? THINGS!-MIGS>
			      <SET A .THINGS!-MIGS>)
			     (<==? <ANDB *77* <11 .INCHAN>> #WORD *2*> 
			      <SET A !"A >)
			     (T <SET A <TYI .INCHAN>>)>
		       <SETG TRMTYP!-MIGS .A>
		       <TERPRI>
		       <COND (<==? .A !"T>
			      <SETG PHYCEN!-MIGS (512 512)>
			      <USE "MTPRIM">
			      <RETURN " TEKTRONIX PRIMITIVES LOADED">)
			     (<==? .A !"A>
			      <SETG PHYCEN!-MIGS (0 0)>
			      <USE "MAPRIM">
			      <RETURN " ARDS PRIMITIVES LOADED">)
			     (<==? .A !"I>
			      <SETG PHYCEN!-MIGS (0 0)>
			      <USE "MAPRIM">
			      <RETURN " IMLAC PRIMITIVES LOADED">)
			     (<==? .A !"G>
			      <USE "MGPRIM">
			      <RETURN " GT42 PRIMITIVES LOADED">)
			     (<==? .A !"H>
			      <MIGSHELP>
			      <PRINC "IDENTIFY YOUR TERMINAL :">)
			     (ELSE <PRINC "INVALID ENTRY! TRY AGAIN :">)>>>
	<USE "MIGSMN">
	<TERPRI>
       <USE "DISPLA">
       <TERPRI>
	<PRINC "
MIGS LOADED
">
	<SET REDEFINE <>>
	<UNASSIGN THINGS!-MIGS>
	,NULL>

<DEFINE MIGSHELP ()
	<USE "MGHELP">
	<APPLY ,<PARSE "HELP1!-MGHELP!-PACKAGE!- "> 
	       .<PARSE "HLIST!-MGHELP!-PACKAGE!- "> 
	       .<PARSE "INFOT!-MGHELP!-PACKAGE!- ">>
	<L-UNUSE "MGHELP">>

<DEFINE FROB() <PRINC "DOING"><TERPRI><EVAL <PARSE "<FLOAD \"RRS;TEST GRAPHS\">">>
		<TERPRI> "DONE">

<DEFINE WHO()
	<FILECOPY <OPEN "READ" "TTY:.FILE. (DIR)"> .OUTCHAN>
	<TERPRI>
	,NULL>
<DEFINE DIR ("OPTIONAL" DIR0 "AUX" (D <SNAME>))
	<COND (<ASSIGNED? DIR0> <SET D <PNAME .DIR0>>)>
	<FILECOPY <OPEN "READ" <STRING .D ";.FILE. (DIR)">>
		  .OUTCHAN>
<TERPRI>
,NULL>

<ENDBLOCK>
<ENDPACKAGE>
<COND (<GASSIGNED? TRMTYP!-MIGS!-SMCELLS!-PACKAGE> "GOOD")
      ("ELSE" <MIGSINIT!-MIGSPK!-PACKAGE>)>
<USE "MIGSPK">
<USE "SMCELLS">
<USE "MIGSMN">
<USE "DISPLA">
<BLOCK (<GET MIGS!-SMCELLS!-PACKAGE OBLIST> <ROOT>)>
<COND (<=? ,TRMTYP !"T> <USE "MTPRIM">)
      (<=? ,TRMTYP !"A> <USE "MAPRIM">)
      (<=? ,TRMTYP !"G> <USE "MGPRIM">)>
<ENDBLOCK>
