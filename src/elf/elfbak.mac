	TITLE ELFRBK	RECEIVE BACKUP

	PRINTX	FILE PRSV REQUIRED

	MOVEI	1,400000	;ENABLE MYSELF
	RPCAP
	MOVE	3,2
	EPCAP
	HRLZI	1,100001	;SET UP SOCKET TO LISTEN FOR STUFF
	HRROI	2,[ASCIZ/NET:3000#./]
	GTJFN
	JSR	ERROR
	MOVEM	1,SKTJFN
	MOVE	2,[XWD 100000,200000]	;OPEN 8-BIT BYTES
	OPENF
	JSR	ERROR
	MOVE	1,SKTJFN
	BIN			;WAIT FOR SOMETHING TO HAPPEN
	PUSH	PDP,2		;SAVE THE BYTE
	MOVE	1,SKTJFN	;YEP, SEE IF IT IS ONE OF OURS
	GDSTS
	CAIE	3,26		;ISI-SPEECH11?
	CAIN	3,326		;ISI-TSP?
	JRST	.+2		;YES TO ONE OF THESE
	JRST	START		;NO
	HRLZI	1,400001	;YEP, OPEN A FILE TO RECEIVE IT
	HRROI	2,[ASCIZ/<VOICE>CARTRIDGE.BAK/]
	GTJFN
	JSR	ERROR		;STRANGE ERROR
	MOVEM	1,FILJFN
	MOVE	2,[XWD 110000,100000]	;OPEN 9-BIT BYTES (EASY TO EXAMINE)
	OPENF
	JSR	ERROR		;CANT GET THERE FROM HERE
	POP	PDP,2		;RETRIEVE THE BYTE
	JRST	LOOP2		;POP IT IN
LOOP:
	MOVE	1,SKTJFN	;GET THAT BYTE
	BIN
	JUMPE	2,CHKJFN	;IS IT NULL?
LOOP2:
	MOVE	1,FILJFN	;NO, STUFF THAT BYTE
	BOUT
	JRST	LOOP		;GET THE NEXT BYTE
CHKJFN:
	MOVE	1,SKTJFN	;NULL BYTE, SEE IF CONNECTION OPEN
	GTSTS
	TLNN	2,1000
	JRST	[SETZ	2,2	;STILL OPEN, STUFF THE BYTE
		JRST	LOOP2]
	MOVE	1,FILJFN	;CLOSED - TRANSFER DONE
	CLOSF
	JSR	ERROR		;CURIOUS
	JRST	START
ERROR:
	JFCL			;HOLDS ADDRESS
	HRLZI	1,400001
	HRROI	2,[ASCIZ/<SYSTEM>ELFBAK.ERRORS;1/]
	GTJFN
	JRST	ERROR1		;FAIL, FORGET IT
	MOVEM	1,ERRJFN
	MOVE	2,[XWD 70000,020000]
	OPENF
	JRST	ERROR1
	GTAD
	JUMPLE	1,ERROR2	;BE SURE WE KNOW WHAT TIME IT IS
	MOVE	2,1
	MOVE	1,ERRJFN
	SETZ	3,3
	ODTIM
ERROR2:
	MOVE	1,ERRJFN
	HRROI	2,[ASCIZ/ AT LOC /]
	SETZB	3,4
	SOUT
	HRRZ	2,ERROR
	MOVE	3,[XWD 7,10]
	NOUT
	JFCL
	MOVE	1,ERRJFN
	MOVE	2,[XWD 400000,-1]
	SETZ	3,3
	ERSTR
	JRST	[HRROI	2,[ASCIZ/UNKNOWN ERROR
/]
		JRST	.+2]
	JRST	[HRROI	2,[ASCIZ/ERROR IN ERROR PRINT ROUTINE
/]
		JRST	.+1]
	SETZB	3,4
	SOUT
	MOVE	1,ERRJFN
	CLOSF
	JFCL
ERROR1:
	MOVE	1,[^D300000]	;GO AWAY TO LET ERROR CLEAR ITSELF
	DISMS
	JRST	START		;DONE

	LIT

SKTJFN:	0
FILJFN:	0
ERRJFN:	0
NAMSTG:	BLOCK 10
LPTNAM:	BLOCK 10

	END	START
