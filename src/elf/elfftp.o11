	.TITLE	NETFTP -- SCRL NETWORK FTP

;			THIS MODULE CONTAINS A PROCESS
;			FOR ACCEPTING FTP COMMANDS

;			IT IS A TEMPORARY MECHANISM
;			FOR RECEIVING PRINT FILES
;			FROM THE NETWORK

;			28-AUG-73

	.MCALL	$N.LSN,$N.CON,$N.CNT	;GET NETWORK MACRO CALLS
	.MCALL	$N.RCV,$N.CLS
	.MCALL	$DFCCT,$CNFIG,$DFREG,$DISCC
	.MCALL	$WAIT,$STIME

	.GLOBL	$FTP0,$DEAD		;SCRL FTP SOCKET (LISTENING)
	.GLOBL	$LPLST

	$CNFIG
	$DFCCT			;DEFINE CCT FORMAT
	$DFREG			;DEFINE REGS
	$DISCC			;DEFINE DISPATCHER CONSTS



$FTP0:	TSTB	$DEAD		;ARE WE DEAD?
	BNE	FTW00		;WAIT UP. IF SO.
	MOV	#FTPCCT,R0	;GET CCT ADDRX
	$N.LSN			;ISSUE LISTEN
FTH1:	$WAIT			;WAIT FOR INCOMING RFC
	MOV	#FTPCCT,R0	;CCT ADDRX TO R0
	TSTB	CCTCC(R0)	;TEST COMPLETE
	BPL	FTH1	;WAIT
	BITB	#37,CCTCC(R0)		;WAIT
	BNE	$FTP0		;WAIT ON ERROR
	$N.CON			;ACCEPT RFC, ESTABLISH CONN.
FTST:	MOV	#FTPCCT,R0		;CCT ADDRX AGAIN
	TSTB	CCTCC(R0)	;SEE IF COMPLETE CONN.
	BMI	FTGO		;OK, ITS OPEN
	$WAIT
	BR	FTST		;WATE AND RESUME
FTGO:	MOV	#FTBUF,R4	;GET INPUT BUFFER
	MOV	#FTBSIZ*10,R3	;LENGTH IN BITS
	$N.RCV			;RECEIVE DATA
FTH2:	$WAIT			;WAIT FOR DATA ARRIVED
	MOV	#FTPCCT,R0		;ADDRX OF CCT TO R0
	TSTB	CCTCC(R0)
	BPL	FTH2
	BITB	#37,CCTCC(R0)	;SEE IF EROR (CLOSED)
	BNE	FTPAB		;IF SO, CLOSE CONN.
	$N.CNT			;GET COUNT (BITS)
	ASR	R3
	ASR	R3
	ASR	R3		;GET BYTE COUNT
	MOV	#FTBUF,R0	;BUFFER ADDRESS
	MOV	#FTBSIZ,R1	;BUFFER SIZE, BYTES
	SUB	R3,R1		;GET NR GOTTEN.
	JSR	PC,$LPLST	;LIST LINE
	MOV	#FTPCCT,R0	;CCT ADDR
	BR	FTGO		;BACK TO RECEIVE MORE
FTPAB:	MOV	#FTPCCT,R0	;GET CCT
	$N.CLS			;CLOSE CONN.
FNW0:	$WAIT			;WAIT TILL COMPLETE
	MOV	#FTPCCT,R0	;CCT ADDRESS
	TSTB	CCTCC(R0)		;COMPLETE?
	BPL	FNW0		;WAIT
	JMP	$FTP0		;LISTEN AGAIN.
FTW00:	$STIME	10000.		;WAIT 10 SECS. IF $DEAD
FTW01:	$WAIT
	BIT	#TIM,-(R4)	;TIMER?
	BEQ	FTW01		;WAIT ON TIMER
	JMP	$FTP0		;BACK TO FTP

FTPCCT:	.WORD	0		;HOST NR FILLED IN BY CALLER
	.WORD	0		;B.S. FILLED BY CALLER
	.WORD	10		;LOCAL SOCKET 8.
	.WORD	0,0		;FGN SCKT

FTBUF:	.BLKB	140.
FTBSIZ = .-FTBUF		;BUFFER FOR INPUT LINE
	.ENDî
