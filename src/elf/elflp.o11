	.TITLE	NETLP -- LINE PRINTER DRIVER
	.MCALL	$CNFIG,$DFREG,$POST,$WAIT,$DISCC,$DFDCT
	.MCALL	$PUSH,$POP

	.GLOBL	$LPLST
	.GLOBL	$SVST,$IEXIT,$APCT
	$DFDCT
	$CNFIG
	$DFREG
	$DISCC

LP:	.WORD	0		;SAVE AREA FOR DCT ADDR
LP.IV = 304
LP.STV = 306
PRSIZE=80.		; NUMBER OF COLUMNS FOR THIS PRINTER
LP.FRM:	.BYTE	14,0		; USED FOR OPEN & CLOSE
LP.LIN:	.WORD	0
BTCT:	.WORD	0		; INTERNAL COUNT
BUFAD:	.WORD	0		; BUFFER POINTER
LP.TAB:	.WORD	0		; TABULATION COUNT

;			NETSYS LP DRIVER
;			ASSUMES R0 = ADDR, R1 = CNT


$LPLST:	MOV	R0,LPDCT+DCTA	;INIT BUF ADDR
	MOV	R1,LPDCT+DCTCNT	;AND BYTE COUNT
	MOV	$APCT,LPDCT+DCTPCT	;SAVE PCT ADDR
	MOV	#LPDCT,R0	;DCT ADDRESS TO R0
	JSR	PC,LP.TF	;INIT TRANSFER
LPWT:	TST	LPDCT+DCTCNT	;COMPLETE?
	BEQ	LPRTS		;BRCH IF SO
	$PUSH	R5		;SAVE CALLER'S R5
	$WAIT			;WAIT FOR POSTED
	$POP	R5
	BR	LPWT
LPRTS:	RTS	PC		;RETURN
LPDCT:	.WORD	0,0,0,0,0,0,0,0

LP.TF:	MOV	R0,LP		; PICK UP DDB
	MOV	(SP),-(SP)	;FAKE AN INTERRUPT
	MOV	@#PS,2(SP)	;WITH PROC STATUS
	MOV	#LP.IN,@#LP.IV
	MOV	#340,@#LP.STV
	MOV	#340,@#PS
	MOV	DCTA(R0),BUFAD
	MOV	DCTCNT(R0),BTCT
LP.IN:	BIC	#100,@#LP.CSR	;DISABLE INTERRUPTS
	TST	@#LP.CSR	; CHECK FOR ERROR
	BMI	LP.ERR		; YES
	MOV	R1,-(SP)	;QUICK SAVE
	MOV	R2,-(SP)		;  REGS.
	MOV	BTCT,R1		; GET CURRENT BYTE COUNT
	BEQ	LP.DNE		; NO MORE
	MOV	BUFAD,R2	; GET CURRENT BUF LOC.
LP.LOP:	TSTB	@#LP.CSR		; IS PRINTER GOING
	BPL	LP.STI		;  YES
	.IF NE	0	;========================
	CMPB	(R2),#11	; TAB ?
	BEQ	LP.PTB
	CMPB	(R2),#15	; CARRIAGE RETURN ...
	BEQ	LP.RSC		; ... RESET COUNTS
	TSTB	@R2		; IGNORE NULL ...
	BEQ	LP.DNP
	CMPB	@R2,#13		; VERTICAL TAB ...
	BEQ	LP.DNP
	CMPB	@R2,#177	; ... & RUBOUT
	BEQ	LP.DNP
	CMPB	@R2,#12		; IF LINE TERMINATOR ...
	BEQ	LP.RSC
	CMPB	@R2,#14
	BNE	LP.CLO
LP.RSC:	MOV	#9.,LP.TAB	; ... RESET COUNTS
	MOV	#PRSIZE+1,LP.LIN
	BR	LP.TBF		;... & OMIT NEXT
LP.CLO:	TST	LP.LIN		; OTHERWISE CHECK LINE OFLO
	BEQ	LP.DNP		; IGNORE CHAR IF FULL
	.ENDC	;================
LP.TBF:	MOVB	(R2)+,@#LP.BUF	; PRINT CHAR
	DEC	LP.LIN		; COUNT CHARS. DESPATCHED
	DEC	LP.TAB		; UPDATE TAB COUNT
	BNE	LP.TRT
	MOV	#8.,LP.TAB	; RESET TAB COUNT
LP.TRT:	DEC	R1		; UPDATE COUNT
	BNE	LP.LOP	; MORE
	TSTB	@#LP.CSR	; PRINTER GOING
	BMI	LP.DNE		; NO, SO NO INTERRUPT
LP.STI:	MOV	R1,BTCT		; SET UP FOR NEXT TIME
	MOV	R2,BUFAD
LP.TWC:	BIS	#100,@#LP.CSR	; ENABLE INTERRUPT
	MOV	(SP)+,R2	; RESTORE REGS
	MOV	(SP)+,R1
	RTI			; THROUGH INTERRUPT
LP.ERR:
LP.DNE:	MOV	(SP)+,R2	; RESTORE REGS
	MOV	(SP)+,R1
	JSR	R5,$SVST	;SAVE STATUS
	CLR	@#LP.CSR	; DISABLE INTERRUPT
	MOV	LP,R0
	CLR	DCTCNT(R0)	;SET WC=0
	$POST	DCTPCT(R0),#RDY+IO
	JMP	$IEXIT

	.IF NE	0	; -=====================
LP.PTB:	TST	LP.TAB		; AT NEW TAB ALREADY?
	BEQ	LP.EVN
LP.MTB:	MOVB	#40,@#LP.BUF	; SPACE FOR TABS
	TST	LP.LIN		; LINE OVERFLOW
	BEQ	LP.DNP		;  YES, IGNORE REST
	DEC	LP.LIN		; INCLUDE IN LINE COUNT
	DEC	LP.TAB		;  DONE ?
	BNE	LP.LOP
LP.EVN:	MOV	#8.,LP.TAB	; RESET
LP.DNP:	INC	R2
	BR	LP.TRT
	.ENDC	;	============================
LP.CSR=176500
LP.BUF=176506
	.END

