;-*-Midas-*-

	Title :YOW

A=1
B=2
C=3
D=4
E=5
T=10
TT=11
P=17

PDLen==40
BufLen==40

TTYo==1
CHAOSi==2
CHAOSo==3

Call=<PUSHJ P,>
Return=<POPJ P,>

Define SYSCAL op,args
	.Call [Setz ? Sixbit /op/ ? args ((Setz))]
Termin

PDL:	-PDLen,,.
	Block PDLen

Buffer:	Block BufLen

Debug:	0

HostNo:	16310			;Kludge built-in host# for MIT-CCC


$$CHAOS==1
$$ANALYZE==1
$$ERRHAN==1
$$SIMPLE==1
.INSRT SYSENG;NETWRK

Begin:	Move P,PDL
	Syscal OPEN,[%Clbit,,.uao
		     %Climm,,TTYo
		     [Sixbit /TTY/]]
	  .Lose %lsFile
	Movei A,CHAOSi
	Move B,HostNo
	Movei C,[Asciz "YOW"]
	Move D,[-BufLen,,Buffer]
	Call NETWRK"CHASMP
	  Jrst Die
	  Jfcl
	Move B,[440700,,Buffer]
Loop:	Ildb A,B
	Jumpe A,Die
	.Iot TTYo,A
	Jrst Loop

Die:	.Logout 1,

PutChr:	.Iot TTYo,T
	Return

	End Begin
