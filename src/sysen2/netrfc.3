	TITLE	NETRFC BOOTSTRAPPER
.MLLIT==1
A=1
B=2
Q=16

BEG:	MOVE	A,[SQUOZE 0,IMPBPQ]
	.EVAL	A,
	 JRST	ENDQ
	MOVE	Q,A
NEXTQ:	MOVSS	Q
	HRRI	Q,Q
	.GETLOC	Q,
	JUMPL	Q,ENDQ		;-1 IS END OF LIST
	MOVSI	A,1(Q)
	HRRI	A,A
	.GETLOC	A,		;GET SLOT 1, LCL SOCK
	CAIL	A,1000		;SKIP IF SHOULD SIGNAL
	 JRST	NEXTQ		;JUMP TO TRY ANOTHER ONE
	DPB	A,[000300,,LOGNAM] ;STASH IN 'RFC000
	LSH	A,-3
	DPB	A,[060300,,LOGNAM]
	LSH	A,-3
	DPB	A,[140300,,LOGNAM]
	.CALL	[SETZ ? 'DEMSIG ? LOGNAM ? SETZ [-1]]
	 JRST	NORFC		;FAIL-LEAVE TRAIL
	JRST	ENDQ

NORFC:	MOVSI	A,3(Q)		;SLOT 3, HOST CALLING
	HRRI	A,A
	.GETLOC	A,
	LSH	A,-8		;GET HOST #
	DPB	A,[000300,,TRMNAM]
	LSH	A,-3
	DPB	A,[060300,,TRMNAM]
	LSH	A,-3
	DPB	A,[140300,,TRMNAM]
	.CALL	[SETZ ? 'LOGIN_6 ? LOGNAM ? SETZ TRMNAM]
	 JFCL			;DONT SVEAT IT
ENDQ:	.LOGOUT
	.VALUE

LOGNAM:	'RFC000
TRMNAM:	'HST000

	END	BEG
