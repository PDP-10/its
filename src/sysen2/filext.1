;Reconstructed from SYS2; TS TVEDIT dated 1977-08-27.

filext:	push p,a
	push p,b
	push p,e
	setz e,
filoop:	pushj p,sixin
	cain c,":
	 jrst fildev
	cain c,";
	 jrst fildir
	skipe n
	 movem n,nam1(e)
	movei e,1
	cain c," 
	 jrst filoop
	pop p,e
	pop p,b
	pop p,a
	popj p,

fildev:	skipe n
	 movem n,dev
	jrst filoop

fildir:	skipe n
	 movem n,snam
	jrst filoop

sixin:	move a,[440600,,n]
	setz n,
sixin1:	pushj p,getlin
	cain c,^M
	 jrst sixin2+1
	caige c,40
	 jrst sixin1
	move b,[440700,,sixlst]
	push p,c
sixin4:	ildb c,b
	jumpe c,sixin3
	came c,(p)
	 jrst sixin4
sixin2:	pop p,c
	popj p,
sixin3:	pop p,c
	cail c,"a
	 caile c,"z
	  addi c,40
	tlne a,770000
	 idpb c,a
	jrst sixin1

sixlst:	asciz \ /,;:î_\

getlin:	skipl linflg'
	jrst readln
	ildb c,ptr
	cain c,^M
	setzm linflg
	popj p,
readln:	push p,a
	push p,b
	move a,[440700,,linbuf]
	movem a,ptr
read2:	.iot tti,c
	cain c,177
	 jrst rubout
	cain c,^D
	 jrst cntrld
	idpb c,ptr
	cain c,^M
	 jrst carg
	jrst read2

carg:	setom linflg
	move a,[440700,,linbuf]
	movem a,ptr
	pop p,b
	pop p,a
	jrst getlin

cntrld:	movei c,[asciz / XXX? /]
	pushj p,outstr
	jrst readln+1

rubout:	ldb c,ptr
	ldb a,[360600,,ptr]
	addi a,7
	caig a,44
	jrst rubot2
	movei a,1
	dpb a,[360600,,ptr]
	sos ptr
	hrrz b,ptr
	caige b,linbuf
	 jrst beglin
	ldb c,ptr
	movei a,10
rubot2:	dpb a,[360600,,ptr]
	skipe prtty'
	 jrst rubot3
	.call [ setz
		sixbit /cnsget/
		1000,,a
		2000,,a
		2000,,a
		setzm a ]
	 .value
	setom prtty
	skipe a
	 movem a,prtty
rubot3:	skipl prtty
	 jrst rubot4
	.iot tto,c
	jrst read2
rubot4:	.iot tto,[^H]
	pushj p,ttcntp
	.iot tto,["K]
	jrst read2

beglin:	movei c,7
	move a,[440700,,linbuf]
	movem a,ptr
	jrst rubot2+1

linbuf:	block 20.
ptr:	0
