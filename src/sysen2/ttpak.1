;Reconstructed from SYS2; TS TVEDIT dated 1977-08-27.

ttcntp:	movei c,^P
	.iot tto,c
	popj p,

tthome:	pushj p,ttcntp
	movei c,"T
ttout:	.iot tto,c
	popj p,

ttspos:	push p,b
	hrre b,a
	hlre a,a
	pushj p,ttcntp
	movei c,"H
	.iot tto,c
	addi a,10
	.iot tto,a
	pushj p,ttcntp
	movei c,"V
	.iot tto,c
	addi b,10
	.iot tto,b
	pop p,b
	popj p,

ttclea:	pushj p,ttcntp
	movei c,"C
	jrst ttout

ttcleo:	pushj p,ttcntp
	movei c,"E
	jrst ttout

ttclin:	pushj p,ttcntp
	movei c,"L
	jrst ttout

ttcchr:	push p,a
	pushj p,ttcntp
	movei c,"K
	.iot tto,c
	pushj p,ttcntp
	movei c,"F
	.iot tto,c
	sojg a,ttcchr+1
	pop p,a
ttchr2:	pushj p,ttcntp
	movei c,"B
	.iot tto,c
	sojg a,ttchr2
	popj p,

tthpos:	pushj p,ttcntp
	movei c,"H
	.iot tto,c
	addi a,10
	.iot tto,a
	popj p,

ttvpos:	pushj p,ttcntp
	movei c,"V
	.iot tto,c
	addi a,10
	.iot tto,a
	popj p,

ttup:	pushj p,ttcntp
	movei c,"U
	jrst ttout

tthomd:	pushj p,ttcntp
	movei c,"Z
	jrst ttout

outstr:	push p,b
	hrli c,440700
outst1:	ildb b,c
	jumpe b,outst2
	.iot tto,b
	jrst outst1
outst2:	pop p,b
	popj p,

decout:	idivi n,10.
	push p,n2
	skipe n
	 pushj p,decout
	pop p,c
	addi c,"0
	jrst ttout

ttscrs:	pushj p,ttcntp
	movei c,"S
	jrst ttout

ttrcrs:	pushj p,ttcntp
	movei c,"R
	jrst ttout
