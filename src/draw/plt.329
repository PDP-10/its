;<DRAW>PLT.FAI.47, 15-NOV-75 18:07:25, EDIT BY HELLIWELL
VERSION(PLT,6)

COMMENT 
PLTVER 15	;HAS CHARACTER SIZE AS <WIDTH IN PTS>*8
		; (USED TO BE 1,2,...)

;SETUP

MPC,<
SIGPLT:	SETZM FILNAM
	TLO WFLAG
	JRST SIGPL1
>;MPC

EWPLOT:	MOVSI T,EXTPLT
	PUSHJ P,SETLST		;WILL SKIP IF THERE IS SAVED NAME
	JRST PLOT
NOSTAN,<
MPC,<	MOVE T,[POINT 6,FILNAM]
EWPLT1:	ILDB TT,T
	JUMPE TT,EWPLT2
	TLNE T,770000
	JRST EWPLT1
EWPLT2:	TLNN SID,FRONT
	SKIPA TT,['S']		;BACK IS SOLDER
	MOVEI TT,'C'		;FRONT IS COMPONENT
	DPB TT,T
>;MPC
>;NOSTAN
	CAIA
PLOT:	SETZM FILNAM
MPC,<	TLZ WFLAG		;CLEAR FLAG FOR SIG FILES
SIGPL1:	SETZM CPLANE
	TRNN M,FLIP		;DO WE WARN HIM?
	JUMPL SID,PLPLT1
	TRNE M,FLIP
	JUMPGE SID,PLPLT1
	OUTSTR[ASCIZ/TEXT IS INVALID UNLESS YOU PLOT THIS FLIPPED!
/]
	CAIA
PLPLOT:	SETZM FILNAM		;FLAG NEED TO ASK FILENAME
PLPLT1:	>;MPC
MD,<	MOVEI T,13		;1.0 (binary point 3)
	MOVEM T,CSCALE		;CSCALE/8 IS ADJUSTMENT FOR CHARACTER SIZE
				; (USED BY PBOX)
	SETOM CBOX		;NO DEFAULT BOX SIZE
	CAIA
DPLOT:	SETZM FILNAM
	MOVE C,STDBIG
	MOVE T,PLTPTX(C)
	IMUL T,CSCALE
	MOVEM T,LSCALX		;X,Y UNITS FOR DRAWING CONN BOX
	MOVE T,PLTPTY(C)
	IMUL T,CSCALE
	ASH T,-1
	MOVEM T,LSCALY
>;MD
	SKIPE FILNAM		;ALREADY GOT NAME?
	JRST GOTPNM
	TLNN M,DSKACT!MACACT
	OUTSTR [ASCIZ /PLOT /]
	MOVSI T,EXTPLT
MPC,<	TLNE WFLAG
	MOVSI T,EXTSIG
>;MPC
	PUSHJ P,SETNAM
	POPJ P,
GOTPNM:	ENTPPN
	INIT DAT,10
	SIXBIT /DSK/
	XWD IOHD,0
	JRST [	OUTSTR[ASCIZ/WHERE'S THE DISK!
/]
		POPJ P,]
	PUSHJ P,EXIST	;SEE IF IT ALREADY EXISTS
	POPJ P,		;DON'T OVERWRITE
	OUTSTR[ASCIZ/WRITING /]
	MOVEI A,FILNAM
	JSR FPRINT
	ENTER DAT,FILNAM
	JRST [	OUTSTR [ASCIZ/, ENTER FAILED.
/]
		POPJ P,]
	OUTSTR[ASCIZ/
/]
	MOVEI A,IOBUF
	EXCH A,.JBFF
	OUTBUF DAT,2
	MOVEM A,.JBFF
MPC,<	SETZM MXPADN	>
MD,<	MOVEI TTT,PLTVER	;PUT OUT PLOT VERSION #	>
MPC,<	MOVEI TTT,PCPVER	;PUT OUT PLOT VERSION #	>
	PUSHJ P,PUTWRD
;PUT OUT BODYS
MD,<	MOVE B,DBODPN
	JUMPE B,NOBOSS
PBLP:	TLNN M,BLOCS		;SHALL WE DO DIP NUMBER?
	 JRST NOLOC
	FETCH(T,B,BBIT)
	TRNN T,FIXLOC		;IS IT TRACKING DEF?
	 JRST DISLC3		;NO, NO CHECK
	FETCH(T,B,BTYP)
	FETCH(T,T,TBIT)
	TRNE T,XDISLOC		;SHOULD WE DISPLAY LOC?
	 JRST NOLOC		;NO
DISLC3:	FETCH(D,B,BLOC)
	JUMPE D,NOLOC		;SKIP IF NO LOC BLOCK
	FETCH(T,B,BXY)
	ADJUST(ADD,T,<ADDR(B,BLXY)>)	;OFFSET FOR NLNLN
	MOVEI TTT,-1+ADDR(B,BLO)
	MOVE C,STDBIG
	PUSHJ P,TXTCAL
	PUSHJ P,PUTWRD
	PUSHJ P,STDSIZ		;SMALLEST SIZE
	PUSHJ P,PLTBNM		;PLOT DIP NUMBER
NOLOC:	FETCH(D,B,BTYP)
	FETCH(F,B,BORI)
	FETCH(A,D,TLIN)
	JUMPE A,NOLINS		;ANY LINES IN DEF?
PLOPP1:	FETCH(T,A,QXY)		;GET X,Y OF LINE
	TRZE T,1		;VIS OR INVIS?
	JRST [PUSHJ P,PINVS	;INVISIBLE
		JRST .+2]
	PUSHJ P,PVIS		;VISIBLE
	HRRZ A,(A)		;GO TO NEXT LINE
	JUMPN A,PLOPP1
NOLINS:	TRNE M,NBTEXT
	 JRST NOTINS
	FETCH(A,B,BTXT)
	JUMPE A,PBTLP0
PBTLP1:	FETCH(T,A,TXBIT)
	TRNN T,TXBIND
	JRST PBTLP2
	PUSH P,A
	FETCH(A,A,TXIND)
	PUSHJ P,PBTLPX
	POP P,A
	JRST PBTLP3

PBTLP2:	PUSHJ P,PBTLPX
PBTLP3:	HRRZ A,(A)
	JUMPN A,PBTLP1
	JRST NOTINS

PBTLP0:	FETCH(A,B,BTYP)
	FETCH(A,A,TPROP)
	JUMPE A,NOTINS		;NONE?
PBTLP:	PUSHJ P,PBTLPX
	HRRZ A,(A)		;GET NEXT TEXT
	JUMPN A,PBTLP
NOTINS:	HRRZ B,(B)		;GET NEXT BODY
	JUMPN B,PBLP		;LOOP
	JRST NOBOSS

PBTLPX:	FETCH(T,A,TXVAL)
	FETCH(T,T,TSSIZ)	;SIZE (0 = INVIS)
	TLNN M,%IDENT
	 JUMPE T,CPOPJ
	FETCH(T,A,TXXY)
	TDZ T,[1,,1]
	PUSHJ P,ORIENT		;ORIENT IT
	ADJUST(ADD,T,<1(B)>)	;ADD BODY CENTER
	MOVEI TTT,-1+ADDR(A,TXOFF)	;PTR TO OFFSET BLOCK, DATA
	JRST PTXTS		;PLOT TEXT (GENERAL FORMAT)
;CALL POINT SUBRS, DO END STUFF
NOBOSS:	MOVE B,PONPNT	;GET ON-SCREEN POINTER
	PUSHJ P,SETBTS	;SET THE BITS UP
	TLNN M,DIAMONDS	;SHALL WE DO THE DIAMONDS?
	JRST NODIAMONDS
	MOVE B,PONPNT
	PUSHJ P,DODIAMONDS
NODIAMONDS:
	SETZM CIRC	;DON'T DO CLOSED LOOPS YET!
>;MD
	SKIPE B,PONPNT	;GET ON-SCREEN POINTER
	PUSHJ P,DOPNTS	;DO THE POINTS
MD,<	SETOM CIRC	;NOW GET THE CLOSED LOOPS
	SKIPE B,PONPNT
	PUSHJ P,DOPNTA
	AOSG TTT,CBOX		;GET BOX ID
	JRST NOTBOX		;NONE
	MOVE TTT,BTAB-1(TTT)	;GET LETTER FOR THIS BOX
	SUBI TTT,"A"-1		;AND NORMALIZE IT SO A=1
	TLZE TTT,400000		;VERT?
	TRO TTT,200000		;YES
	LSH TTT,1		;MAKE SURE LOW ORDER BIT ISN'T ON!
NOTBOX:	HRLI TTT,400001
	PUSHJ P,PUTWRD
FOR I IN(TAUTHOR,TIT1,TIT2)
<	MOVE T,I
	PUSHJ P,PSTR
>
	MOVE TTT,DRWDAT
	PUSHJ P,WORDOUT		;STUFF DATE INTO FILE AFTER AUTHOR TITLE STUFF
	SKIPN TTT,DRWNAM
	MOVE TTT,FILNAM
	PUSHJ P,WORDOUT
	MOVE TTT,DRWEXT
	PUSHJ P,WORDOUT
	MOVE TTT,DRWPPN
	PUSHJ P,WORDOUT
	PUSHJ P,INIPCH
	HLRZ A,CRDLOC
	PUSHJ P,SLTOUT		;MAKE STRING OF SLOT LOCATION
	PUSHJ P,FINPCZ
	MOVE T,TREV
	PUSHJ P,PSTR
	PUSHJ P,INIPCH
	SKIPE T,TMODULE
	PUSHJ P,PCHRS
	SKIPN T,TVARIABLE
	JRST NOPVAR
	PUTBYT "-"
	PUSHJ P,PCHRS
NOPVAR:	SKIPN T,TPREFIX
	JRST NOPFIX
	PUTBYT "-"
	PUSHJ P,PCHRS
NOPFIX:	PUSHJ P,FINPCZ
FOR I IN(TPROJ,TPAGE,TOF,TDCODE,SITE1,SITE2)
<	MOVE T,I
	PUSHJ P,PSTR
>
DEC,<	MOVE T,TNXTHI
	PUSHJ P,PSTR
FOR @$I IN(DRN,CHK,ENG)
<	SKIPN TTT,I$NAM
	JRST NOP$I
	PUSHJ P,WORDOUT
	MOVE TTT,I$NAM+1
	PUSHJ P,WORDOUT
	MOVE TTT,I$NAM+3
NOP$I:	PUSHJ P,WORDOUT
>
>;DEC
>;MD
MPC,<	HLRZ TTT,SID
	SKIPE CPLANE	;INNER PLANE?
	TRO TTT,200000	;YES
	HRLI TTT,400001
	PUSHJ P,PUTWRD
	TLNE WFLAG
	JRST SIGPL2
	TRNN M,FLIP		;FLIPPED?
	TDZA TTT,TTT
	MOVEI TTT,1		;FLIPPED BIT TO PLOT PROG
	PUSHJ P,PUTWRD
	MOVE TTT,CRDNAM
	PUSHJ P,WORDOUT
	HLLZ TTT,CRDEXT
	PUSHJ P,WORDOUT
	MOVE TTT,CRDPPN
	PUSHJ P,WORDOUT
	PUSHJ P,MXPADQ		;PRINT MAX PAD # SEEN
SIGPL2:
>;MPC
	RELEASE DAT,
	POPJ P,
;POINTS (D)
;B = POINT LIST

MD,<
DOPNTS:	FETCH(D,B,PBIT)
	TLNE M,CLOCS		;DOING LOCATIONS?
	TRNN D,CPIN		;AND CONNECTOR PIN!?
	 JRST CNOPIN		;NO
	FETCH(T,B,PXY)		;CPIN X,Y
	FETCH(E,B,PLOC)		;B-R-S,DIP-LOC OF CPIN
	FETCH(F,B,PTXT)
	JUMPE F,NTXCPN		;NO TEXT
	FETCH(F,F,TCSTR)
	FETCH(F,F,TSSIZ)	;GET TEXT SIZE FOR REFERENCE
NTXCPN:	MOVE TTT,E		;PLOC: B-R-S,,DIPLOC
	MOVE C,F
	ANDI C,377777
	PUSHJ P,TXTCAL		;SUM TEXCOF (T,TTT)
	PUSHJ P,PUTWRD		;WRITE IT
	TLZ TTT,1
	PUSH P,TTT
	PUSHJ P,VSIZES		;CHECKS FOR VERT BIT IN F
	PUSHJ P,PLTCNM		;PLOT CONN NUMBER
	TRNE M,NOCBOX		;DOING BOX?
	 JRST NPTCBX
	MOVE TTT,(P)
	PUSHJ P,PUTWRD			;START BOX
	MOVE A,B
	PUSHJ P,CONCAL			;CALC LENGTH OF CPIN (=T)
;FALLS THRU
;PLOT CONNECTOR BOX
	IMUL T,LSCALX	;SCALE IT
	TRNE F,400000	;VERT?
	 JRST VCONBX
	MOVSS TTT,T	;MAKE IT X PART
	ADD TTT,(P)	;GO RIGHT
	PUSHJ P,PUTLIN	;+X,0
	HRLZ TT,LSCALX
	ADD TTT,LSCALY
	ADD TTT,TT
	PUSHJ P,PUTLIN	;+X,+Y
	SUB TTT,TT
	ADD TTT,LSCALY
	PUSHJ P,PUTLIN	;-X,+Y
	SUB TTT,T
	PUSHJ P,PUTLIN	;-X,0
	SUB TTT,TT
	SUB TTT,LSCALY
	PUSHJ P,PUTLIN	;-X,-Y
	ADD TTT,TT
	SUB TTT,LSCALY
	PUSHJ P,PUTLIN	;+X,-Y
	JRST NPTCBX

VCONBX:	MOVE TTT,T	;GET IT HERE
	ADD TTT,(P)	;GO UP
	TLZ TTT,1
	PUSHJ P,PUTLIN	;0,+Y
	HRLZ TT,LSCALY
	SUB TTT,TT
	ADD TTT,LSCALX
	TLZ TTT,1
	PUSHJ P,PUTLIN	;-X,+Y
	SUB TTT,TT
	TLO TTT,1
	SUB TTT,LSCALX
	TLZ TTT,1
	PUSHJ P,PUTLIN	;-X,-Y
	TLO TTT,1
	SUB TTT,T
	TLZ TTT,1
	PUSHJ P,PUTLIN	;0,-Y
	ADD TTT,TT
	TLO TTT,1
	SUB TTT,LSCALX
	TLZ TTT,1
	PUSHJ P,PUTLIN	;+X,-Y
	ADD TTT,TT
	ADD TTT,LSCALX
	TLZ TTT,1
	PUSHJ P,PUTLIN	;+X,+Y
NPTCBX:	POP P,(P)
	JRST TSTXT

;CNOPIN
;B = POINT LIST
;D = FLAGS

CNOPIN:	TRNN D,ISPIN		;BODY PIN?
	 JRST TSTXT		;NO, CHECK FOR TEXT
	TLNN M,PLOCS		;DOING PINS?
	 JRST PCKPND		;NO, CHECK ID'S
	FETCH(TT,B,BPLOC)	;PINLOC BLOCK IN TYPE
	FETCH(T,TT,TPBIT)
	TRNE T,BUSSED
	 JRST TSTXT		;SKIP BUSS THRU PINS
	FETCH(A,B,BPPN)		;PIN NAME
	JUMPN A,PLTPNN
	TLNN M,PINIDS		;PRINT DEFAULT PIN NAMES?
	 JRST TSTXT
	FETCH(A,TT,TPNAM)	;DEFAULT PIN NAME FROM TYPE
PLTPNN:	MOVEM B,CURPIN		;SAVE POINT
	MOVEM A,PINNAM
	FETCH(C,B,BBODY)	;BODY OF POINT
	PUSH P,C		;BODY
	SETZM CHRCNT
	PUSH P,PUTCHR
	MOVE TTT,[AOS CHRCNT]
	MOVEM TTT,PUTCHR
	FETCH(B,C,BSOC)
	FETPAK(C,C)
	PUSHJ P,PINPLS		;PRINT AS SOCKET PIN (C=BPOINT)
	POP P,PUTCHR
	MOVN A,CHRCNT		;GET LENGTH OF PIN NAME
	MOVE TT,(P)		;BODY
	FETCH(TTT,TT,BORI)	;GET ORIENTATION
	MOVE T,CURPIN
	FETCH(T,T,BPLOC)	;GET PIN TYPE BLOCK POINTER
	MOVEI C,PLTPTX		;CHARS DESTINED FOR PLT
	PUSHJ P,PINCAL		;PINCAL(T=TYPIN, A=-<NCHARS-PIN-NAME>, TTT=BORI)
	MOVE B,CURPIN
	PUSHJ P,PNSPOT		;CALC PIN LOC, OUTPUT IT, SWITCH TO SIZE 1 ( OR 2)
	PUSHJ P,INIPCH
	POP P,C
	FETCH(B,C,BSOC)
	FETPAK(C,C)
	MOVE A,PINNAM
	PUSHJ P,PINPLS		;PRINT SOCKET PIN (C=BPOINT)
	PUSHJ P,FINPCH
	MOVE B,CURPIN
	JRST TSTXT

PCKPND:	TLNN M,PINIDS
	JRST TSTXT
	FETCH(T,B,BPLOC)
	FETCH(TTT,B,BBODY)
	FETCH(TTT,TTT,BORI)	;GET ORIENTATION
	PUSHJ P,PIDCAL
	PUSHJ P,PNSPOT
	PUSHJ P,PLTPND
TSTXT:	TRNE M,NPTEXT
	JRST NOPTXT
	FETCH(TTT,B,PTXT)	;GET TEXT
	JUMPE TTT,NOPTXT	;NONE?
	FETCH(T,B,PXY)
	PUSHJ P,PTXTS		;DO IT
NOPTXT:	FETCH(E,B,PBIT)		;GET BITS
	ANDI E,17		;CLEAR OTHERS
	PUSH P,B	
	XCT PPTB1(E)		;CALL THE ROUTINE OR NOT
	POP P,B
	HRRZ B,(B)		;GET NEXT ONE
	JUMPN B,DOPNTS
	POPJ P,

;PNSPOT
;CALC PIN LOC, OUTPUT IT, SWITCH TO SIZE 1 ( OR 2)
;A = X
;TT = Y (III FORMAT)
;B = POINT

PNSPOT:	IMUL A,CSCALE		;DO CHAR SCALE THING
	IMUL TT,CSCALE
	FETCH(TTT,B,PY)
	SUB TTT,YOFF
	SCALET (TTT)
	ADD TTT,TT	;Y PART
	TRZE TTT,1
	ADDI TTT,2
	FETCH(TT,B,PX)
	SUB TT,XOFF
	SCALET (TT)
	ADD TT,A
	HRL TTT,TT
	TLO TTT,1
	PUSHJ P,PUTWRD	;THIS WILL PUT THE PIN # IN THE RIGHT PLACE (I HOPE!)
	JRST STDSIZ

;LINE SUBR (D)

DOPNTA:	FETCH(E,B,PBIT)
	ANDI E,17		;JUST GOOD BITS
	PUSH P,B		;SAVE THIS GUY
	CAIE E,17		;ALL DONE?
	 PUSHJ P,DOPPIT		;NO, FINISH THE BUM
	POP P,B
	HRRZ B,(B)
	JUMPN B,DOPNTA
	POPJ P,

DOPPIT:	XCT PPTB2(E)		;GET POINTER TO NEXT (LINE) POINT IN F
	HRLZ C,PPTB3(E)		;TURN ON BIT FOR LINE WE ARE DOING
	IORM C,ADDR(B,PBIT)	;......
	FETCH(T,B,PXY)
	MOVEI TTT,0
	PUSHJ P,PUTXY		;DO THE LINE (BEGINING POINT)
PLPLOP:	HLRZ A,PPTB3(E)		;GET MAGIC BITS
	MOVE B,F		;GET POINTER TO NEXT POINT IN LINE
	FETCH(D,B,PBIT)
	IOR A,D			;TURN ON BIT FOR LINE WE ARE DOING
	STORE(A,B,PBIT)
	ANDI A,17		;GET BITS
	FETCH(T,B,PXY)
	MOVEI TTT,1
	PUSHJ P,PUTXY		;DRAW THE LINE
	HRLZ E,PPTB3(A)		;GET MAGIC BITS
	XCT PPTB5(A)		;TO SKIP OR NOT TO SKIP!
	 POPJ P,		;QUIT IF NO SKIP!
	IORM E,ADDR(B,PBIT)	;TURN ON BIT FOR NEXT SEGMENT
	MOVE E,A
	XCT PPTB2(E)		;GET POINTER TO NEXT POINT IN LINE
	JRST PLPLOP

SETBTA:	PUSHJ P,SETBTO
	HRRZ B,(B)
SETBTS:	JUMPN B,SETBTA
	POPJ P,

;SETBTO - SET MARK BITS FOR NEIGHBORS "DONE", I.E. NON-EX
;B = POINT
;RETURNS
;F = FLAG FWD
;	10=LEFT, 4=RIGHT, 2=DOWN, 1=UP
SETBTO:	PUSH P,A
	FETCHL(F,B,PBIT)
	TLZ F,17	;TURN OFF THE "SEGMENT DONE" BITS

DEFINE	FOO (LINK,BIT)
<	FETCH(A,B,LINK)
	SKIPN A
	 TLO F,BIT
>
	FOO(PNU,1)
	FOO(PND,2)
	FOO(PNR,4)
	FOO(PNL,10)
	STOREL(F,B,PBIT)
	POP P,A
	POPJ P,
;DIAMONDS, RANDOM NUMBERS (D)
DODIAMONDS:
	JUMPE B,CPOPJ
	FETCH(A,B,PBIT)
	ANDI A,17		;THESE ARE THE BITS
	XCT PPTB4(A)		;DISPATCH
	HRRZ B,(B)
	JRST DODIAMONDS

PUTTI3:	TLNN M,JUNC3		;DOING JUNCTIONS OF 3?
	POPJ P,			;NO
PUTTIT:	FETCH(T,B,PXY)
	MOVSI TTT,1
	PUSHJ P,PUTXY
	MOVE TTT,[2,,1]		;CODE FOR DIAMOND
	JRST PUTWRD

PLTPND:	PUSHJ P,INIPCH
	PUSHJ P,OUTPID
	JRST FINPCH

PLTBNM:	PUSHJ P,INIPCH
	PUSH P,A	;I DONT THINK THIS IS NEEDED
	FETCH(A,B,BRSLOC)
	PUSHJ P,SLTLPN
FNPCHA:	POP P,A
	JRST FINPCH

PLTCNM:	PUSHJ P,INIPCH
	PUSH P,A		;IS THIS NEEDED?
	MOVE A,(E)
	PUSHJ P,CSLTLP
	JRST FNPCHA
;LINE TABLES (D)
PPTB1:	JFCL
	PUSHJ P,DOPPIT
	PUSHJ P,DOPPIT
	JFCL
	PUSHJ P,DOPPIT
	JFCL
	JFCL
	PUSHJ P,DOPPIT
	PUSHJ P,DOPPIT
	JFCL
	JFCL
	PUSHJ P,DOPPIT
	JFCL
	PUSHJ P,DOPPIT
	PUSHJ P,DOPPIT
	JFCL

PPTB2:	FETCH(F,B,PNL)	;		
	FETCH(F,B,PND)	;		
	FETCH(F,B,PNU)	;		
	FETCH(F,B,PNL)	;		
	FETCH(F,B,PNL)	;		
	FETCH(F,B,PNL)	;		
	FETCH(F,B,PNL)	;		
	FETCH(F,B,PNL)	;		
	FETCH(F,B,PNR)	;		
	FETCH(F,B,PNR)	;		
	FETCH(F,B,PNR)	;		
	FETCH(F,B,PNR)	;		
	FETCH(F,B,PNU)	;		
	FETCH(F,B,PND)	;		
	FETCH(F,B,PNU)	;		
	JRST 4,.	;0		0

PPTB3:	4,,10
	1,,2
	2,,1
	4,,10
	4,,10
	4,,10
	4,,10
	4,,10
	10,,4
	10,,4
	10,,4
	10,,4
	2,,1
	1,,2
	2,,1
	0

PPTB4:	PUSHJ P,PUTTIT		;0
	PUSHJ P,PUTTI3		;1
	PUSHJ P,PUTTI3		;2
	JFCL			;3
	PUSHJ P,PUTTI3		;4
	JFCL			;5
	JFCL			;6
	JFCL			;7
	PUSHJ P,PUTTI3		;10
	JFCL			;11
	JFCL			;12
	JFCL			;13
	JFCL			;14
	JFCL			;15
	JFCL			;16
	JFCL			;17

PPTB5:	SKIPN CIRC
	CAIA
	CAIA
	SKIPN CIRC
	CAIA
	SKIPN CIRC
	SKIPN CIRC
	CAIA
	CAIA
	SKIPN CIRC
	SKIPN CIRC
	CAIA
	SKIPN CIRC
	CAIA
	CAIA
	JFCL		;ALWAYS QUIT ON THIS ONE!!!
>;MD
;POINTS (PC)
;B = POINT LIST

MPC,<
DOPNTS:	FETCH(G,B,PXY)		;X,Y OF POINT
	TLNE WFLAG		;IF SIG FILE
	 JRST PNOPIN		;SKIP ALL PADS ETC.
	TRNE M,NPTEXT
	 JRST NOPTXT
	FETCH(TTT,B,PTXT)
	JUMPE TTT,NOPTXT	;NONE?
	FETCH(T,B,PXY)
	PUSHJ P,PTXTS
NOPTXT:	FETCHL(E,B,PBIT)
	TLNN E,ISPIN!FEEDTH
	 JRST DOPADA
	MOVE T,G
	MOVSI TTT,1
	PUSHJ P,PUTXY
	MOVE TTT,[2,,1]		;DRILL HOLE!
	PUSHJ P,PUTWRD
DOPADA:	FETCH(D,B,PIN)		;ANY PAD TYPE?
	JUMPE D,PNOPIN
	SKIPN CPLANE
	 JRST DOPAD
	LDB T,[%%PLANES,,E]	;PLANE NUMBER
	CAMN T,CPLANE
	 JRST PNOPIN
DOPAD:	MOVE T,G
	MOVSI TTT,1
	PUSHJ P,PUTXY
	HRRZ TTT,D		;PAD TYPE
	CAMLE TTT,MXPADN
	MOVEM TTT,MXPADN
	LSH TTT,1
	TDO TTT,[4,,1]		;MARK AS PAD!
	PUSHJ P,PUTWRD
PNOPIN:	PUSH P,B	
	MOVE C,B
	FETCH(B,B,PNEB)
	JUMPE B,PPTB4
PPTB1:	MOVEI H,2
PPTB2:	XCT (H)[HLRZ A,(B)
		HRRZ A,1(B)
		HLRZ A,1(B)]
	JUMPE A,PPTB3
	CAML G,1(A)
	JRST PPTB3
	MOVE TTT,1(A)
	PUSH P,G
	PUSH P,C
	PUSHJ P,PADFIX		;PERHAPS DIDDLE LINE IF GOING TO PAD?
	MOVE T,G
	MOVEI TTT,0
	PUSHJ P,PUTXY
	MOVE C,A
	MOVE TTT,-1(P)
	MOVE G,1(A)
	PUSHJ P,PADFIX
	MOVE T,G
	MOVEI TTT,1
	PUSHJ P,PUTXY
	POP P,C
	POP P,G
PPTB3:	SOJGE H,PPTB2
	HRRZ B,(B)
	JUMPN B,PPTB1
PPTB4:	POP P,B
	HRRZ B,(B)	;GET NEXT ONE
	JUMPN B,DOPNTS
	POPJ P,

PADFIX:	TLNE WFLAG		;NO FIX FOR SIG FILES
	POPJ P,
	POPJ P,
NIL,<
;!!! THIS CODE NEVER COULD'VE BEEN EXECUTED ANYWAY !!!
	MOVE T,ADDR(C,BPPN)	;PBIT,,PAD
	CAIE T,1
	CAIN T,3
	TLNN T,ISPIN
	 POPJ P,		;ONLY FIX DIP PINS
	HRRZS T
	CAIE T,1		;AND ONLY THE ONES WE KNOW ABOUT
	CAIN T,2
	CAIA
	 POPJ P,
	ADJUST(SUB,TTT,G)
	FETCH(T,C,BBODY)
	FETCH(T,T,BORI)
	JRST @(T)[VFIX
		HFIX
		VFIX
		HFIX]

VFIX:	TRNN TTT,-1
	 JRST VFIX1
HFIX1:	TRNE TTT,400000
	SKIPA T,[XWD 0,-=8]
	MOVEI T,=8
VHFIX:	ADJUST(ADD,G,T)
	POPJ P,

HFIX:	TLNN TTT,-1
	 JRST HFIX1
VFIX1:	SKIPGE TTT
	SKIPA T,[XWD -=8,0]
	MOVSI T,=8
	JRST VHFIX
>;NIL
>;MPC
;OFFSET TEXT ROUTINE
;T = MAIN X,Y
;TTT =  POINTER TO CONSTANT OFFSET BLOCK
;C = CHARACTER SIZE
;WRITES X,Y AND RETURNS IT IN TTT

TXTCAL:
MD,<	PUSHJ P,PSETXY
	HRL T,TT		;X,,Y
	TDZ T,[1,,1]
	PUSH P,TTT
	HRRE TT,1(TTT)		;Y
	IMUL TT,CSCALE
	IMUL TT,PLTPTY(C)
	IDIV TT,VIRPTY(C)
	EXCH TT,(P)		;GET BACK BLOCK PTR
	HLRE TT,1(TT)		;X
	IMUL TT,CSCALE		;CHARACTERS TO PLOT POINTS
	IMUL TT,PLTPTX(C)	;ADJUST FOR LIE WE GAVE FOR CHAR WIDTH
	IDIV TT,VIRPTX(C)
	POP P,TTT
	HRL TTT,TT
	TDZ TTT,[1,,1]
	EXCH TTT,T		;LEAVE OFFSET VALUES IN T
	ADD TTT,T
	TLO TTT,1
	POPJ P,
>;MD
MPC,<	ADJUST(ADD,T,<1(TTT)>)
	MOVSI TTT,1
	JRST PUTXY
>;MPC

;TTT = TEXT STR, WITH OFFSET BLOCK
;T = X,Y

PTXTS:	PUSH P,TTT
MD,<	FETCH(C,TTT,TCSTR)
	FETCH(C,C,TSSIZ)
	ANDI C,377777
>;MD
	PUSHJ P,TXTCAL		;CALC LOC+OFFSET AND WRITE IT OUT
MPC,<	POP P,T
	FETCH(T,T,TCSTR)
	FETCH(TTT,T,TSSIZ)
	LSH TTT,4		;SCALE APPROP FOR PLT PROGRAM
	TLZE TTT,10		;TEST VERT BIT
	 TRO TTT,400000		;AND COPY TO HERE
	TRO TTT,1		;MARK AS PART OF THIS COMMAND
	PUSHJ P,PUTWRD		;WRITE SIZE
	JRST PSTR		;AND NOW TEXT
>;MPC
;For D, break multi-line text into separate lines,
; each with its own X,Y positioning.
MD,<	PUSHJ P,PUTWRD		;WRITE X,Y WORD
	EXCH TTT,(P)		;SAVE X,Y JUST PUT OUT AND GET TEXT POINTER
	MOVEI TT,1(TTT)
	HRLI TT,(<POINT 7,0,35>)
	FETCH(TTT,TTT,TCSTR)
	FETCH(TTT,TTT,TSSIZ)
	SKIPN TTT
	 MOVE TTT,STDBIG	;CHANGE 0 TO STANDARD
	MOVE T,TTT
	ANDI TTT,377777
	PUSH P,TTT		;SAVE THIS HERE
	MOVE TTT,PLTPTX(TTT)
	IMUL TTT,CSCALE		;SCALE IT CSCALE/8
	LSH TTT,1
	TRO TTT,1
	TRNE T,400000		;WAS IT VERTICAL?
	 TRO TTT,400000		;YES, FLAG AS SUCH
	PUSHJ P,PUTWRD		;PUT OUT SIZE
	EXCH TTT,(P)		;SAVE SIZE TO WRITE OUT
	MOVE TTT,PLTPTY(TTT)
	IMUL TTT,CSCALE
;	ASH TTT,2		;SCALE III PTS INTO PLOT PTS
;	ASH TTT,-3		;REMOVE CSCALE BINARY POINT
;	ASH TTT,1		;FOR I/O FORMAT
	TRZ TTT,1		;LOW ORDER BIT MUST BE OFF
				;4 plot points are worth 1 dpy point
				;the factor of 8 implied in CSCALE
				;causes this delta-Y to be plot points
				; shifted by 1 for I/O format. God damn
	TRNN T,400000
	 MOVN TTT,TTT
	HRRZS TTT
	TRNE T,400000
	 MOVSS TTT
	PUSH P,TTT	;SAVE LF INCREMENT
PTXTS1:	SETZ TTT,	;GATHER TEXT HERE
PTXTS2:	TLNN TT,760000	;END OF WORD?
	JRST [	HRR TT,-1(TT)	;FOLLOW LINK
		TRNE TT,-1	;WAS THERE ONE THERE?
		JRST .+1	;YES
		POP P,(P)
		POP P,(P)	;FIX UP STACK
		POP P,(P)
		JRST PTXTFN]	;NO
	ILDB T,TT
	JUMPE T,PTXTS2		;SKIP NULLS
	CAIN T,DBLARR		;THIS IS REALLY CRLF
	 JRST PTXTS3
	TLNE TTT,776000		;TTT FULL?
	PUSHJ P,PTXTFN		;FLUSH IT OUT
	LSH TTT,7
	IOR TTT,T		;OR IN NEW CHAR
	JRST PTXTS2		;LOOP FOR ANOTHER CHAR

PTXTFN:	JUMPE TTT,CPOPJ		;IGNORE NULL WORDS
	JRST PTXTF2
PTXTF1:	LSH TTT,7
PTXTF2:	TLNN TTT,776000
	 JRST PTXTF1
	LSH TTT,1
	TRO TTT,1
	PUSHJ P,PUTWRD
	SETZ TTT,		;CLEAR OUT TTT FOR ANOTHER WORD
	POPJ P,

PTXTS3:	PUSHJ P,PTXTFN		;FLUSH OUT CURRENT WORD
	MOVE TTT,-2(P)		;GET LAST X,Y
	TLZ TTT,1
	ADD TTT,(P)		;MOVE ONE LINE
	MOVEM TTT,-2(P)		;SAVE THIS AS LAST
	TLO TTT,1		;MAKE SURE THIS IS ON
	PUSHJ P,PUTWRD
	MOVE TTT,-1(P)		;SIZE WORD
	PUSHJ P,PUTWRD
	JRST PTXTS1
>;MD
;OUTPUT ROUTINES

;PSETXY - CONVERT TO PLOT COORDS
;T = INTERNAL X,Y
;RETURNS
;T = Y (PLOT COORDS)
;TT = X (PLOT COORDS)

MD,<
PSETXY:	HLRE TT,T	;GET X
	HRRES T		;GET Y
	SUB TT,XOFF	;OFFSET
	SUB T,YOFF	;OFFSET	
	SCALET (T)		;SCALE TO PLOTTER, AND SHIFT ONE FOR I/O
	SCALET (TT)
	POPJ P,
>;MD
PUTXY:
MD,<	PUSHJ P,PSETXY
	TRZ T,1
	TRZ TT,1
	TLO TTT,(TT)
	TRO TTT,(T)
>;MD
MPC,<	TRNN M,FLIP
	JRST PUTXYF
	TLC T,-1	;INVERT X
	ADD T,[2,,0]
PUTXYF:	TDZ T,[1,,1]
	TDO TTT,T
>;MPC
PUTWRD:	SOSG IOHD+2
	OUT DAT,
	CAIA
	OUTSTR[ASCIZ/PLOT FILE OUTPUT ERROR!
/]
	IDPB TTT,IOHD+1
	POPJ P,

PONE:	MOVEI TTT,1
	JRST PUTWRD

PSTR:	JUMPE T,PONE
	FETCH(TTT,T,TSASC)
	TRO TTT,1
	PUSHJ P,PUTWRD
	FETCH(T,T,TSNXT)
	TRNE TTT,376
	JRST PSTR
	POPJ P,

PCHRS:	ADD T,[POINT 7,1]
PCHRS1:	PUSHJ P,GETTT
	POPJ P,
	PUTBYT (C)
	JRST PCHRS1

MD,<
PUTLIN:	TLZ TTT,1
	TRO TTT,1
	JRST PUTWRD

PINVS:	PUSHJ P,ORIENT
	ADD T,1(B)	;GET X,Y
	TDZ T,[(1)1]
	MOVEM T,SAVLOC	;SAVE IT
	POPJ P,

PVIS:	PUSHJ P,ORIENT	;ORIENT IT
	ADD T,1(B)	;ADD IN BODY CENTER
	TDZ T,[(1)1]
	EXCH T,SAVLOC
	MOVEI TTT,
	PUSHJ P,PUTXY
	MOVE T,SAVLOC
	MOVEI TTT,1
	JRST PUTXY

VSIZES:	TRNN F,400000
	JRST STDSIZ
	MOVE TTT,STDBIG
	MOVE TTT,PLTPTX(TTT)
	IMUL TTT,CSCALE
	LSH TTT,1	
	TRO TTT,400001
	JRST PUTWRD

STDSIZ:	MOVE TTT,STDBIG
	MOVE TTT,PLTPTX(TTT)
	IMUL TTT,CSCALE
	LSH TTT,1	
	TRO TTT,1
	JRST PUTWRD
>;MD

INIPCH:	SETZM PCHBUF
	MOVE TTT,[PUSHJ P,PLTCH]
	EXCH TTT,PUTCHR
	MOVEM TTT,PLTPSV
	POPJ P,

PLTCH:	IOR TTT,PCHBUF
	TLNN TTT,376000
	JRST [	LSH TTT,7
		MOVEM TTT,PCHBUF
		POPJ P,]
	SETZM PCHBUF
PLTCH2:	LSH TTT,1
	TRO TTT,1
	JRST PUTWRD

FINPCZ:	MOVE TTT,PLTPSV
	MOVEM TTT,PUTCHR
	SKIPN TTT,PCHBUF
	JRST PLTCH2
	JRST FNPCH2

FINPCH:	MOVE TTT,PLTPSV
	MOVEM TTT,PUTCHR
	SKIPN TTT,PCHBUF
	POPJ P,
FNPCH2:	TLNE TTT,376000
	JRST PLTCH2
	LSH TTT,7
	JRST FNPCH2
