<PACKAGE "CHARAP">

<ENTRY CHARAPPEND CR FF>

<USE-DEFER "FP">			; "THE FPRINT PACKAGE"

<SETG CR <SET CR "
"	>>				; "CR - LF FOR OTHERS TO USE"

<SETG FF <SET FF <ASCII 12>>>		; "FORM FEED"

<MANIFEST CR FF>			; "MANIFEST FOR COMPILATIONS"
					; "HOWEVER, THE ATOM CR WILL CAUSE A <TERPRI>"
					; "    RATHER THAN A <PRINC ,CR>"


<DEFINE CHARAPPEND (OUT-TO PTYP CLSSW "TUPLE" THINGS
		    "AUX" CHNL LEN TSLEN FNAME (TSTR <ISTRING 10>))
    #DECL ((VALUE CHNL) <OR CHANNEL FALSE>
	   (TSTR FNAME) STRING
	   (OUT-TO) <OR STRING CHANNEL>
	   (THINGS) TUPLE
	   (PTYP LEN TSLEN) FIX
	   (A) <OR CHARACTER FALSE>)

   <PROG ()
	 <COND (<TYPE? .OUT-TO CHANNEL>				; "COMPUTE FNAME"
		<AND <=? <9 .OUT-TO> "NUL"> <RETURN .OUT-TO>>	; "TO NUL: IS PUNTED"
		<SET FNAME <STRING <9 .OUT-TO> !": 
				   <10 .OUT-TO> !"; 
				   <7 .OUT-TO> !"  
				   <8 .OUT-TO>>>)
	       (<SET FNAME .OUT-TO>)>

	 <COND (<=? .FNAME "NUL:"> <RETURN .OUT-TO>)	; "GET CHANNEL TO PRINT ON"
	       (<=? .FNAME "TTY:"> <SET CHNL .OUTCHAN> <SET CLSSW %<>>)	; "TO TTY IS OK"
	       (<AND <TYPE? .OUT-TO CHANNEL>		; "ALREADY HAVE PRINT OR PRINTO"
		     <N==? <1 .OUT-TO> 0>
		     <MEMBER <2 .OUT-TO> '["PRINT" "PRINTO"]>>
		<SET CHNL .OUT-TO>)			; "JUST GO PRINT MORE ON IT"

	       (<COND (<AND <TYPE? .OUT-TO CHANNEL>	; "HAVE AN OPEN 'READ' CHANNEL"
			    <N==? <1 .OUT-TO> 0>
			    <=? <2 .OUT-TO> "READ">>
		       <SET CHNL .OUT-TO>)
		      (<TYPE? .OUT-TO CHANNEL>		; "SOME OTHER OR CLOSED CHANNEL"
		       <CLOSE .OUT-TO>
		       <SET CHNL <OPEN "READ" .FNAME>>)	; "CHANGE IT FOR A READ CHANNEL"
		      (<SET CHNL <OPEN "READ" .FNAME>>)>	; "GET A READ CHANNEL"

		<COND (<=? <9 .CHNL> "NUL"> <RETURN .CHNL>)
		      (<AND <SET LEN <FILE-LENGTH .CHNL>>	; "MUST HAVE STUFF IN IT"
			    <G? .LEN 0>>
		       <ACCESS .CHNL <* <MAX <- </ .LEN 5> 2> 0>
					5>>		; "ACCESS SECOND TO LAST WORD"
		       <SET TSLEN <READSTRING .TSTR .CHNL 10>>	; "READ THE REMAINS"
		       <CLOSE .CHNL>
							; "OPEN IN PRINT-OVER MODE"
		       <OR <SET CHNL <OPEN "PRINTO" .FNAME>> <RETURN .CHNL>>
		       <ACCESS .CHNL <MAX 0 <- </ .LEN 5> 2>>>	; "GET TO SAME PLACE"
		       <PRINTSTRING .TSTR .CHNL .TSLEN>)	; "PUT LAST CHARS BACK IN"
		      (ELSE		; "LENGTH OF THE FILE IS 0 OR UNDEFINED"
		       <CLOSE .CHNL>	; "SO GET A PRINT CHANNEL"
		       <OR <SET CHNL <OPEN "PRINT" .FNAME>> <RETURN .CHNL>>)>)

	       (<NOT <SET CHNL <OPEN "PRINT" .FNAME>>>		; "NO SUCH FILE YET"
		<RETURN .CHNL>)>			; "SO JUST GET A PRINT CHANNEL"

	 <COND (<==? 3 .PTYP>				; "PTYP = 3 ==> FPRINT"
		<FPRINT <1 .THINGS> .CHNL <REST .THINGS>>)
	       (ELSE
		<REPEAT ()			; "ELSE PRINT/PRINC ALL THE GOODIES"
			<COND (<EMPTY? .THINGS> <RETURN T>)
			      (<==? <1 .THINGS> CR> <TERPRI .CHNL>)
			      (<==? <1 .THINGS> FF> <PRINC %<ASCII 12> .CHNL>)
			      (<0? .PTYP> <PRINC <1 .THINGS> .CHNL>)
			      (<1? .PTYP> <PRINT <1 .THINGS> .CHNL>)>
			<SET THINGS <REST .THINGS>>>)>

	 <COND (.CLSSW <CLOSE .CHNL>)>
	 .CHNL>>

<ENDPACKAGE>
