<PACKAGE "PIOSUB">

<ENTRY	PIO-PAGE-FIND
	PIO-PAGE-GIVE
	REQUEST-NORM
	FILL-PAGE-REQUESTS
	FILL-UP
	SEARCH-CACHE-MAP
	INSERT-INTO-CACHE-MAP
	INSERT-INTO-ALLOC-TBL>


;"RSUBR TO FIND FREE CORE PAGES"

<TITLE	PIO-PAGE-FIND>
	<DECLARE ("VALUE" <OR FIX FALSE> "OPTIONAL" FIX)>
	<JUMPGE	AB* ONEARG>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* IPIO-PAGE-FIND2>
	<JRST	FINIS>

ONEARG	<PUSHJ	P* IPIO-PAGE-FIND1>
	<JRST	FINIS>

<INTERNAL-ENTRY IPIO-PAGE-FIND1 0>
	<SUBM	M* (P)>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* [<1>]>
	<JRST	PF1>

<INTERNAL-ENTRY IPIO-PAGE-FIND2 1>
	<SUBM	M* (P)>
PF1	<MOVE	A* (TP)>	;"YES, GET IT"
	<JUMPLE	A* WRONGT>
	<PUSHJ	P* PGFIND>
	<JUMPL	B* [<MOVE A* <TYPE-WORD FALSE>>
		    <SETZ B*>
		    <JRST PFX>]>
	<MOVE	A* <TYPE-WORD FIX>>
PFX	<SUB	TP* [<2 (2)>]>
	<JRST	MPOPJ>



;"RSUBR TO GIVE BACK CORE PAGES"

<TITLE	PIO-PAGE-GIVE>
	<DECLARE ("VALUE" FIX FIX "OPTIONAL" FIX)>
	<DPUSH	TP* (AB)>
	<AOBJN	AB* HERE 1>
	<AOBJP	AB* ONEARG>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* IPIO-PAGE-GIVE2>
	<JRST	FINIS>

ONEARG	<PUSHJ	P* IPIO-PAGE-GIVE1>
	<JRST	FINIS>

<INTERNAL-ENTRY IPIO-PAGE-GIVE1 1>
	<SUBM	M* (P)>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* [<1>]>
	<JRST	PG1>

<INTERNAL-ENTRY IPIO-PAGE-GIVE2 2>
	<SUBM	M* (P)>
PG1	<MOVE	A* (TP)>	;"YES, GET IT"
	<JUMPLE	A* WRONGT>
	<MOVE	B* (TP) -2>	;"GET NUMBER OF FIRST BLOCK"
	<JUMPL	B* WRONGT>
	<MOVEI	O* (B)>
	<ADDI	O* (A)>
	<CAIGE	B* 256>		;"VALID FIRST BLOCK NUMBER ?"
	<CAILE	O* 256>		;"VALID LAST+1 BLOCK NUMBER ?"
	<JRST	WRONGT>		;"NO, LOSE"
	<PUSHJ	P* PGGIVE>
	<MOVSI	A* <TYPE-CODE FIX>>
	<SUB	TP* [<4 (4)>]>
	<JRST	MPOPJ>





<TITLE REQUEST-NORM>

<DECLARE ("VALUE" <VECTOR [REST <OR FIX FALSE> <OR FIX FALSE>
					<OR UVECTOR <PRIMTYPE WORD> FALSE>
					<OR FIX FALSE>]>
			<OR UVECTOR <PRIMTYPE WORD>> <PRIMTYPE WORD>
			<PRIMTYPE WORD> VECTOR)>

;"
ARG1/	UVECTOR (or core address) from/to which transfer is to be made
ARG2/	FIX, length of transfer
ARG3/	address from/to which transfer is to be made
ARG4/	VECTOR to fill

RETURNS ==> VECTOR, filled with quads:
	1/ number of page in file referenced (1024 words/page)
	2/ offset of beginning of transfer in file page
	3/ UVECTOR, RESTed down to appropriate place (or PRIMTYPE WORD, core address)
	4/ number of words to be transferred between UVECTOR and page (or vice versa)"



	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<PUSHJ	P* IREQUEST-NORM>
	<JRST	FINIS>

<INTERNAL-ENTRY IREQUEST-NORM 4>

	<SUBM	M* (P)>
	<HRRZ	A* -4(TP)>
	<IDIVI	A* 1024>
	<ADDI	A* 2>
	<IMULI	A* 8>
	<HLRE	C* (TP)>
	<MOVMS	C>
	<CAMGE	C* A>
	<PUSHJ	P* REQ-NEW-VCT>
	<SUB	C* A>
	<HRLS	C>
	<ADDM	C* (TP)>
	<MOVE	E* (TP)>
RQLOOP	<MOVE	A* -2(TP)>
	<IDIVI	A* 1024>
	<MOVE	O* <TYPE-WORD FIX>>
	<MOVEM	O* (E)>
	<MOVEM	A* 1(E)>
	<MOVEM	O* 2(E)>
	<MOVEM	B* 3(E)>
	<MOVE	O* -7(TP)>
	<MOVEM	O* 4(E)>
	<MOVE	O* -6(TP)>
	<MOVEM	O* 5(E)>
	<HRRZ	B* -4(TP)>
	<MOVEI	C* 1(A)>
	<IMULI	C* 1024>
	<SUB	C* -2(TP)>
	<CAMG	B* C>
	<JRST	RQLAST>
	<MOVE	O* <TYPE-WORD FIX>>
	<MOVEM	O* 6(E)>
	<MOVEM	C* 7(E)>
	<ADDM	C* -2(TP)>
	<SUB	B* C>
	<MOVEM	B* -4(TP)>
	<HRLS	C>
	<ADDM	C* -6(TP)>
	<ADD	E* [<8(8)>]>
	<JUMPL	E* RQLOOP>
OVFERR	<DPUSH	TP* <PQUOTE OVERFLOWED-REQUEST-VECTOR!-ERRORS>>
	<MCALL	1 ERROR>
	<JRST	OVFERR>


RQLAST	<MOVE	O* <TYPE-WORD FIX>>
	<MOVEM	O* 6(E)>
	<MOVEM	B* 7(E)>
	<ADD	E* [<8(8)>]>
	<POP	TP* B>
	<POP	TP* A>
	<SUB	TP* [<6(6)>]>
LASTLOOP	<JUMPGE	E* MPOPJ>
	<MOVE	O* <TYPE-WORD FALSE>>
	<MOVEM	O* (E)>
	<SETZM	1(E)>
	<ADD	E* [<2(2)>]>
	<JRST	LASTLOOP>


REQ-NEW-VCT	<SUBM	M* (P)>
	<IDIVI	A* 2>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* A>
	<PUSH	TP* <TYPE-WORD FALSE>>
	<PUSH	TP* [<0>]>
	<MCALL	2 IVECTOR>
	<MOVEM	A* -1(TP)>
	<MOVEM	B* (TP)>
	<HLRE	A* (TP)>
	<MOVMS	A>
	<MOVE	C* A>
	<JRST	MPOPJ>






<TITLE FILL-PAGE-REQUESTS>

<DECLARE ("VALUE" VECTOR VECTOR UVECTOR ANY)>


	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<PUSHJ	P* IFILL-PAGE-REQUESTS>
	<JRST	FINIS>

;"
ARG1/	VECTOR, normalized request VECTOR from REQUEST-NORM
ARG2/	UVECTOR, page-map from file
ARG3/	ANY, non-FALSE signals READ from file, FALSE signals
	WRITE to file.

RETURNS ==> normalized request vector, all FILLED requests are
	turned off by putting a FALSE in the first member of the quad."


<INTERNAL-ENTRY IFILL-PAGE-REQUESTS 3>

	<SUBM	M* (P)>
	<MOVE	E* -4(TP)>
	<HLRE	A* E>
	<MOVMS	A>
	<JUMPE	A* FILLRET>
	<IDIVI	A* 8>
	<JUMPN	B* FILLRET>
	<HLRE	A* -2(TP)>
	<MOVMS	A>
	<JUMPE	A* FILLRET>
	<IDIVI	A* 4>
	<JUMPN	B* FILLRET>
RQLOOP	<GETYP	O* (E)>
	<CAIE	O* <TYPE-CODE FIX>>
	<JRST	NXTREQ>
	<MOVE	D* -2(TP)>
MPLOOP	<SKIPL	A* (D)>
	<CAME	A* 1(E)>
	<JRST	NXTPAG>
	<SKIPG	A* 1(D)>
	<JRST	NXTREQ>
	<GETYP	O* -1(TP)>
	<CAIN	O* <TYPE-CODE FALSE>>
	<JRST	RQWRITE>
	<IMULI	A* 1024>
	<ADD	A* 3(E)>
	<HRLS	A>
	<HRR	A* 5(E)>
	<MOVE	B* 5(E)>
	<ADD	B* 7(E)>
RQDONE	<BLT	A* -1(B)>
	<MOVE	O* <TYPE-WORD FALSE>>
	<MOVEM	O* (E)>
	<SETZM	1(E)>
	<MOVE	A* <MQUOTE '%<RGLOC PIO-REF-COUNT T>>>
	<ADD	A* GLOTOP 1 (TVP)>
	<MOVE	B* 1(A)>
	<ADDI	B* 1>
	<MOVEM	B* 3(D)>
	<MOVEM	B* 1(A)>
	<JRST	NXTREQ>

RQWRITE	<MOVE	C* 2(D)>
	<TLO	C* *400000*>
	<MOVEM	C* 2(D)>
	<MOVE	A* 1(D)>
	<IMULI	A* 1024>
	<ADD	A* 3(E)>
	<HRL	A* 5(E)>
	<MOVE	B* 7(E)>
	<ADD	B* A>
	<JRST	RQDONE>

NXTPAG	<ADD	D* [<4(4)>]>
	<JUMPL	D* MPLOOP>
NXTREQ	<ADD	E* [<8(8)>]>
	<JUMPL	E* RQLOOP>
FILLRET	<SUB	TP* [<4(4)>]>
	<POP	TP* B>
	<POP	TP* A>
RETLOOP	<GETYP	O* (B)>
	<CAIE	O* <TYPE-CODE FALSE>>
	<JRST	MPOPJ>
	<ADD	B* [<8(8)>]>
	<JUMPL	B* RETLOOP>
	<JRST	MPOPJ>



<TITLE FILL-UP>

<DECLARE ("VALUE" FIX FIX FIX FIX)>

	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<PUSHJ	P* IFILL-UP>
	<JRST	FINIS>

<INTERNAL-ENTRY IFILL-UP 3>

	<SUBM	M* (P)>
	<MOVE	A* -4(TP)>
	<MOVE	B* (TP)>
	<MOVEM	B* (A)>
	<ADDI	A* 1>
	<HRL	A* -4(TP)>
	<MOVE	B* -4(TP)>
	<ADD	B* -2(TP)>
	<BLT	A* -1(B)>
	<DMOVE	A* -1(TP)>
	<SUB	TP* [<6(6)>]>
	<JRST	MPOPJ>



<TITLE SEARCH-CACHE-MAP>

<DECLARE ("VALUE" <OR FIX FALSE> UVECTOR FIX)>

	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<PUSHJ	P* ISEARCH-CACHE-MAP>
	<JRST	FINIS>

<INTERNAL-ENTRY ISEARCH-CACHE-MAP 2>

	<SUBM	M* (P)>
	<MOVE	E* -2(TP)>
	<JUMPGE	E* SRCFLD>
SRCLOOP	<SKIPG	(E)>
	<JRST	SRCFLD>
	<HLRZ	A* (E)>
	<CAMN	A* (TP)>
	<JRST	SRCWON>
	<AOBJN	E* SRCLOOP>
SRCFLD	<MOVE	A* <TYPE-WORD FALSE>>
	<MOVEI	B* 0>
SRCRET	<SUB	TP* [<4(4)>]>
	<JRST	MPOPJ>

SRCWON	<MOVE	A* <TYPE-WORD FIX>>
	<HRRZ	B* (E)>
	<JRST	SRCRET>



<TITLE INSERT-INTO-CACHE-MAP>

<DECLARE ("VALUE" UVECTOR UVECTOR FIX FIX)>

	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<PUSHJ	P* IINSERT-INTO-CACHE-MAP>
	<JRST	FINIS>

<INTERNAL-ENTRY IINSERT-INTO-CACHE-MAP 3>

	<SUBM	M* (P)>
	<MOVE	E* -4(TP)>
	<JUMPGE	E* NEWUVCT>
SRCLOOP	<SKIPG	(E)>
	<JRST	GOTSLOT>
	<HLRZ	A* (E)>
	<CAMN	A* -2(TP)>
	<JRST	INSERR>
	<AOBJN	E* SRCLOOP>
NEWUVCT	<HLRE	A* -4(TP)>
	<MOVMS	A>
	<MOVE	B* A>
	<IDIVI	B* 5>
	<CAILE	B* 50>
	<MOVEI	B* 50>
	<CAIGE	B* 10>
	<MOVEI	B* 10>
	<ADD	A* B>
	<MOVEI	O* IBLOCK>
	<PUSHJ	P* RCALL>
	<HLRE	C* B>
	<MOVMS	C>
	<ADDI	C* (B)>
	<MOVEI	O* <TYPE-CODE FIX>>
	<PUTYP	O* (C)>
	<MOVE	C* -4(TP)>
	<HLRE	D* C>
	<MOVMS	D>
	<ADDI	D* (B)>
	<HRLS	C>
	<HRR	C* B>
	<BLT	C* -1(D)>
	<HRL	E* -2(TP)>
	<HRR	E* (TP)>
	<MOVEM	E* (D)>
INSRET	<SUB	TP* [<6(6)>]>
	<JRST	MPOPJ>

GOTSLOT	<HRL	A* -2(TP)>
	<HRR	A* (TP)>
	<MOVEM	A* (E)>
	<DMOVE	A* -5(TP)>
	<JRST	INSRET>

INSERR	<DPUSH	TP* <PQUOTE PAGE-ALREADY-IN-CACHE-MAP!-ERRORS>>
	<MCALL	1 ERROR>
	<JRST	INSERR>



<TITLE INSERT-INTO-ALLOC-TBL>

<DECLARE ("VALUE" UVECTOR UVECTOR FIX FIX FIX)>

	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<PUSHJ	P* IINSERT-INTO-ALLOC-TBL>
	<JRST	FINIS>

<INTERNAL-ENTRY IINSERT-INTO-ALLOC-TBL 4>

	<SUBM	M* (P)>
	<HLRE	A* -6(TP)>
	<MOVMS	A>
	<ADDI	A* 10>
	<PUSH	P* A>
	<MOVEI	O* IBLOCK>
	<PUSHJ	P* RCALL>
	<POP	P* C>
	<ADDI	C* (B)>
	<MOVEI	O* <TYPE-CODE FIX>>
	<PUTYP	O* (C)>
	<HRRZ	C* -4(TP)>
	<ADDI	C* (B)>
	<HRL	D* -6(TP)>
	<HRR	D* B>
	<PUSH	P* C>
	<BLT	D* -1(C)>
	<POP	P* C>
	<MOVE	D* -2(TP)>
	<MOVEM	D* (C)>
	<MOVE	D* (TP)>
	<MOVEM	D* 1(C)>
	<ADDI	C* 2>
	<MOVE	D* -4(TP)>
	<ADD	D* -6(TP)>
	<HRL	C* D>
	<HLRE	D* -6(TP)>
	<MOVMS	D>
	<SUB	D* -4(TP)>
	<ADDI	D* (C)>
	<BLT	C* -1(D)>
	<SUB	TP* [<8(8)>]>
	<JRST	MPOPJ>

<END>


<ENDPACKAGE>
