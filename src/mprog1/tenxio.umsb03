%%<USE "JSYS">


<PACKAGE "TENXIO">

<ENTRY JFN GET-FILE OPEN-FILE CLOSE-FILE RELEASE-FILE ACCESS-FILE>

<ENTRY ACCESS-OF-FILE LENGTH-OF-FILE SIZE-OF-FILE NAME-OF-FILE>

<ENTRY READW-FILE PRINTW-FILE>

<ENTRY READB-FILE PRINTB-FILE>

<ENTRY READS-FILE PRINTS-FILE>

<ENTRY READP-FILE PRINTP-FILE>

<ENTRY PMAP-FILE>

<ENTRY GET-PRIMARY-JFNS>

<ENTRY FIND-FIRST-FREE-FILE-PAGE FIND-FIRST-USED-FILE-PAGE>

<ENTRY GET-JOB-INFO GET-DIR-ALLOC>

<ENTRY STR-TO-DIRNUM DIRNUM-TO-STR>

<ENTRY CON-TO-DIR EXPUNGE-DIR EXPUNGE-CON-DIR>

<NEWTYPE JFN WORD>

%%<USE "JSYS">


"******************* ASSEMBLY CODE ************************************"


<TITLE GET-FILE>

<DECLARE ("VALUE" <OR JFN FALSE> STRING <PRIMTYPE WORD>)>

	<DPUSH TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<PUSHJ	P* IGET-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IGET-FILE 2>

	<SUBM	M* (P)>
	<GETYP	O* -3(TP)>
	<CAIE	O* TSTRING>
	<JRST	P2RETFLS>
	<MOVEI	C* -3(TP)>
	<PUSHJ	P* GETDOP>
	<SOS	A>
	<PUSH	P* (A)>
	<PUSH	P* A>
	<SETZM	(A)>
	<MOVE	A* (TP)>
	<MOVE	B* -2(TP)>
	<TLO	A* 1>
	<TLZ	A* 2>
	<GTJFN>
	<JRST	GTFLD>
	<POP	P* E>
	<POP	P* (E)>
	<HRRZ	B* A>
	<HRLZI	A* <TYPE-CODE JFN>>
P2RET	<SUB	TP* [<4(4)>]>
	<JRST	MPOPJ>

GTFLD	<POP	P* E>
	<POP	P* (E)>
P2RETFLD	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* A>
	<MCALL	1 LIST>
	<MOVE	A* <TYPE-WORD FALSE>>
	<JRST	P2RET>

P2RETFLS	<MOVE	A* <TYPE-WORD FALSE>>
	<MOVEI	B* 0>
	<JRST	P2RET>


;"GETS A POINTER TO THE SECOND DOPE WORD OF A STRING, LEAVES IN AC1"
;"C/ POINTER TO TYPE/VALUE PAIR OF STRING"
GETDOP	<SUBM	M* (P)>
	<MOVE	B* 1(C)>
	<LDB	D* [<(*360600*)2>]>
	<LDB	E* [<(*300600*)2>]>
	<MOVEI	A* (E)>
	<IDIVI	D* (E)>
	<HRRZ	E* (C)>
	<SUBM	E* D>
	<JUMPL	D* GETDO1>
	<MOVEI	B* 36>
	<IDIVM	B* A>
	<ADDI	D* -1(A)>
	<IDIVI	D* (A)>
	<ADD	D* 1(C)>
	<MOVEI	A* 2(D)>
	<JRST	MPOPJ>
GETDO1	<MOVEI	A* 1(B)>
	<CAME	D* [-5]>
	<AOJA	A* MPOPJ>
	<JRST	MPOPJ>


<SUB-ENTRY OPEN-FILE ("VALUE" <OR JFN FALSE> JFN <PRIMTYPE WORD>)>

	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<PUSHJ	P* IOPEN-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IOPEN-FILE 2>

	<SUBM	M* (P)>
	<GETYP	O* -3(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P2RETFLS>
	<MOVE	A* -2(TP)>
	<MOVE	B* (TP)>
	<OPENF>
	<JRST	P2RETFLD>
	<DMOVE	A* -3(TP)>
	<JRST	P2RET>



<SUB-ENTRY CLOSE-FILE ("VALUE" <OR JFN FALSE> JFN <PRIMTYPE WORD>)>

	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<PUSHJ	P* ICLOSE-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY ICLOSE-FILE 1>

	<SUBM	M* (P)>
	<GETYP	O* -3(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P1RETFLS>
	<MOVE	A* -2(TP)>
	<SKIPGE	(TP)>
	<HRREI	A* -1>
	<SKIPE	(TP)>
	<TLO	A* *400000*>
	<CLOSF>
	<JRST	P1RETFLD>
	<DMOVE	A* -3(TP)>
P1RET	<SUB	TP* [<2(2)>]>
	<JRST	MPOPJ>

P1RETFLD	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* A>
	<MCALL	1 LIST>
	<MOVE	A* <TYPE-WORD FALSE>>
	<JRST	P1RET>

P1RETFLS	<MOVE	A* <TYPE-WORD FALSE>>
	<MOVEI	B* 0>
	<JRST	P1RET>



<SUB-ENTRY RELEASE-FILE ("VALUE" <OR JFN FALSE> JFN)>

	<DPUSH	TP* (AB)>
	<PUSHJ	P* IRELEASE-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IRELEASE-FILE 1>

	<SUBM	M* (P)>
	<GETYP	O* -1(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P1RETFLD>
	<MOVE	A* (TP)>
	<RLJFN>
	<JRST	P1RETFLD>
	<DMOVE	A* -1(TP)>
	<JRST	P1RET>



<SUB-ENTRY ACCESS-FILE ("VALUE" <OR JFN FALSE> JFN <PRIMTYPE WORD>)>

	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<PUSHJ	P* IACCESS-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IACCESS-FILE 2>

	<SUBM	M* (P)>
	<GETYP	O* -3(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P2RETFLS>
	<MOVE	A* -2(TP)>
	<MOVE	B* (TP)>
	<SFPTR>
	<JRST	P2RETFLD>
	<DMOVE	A* -3(TP)>
	<JRST	P2RET>



<SUB-ENTRY NAME-OF-FILE ("VALUE" <OR STRING FALSE> JFN)>

	<DPUSH	TP* (AB)>
	<PUSHJ	P* INAME-OF-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY INAME-OF-FILE 1>

	<SUBM	M* (P)>
	<GETYP	O* -1(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P1RETFLS>
	<PUSHJ	P* TMTNXS>
	<HRROI	A* 1(E)>
	<MOVE	B* (TP)>
	<MOVEI	C* 0>
	<PUSH	P* E>
	<JFNS>
	<POP	P* E>
	<MOVEI	B* 1(E)>
	<SUBM	P* E>
	<PUSHJ	P* TNXSTR>
	<SUB	P* E>
	<JRST	P1RET>


TMTNXS	<POP	P* D>
	<MOVE	E* P>
	<MOVEI	O* 20>
	<PUSH	P* [0]>
	<SOJG	O* HERE -1>
	<JRST	(D)>


TNXSTR	<SUBI	B* (P)>
	<PUSH	P* B>
	<ADDI	B* -1(P)>
	<SUBI	B* (A)>
	<IMULI	B* 5>
	<LDB	O* [<(*360600*)A>]>
	<IDIVI	O* 7>
	<MOVNS	O>
	<ADDI	O* 5>
	<SUBM	O* B>
	<PUSH	P* B>
	<MOVEI	A* 4(B)>
	<IDIVI	A* 5>
	<MOVEI	O* IBLOCK>
	<PUSHJ	P* RCALL>
	<POP	P* A>
	<POP	P* C>
	<ADDI	C* (P)>
	<MOVE	D* B>
	<MOVE	O* (C)>
	<MOVEM	O* (D)>
	<ADDI	C* 1>
	<AOBJN	D* HERE -3>
	<HRLI	A* <TYPE-CODE STRING>>
	<SUBI	B* 1>
	<HRLI	B* *010700*>
	<POPJ	P* >



<SUB-ENTRY SIZE-OF-FILE ("VALUE" <OR FIX FALSE> JFN)>

	<DPUSH	TP* (AB)>
	<PUSHJ	P* ISIZE-OF-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY ISIZE-OF-FILE 1>

	<SUBM	M* (P)>
	<GETYP	O* -1(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P1RETFLS>
	<MOVE	A* (TP)>
	<SIZEF>
	<JRST	P1RETFLD>
	<MOVE	A* <TYPE-WORD FIX>>
	<MOVE	B* C>
	<JRST	P1RET>


<SUB-ENTRY LENGTH-OF-FILE ("VALUE" <OR FIX FALSE> JFN)>

	<DPUSH	TP* (AB)>
	<PUSHJ	P* ILENGTH-OF-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY ILENGTH-OF-FILE 1>

	<SUBM	M* (P)>
	<GETYP	O* -1(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P1RETFLS>
	<MOVE	A* (TP)>
	<SIZEF>
	<JRST	P1RETFLD>
	<MOVE	A* <TYPE-WORD FIX>>
	<JRST	P1RET>



<SUB-ENTRY ACCESS-OF-FILE ("VALUE" <OR FIX FALSE> JFN)>

	<DPUSH	TP* (AB)>
	<PUSHJ	P* IACCESS-OF-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IACCESS-OF-FILE 1>

	<SUBM	M* (P)>
	<GETYP	O* -1(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P1RETFLS>
	<MOVE	A* (TP)>
	<RFPTR>
	<JRST	P1RETFLD>
	<MOVE	A* <TYPE-WORD FIX>>
	<JRST	P1RET>



<SUB-ENTRY READB-FILE ("VALUE" <OR UVECTOR FALSE> JFN UVECTOR)>

	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<PUSHJ	P* IREADB-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IREADB-FILE 2>

	<SUBM	M* (P)>
	<GETYP	O* -3(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P2RETFLS>
	<GETYP	O* -1(TP)>
	<CAIE	O* <TYPE-CODE UVECTOR>>
	<JRST	P2RETFLS>
	<MOVE	A* -2(TP)>
	<MOVE	B* (TP)>
	<HLRE	C* B>
	<HRLI	B* *444400*>
	<MOVM	D* C>
	<ADDI	D* (B)>
	<PUSH	P* D>
	<SIN-JSYS>
	<POP	P* D>
	<CAIE	D* 1(B)>
	<JRST	P2RETFLS>
	<DMOVE	A* -1(TP)>
	<JRST	P2RET>


<SUB-ENTRY PRINTB-FILE ("VALUE" <OR UVECTOR FALSE> JFN UVECTOR)>

	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<PUSHJ	P* IPRINTB-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IPRINTB-FILE 2>

	<SUBM	M* (P)>
	<GETYP	O* -3(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P2RETFLS>
	<GETYP	O* -1(TP)>
	<CAIE	O* <TYPE-CODE UVECTOR>>
	<JRST	P2RETFLS>
	<MOVE	A* -2(TP)>
	<MOVE	B* (TP)>
	<HLRE	C* B>
	<HRLI	B* *444400*>
	<MOVM	D* C>
	<ADDI	D* (B)>
	<PUSH	P* D>
	<SOUT>
	<POP	P* D>
	<CAIE	D* 1(B)>
	<JRST	P2RETFLS>
	<DMOVE	A* -1(TP)>
	<JRST	P2RET>


<SUB-ENTRY READS-FILE ("VALUE" <OR STRING FALSE> JFN STRING)>

	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<PUSHJ	P* IREADS-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IREADS-FILE 2>

	<SUBM	M* (P)>
	<GETYP	O* -3(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P2RETFLS>
	<GETYP	O* -1(TP)>
	<CAIE	O* <TYPE-CODE STRING>>
	<JRST	P2RETFLS>
	<MOVE	A* -2(TP)>
	<MOVE	B* (TP)>
	<HRRZ	C* -1(TP)>
	<MOVNS	C>
	<SIN-JSYS>
	<DMOVE	A* -1(TP)>
	<JRST	P2RET>


<SUB-ENTRY PRINTS-FILE ("VALUE" <OR STRING FALSE> JFN STRING)>

	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<PUSHJ	P* IPRINTS-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IPRINTS-FILE 2>

	<SUBM	M* (P)>
	<GETYP	O* -3(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P2RETFLS>
	<GETYP	O* -1(TP)>
	<CAIE	O* <TYPE-CODE STRING>>
	<JRST	P2RETFLS>
	<MOVE	A* -2(TP)>
	<MOVE	B* (TP)>
	<HRRZ	C* -1(TP)>
	<MOVNS	C>
	<SOUT>
	<DMOVE	A* -1(TP)>
	<JRST	P2RET>


<SUB-ENTRY READW-FILE ("VALUE" <OR WORD FALSE> JFN FIX)>

	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<PUSHJ	P* IREADW-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IREADW-FILE 2>

	<SUBM	M* (P)>
	<GETYP	O* -3(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P2RETFLS>
	<GETYP	O* -1(TP)>
	<CAIE	O* <TYPE-CODE FIX>>
	<JRST	P2RETFLS>
	<MOVE	A* -2(TP)>
	<MOVE	C* (TP)>
	<RIN>
	<MOVE	A* <TYPE-WORD WORD>>
	<JRST	P2RET>


<SUB-ENTRY PRINTW-FILE ("VALUE" <OR <PRIMTYPE WORD> FALSE> JFN FIX <PRIMTYPE WORD>)>

	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<DPUSH	TP* 4(AB)>
	<PUSHJ	P* IPRINTW-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IPRINTW-FILE 3>

	<SUBM	M* (P)>
	<GETYP	O* -5(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P3RETFLS>
	<GETYP	O* -3(TP)>
	<CAIE	O* <TYPE-CODE FIX>>
	<JRST	P3RETFLS>
	<MOVE	A* -4(TP)>
	<MOVE	B* (TP)>
	<MOVE	C* -2(TP)>
	<ROUT>
	<DMOVE	A* -1(TP)>
P3RET	<SUB	TP* [<6(6)>]>
	<JRST	MPOPJ>

P3RETFLS	<MOVE	A* <TYPE-WORD FALSE>>
	<MOVEI	B* 0>
	<JRST	P3RET>




<SUB-ENTRY READP-FILE ("VALUE" <OR UVECTOR FIX FALSE> JFN <OR UVECTOR FIX> FIX)>

	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<PUSHJ	P* IREADP-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IREADP-FILE 3>

	<SUBM	M* (P)>
	<GETYP	O* -5(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P3RETFLS>
	<MOVE	A* -4(TP)>
	<MOVE	B* -2(TP)>
	<GETYP	O* -3(TP)>
	<CAIN	O* <TYPE-CODE UVECTOR>>
	<HRRZS	B>
	<MOVE	C* (TP)>
	<MOVE	D* B>
	<ADD	D* C>
	<CAMLE	D* [*777777*]>
	<JRST	P3RETFLS>
	<HRLI	B* *444400*>
	<MOVNS	C>
	<PUSH	P* D>
	<SIN-JSYS>
	<POP	P* D>
	<CAIE	D* 1(B)>
	<JRST	P3RETFLS>
	<DMOVE	A* -3(TP)>
	<JRST	P3RET>



<SUB-ENTRY PRINTP-FILE ("VALUE" <OR FIX UVECTOR FALSE> JFN <OR FIX UVECTOR> FIX)>

	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<PUSHJ	P* IPRINTP-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IPRINTP-FILE 3>

	<SUBM	M* (P)>
	<GETYP	O* -5(TP)>
	<CAIE	O* <TYPE-CODE JFN>>
	<JRST	P3RETFLS>
	<MOVE	A* -4(TP)>
	<MOVE	B* -2(TP)>
	<GETYP	O* -3(TP)>
	<CAIN	O* <TYPE-CODE UVECTOR>>
	<HRRZS	B>
	<MOVE	C* (TP)>
	<MOVE	D* B>
	<ADD	D* C>
	<CAMLE	D* [*777777*]>
	<JRST	P3RETFLS>
	<HRLI	B* *444400*>
	<MOVNS	C>
	<PUSH	P* D>
	<SOUT>
	<POP	P* D>
	<CAIE	D* 1(B)>
	<JRST	P3RETFLS>
	<DMOVE	A* -3(TP)>
	<JRST	P3RET>






<SUB-ENTRY PMAP-FILE ("VALUE" <OR 'T FALSE> <PRIMTYPE WORD> <PRIMTYPE WORD>
			<PRIMTYPE WORD> <PRIMTYPE WORD> <PRIMTYPE WORD>)>

	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<PUSHJ	P* IPMAP-FILE>
	<JRST	FINIS>

<INTERNAL-ENTRY IPMAP-FILE 5>

	<SUBM	M* (P)>
	<HRL	A* -8(TP)>
	<HRR	A* -6(TP)>
	<HRL	B* -4(TP)>
	<HRR	B* -2(TP)>
	<MOVE	C* (TP)>
	<PMAP>
	<DMOVE	A* <PQUOTE T>>
	<SUB	TP* [<10(10)>]>
	<JRST	MPOPJ>


<SUB-ENTRY GET-PRIMARY-JFNS ("VALUE" <VECTOR [2 JFN]> "OPTIONAL" <PRIMTYPE WORD>)>

	<HLRE	O* AB>
	<JUMPL	O* GPJ1>
	<PUSHJ	P* IGPJ>
	<JRST	FINIS>

GPJ1	<PUSHJ	P* IGPJ1>
	<JRST	FINIS>


<INTERNAL-ENTRY IGPJ 0>

	<SUBM	M* (P)>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* [<*400000*>]>
	<JRST	IGPJ11>

<INTERNAL-ENTRY IGPJ1 1>

	<SUBM	M* (P)>
IGPJ11	<HRRZ	A* (TP)>
	<GPJFN>
	<PUSH	TP* <TYPE-WORD JFN>>
	<HLRZ	C* B>
	<PUSH	TP* C>
	<PUSH	TP* <TYPE-WORD JFN>>
	<HRRZ	C* B>
	<PUSH	TP* C>
	<MCALL	2 VECTOR>
	<SUB	TP* [<2(2)>]>
	<JRST	MPOPJ>


<SUB-ENTRY FIND-FIRST-FREE-FILE-PAGE ("VALUE" <OR FIX FALSE> JFN)>

	<DPUSH	TP* (AB)>
	<PUSHJ	P* IFFFFP>
	<JRST	FINIS>

<INTERNAL-ENTRY IFFFFP 1>

	<SUBM	M* (P)>
	<MOVE	A* (TP)>
	<FFFFP>
	<SKIPGE	A>
	<JRST	FFFFPFLS>
	<HRRZ	B* A>
	<MOVE	A* <TYPE-WORD FIX>>
	<SUB	TP* [<2(2)>]>
	<JRST	MPOPJ>

FFFFPFLS	<MOVE	A* <TYPE-WORD FALSE>>
	<MOVEI	B* 0>
	<SUB	TP* [<2(2)>]>
	<JRST	MPOPJ>



<SUB-ENTRY FIND-FIRST-USED-FILE-PAGE ("VALUE" <OR FIX FALSE> JFN FIX)>

	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<PUSHJ	P* IFFUFP>
	<JRST	FINIS>

<INTERNAL-ENTRY IFFUFP 2>

	<SUBM	M* (P)>
	<HRL	A* -2(TP)>
	<HRR	A* (TP)>
	<FFUFP>
	<JRST	FFUFPFLS>
	<HRRZ	B* A>
	<MOVE	A* <TYPE-WORD FIX>>
	<SUB	TP* [<4(4)>]>
	<JRST	MPOPJ>

FFUFPFLS	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* A>
	<MCALL	1 LIST>
	<MOVE	A* <TYPE-WORD FALSE>>
	<SUB	TP* [<4(4)>]>
	<JRST	MPOPJ>



<SUB-ENTRY GET-JOB-INFO ("VALUE" <UVECTOR FIX FIX FIX FIX>)>

	<PUSHJ	P* IGET-JOB-INFO>
	<JRST	FINIS>

<INTERNAL-ENTRY IGET-JOB-INFO 0>

	<SUBM	M* (P)>
	<GJINF>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* A>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* B>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* C>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* D>
	<MCALL	4 UVECTOR>
	<JRST	MPOPJ>




<SUB-ENTRY STR-TO-DIRNUM ("VALUE" <OR FIX FALSE> STRING)>

	<DPUSH	TP* (AB)>
	<PUSHJ	P* ISTR-TO-DIRNUM>
	<JRST	FINIS>

<INTERNAL-ENTRY ISTR-TO-DIRNUM 1>

	<SUBM	M* (P)>
	<GETYP	O* -1(TP)>
	<CAIE	O* TSTRING>
	<JRST	P1RETFLS>
	<MOVEI	C* -1(TP)>
	<PUSHJ	P* GETDOP>
	<SOS	A>
	<PUSH	P* (A)>
	<PUSH	P* A>
	<SETZM	(A)>
	<MOVEI	A* 1>
	<MOVE	B* (TP)>
	<MOVEI	C* 0>
	<STDIR>
	<JFCL>
	<JRST	STDFLD>
	<POP	P* E>
	<POP	P* (E)>
	<HRRZ	B* A>
	<MOVE	A* <TYPE-WORD FIX>>
	<JRST	P1RET>
STDFLD	<POP	P* E>
	<POP	P* (E)>
	<JRST	P1RETFLS>


<SUB-ENTRY DIRNUM-TO-STR ("VALUE" <OR STRING FALSE> FIX)>

	<DPUSH	TP* (AB)>
	<PUSHJ	P* IDIRNUM-TO-STR>
	<JRST	FINIS>

<INTERNAL-ENTRY IDIRNUM-TO-STR 1>

	<SUBM	M* (P)>
	<PUSHJ	P* TMTNXS>
	<HRROI	A* 1(E)>
	<MOVE	B* (TP)>
	<DIRST>
	<JRST	P1RETFLS>
	<MOVEI	B* 1(E)>
	<SUBM	P* E>
	<PUSHJ	P* TNXSTR>
	<SUB	P* E>
	<JRST	P1RET>




<SUB-ENTRY CON-TO-DIR ("VALUE" <OR 'T FALSE> FIX)>

	<DPUSH	TP* (AB)>
	<PUSHJ	P* ICON-TO-DIR>
	<JRST	FINIS>

<INTERNAL-ENTRY ICON-TO-DIR 1>

	<SUBM	M* (P)>
	<GETYP	O* -1(TP)>
	<CAIE	O* TFIX>
	<JRST	P1RETFLS>
	<HRRZ	A* (TP)>
	<MOVE	B* 0>
	<CNDIR>
	<JRST	P1RETFLS>
	<DMOVE	A* <PQUOTE T>>
	<JRST	P1RET>



<SUB-ENTRY EXPUNGE-DIR ("VALUE" 'T)>

	<PUSHJ	P* IEXPUNGE-DIR>
	<JRST	FINIS>

<INTERNAL-ENTRY IEXPUNGE-DIR 0>

	<SUBM	M* (P)>
	<GJINF>
	<DELDF>
	<DMOVE	A* <PQUOTE T>>
	<JRST	MPOPJ>



<SUB-ENTRY EXPUNGE-CON-DIR ("VALUE" 'T)>


	<PUSHJ	P* IEXPUNGE-CON-DIR>
	<JRST	FINIS>

<INTERNAL-ENTRY IEXPUNGE-CON-DIR 0>

	<SUBM	M* (P)>
	<GJINF>
	<MOVE	A* B>
	<DELDF>
	<DMOVE	A* <PQUOTE T>>
	<JRST	MPOPJ>


<SUB-ENTRY GET-DIR-ALLOC ("VALUE" <OR <UVECTOR FIX FIX> FALSE> FIX)>

	<DPUSH	TP* (AB)>
	<PUSHJ	P* IGET-DIR-ALLOC>
	<JRST	FINIS>

<INTERNAL-ENTRY IGET-DIR-ALLOC 1>

	<SUBM	M* (P)>
	<MOVE	A* (TP)>
	<GTDAL>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* A>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* B>
	<MCALL	2 UVECTOR>
	<JRST	P1RET>

<END>

<ENDPACKAGE>
