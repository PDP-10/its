<PACKAGE "LIBMNT">

<ENTRY LIB-DUMP
	LIB-UPDATE>

<USE "LIB" "SDM">


<DEFINE LIB-DUMP (LIB FILE "AUX" LCHAN CHAN)
#DECL ((LIB FILE) STRING (VALUE) <OR STRING FALSE> (CHAN) <OR CHANNEL FALSE>
	(LCHAN) <OR PMCHAN FALSE>)
<LIB-DCT>
<COND	(<SET LCHAN <SDMOPN .LIB>>
	<COND	(<SET CHAN <OPEN "PRINT" .FILE>>
		<SDMAPFV <> <FUNCTION (VAL) <PPRINT .VAL .CHAN>> .LCHAN>
		<SDMDCT .LCHAN>
		<CLOSE .CHAN>
		"DUMPED")
		(ELSE <SDMDCT .LCHAN>
			<CHTYPE ("COULDN'T OPEN OUTPUT FILE" .FILE) FALSE>)>)
	(ELSE <CHTYPE ("COULDN'T ACTIVATE LIBRARY FILE" .LIB) FALSE>)>>


<DEFINE LIB-UPDATE (LIB FILE "AUX" VAL CHAN LCHAN)
#DECL ((LIB FILE) STRING (VALUE) <OR STRING FALSE>
	(CHAN) <OR CHANNEL FALSE>
	(LCHAN) <OR PMCHAN FALSE>
	(VAL) ANY)
<LIB-DCT>
<COND	(<SET CHAN <OPEN "READ" .FILE>>
	<COND	(<SET LCHAN <SDMACT .LIB>>
		<REPEAT ()
		<COND	(<SET VAL <READ .CHAN '<>>>
			<COND	(<AND <TYPE? .VAL FORM>
					<G? <LENGTH .VAL> 2>
					<OR <==? <1 .VAL> DEFINE> <==? <1 .VAL> SETG>>
					<TYPE? <2 .VAL> ATOM>>
				<SDMPUT .LCHAN <2 .VAL> .VAL>)
				(ELSE <ERROR BAD-LIBRARY-DATA!-ERRORS
					NOT-A-DEFINE-OR-SETG-FORM!-ERRORS
					ERRET-ANYTHING-TO-TRY-NEXT-DATUM!-ERRORS
					.VAL>)>)
			(ELSE <RETURN>)>>
		<SDMDCT .LCHAN>
		<CLOSE .CHAN>
		"LIBRARY UPDATED")
		(ELSE
		<CLOSE .CHAN>
		<CHTYPE ("COULDN'T ACTIVATE LIBRARY FILE" .LIB) FALSE>)>)
	(ELSE
	<CHTYPE ("COULDN'T OPEN INPUT FILE" .FILE) FALSE>)>>


<ENDPACKAGE>
