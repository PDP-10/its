
/* The following routine returns the homog.-part soln. to 2nd
	order linear diff'l eqns. with const. coeffs. */

MATCHDECLARE([B,C],RATNUMP,F,FREEOF(U))$
ALIAS(D,DIFF)$
DEFMATCH(SOLDE,'D(U,X,2) + B*'D(U,X) + C*U = F,U,X)$

SOLDER(EQN,U,X) := 
   BLOCK([B,C,F,DISC,R1,R2,ALPHA,BETA],
	 IF SOLDE(EQN,U,X) = FALSE THEN RETURN(FALSE) 
	 ELSE (DISC: B^2 - 4*C, ALPHA: -B/2,
	       IF DISC = 0 THEN RETURN(%E^(ALPHA*X) * (A1 + A2*X))
	       ELSE (BETA: SQRT(DISC)/2,
		     IF DISC > 0
		     THEN (R1: ALPHA + BETA, R2: ALPHA - BETA,
			   RETURN(A1*%E^(R1*X) + A2*%E^(R2*X)))
		     ELSE (BETA: %I*BETA,
			   RETURN(%E^(ALPHA*X) * (A1*COS(BETA*X)
						  + A2*SIN(BETA*X)))))))$


/* An example - The method of undetermined coeffs. for
	obtaining the particular soln. as well */

DE: 'D(Y,X,2) - 'D(Y,X) - 6*Y = SIN(X);
YH(X) := ''(SOLDER(%,Y,X));
YP(X) := B1*SIN(X) + B2*COS(X)$
YG(X) := YH(X) + YP(X)$
PLUGIN: EV(DE,DIFF,EXPAND,Y=YP(X));
EQN1: COEFF(PLUGIN,SIN(X));
EQN2: COEFF(PLUGIN,COS(X));
GLOBALSOLVE: TRUE$
SOLN: LINSOLVE([EQN1,EQN2],[B1,B2]);
YG(X);

/* Plugging in initial conditions of Y(0)=1 and Y'(0)=0 */

EQN1: YG(0) = 1;
DIFF(YG(X),X);
EQN2: EV(%,X=0) = 0; 
SOLN: LINSOLVE([EQN1,EQN2],[A1,A2]);
YG(X);


/* Resetting of options */

GLOBALSOLVE: FALSE$

/* Solution by Laplace transforms */

DE: SUBST(Y(X),Y,DE);
[ATVALUE(Y(X),X=0,1), ATVALUE('DIFF(Y(X),X),X=0,0)];
LAPLACE(DE,X,S);
LINSOLVE([%],['LAPLACE(Y(X),X,S)]);
ILT(FIRST(%),S,X);
