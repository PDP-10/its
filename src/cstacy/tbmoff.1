;;;-*-MIDAS-*-

TITLE	TBMOFF - Turn off BINARY mode if on a TAC or TIP.
	;CStacy 5/4/83

A=1
B=2
C=3

USRC=12				   ;User input.
TTYO=13				   ;TTY typeout.

TIPNUM=121			   ;Location in TELSER.

DEFINE SYSCAL OP,ARGS
	.CALL [SETZ ? SIXBIT/OP/ ? ARGS ((SETZ))]
TERMIN

GO:	.SUSET [.RTTY,,A]	   ;Get TTY number.
	MOVE B,[SIXBIT /00TLNT/]   ;Cons up expected TELSER jname.
	LDB C,[030300,,A]
	DPB C,[360300,,B]
	LDB C,[000300,,A]
	DPB C,[300300,,B]
	SYSCAL OPEN,[%CLBIT,,10+.UII ? %CLIMM,,USRC
			[SIXBIT /USR/] ? B ? [SIXBIT /TELSER/]]
	 JRST DEATH
PEEK:	.ACCESS USRC,[TIPNUM]
	.IOT USRC,A		   ;Get TTYLOC, if any.
	JUMPE A,DEATH		   ;If on a TIP, type "IAC DONT BINARY".
DOBIN:	SYSCAL OPEN,[%CLBIT,,<.UIO+%TJSIO> ? %CLIMM,,TTYO ? [SIXBIT /TTY/]]
	 .LOSE %LSFIL
	.IOT TTYO,[%TDQOT]
	.IOT TTYO,[377]	
	.IOT TTYO,[%TDQOT]
	.IOT TTYO,[376]
	.IOT TTYO,[%TDQOT]
	.IOT TTYO,[0]	
	.CLOSE TTYO,
DEATH:	.LOGOUT 1,

END GO

