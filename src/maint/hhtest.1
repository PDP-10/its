	TITLE "HACK HACK" TEST

;This tests the ITS "hack hack" for handling Type 340 display
;interrupts.  Data is output to the display through a BLKO from a low
;priority data channel, whereas other conditions are handled through a
;higher priority special channel.  The "hack hack" is that when the
;BLKO overflows, ITS triggers an interrupt on the special channel.
;This interrupt is handled still in the overflow state, so the second
;interrupt location should be used.  This hack goes back to at least
;ITS 672.

NOSYMS
RIM10

APR==0
PI==4
TTY==120

SCHN==4
DCHN==6

LOC 40+2*DCHN
	BLKO TTY,PTR			;2. BLKO overflows.
	CONO PI,4000+200_<-SCHN>	;3. Trigger special interrupt.

LOC 40+2*SCHN
	JRST 4,.
	JSR OFLOW			;4. Execute second instruction.

LOC 100

GO:	CONO APR,675550			;Reset APR.
	CONO PI,711377			;Reset PI.
	CONO PI,2000+<200_<-SCHN>>+<200_<-DCHN>> ;Enable channels.
	CONO TTY,DCHN
	CONO PI,4000+200_<-DCHN>	;1. Trigger data interrupt.
REPEAT 10,JFCL
	JRST 4,.			;Unsuccessful test ends here.

LOC 200

OFLOW:	0
	JRST 4,.			;5. Successful test ends here.

PTR:	-1,,["A]-1

END GO
