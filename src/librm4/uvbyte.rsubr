<PACKAGE "UVBYTES">
<ENTRY UV-BYTES>

;"UV-BYTES RETURNS BYTES FOR ANY GIVEN BYTE SIZE TO A UVECTOR
  OF PRIMTYPE WORD. THE CALL TO THIS ROUTINE IS <UV-BYTES .UV .BSIZE>.
  UV-BYTES RETURNS AN OBJECT OF TYPE BYTES. ARGS ARE:
  .UV ........ A UVECTOR OF PRIMTYPE WORDS
  .BSIZE ..... THE BYTE SIZE OF THE DESIRED BYTES, MUST BE LEES THAN 36."
	<TITLE UV-BYTES>
	<DECLARE ("VALUE"  BYTES
	  	  <UVECTOR [REST <PRIMTYPE WORD>]>
	  	  FIX )>
	<PUSH TP* (AB)>
	<PUSH TP* (AB)1>		;"PUSH UVECTOR"
	<PUSH TP* (AB)2>
	<PUSH TP* (AB)3>		;"PUSH BYTE BIZE"
	<PUSHJ P* UVBT0>		;"CONVERT UVECTOR TO BYTES"
	<JRST FINIS>			;"RETURN THE BYTES"
	<INTERNAL-ENTRY UVBT0 2>	;"2 ARGS"
	<SUBM M* (P)>			;"MAKE RETURN ADR M* RELATIVE"
	<INTGO>				;"CHECK FOR INTERRUPTS"
	<HRRZ E* (TP)-2>		;"UV ADR TO E*"
	<HLRE O* (TP)-2>
	<MOVM O* O>			;"UV LENGTH TO O*"
	<MOVE C* (TP)>			;"GET BYTE SIZE, THEN CHECK VALIDITY"
	<CAIG C* 0>			;"BSIZE < 1 ?"
	<JRST ERR:>			;"YES, BYTES SIZE ERROR"
	<CAILE C* 36>			;"BSIZE > 36 ??"
	<JRST ERR:>			;"YES, BYTE SIZE ERROR"
	<HRRZI A* 36>
	<IDIV A* (TP)>			;"CALC. BYTES/WORD IN A*"
	<IMUL A* O>			;"CALC. LENGTH OF BYTES"
	<HRLI A* <TYPE-CODE BYTES>>	;"MAKE THE TYPE WORD FOR THE BYTES"
	<HRRZ B* (TP)>			;"GET BYTE SIZE WANTED"
	<LSH B* 6>
	<IORI B* *440000*>
	<HRLZ B* B>			;"MAKE LEFT 1/2 OF BYTEPOINTER"
	<HRR B* E>			;"BYTEPOINTER IS VALUE CELL"
	<JRST MPOPJ>			;"ALL DONE"
ERR:	<PUSH TP* <MQUOTE UV-BYTES> -1> ;"BYTE SIZE ERROR HANDLER"
	<PUSH TP* <MQUOTE UV-BYTES>>
	<PUSH TP* (TP) -3>
	<PUSH TP* (TP) -3>		;"GET BAD BSIZE"
	<PUSH TP* <MQUOTE INVALID.BYTE.SIZE> -1>
	<PUSH TP* <MQUOTE INVALID.BYTE.SIZE>>
	<MCALL 3 ERROR>
	<JRST MPOPJ>
	<END>

<ENDPACKAGE>

